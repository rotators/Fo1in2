/*

   Cathedral - secret door to Master's Lair

*/

/* Include Files */
#include "define.h"

#define NAME                    SCRIPT_BOOKCASE

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure use_p_proc;
procedure pickup_p_proc;
procedure use_skill_on_p_proc;

procedure secret;

import variable J_Door_Ptr;

variable Door_Test;

#define LVAR_Latch_Found         (0)

procedure start begin
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(200));
end

procedure use_p_proc begin
   script_overrides;
   if ((local_var(LVAR_Latch_Found) == 1) or (Door_Test == 1)) then begin
      display_msg(mstr(104));
      use_obj(J_Door_Ptr);
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure pickup_p_proc begin
   script_overrides;
end

procedure use_skill_on_p_proc begin
   if ((local_var(LVAR_Latch_Found) == 0) and (Door_Test == 0)) then begin
      if (action_being_used == SKILL_LOCKPICK) then begin
         script_overrides;
         if (dude_skill_success(SKILL_LOCKPICK, 0)) then begin
            set_local_var(LVAR_Latch_Found, 1);
            Door_Test := 1;
            if (source_obj == dude_obj) then
               display_msg(mstr(102));
            else
               display_msg(obj_name(source_obj) + mstr(202));
         end
         else begin
            if (source_obj == dude_obj) then
               display_msg(mstr(105));
            else
               display_msg(obj_name(source_obj) + mstr(203));
         end
      end
      else if (action_being_used == SKILL_TRAPS) then begin
         script_overrides;
         if (dude_skill_success(SKILL_TRAPS, 0)) then begin
            set_local_var(LVAR_Latch_Found, 1);
            Door_Test := 1;
            display_msg(mstr(102));
         end
         else begin
            display_msg(mstr(105));
         end
      end
      else begin
         display_msg(mstr(103));
      end
   end
   else begin
      display_msg(message_str(SCRIPT_BOOKCASE, 103));
   end
end

procedure secret begin
   set_local_var(LVAR_Latch_Found, 1);
   display_msg(mstr(102));
end
