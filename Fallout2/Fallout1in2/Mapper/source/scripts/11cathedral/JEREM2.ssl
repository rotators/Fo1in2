/*

   Cathedral - Jerem, starts in the Cathedral level 3 elevator.

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_JEREM
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;

procedure jeremend;
procedure jeremcbt;
procedure jeremret;

#define LVAR_MoveTo           (2)
#define LVAR_MoveHex          (3)

variable tmp_hostile;
variable Only_Once := 1;
variable moving := 1;
variable my_hex;
variable home_tile;

procedure start begin
   if (local_var(0) == 0) then begin
      set_local_var(0, 1);
      set_local_var(1, 0);
      set_local_var(LVAR_MoveTo, 4);
      set_local_var(LVAR_MoveHex, 16912);
   end
   if Only_Once then begin
      Only_Once := 0;
      home_tile := 16912;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_CHILDREN_TRUE);
      if (global_var(GVAR_JEREMIAH_STATUS) == 1) then begin
         if not(is_loading_game) then set_self_invisible;
         moving := 0;
      end
      else if (global_var(GVAR_JEREMIAH_MOVING) == 0) then begin
         if not(is_loading_game) then set_self_invisible;
         moving := 0;
         add_timer_event(self_obj, game_ticks(300), 1);
      end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (global_var(GVAR_JEREMIAH_CHILDREN_TALKED) == 1) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_global_var(GVAR_JEREMIAH_CHILDREN_TALKED, 1);

   if (global_var(GVAR_JEREMIAH_TRICKED) == 1) then begin
      float_msg(self_obj, mstr(187), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(120), FLOAT_MSG_RED);
   end
   call jeremcbt;
end

procedure jeremend begin
end

procedure jeremcbt begin
   tmp_hostile := 1;
end

procedure jeremret begin
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      moving := 1;
      set_global_var(GVAR_JEREMIAH_MOVING, 1);
      move_to(self_obj, home_tile, 0);
      set_local_var(1, 0);
      set_local_var(LVAR_MoveTo, 4);
      set_local_var(LVAR_MoveHex, 16912);
      if not(is_loading_game) then set_self_visible;
   end
   else begin
      if (fixed_param == 2) then begin
         moving := 1;
      end
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   if (moving) then begin
      my_hex := self_tile;
      if (my_hex == local_var(LVAR_MoveHex)) then begin
         if (local_var(1)) then begin
            set_local_var(LVAR_MoveTo, local_var(LVAR_MoveTo) + 1);
         end
         else begin
            set_local_var(LVAR_MoveTo, local_var(LVAR_MoveTo) - 1);
         end
         if (local_var(LVAR_MoveTo) > 4) then begin
            moving := 0;
            set_local_var(1, 0);
            set_local_var(LVAR_MoveTo, 4);
            if not(is_loading_game) then set_self_invisible;
            set_global_var(GVAR_JEREMIAH_MOVING, 0);
            add_timer_event(self_obj, game_ticks(300), 1);
         end
         else begin
            if (local_var(LVAR_MoveTo) < 0) then begin
               moving := 0;
               set_local_var(1, 1);
               set_local_var(LVAR_MoveTo, 0);
               add_timer_event(self_obj, game_ticks(300), 2);
            end
         end
         if (local_var(LVAR_MoveTo) == 0) then begin
            set_local_var(LVAR_MoveHex, 23689);
         end
         else if (local_var(LVAR_MoveTo) == 1) then begin
            set_local_var(LVAR_MoveHex, 22900);
         end
         else if (local_var(LVAR_MoveTo) == 2) then begin
            set_local_var(LVAR_MoveHex, 22312);
         end
         else if (local_var(LVAR_MoveTo) == 3) then begin
            set_local_var(LVAR_MoveHex, 19512);
         end
         else if (local_var(LVAR_MoveTo) == 4) then begin
            set_local_var(LVAR_MoveHex, 16912);
         end
      end
      else begin
         self_walk_to_tile(local_var(LVAR_MoveHex));
      end
      if (self_can_see_dude) then begin
         coc_disguise_check;
         if not(DISGUISED) then begin
            dialogue_system_enter;
         end
      end
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_JEREMIAH_STATUS, 1);
   inc_inv_evil_crit
end
