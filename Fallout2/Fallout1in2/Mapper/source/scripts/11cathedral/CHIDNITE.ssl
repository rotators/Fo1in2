/*

   Cathedral - Nightkin guards

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h

#define NAME                    SCRIPT_CHIDNITE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure Nightkin01;
procedure Nightkin01a;
procedure Nightkin02;
procedure Nightkin03;
procedure Nightkin04;
procedure Nightkin05;
procedure Nightkin05a;
procedure Nightkin06;
procedure Nightkin07;
procedure Nightkinend;
procedure Combat;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      set_self_team(TEAM_CATHEDRAL);
      set_self_ai(AI_NIGHTKIN);
      Only_Once := 0;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (not(dude_item_count(PID_RED_COC_BADGE))) then begin
         if (not(dude_item_count(PID_BLACK_COC_BADGE))) then begin
            if (obj_pid(dude_armor) != PID_PURPLE_ROBE) then begin
               tmp_hostile := 1;
            end
         end
      end
   end
   if ((party_size > 1) and self_can_see_dude) then begin//  PARTY_COUNT > 1.    Is player travelling with homies?
      tmp_hostile := 1;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   script_overrides;
   if (global_var(GVAR_CATHEDRAL_ENEMY) == 1) then begin
      float_msg(self_obj, mstr(101), FLOAT_MSG_NORMAL);
      tmp_hostile := 1;
   end
   else begin
      start_dialog_at_node(Nightkin01);
   end
end

procedure Nightkin01 begin
   Reply(102);
   NOption(103, Nightkin01a, 7);
   NOption(104, Nightkin04, 4);
   NOption(105, Nightkin05, 4);
   if (global_var(GVAR_ALCOHOL_ADDICT) == 1) then begin
      NOption(106, Nightkin05, 4);
   end
   if (has_rep_berserker) then begin
      NOption(107, Nightkin04, 4);
   end
   NOption(108, Nightkinend, -3);
end

procedure Nightkin01a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Nightkin02;
   end
   else begin
      call Nightkin03;
   end
end

procedure Nightkin02 begin
   Reply(109);
   gsay_option(NAME, 125, Nightkinend, 50);
end

procedure Nightkin03 begin
   Reply(110);
   call Combat;
end

procedure Nightkin04 begin
   Reply(111);
   call Combat;
end

procedure Nightkin05 begin
   Reply(112);
   NOption(113, Nightkin05a, 7);
   NOption(114, Nightkin04, 4);
   NOption(115, Nightkin07, 4);
   NOption(116, Nightkin07, 4);
   NOption(117, Nightkinend, 4);
   if (has_rep_berserker) then begin
      NOption(118, Nightkin04, 4);
   end
end

procedure Nightkin05a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Nightkin06;
   end
   else begin
      call Nightkin03;
   end
end

procedure Nightkin06 begin
   Reply(119);
   gsay_option(NAME, 125, Nightkinend, 50);
end

procedure Nightkin07 begin
   Reply(120);
   NOption(121, Nightkin04, 4);
   NOption(122, Nightkin04, 4);
   NOption(123, Nightkinend, 4);
   if (has_rep_berserker) then begin
      NOption(124, Combat, 4);
   end
end

procedure Nightkinend begin
end

procedure Combat begin
   set_global_var(GVAR_CATHEDRAL_ENEMY, 1);
   tmp_hostile := 1;
end


procedure damage_p_proc begin
   set_global_var(GVAR_MUTANTS_ENEMY_UNSURE, 1);
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end
