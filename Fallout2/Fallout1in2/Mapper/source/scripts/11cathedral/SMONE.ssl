/*

   Master's Lair - Mutant medic - cut content.

*/

/* Include Files */
#include "define.h"
//#include "maps/v13ent.h"

#define NAME                    SCRIPT_SMONE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure goto00;
procedure goto01;
procedure goto02;
procedure goto03;

procedure zamin;
procedure combat;

#define LVAR_FloatMSG      (1)
#define LVAR_FloatStart    (2)
#define LVAR_LetsGo        (3)

import variable patient;
import variable lets_go;

variable my_tile;
variable tmp_hostile;
variable Only_Once := 1;
variable ARMED;
variable PICK;

#define hex_elevator          (12098)

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
   end
   if not(fo1in2_cut_characters_enabled) then destroy_object(self_obj);
end

procedure timed_event_p_proc begin
   if (tmp_hostile < 1) then begin
      inc_local_var(LVAR_FloatMSG);
      call goto00;
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   my_tile := self_tile;
   if not(combat_is_initialized) then begin
      if (my_tile == hex_elevator) then begin
         move_to(self_obj, 7000, 0);
      end
      else begin
         if (tile_distance(my_tile, dude_tile) < 12) then begin
            if (local_var(LVAR_FloatStart) != 1) then begin
               set_local_var(LVAR_FloatStart, 1);
               add_timer_event(self_obj, game_ticks(1), 0);
            end
         end
      end

      if (self_can_see_dude) then begin
         call zamin;
         if ((ARMED == 1) or not(DISGUISED)) then begin
            call combat;
         end
      end

      if (lets_go == 1) then begin
         self_walk_to_tile(hex_elevator);
      end
   end

   // Failsafe on game reload
   if ((local_var(LVAR_LetsGo) == 1) and (lets_go == 0)) then begin
      lets_go := 1;
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   call zamin;
   if ((ARMED == 1) or not(DISGUISED)) then begin
      call combat;
   end
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure look_at_p_proc begin
   script_overrides;
end

procedure zamin begin
   ARMED := 0;
   if (dude_is_armed) then
      ARMED := 1;
   coc_disguise_check;
end

procedure goto00 begin
   PICK := local_var(LVAR_FloatMSG);
   if (PICK == 1) then begin
      display_msg(mstr(100));
      float_msg(self_obj, message_str(SCRIPT_SMONE, 101), FLOAT_MSG_RED);
   end
   else if (PICK == 2) then
      float_msg(patient, message_str(SCRIPT_SMONE, 102), FLOAT_MSG_YELLOW);
   else if (PICK == 3) then
      float_msg(self_obj, message_str(SCRIPT_SMONE, 103), FLOAT_MSG_RED);
   else if (PICK == 4) then
      call goto01;
   else if (PICK == 5) then
      call goto02;
   else if (PICK == 6) then
      float_msg(patient, message_str(SCRIPT_SMONE, 108), FLOAT_MSG_YELLOW);
   else if (PICK == 7) then
      float_msg(self_obj, message_str(SCRIPT_SMONE, 109), FLOAT_MSG_RED);
   else if (PICK == 8) then
      float_msg(patient, message_str(SCRIPT_SMONE, 110), FLOAT_MSG_YELLOW);
   else if (PICK == 9) then
      call goto03;
   add_timer_event(self_obj, game_ticks(3), 1);
end

procedure goto01 begin
   if (dude_is_male) then
      float_msg(patient, message_str(SCRIPT_SMONE, 104), FLOAT_MSG_YELLOW);
   else
      float_msg(patient, message_str(SCRIPT_SMONE, 105), FLOAT_MSG_YELLOW);
end

procedure goto02 begin
   if (dude_is_male) then
      float_msg(self_obj, message_str(SCRIPT_SMONE, 106), FLOAT_MSG_RED);
   else
      float_msg(self_obj, message_str(SCRIPT_SMONE, 107), FLOAT_MSG_RED);
end

procedure goto03 begin
   set_local_var(LVAR_LetsGo, 1);
   lets_go := 1;
   self_walk_to_tile(hex_elevator);
end
