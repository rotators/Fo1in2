#include "define.h"

#define NAME                    SCRIPT_DOOR

#include "command.h"

procedure start;
procedure use_p_proc;
procedure use_skill_on_p_proc;
procedure description_p_proc;
procedure use_obj_on_p_proc;
procedure map_update_p_proc;
procedure damage_p_proc;

#define LVAR_Gave_Locks_XP    (3)

import variable Red_Door_Ptr;
import variable Laura_Ptr;

procedure start begin
end

procedure use_p_proc begin
   if (source_obj == Laura_Ptr) then begin
      set_local_var(0, 1);
   end
   if (local_var(0) == 0) then begin
      obj_lock(self_obj);
   end
   else begin
      obj_unlock(self_obj);
   end
   if ((local_var(0) == 0) and source_is_dude) then begin
      script_overrides;
      display_msg(mstr(104));//{104}{}{The door is locked.}
   end
end

procedure use_skill_on_p_proc begin
   variable Item := 0;
   if (local_var(0) == 0) then begin
      if (action_being_used == SKILL_LOCKPICK) then begin
         script_overrides;
         Item := roll_vs_skill(source_obj, SKILL_LOCKPICK, -60);
         if (is_success(Item)) then begin
            set_local_var(0, 1);
            display_msg(mstr(100));//{100}{}{You unlock the door.}
            if (local_var(LVAR_Gave_Locks_XP) == 0) then begin
               set_local_var(LVAR_Gave_Locks_XP, 1);
               give_xp(EXP_LOCKPICK_NEG_60);
            end
         end
         else begin
            if (is_critical(Item)) then begin
               jam_lock(self_obj);
               display_msg(mstr(110));//{110}{}{The lock is beyond your ability to pick.}
            end
            else begin
               display_msg(mstr(103));//{103}{}{You were not able to pick the lock.}
            end
         end
      end
   end
   else begin
      display_msg(mstr(109));//{109}{}{The door is already unlocked.}
   end
end

procedure description_p_proc begin
   if (local_var(0) == 0) then begin
      display_msg(mstr(104));//{104}{}{The door is locked.}
   end
end

procedure use_obj_on_p_proc begin
   variable Item := 0;
   variable Skill := 0;
   Item := obj_being_used_with;
   Skill := roll_vs_skill(dude_obj, SKILL_LOCKPICK, -40);

   if (obj_pid(Item) == PID_LOCKPICKS or obj_pid(Item) == PID_EXP_LOCKPICK_SET) then begin
      script_overrides;
      
      if (obj_pid(Item) == PID_EXP_LOCKPICK_SET) then
         Skill := roll_vs_skill(dude_obj, SKILL_LOCKPICK, -30);
         
      if (is_success(Skill)) then begin
         set_local_var(0, 1);
         display_msg(mstr(100));
         if (local_var(LVAR_Gave_Locks_XP) == 0) then begin
            set_local_var(LVAR_Gave_Locks_XP, 1);
            give_xp(EXP_LOCKPICK_NEG_60);
         end
      end
      else begin
         if (is_critical(Skill)) then begin
            rm_obj_from_inven(dude_obj, Item);
            destroy_object(Item);
            jam_lock(self_obj);
            display_msg(mstr(110));//{110}{}{The lock is beyond your ability to pick.}
            display_msg(mstr(101));//{101}{}{You broke your lockpicks!}
         end
         else begin
            display_msg(mstr(103));//{103}{}{You were not able to pick the lock.}
         end
      end
   end
   else if ((obj_pid(Item) == PID_BLACK_COC_BADGE) or (obj_pid(Item) == PID_RED_COC_BADGE)) then begin
      script_overrides;
      set_local_var(0, 1);
      display_msg(mstr(100));//{100}{}{You unlock the door.}
   end
end

procedure map_update_p_proc begin
   Red_Door_Ptr := self_obj;
   if (local_var(0) == 0) then begin
      obj_lock(self_obj);
   end
   else begin
      obj_unlock(self_obj);
   end
end

procedure damage_p_proc begin
   if (weapon_dmg_type(target_obj) == DMG_explosion) then begin
      if not(is_loading_game) then set_self_invisible;
      set_local_var(1, 1);
   end
   else begin
      display_msg(mstr(193));
   end
end
