/*

   ??

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_MADSCI
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure vincent00;
procedure vincent01;
procedure vincent01n;
procedure vincent02;
procedure vincent02n;
procedure vincent03;
procedure vincent04;
procedure vincent05;
procedure vincday;
procedure vincnight;

procedure combat;
procedure check_status;

variable tmp_hostile;
variable Only_Once := 1;
variable DISGUISED;
variable ARMED;
variable again;
variable home_tile;
variable sleep_tile;

procedure start
begin


   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_NORMAL);
      home_tile := self_tile;
      sleep_tile := 22284;
   end
   if (script_action == damage_proc) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
      call damage_p_proc;
   end
   else begin
      if (script_action == look_at_proc) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
         call look_at_p_proc;
      end
      else begin
         if (script_action == pickup_proc) then begin//<---caught stealing! (pickup_p_proc)
            call pickup_p_proc;
         end
         else begin
            if (script_action == talk_proc) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
               call check_status;
               call talk_p_proc;
            end
            else begin
               if (script_action == timed_event_proc) then begin//<-- timed_event_p_proc -- called by ADD_TIMER_EVENT commands. "fixed_param==#" in this procedure is the number of the event in question (i.e. Add_Timer_Event dude,5,1 would be fixed_param 1) -- can also be "timeforwhat"
                  call vincent03;
               end
            end
         end
      end
   end
end



procedure combat
begin
   tmp_hostile := 1;
end

procedure damage_p_proc begin
   if (global_var(GVAR_MUTANTS_ENEMY_UNSURE) == 0) then begin
      set_global_var(GVAR_MUTANTS_ENEMY_UNSURE, 1);
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude) then begin
         call check_status;
         if ((ARMED == 1) or (DISGUISED == 0)) then begin
            call vincent02;
         end
         else begin
            if (local_var(5) == 0) then begin
               call talk_p_proc;
            end
         end
      end
      else begin
         if (day) then begin
            if (local_var(6)) then begin
               set_local_var(6, 0);
               self_walk_to_tile(home_tile);
            end
            else begin
               if (self_tile != home_tile) then begin
                  self_walk_to_tile(home_tile);
               end
            end
         end
         else begin
            if (local_var(6)) then begin
               if (self_tile != sleep_tile) then begin
                  self_walk_to_tile(sleep_tile);
               end
            end
            else begin
               set_local_var(6, 1);
               self_walk_to_tile(sleep_tile);
            end
         end
      end
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   call check_status;
   set_local_var(5, 1);
   if ((ARMED == 1) or (DISGUISED == 0)) then begin
      call vincent02;
   end
   else begin
      if (self_tile == sleep_tile) then begin
         call vincday;
      end
      else begin
         call vincnight;
      end
   end
end

procedure destroy_p_proc begin
   rm_timer_event(self_obj);
   inc_inv_evil_crit
   rm_timer_event(self_obj);
end

procedure look_at_p_proc
begin
   script_overrides;
   if (day) then begin
      display_msg(mstr(400));
   end
   else begin
      display_msg(mstr(200));
   end
end

procedure check_status begin
   DISGUISED := 0;
   ARMED := 0;

   if (dude_is_armed) then begin
      ARMED := 1;
   end
   coc_disguise_check
end

procedure vincday
begin
   again := local_var(4);
   if (again == 0) then begin
      call vincent00;
   end
   else begin
      if (again == 1) then begin
         call vincent01;
      end
      else begin
         if (again > 1) then begin
            call vincent03;
         end
      end
   end
   if (again < 2) then begin
      again := again + 1;
   end
   set_local_var(4, again);
end

procedure vincnight
begin
   again := local_var(4);
   if (again == 0) then begin
      call vincent01n;
   end
   else begin
      if (again > 0) then begin
         call vincent02n;
      end
   end
   if (again < 2) then begin
      again := again + 1;
   end
   set_local_var(4, again);
end

procedure vincent00
begin
   float_msg(self_obj, mstr(402), FLOAT_MSG_RED);
   add_timer_event(self_obj, game_ticks(30), 1);
end

procedure vincent01
begin
   float_msg(self_obj, mstr(403), FLOAT_MSG_RED);
   add_timer_event(self_obj, game_ticks(15), 1);
end

procedure vincent01n
begin
   float_msg(self_obj, mstr(203), FLOAT_MSG_RED);
end

procedure vincent02
begin
   float_msg(self_obj, mstr(202), FLOAT_MSG_RED);
   call combat;
end

procedure vincent02n
begin
   float_msg(self_obj, mstr(204), FLOAT_MSG_RED);
end

procedure vincent03
begin
   if (self_can_see_dude) then begin
      float_msg(self_obj, mstr(404), FLOAT_MSG_RED);
      call combat;
   end
end

procedure vincent04
begin
   float_msg(self_obj, mstr(203), FLOAT_MSG_RED);
end

procedure vincent05
begin
   float_msg(self_obj, mstr(204), FLOAT_MSG_RED);
end
