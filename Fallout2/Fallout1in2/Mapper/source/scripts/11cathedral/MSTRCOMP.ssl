/*

   Masters Vault, computer terminals

*/

/* Include Files */
#include "define.h"
#include "maps/mstrlr12.h"

#define NAME                    SCRIPT_TERM

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure use_skill_on_p_proc;
procedure use_p_proc;
procedure look_at_p_proc;

procedure skills;

variable XP;

procedure start begin
end

procedure use_skill_on_p_proc begin
   script_overrides;
   call skills;
end

procedure use_p_proc begin
   display_msg(mstr(201));
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(200));
end

procedure skills begin
   if (action_being_used == SKILL_SCIENCE) then begin

      if is_success(roll_vs_skill(dude_obj, SKILL_SCIENCE, -50)) then begin
         display_msg(mstr(203));

         if military_base_visit then begin
            XP := EXP_WENT_TO_MILITARY_BASE;
         end
         else if military_base_visible_on_map then begin
            XP := EXP_KNEW_ABOUT_MILITARY_BASE;
            set_global_var(GVAR_WORLDMAPLIST_MILITARYBASE, 1); // Was 2 before !! Causes weird dialog, because the player wasn't actually there yet.
         end
         else begin
            XP := EXP_FOUND_MILITARY_BASE;
            set_global_var(GVAR_WORLDMAPLIST_MILITARYBASE, 1);
         end

         if not(military_base_destroyed) then
            mark_on_map(AREA_MILITARY_BASE)

         if (map_var(MVAR_TERMINAL_EXP) == 0) then begin
            set_map_var(MVAR_TERMINAL_EXP, 1);
            give_xp_msg(XP, mstr(205));
         end
      end
      else begin
         display_msg(mstr(201));
      end

   end
   else if (action_being_used == SKILL_REPAIR) then begin
      display_msg(mstr(202));
   end
end
