/*

	Cathedral - Generic prisoner

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GENPRIS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure explode;
procedure escape;

variable tmp_hostile;
variable Only_Once := 1;
variable temp;
variable target_hex;


// USED - women and child

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_SHADY_SANDS);
		set_self_ai(AI_PEASANT);
	end
end

procedure critter_p_proc
begin
	if (self_tile < 25000) then begin
		call explode;
	end
	else begin
		if ((map_var(2) == 1) or (map_var(7) == 1)) then begin
			call escape;
		end
		else begin
			if (tmp_hostile == 1) then begin
				attack(dude_obj);
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (self_pid == PID_BOY) then begin
		display_msg(message_str(SCRIPT_MLPRISON, random(102, 104)));
	end
	else begin
		temp := random(102, 104);
		if (temp == 104) then begin
			temp := 105;
		end
		display_msg(message_str(SCRIPT_MLPRISON, temp));
	end
end

procedure destroy_p_proc begin
	inc_good_critter
	inc_childkiller
end

procedure look_at_p_proc
begin
	script_overrides;
end

procedure explode
begin
	critter_dmg(self_obj, random(128, 150), DMG_explosion);
end

procedure escape
begin
	target_hex := 22912;
	self_run_to_tile(target_hex);
end
