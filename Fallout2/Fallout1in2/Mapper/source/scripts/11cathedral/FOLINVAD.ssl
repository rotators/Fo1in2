/*

   Followers of the Apocalypse - Cathedral invasion force

*/

/* Include Files */
#include "define.h"
#include "area/cathedral.h"

#define NAME                    SCRIPT_FOLINVAD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure push_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure destroy_p_proc;
procedure timed_event_p_proc;

procedure Initialize_p_proc;

#define LVAR_Add_to_Party     (0)
#define LVAR_FloatVault       (1)
#define LVAR_FloatMaster      (2)
#define LVAR_FloatExit        (3)
#define LVAR_Herebefore       (4)

variable Only_Once := 1;
variable Hex_Number;

procedure start begin
   if Only_Once == 1 then begin
      call Initialize_p_proc;
   end
end

procedure push_p_proc begin
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(3) == 0) then begin
      float_msg(self_obj, mstr(random(100, 107)), FLOAT_MSG_WHITE);
   end
end

procedure Initialize_p_proc begin
   Only_Once := 0;
   set_self_team(TEAM_PLAYER);
   set_self_ai(AI_FOLLOWER_GUARD);// was 65
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   if (global_var(GVAR_FOLLOWER_MACHO_HELP) == 1) then begin

      // Initial walk to the player
      if (local_var(LVAR_Herebefore) == 0) then begin
         if (Hex_Number == 0) then begin
            if not(is_loading_game) then set_self_visible;

            LVar0 := random(1, 10);
            if (LVar0 == 1) then begin
               Hex_Number := 19905;
            end
            else if (LVar0 == 2) then begin
               Hex_Number := 20308;
            end
            else if (LVar0 == 3) then begin
               Hex_Number := 20506;
            end
            else if (LVar0 == 4) then begin
               Hex_Number := 20906;
            end
            else if (LVar0 == 5) then begin
               Hex_Number := 20110;
            end
            else if (LVar0 == 6) then begin
               Hex_Number := 19503;
            end
            else if (LVar0 == 7) then begin
               Hex_Number := 19103;
            end
            else if (LVar0 == 8) then begin
               Hex_Number := 20904;
            end
            else if (LVar0 == 9) then begin
               Hex_Number := 21113;
            end
            else begin
               Hex_Number := 19910;
            end

         end
         if (self_tile != Hex_Number) then begin
            self_walk_to_tile(Hex_Number);
         end
         else if (self_tile == Hex_Number) then begin
            set_local_var(LVAR_Herebefore, 1);
         end
      end
      else begin
         // Keep following the player
         if ((tile_distance_objs(dude_obj, self_obj) > 10) and (tile_distance_objs(dude_obj, self_obj) < 15)) then begin
            self_walk_to_tile(tile_num_in_direction(dude_tile, random(0, 5), random(3, 6)));
         end
         else if (tile_distance_objs(dude_obj, self_obj) >= 15) then begin
            self_run_to_tile(tile_num_in_direction(dude_tile, random(0, 5), random(3, 6)));
         end
         else if (random(1, 100) == 1) then begin
            self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(3, 6)));
         end
      end

      // Commentary floats
      if (local_var(LVAR_Herebefore) == 1) then begin
         if (CUR_MAP_CHILDRN1) or (CUR_MAP_CHILDRN2) then begin
            if (local_var(LVAR_Add_to_Party) == 0) then begin
               set_local_var(LVAR_Add_to_Party, 1);
               party_add(self_obj);
               if (invad_status(FOLINVAD_FLOAT_Entrance) == false) then begin
                  set_invad_status(FOLINVAD_FLOAT_Entrance);
                  float_msg(self_obj, mstr(112), FLOAT_MSG_WHITE);
               end
            end
         end
         else if CUR_MAP_MSTRLR12 then begin
            if (invad_status(FOLINVAD_FLOAT_Vault) == false) then begin
               if (self_elevation == 1) then begin
                  set_invad_status(FOLINVAD_FLOAT_Vault);
                  float_msg(self_obj, mstr(109), FLOAT_MSG_WHITE);
               end
            end
         end
         else if CUR_MAP_MSTRLR34 then begin
            if (invad_status(FOLINVAD_FLOAT_Master) == false) then begin
               set_invad_status(FOLINVAD_FLOAT_Master);
               float_msg(self_obj, mstr(110), FLOAT_MSG_WHITE);
            end
         end
         else begin
            if (invad_status(FOLINVAD_FLOAT_Exit) == false) then begin
               set_invad_status(FOLINVAD_FLOAT_Exit);
               //float_msg(self_obj, mstr(111), FLOAT_MSG_WHITE);
               set_local_var(LVAR_Add_to_Party, 0);
               party_remove(self_obj);
               reg_anim_clear(self_obj);
               set_global_var(GVAR_FOLLOWER_MACHO_HELP, 5);
               self_walk_to_tile(26000);
            end
            // Not necessary
            /*else if not(combat_is_initialized) then begin
               debug("remove FOLINVAD!");
               add_timer_event(self_obj, 10, 1);
            end*/
         end
      end
   end
   else begin
      if not(is_loading_game) then set_self_invisible;
   end
end

procedure map_enter_p_proc begin
   if (global_var(GVAR_FOLLOWER_MACHO_HELP) != 1) then begin
      if not(is_loading_game) then set_self_invisible;
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      inc_good_critter
   end
end

procedure timed_event_p_proc begin
   if not(combat_is_initialized) then begin
      if not(is_loading_game) then begin
         set_self_invisible;
         destroy_object(self_obj);
      end
      if (global_var(GVAR_FOLLOWER_MACHO_HELP) == 5) then begin
         set_global_var(GVAR_FOLLOWER_MACHO_HELP, 6);
         gfade_out(1);
            float_msg_clear(self_obj);
         gfade_in(1);
      end
   end
   else begin
      add_timer_event(self_obj, 10, 1);
   end
end
