/*

   Cathedral Masters Lair - Technician

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_MLOPS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

#define LVAR_Herebefore          (0)

variable tmp_hostile;
variable Only_Once := 1;

#define float(x)                    float_msg(self_obj, mstr(x), FLOAT_MSG_RED)

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end

   if self_can_see_dude then begin
      coc_disguise_check;
      if (tmp_hostile == 0) and Current_Distance_From_Dude < 15 and not(DISGUISED) then begin
         self_look_at_dude;
         float(103);
         tmp_hostile := 1;
      end
   end
end

procedure pickup_p_proc begin
   if obj_in_party(source_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;

   coc_disguise_check;
   if (DISGUISED) then begin
      if (local_var(LVAR_Herebefore) == 1) then begin
         float(102);
      end
      else begin
         float(101);
      end
      set_local_var(LVAR_Herebefore, 1);
   end
   else begin
      float(103);
      call combat;
   end

end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end
