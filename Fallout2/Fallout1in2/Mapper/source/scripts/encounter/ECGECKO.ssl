/*
   Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/* Random Encounter scripts. These will be filled in more once they
   have been designed.
*/

#include "define.h"
#include "sfall/main.h"

#define NAME                    SCRIPT_ECGECKO

#include "command.h"
#include "mapenc.h"

procedure start;
procedure combat_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure map_enter_p_proc;

variable starttile := 0;

#define set_lootable(PID)     set_proto_data(PID, PROTO_CR_FLAGS, get_proto_data(PID, PROTO_CR_FLAGS) bwand bwnot(CFLG_NOSTEAL))
#define set_unlootable(PID)   set_proto_data(PID, PROTO_CR_FLAGS, get_proto_data(PID, PROTO_CR_FLAGS) bwor CFLG_NOSTEAL)

procedure start begin
end

procedure map_enter_p_proc begin
   starttile := self_tile;
   set_self_team(TEAM_RND_GECKO);
   self_rotate(Random(0, 5));

   if map_first_run then begin
      // Add flame attack weapon for Fire Geckos
      if (self_pid == PID_FIRE_GECKO) then begin
         if (self_item_count(PID_FIRE_GECKO_FLAME_WEAPON) == 0) then begin
            Item := create_object(PID_FIRE_GECKO_FLAME_WEAPON, 0, 0);
            add_obj_to_inven(self_obj, Item);
            wield_obj_critter(self_obj, Item);
         end
      end
   end
end

procedure combat_p_proc begin
   if debug_mode and map_is_caravan_escort then begin
      if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
         script_overrides;
         call destroy_p_proc;
         //critter_dmg(self_obj, 999, (DMG_normal_dam BWOR DMG_BYPASS_ARMOR));
         kill_critter(self_obj, ANIM_stand);
      end
   end
end

procedure critter_p_proc begin
   if map_is_caravan_escort then begin
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude) then begin
         if (dude_perk(PERK_animal_friend) and self_pid == PID_GECKO) then begin
            // do nothing for now
         end
         else
            attack(dude_obj);
      end
   end

   if (not anim_busy(self_obj)) then begin
      reg_anim_clear(self_obj);
      reg_anim_begin();
      self_walk_to_tile(tile_num_in_direction(starttile, Random(0, 5), Random(3, 10)));
      reg_anim_end();
   end
end

procedure destroy_p_proc begin
   variable tail := 0;

   if (dude_perk(PERK_gecko_skinning_perk) and not(self_fid == FID_MAFIRE)) then begin // Fire Geckos don't drop skins
       if (self_pid == PID_GECKO) then
            tail := create_object(PID_GECKO_PELT, 0, 0);
       else if (self_pid == PID_GOLDEN_GECKO) then
            tail := create_object(PID_GOLDEN_GECKO_PELT, 0, 0);

      add_obj_to_inven(self_obj, tail);
      set_proto_data(self_pid, PROTO_CR_FLAGS, get_proto_data(self_pid, PROTO_CR_FLAGS) bwand bwnot(CFLG_NOSTEAL));
   end
   else begin
      set_proto_data(self_pid, PROTO_CR_FLAGS, get_proto_data(self_pid, PROTO_CR_FLAGS) bwor CFLG_NOSTEAL);
   end

   if map_is_caravan_escort then
      dec_map_var(MVAR_Hostile_Total);
end
