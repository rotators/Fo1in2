/*

   Encounter - Lance, the traveler

*/

/* Include Files */
#include "define.h"
#include "shadysands.h"

#define NAME                    SCRIPT_LANCE

#include "command.h"
#include "modreact.h"
#include "updatmap.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Lance00;
procedure Lance00a;
procedure Lance01;
procedure Lance02;
procedure Lance03;
procedure Lance04;
procedure Lance04a;
procedure Lance05;
procedure Lance06;
procedure Lance07;
procedure Lance08;
procedure Lance09;
procedure Lance10;
procedure Lance11;
procedure Lance12;
procedure Lance13;
procedure Lance14;
procedure Lance15;
procedure Lance16;
procedure Lance17;
procedure Lance18;
procedure Lance19;
procedure Lance19a;
procedure Lance20;
procedure Lance21;
procedure Lance22;
procedure Lance23;
procedure Lance24;
procedure Lance25;
procedure Lance26;
procedure Lance27;
procedure Lance28;
procedure Lance29;
procedure Lance30;
procedure Lance31;
procedure Lance31a;
procedure Lance32;
procedure Lance33;
procedure Lance34;
procedure Lance35;
procedure Lance36;
procedure Lance37;
procedure Lance38;
procedure Lance39;
procedure Lance40;
procedure Lance41;
procedure Lance42;
procedure Lance43;
procedure Lance44;
procedure Lance45;
procedure Lance46;
procedure Lance47;
procedure Lance48;
procedure Lance49;
procedure Lance50;
procedure Lance51;
procedure Lance52;
procedure LanceCombat;
procedure LanceEnd;
procedure LanceLoot;

variable initial := 0;
variable go_to_Shady;
variable go_to_Raiders;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_SHADY_SANDS);
      set_self_ai(AI_GUARD);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if ((global_var(GVAR_LANCE_STATUS) bwand 1) != 0) then
      display_msg(mstr(101));
   else
      display_msg(mstr(100));
end

procedure critter_p_proc begin
   if ((global_var(GVAR_LANCE_STATUS) bwand 2) != 0) then begin
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 4);
   set_global_var(GVAR_LANCE_DEAD_ALREADY_SEEN, 1);
end

procedure pickup_p_proc begin
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 2);
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   if (shady_invaded) then begin
      if ((global_var(GVAR_LANCE_STATUS) bwand 1) != 0) then
         call Lance52;
      else
         start_dialog_at_node(Lance49);
   end
   else if ((global_var(GVAR_LANCE_STATUS) bwand 1) != 0) then begin
      if (local_var(1) == 1) then
         call Lance02;
      else
         start_dialog_at_node(Lance03);
   end
   else if (global_var(GVAR_ENEMY_SHADY_SANDS) or (check_general_rep < -30) or (has_rep_berserker) or has_rep_childkiller) then begin
      if (tandi_quest_completed and radscorp_quest_completed) then
         start_dialog_at_node(Lance00);
      else
         call Lance01;
   end
   else if (global_var(GVAR_WORLDMAPLIST_SHADY_SANDS) < 2) then begin
      if (local_var(1) >= 2) then
         start_dialog_at_node(Lance04);
      else
         call Lance05;
   end
   else begin
      if (local_var(1) >= 2) then
         start_dialog_at_node(Lance07);
      else
         start_dialog_at_node(Lance06);
    end

   if (go_to_Shady) then begin
      gfade_out(1);
      advance_time_travel_to_shady;
   end
   else if (go_to_Raiders) then begin
      gfade_out(1);
      advance_time_travel_to_raiders;
   end
end

procedure Lance00 begin
   BottomReact
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 1);
   Reply(102);
   BOption(103, Lance00a, 4);
   NOption(104, Lance10, 4);
   NOption(105, Lance11, 4);
   NOption(106, Lance12, 4);
   NLowOption(107, Lance13);
end

procedure Lance00a begin
   if ((dude_strength > 7) or dude_item_count(PID_PLASMA_RIFLE) or dude_item_count(PID_LASER_RIFLE) or dude_item_count(PID_ROCKET_LAUNCHER) or dude_item_count(PID_MINIGUN)) then begin
      call Lance08;
   end
   else begin
      call Lance09;
   end
end

procedure Lance01 begin
   BottomReact
   float_msg(self_obj, mstr(108), FLOAT_MSG_NORMAL);
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 2);
end

procedure Lance02 begin
   float_msg(self_obj, mstr(109), FLOAT_MSG_NORMAL);
end

procedure Lance03 begin
   Reply(110);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(111, Lance14, 4);
   if (tandi_is_kidnapped and tandi_quest_active) then
      NOption(112, Lance15, 4);
   if tandi_quest_completed then
      NOption(113, Lance16, 4);
   NOption(114, Lance17, 4);
   NLowOption(115, Lance18);
end

procedure Lance04 begin
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 1);
   Reply(116);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(111, Lance14, 4);
   NOption(118, Lance04a, 4);
   NOption(119, Lance22, 4);
   NOption(120, Lance23, 4);
   NLowOption(121, Lance24);
end

procedure Lance04a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Lance19;
   end
   else begin
      call Lance20;
   end
end

procedure Lance05 begin
   float_msg(self_obj, mstr(122), FLOAT_MSG_NORMAL);
end

procedure Lance06 begin
   Reply(mstr(123) + dude_name + mstr(124));
   NOption(125, Lance25, 4);
   NOption(113, Lance16, 4);
   NOption(127, Lance26, 4);
   NOption(128, Lance28, 4);
   BLowOption(121, Lance18);
end

procedure Lance07 begin
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 1);
   Reply(mstr(123) + dude_name + mstr(130));
   NOption(131, Lance29, 4);
   NOption(113, Lance16, 4);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(111, Lance14, 4);
   BOption(132, Lance00a, 4);
   NOption(133, Lance30, 4);
   NLowOption(121, Lance18);
end

procedure Lance08 begin
   Reply(134);
   BOption(g_more, LanceLoot, 1);
end

procedure Lance09 begin
   BMessage(135);
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 2);
end

procedure Lance10 begin
   Reply(126);
   BOption(136, Lance00a, 4);
   NOption(137, Lance31, 4);
   NOption(105, Lance11, 4);
   NOption(106, Lance12, 4);
   BOption(138, LanceCombat, 4);
end

procedure Lance11 begin
   BottomReact
   BMessage(139);
end

procedure Lance12 begin
   BMessage(140);
end

procedure Lance13 begin
   NMessage(141);
end

procedure Lance14 begin
   Reply(142);
   NOption(143, Lance14, 4);
   if (tandi_quest_active and tandi_is_kidnapped) then
      NOption(112, Lance15, 4);
   if tandi_quest_completed then
      NOption(113, Lance16, 4);
   NOption(114, Lance17, 4);
   if radscorp_quest_completed then
      NOption(144, Lance32, 4);
end

procedure Lance15 begin
   Reply(145);

   if not(car_in_any_encounter) then begin
      NOption(146, Lance33, 4);
      NOption(147, Lance34, 4);
   end

   NOption(148, Lance35, 4);
   NOption(149, Lance36, 4);
end

procedure Lance16 begin
   Reply(150);
   NOption(151, Lance37, 4);
   NOption(152, Lance38, 4);
   NOption(144, Lance32, 4);
   NOption(153, Lance39, 4);
end

procedure Lance17 begin
   Reply(154);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(111, Lance14, 4);
   if (tandi_quest_active and tandi_is_kidnapped) then
      NOption(112, Lance15, 4);
   if tandi_quest_completed then
      NOption(113, Lance16, 4);

   if not(car_in_any_encounter) then
      NOption(155, Lance29, 4); // Was Lance17 before, but 29 makes way more sense.
   NOption(156, Lance36, 4);
end

procedure Lance18 begin
   BMessage(157);
end

procedure Lance19 begin
   Reply(158);
   NOption(159, Lance19a, 4);
   NOption(160, Lance42, 4);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(161, Lance43, 4);
   NOption(162, Lance48, 4);
end

procedure Lance19a begin
   if (dude_charisma > 6) then begin
      call Lance40;
   end
   else begin
      call Lance41;
   end
end

procedure Lance20 begin
   BottomReact
   BMessage(163);
end

procedure Lance21 begin
   BottomReact
   BMessage(164);
end

procedure Lance22 begin
   if (global_var(GVAR_WORLDMAPLIST_JUNKTOWN) < 1) then
      set_global_var(GVAR_WORLDMAPLIST_JUNKTOWN, 1);
   mark_on_map(AREA_JUNKTOWN)

   NMessage(165);
end

procedure Lance23 begin
   BottomReact
   BMessage(166);
end

procedure Lance24 begin
   BMessage(167);
end

procedure Lance25 begin
   Reply(168);
   NOption(169, Lance44, 4);
   BOption(170, LanceCombat, 4);
   NOption(171, Lance28, 4);
   NOption(172, Lance31, 4);
end

procedure Lance26 begin
   Reply(173);
   NOption(169, Lance44, 4);
   BOption(170, Lance45, 4);
   NOption(171, Lance28, 4);
   NOption(172, Lance31, 4);
end

procedure Lance27 begin
   BottomReact
   Reply(174);
   BOption(g_more, LanceLoot, 1);
end

procedure Lance28 begin
   NMessage(175);
end

procedure Lance29 begin
   Reply(176);
   NOption(177, Lance46, 4);
   NOption(178, Lance47, 4);
end

procedure Lance30 begin
   Reply(179);
   NOption(g_more, Lance29, 1);
end

procedure Lance31 begin
   Reply(180);
   BOption(g_more, Lance31a, 1);
end

procedure Lance31a begin
   NMessage(181);
end

procedure Lance32 begin
   Reply(182);
   if ((global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 1) and (dude_item_count(PID_WATER_CHIP) == 0)) then
      NOption(161, Lance43, 4);
   if (tandi_quest_active and tandi_is_kidnapped) then
      NOption(112, Lance15, 4);
   if tandi_quest_completed then
      NOption(113, Lance16, 4);
   NOption(114, Lance17, 4);
   NOption(183, Lance36, 4);
end

procedure Lance33 begin
   NMessage(184);
   go_to_Shady := 1;
end

procedure Lance34 begin
   NMessage(185);
   go_to_Raiders := 1;
end

procedure Lance35 begin
   NMessage(186);
end

procedure Lance36 begin
   NMessage(187);
end

procedure Lance37 begin
   NMessage(188);
   call LanceLoot;
end

procedure Lance38 begin
   NMessage(189);
end

procedure Lance39 begin
   GMessage(190);
end

procedure Lance40 begin
   NMessage(191);
   go_to_Shady := 1;
end

procedure Lance41 begin
   NMessage(192);
end

procedure Lance42 begin
   NMessage(193);
end

procedure Lance43 begin
   Reply(194);
   NOption(g_more, Lance36, 1);
end

procedure Lance44 begin
   NMessage(195);
end

procedure Lance45 begin
   BottomReact
   BMessage(196);
end

procedure Lance46 begin
   NMessage(197);
   go_to_Shady := 1;
end

procedure Lance47 begin
   NMessage(198);
end

procedure Lance48 begin
   NMessage(199);
end

procedure Lance49 begin
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 1);

   Reply(200);
   NOption(201, Lance50, 4);
   NOption(202, Lance51, 4);
   NOption(203, Lance50, 4);
   NOption(204, Lance51, 4);
   NLowOption(205, Lance51);
end

procedure Lance50 begin
   NMessage(206);
end

procedure Lance51 begin
   NMessage(207);
end

procedure Lance52 begin
   float_msg(self_obj, mstr(208), FLOAT_MSG_NORMAL);
end

procedure LanceCombat begin
   set_global_var(GVAR_LANCE_STATUS, global_var(GVAR_LANCE_STATUS) bwor 2);
end

procedure LanceEnd begin
end

procedure LanceLoot begin
   inven_unwield(self_obj);
   move_obj_inven_to_obj(self_obj, dude_obj);
end
