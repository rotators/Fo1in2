/*

   Encounter - Gecko hunter, War Party / Raider

*/

#include "define.h"

#define NAME                    SCRIPT_ECHUNTRC

#include "command.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

variable tmp_hostile;
variable initial := 0;
variable scared;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_RND_GECKO);
      set_self_ai(20 + random(0, 1));
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      scared := 1;
      attack(dude_obj);
   end
   else begin
      if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
         if (scared) then begin
            call flee_dude;
         end
         else begin
            tmp_hostile := 1;
         end
      end
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_GECKO_HUNTER_WARPARTY);
   inc_good_critter
end

procedure pickup_p_proc begin
   if (not(scared)) then begin
      tmp_hostile := 1;
   end
end
