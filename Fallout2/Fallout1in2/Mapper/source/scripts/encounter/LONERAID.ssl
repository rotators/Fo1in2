/*

   Encounter - Lone Raider

*/

#include "define.h"
#include "shadysands.h"

#define NAME                    SCRIPT_LONERAID
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure LoneRaid00;
procedure LoneRaid01;
procedure LoneRaid02;
procedure LoneRaid03;
procedure LoneRaid03a;
procedure LoneRaid04;
procedure LoneRaid04a;
procedure LoneRaid05;
procedure LoneRaid06;
procedure LoneRaid07;
procedure LoneRaid08;
procedure LoneRaid09;
procedure LoneRaid10;
procedure LoneRaid11;
procedure LoneRaid12;
procedure LoneRaid13;
procedure LoneRaid14;
procedure LoneRaid15;
procedure LoneRaid16;
procedure LoneRaid17;
procedure LoneRaid18;
procedure LoneRaid19;
procedure LoneRaid20;
procedure LoneRaid21;
procedure LoneRaid22;
procedure LoneRaidCombat;
procedure LoneRaidEnd;
procedure LoneRaidLoot;

variable tmp_hostile;
variable initial := 0;
variable known;
variable broken;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_GENERIC_RAIDER);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if ((tile_distance_objs(self_obj, dude_obj) > 8) and (tile_distance_objs(self_obj, dude_obj) < 24)) then begin
         call flee_dude;
      end
   end
end

procedure destroy_p_proc begin
   inc_evil_critter
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc
begin
   dude_look_at_self;
   if (broken) then begin
      call LoneRaid01;
   end
   else begin

      if (dude_is_armed) then begin
         call LoneRaid00;
      end
      else begin
         if (known) then begin
            call LoneRaid02;
         end
         else begin
            start_gdialog(700, self_obj, 4, -1, -1);
            gsay_start;
            known := 1;
            if (shady_invaded) then begin//GLOBAL_VAR_12 = Shady Sands invaded
               call LoneRaid06;
            end
            else begin
               if (get_tandi_returned or global_var(GVAR_ENEMY_RAIDERS)) then begin
                  call LoneRaid05;
               end
               else begin
                  if (global_var(GVAR_GARLS_FRIEND)) then begin
                     call LoneRaid04;
                  end
                  else begin
                     call LoneRaid03;
                  end
               end
            end
            gsay_end;
            end_dialogue;
         end
      end
   end
end

procedure LoneRaid00
begin
   float_msg(self_obj, message_str(SCRIPT_LONERAID, 100), FLOAT_MSG_NORMAL);
end

procedure LoneRaid01
begin
   float_msg(self_obj, message_str(SCRIPT_LONERAID, 101), FLOAT_MSG_NORMAL);
end

procedure LoneRaid02
begin
   float_msg(self_obj, message_str(SCRIPT_LONERAID, 102), FLOAT_MSG_NORMAL);
end

procedure LoneRaid03
begin
   gsay_reply(SCRIPT_LONERAID, 103);
   NOption(104, LoneRaid07, 7);
   BOption(105, LoneRaid03a, 4);
   NOption(106, LoneRaid09, 4);
   BOption(107, LoneRaid10, 4);
   BLowOption(108, LoneRaid11);
   NLowOption(109, LoneRaid12);
end

procedure LoneRaid03a
begin
   if ((dude_strength + dude_endurance) > 13) then begin
      call LoneRaid08;
   end
   else begin
      call LoneRaidCombat;
   end
end

procedure LoneRaid04
begin
   gsay_reply(SCRIPT_LONERAID, 110 + (dude_is_female));
   GOption(112, LoneRaid13, 7);
   NOption(113, LoneRaid04a, 4);
   NOption(114, LoneRaid16, 4);
   NOption(115, LoneRaid17, 4);
   NLowOption(109, LoneRaid12);
end

procedure LoneRaid04a
begin
   if (dude_luck < 5) then begin
      call LoneRaid14;
   end
   else begin
      call LoneRaid15;
   end
end

procedure LoneRaid05
begin
   gsay_reply(SCRIPT_LONERAID, 116);
   if (tandi_quest_active and tandi_is_kidnapped) then begin
      NOption(117, LoneRaid18, 7);
   end
   BOption(118, LoneRaid03a, 4);
   NOption(119, LoneRaid09, 4);
   BOption(120, LoneRaid10, 4);
   BLowOption(108, LoneRaid11);
   NLowOption(109, LoneRaid12);
end

procedure LoneRaid06
begin
   gsay_reply(SCRIPT_LONERAID, 121);
   NOption(122, LoneRaid18, 7);
   BOption(123, LoneRaid19, 4);
   NOption(124, LoneRaid20, 4);
   NOption(125, LoneRaid21, 4);
   NLowOption(109, LoneRaid12);
end

procedure LoneRaid07
begin
   gsay_reply(SCRIPT_LONERAID, 126);
   NOption(127, LoneRaid03a, 4);
   NOption(128, LoneRaid09, 4);
   BOption(129, LoneRaid22, 4);
end

procedure LoneRaid08
begin
   broken := 1;
   gsay_reply(SCRIPT_LONERAID, 130);
   NOption(g_more, LoneRaidLoot, 1);
end

procedure LoneRaid09
begin
   gsay_message(SCRIPT_LONERAID, 131, NEUTRAL_REACTION);
end

procedure LoneRaid10
begin
   gsay_message(SCRIPT_LONERAID, 132, BAD_REACTION);
end

procedure LoneRaid11
begin
   gsay_message(SCRIPT_LONERAID, 133, BAD_REACTION);
end

procedure LoneRaid12
begin
   gsay_message(SCRIPT_LONERAID, 135, NEUTRAL_REACTION);
end

procedure LoneRaid13
begin
   gsay_message(SCRIPT_LONERAID, 135 + (dude_is_female), GOOD_REACTION);
end

procedure LoneRaid14
begin
   gsay_message(SCRIPT_LONERAID, 137, BAD_REACTION);
   tmp_hostile := 1;
end

procedure LoneRaid15
begin
   gsay_message(SCRIPT_LONERAID, 138 + (dude_is_female), NEUTRAL_REACTION);
end

procedure LoneRaid16
begin
   gsay_message(SCRIPT_LONERAID, 140, NEUTRAL_REACTION);
end

procedure LoneRaid17
begin
   gsay_message(SCRIPT_LONERAID, 141 + (dude_is_female), GOOD_REACTION);
end

procedure LoneRaid18
begin
   gsay_message(SCRIPT_LONERAID, 143, NEUTRAL_REACTION);
end

procedure LoneRaid19
begin
   gsay_message(SCRIPT_LONERAID, 144, BAD_REACTION);
   tmp_hostile := 1;
end

procedure LoneRaid20
begin
   gsay_message(SCRIPT_LONERAID, 145, NEUTRAL_REACTION);
end

procedure LoneRaid21
begin
   gsay_message(SCRIPT_LONERAID, 146, GOOD_REACTION);
end

procedure LoneRaid22
begin
   gsay_message(SCRIPT_LONERAID, 147, BAD_REACTION);
   tmp_hostile := 1;
end

procedure LoneRaidCombat
begin
   tmp_hostile := 1;
end

procedure LoneRaidEnd
begin
end

procedure LoneRaidLoot
begin
   gdialog_mod_barter(0);
   giq_option(0, SCRIPT_MODREACT, 103, LoneRaidEnd, BAD_REACTION);
end
