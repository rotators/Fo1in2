/*

   Caravan escort job - Caravan Leader

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "mapenc.h"

#define NAME                    SCRIPT_CARVLEAD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "modreact.h"
#include "caravan.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure map_update_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure CarvLead00;
procedure CarvLead01;
procedure CarvLead02;
procedure CarvLead03;
procedure CarvLead04;
procedure CarvLead05;
procedure CarvLead06;
procedure CarvLead07;
procedure CarvLead09;
procedure CarvLead10;
procedure CarvLead11;
procedure CarvLead12;

procedure Abandoned;
procedure Lost;
procedure HubAbandoned;
procedure HubLost;
procedure CarvLead13;
procedure CarvLead14;
procedure create_caravan;
procedure remove_caravan;
procedure Junktown_Caravan;
procedure BOS_Caravan;
procedure Necropolis_Caravan;
procedure LABone_Caravan;

/* LVAR Defines */
#define LVAR_CaravanTimer        (6)
#define LVAR_CaravanStatus       (7)

#define LVAR_UnloadingReward     (9)

/* Defines */
#define reset_caravan_status     set_global_var(GVAR_CRIMSON_CARAVANS_STATUS, 0);   \
                                 set_global_var(GVAR_WATER_MERCHANTS_STATUS, 0);    \
                                 set_global_var(GVAR_FARGO_TRADERS_STATUS, 0)       \

#define crimson_arrived          (global_var(GVAR_CRIMSON_CARAVANS_STATUS) == CARAVAN_ARRIVED)
#define fargo_arrived            (global_var(GVAR_FARGO_TRADERS_STATUS) == CARAVAN_ARRIVED)
#define waterm_arrived           (global_var(GVAR_WATER_MERCHANTS_STATUS) == CARAVAN_ARRIVED)

#define crimson_abandoned        (global_var(GVAR_CRIMSON_CARAVANS_STATUS) == CARAVAN_ABANDONED)
#define fargo_abandoned          (global_var(GVAR_FARGO_TRADERS_STATUS) == CARAVAN_ABANDONED)
#define waterm_abandoned         (global_var(GVAR_WATER_MERCHANTS_STATUS) == CARAVAN_ABANDONED)

#define crimson_lost             (global_var(GVAR_CRIMSON_CARAVANS_STATUS) == CARAVAN_LOST)
#define fargo_lost               (global_var(GVAR_FARGO_TRADERS_STATUS) == CARAVAN_LOST)
#define waterm_lost              (global_var(GVAR_WATER_MERCHANTS_STATUS) == CARAVAN_LOST)

#define CARAVAN_ON_JOB           (1) // TODO: in Fo2 defines this is CARAVAN_STATUS_ON_JOB
#define CARAVAN_ARRIVED          (2) // TODO: in Fo2 defines this is CARAVAN_STATUS_SUCCESS
#define CARAVAN_ABANDONED        (3) // TODO: in Fo2 defines this is CARAVAN_STATUS_ABANDONED
// #define CARAVAN_???           (4)
#define CARAVAN_LOST             (5) // TODO: in Fo2 defines this is CARAVAN_STATUS_TOTAL_FAILED

#define car1_junktown_hex        (30707)
#define car2_junktown_hex        (30502)

#define car1_bos_hex             (25111)
#define car2_bos_hex             (25911)

#define car1_necropolis_hex      (26894)
#define car2_necropolis_hex      (26889)

#define car1_boneyard_hex        (9723)
#define car2_boneyard_hex        (9121)

#define create_crvn_master(hex)                                                              \
   if (caravan_masters_left >= 1) then begin                                                 \
      if caravan_style_crimson then                                                          \
         LVar0 := create_object_sid(PID_MERCENARY_HMLTHR, hex, all_elev, SCRIPT_CRVNDRVR);   \
      else if caravan_style_fargo then                                                       \
         LVar0 := create_object_sid(PID_MERCENARY_NMLTHR, hex, all_elev, SCRIPT_CRVNDRVR);   \
      else if caravan_style_water then                                                       \
         LVar0 := create_object_sid(PID_MERC_MALE_LEATHER, hex, all_elev, SCRIPT_CRVNDRVR);  \
      set_team(LVar0, TEAM_HUB_CARTS_GUARD);                                                 \
      obj_rotate(LVar0, all_rotation);                                                       \
      Item := create_object(PID_SHOTGUN, 0, 0);                                              \
      add_obj_to_inven(LVar0, Item);                                                         \
      wield_obj_critter(LVar0, Item);                                                        \
   end

#define create_crvn_guard(hex)                                                               \
   if (caravan_guards_left >= 2) then begin                                                  \
      if caravan_style_crimson then                                                          \
         LVar1 := create_object_sid(PID_MERCENARY, hex, all_elev, SCRIPT_CRVNTEAM);          \
      else if caravan_style_fargo then                                                       \
         LVar1 := create_object_sid(PID_MERCENARY_HMLTHR, hex, all_elev, SCRIPT_CRVNTEAM);   \
      else if caravan_style_water then                                                       \
         LVar1 := create_object_sid(PID_MERC_MALE_METAL, hex, all_elev, SCRIPT_CRVNTEAM);    \
      set_team(LVar1, TEAM_HUB_CARTS_GUARD);                                                 \
      obj_rotate(LVar1, all_rotation);                                                       \
      Item := create_object(PID_DESERT_EAGLE, 0, 0);                                         \
      add_obj_to_inven(LVar1, Item);                                                         \
      wield_obj_critter(LVar1, Item);                                                        \
   end

#define create_crvn_ns_carts(hex_1, hex_2)                                    \
   if (caravan_carts_left >= 2) then Create_NS_Red_Caravan(hex_1, all_elev)   \
   if (caravan_carts_left >= 1) then Create_NS_Grey_Caravan(hex_2, all_elev)

#define create_crvn_ew_carts(hex_1, hex_2)                                    \
   if (caravan_carts_left >= 2) then Create_EW_Red_Caravan(hex_1, all_elev)   \
   if (caravan_carts_left >= 1) then Create_EW_Grey_Caravan(hex_2, all_elev)

// TODO: Timer event is useless????
#define create_crvn_brahmin(hex_1, hex_2)                                         \
   if (caravan_carts_left >= 2) then begin                                       \
      LVar1 := create_object_sid(PID_BRAHMIN, hex_1, all_elev, SCRIPT_CARCOW);   \
      obj_rotate(LVar1, all_rotation);                                           \
      add_timer_event(LVar1, 0, 10);                                             \
   end                                                                           \
   if (caravan_carts_left >= 1) then begin                                       \
      LVar1 := create_object_sid(PID_BRAHMIN, hex_2, all_elev, SCRIPT_CARCOW);   \
      obj_rotate(LVar1, all_rotation);                                           \
      add_timer_event(LVar1, 0, 10);                                             \
   end

#define create_crvn_customer(pid, hex)                      \
   LVar0 := tile_num_in_direction(hex, random(0, 5), 3);    \
   LVar1 := create_object_sid(pid, 0, 0, SCRIPT_CARCUST);   \
   critter_attempt_placement(LVar1, LVar0, all_elev);       \
   obj_rotate(LVar1, random(0, 5))

variable tmp_hostile;
variable Only_Once := 1;
variable PaymentAmount;
variable FullPayment;
variable WaitForTwoHours;
variable spawnDialogue;
variable beenPaid := 1;
variable UnloadCaravan;

procedure start begin
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (self_can_see_dude and tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   if (spawnDialogue == 1) then begin
      spawnDialogue := 0;
      set_local_var(LVAR_CaravanTimer, GAME_TIME_IN_HOURS);
      dialogue_system_enter;
   end
   else begin
      if (local_var(5) == 1) then begin
         if ((WaitForTwoHours == 1) and (((GAME_TIME_IN_HOURS) - local_var(LVAR_CaravanTimer)) >= 2)) then begin
            WaitForTwoHours := 0;
            gfade_out(1);
               set_local_var(5, 0);
               set_local_var(4, 0);
               if not(is_loading_game) then set_self_invisible;
               if (cur_map_index != MAP_HUBENT) then
                  call remove_caravan;
            gfade_in(1);
            display_msg(mstr(151));
         end
      end
      else begin
         if not(is_loading_game) then set_self_invisible;
      end
   end
end

procedure map_update_p_proc begin
end

procedure map_enter_p_proc begin
   variable LVar0 := 0;
   if ((CUR_MAP_LAADYTUM) and boneyard_invaded or ((CUR_MAP_BROHDENT) and bos_invaded) or ((CUR_MAP_HOTEL) and necropolis_invaded) or ((CUR_MAP_JUNKENT) and junktown_invaded) or ((CUR_MAP_HUBENT) and hub_invaded)) then begin //  L.A. BONEYARD ENTRANCE (ADYTUM), BROHERHOOD ENTRANCE, NECROPOLIS HOTEL, JUNKTOWN ENTRANCE, THE HUB ENTRANCE
      LVar0 := 1;
   end

   set_self_team(TEAM_HUB_CARTS_GUARD);
   set_self_ai(AI_MERCHANT);

   if (((GAME_TIME_IN_HOURS) - local_var(LVAR_CaravanTimer)) >= 2) then begin
      WaitForTwoHours := 0;
      if not(is_loading_game) then set_self_invisible;
      set_local_var(5, 0);
      set_local_var(4, 0);
      if (cur_map_index != MAP_HUBENT) then
         call remove_caravan;
   end

   if ((local_var(5) == 1) and (LVar0 == 0)) then begin
      if (((GAME_TIME_IN_HOURS) - local_var(LVAR_CaravanTimer)) < 2) then begin
         WaitForTwoHours := 1;
         if not(is_loading_game) then set_self_visible;
      end
      else begin
         WaitForTwoHours := 0;
         if not(is_loading_game) then set_self_invisible;
         set_local_var(5, 0);
         set_local_var(4, 0);
         if (cur_map_index != MAP_HUBENT) then
            call remove_caravan;
      end
   end
   else if ((LVar0 == 0) and (crimson_arrived or crimson_abandoned) or (waterm_arrived or waterm_abandoned) or (fargo_arrived or fargo_abandoned)) then begin
      // Was a Water Merchants job:
      if waterm_arrived then begin
         if not(is_loading_game) then set_self_visible;
         PaymentAmount := 200;
         set_local_var(LVAR_UnloadingReward, 10);
         FullPayment := 200;

         if (caravan_masters_killed > 0) then
            PaymentAmount := PaymentAmount - 50;
         if (caravan_guards_killed > 0) then
            PaymentAmount := PaymentAmount - ((50 / caravan_total_guards) * caravan_guards_killed);
         if (caravan_carts_lost > 0) then
            PaymentAmount := PaymentAmount - ((PaymentAmount / caravan_total_carts) * caravan_carts_lost);
         if (caravan_carts_left == 0) or (PaymentAmount < 0) then
            PaymentAmount := 0;

         set_local_var(LVAR_CaravanStatus, 0);
         set_global_var(GVAR_WATER_JOB, 4);
         reset_caravan_status;
      end

      // Was a Far-Go Traders job:
      else if fargo_arrived then begin
         if not(is_loading_game) then set_self_visible;
         PaymentAmount := 400;
         set_local_var(LVAR_UnloadingReward, 20);
         FullPayment := 400;

         if (caravan_masters_killed > 0) then
            PaymentAmount := PaymentAmount - 75;
         if (caravan_guards_killed > 0) then
            PaymentAmount := PaymentAmount - 25;
         else
            PaymentAmount := PaymentAmount - ((100 / caravan_total_guards) * caravan_guards_killed);
         if (caravan_carts_lost > 0) then
            PaymentAmount := PaymentAmount - ((PaymentAmount / caravan_total_carts) * caravan_carts_lost);
         if (caravan_carts_left == 0) or (PaymentAmount < 0) then
            PaymentAmount := 0;

         set_local_var(LVAR_CaravanStatus, 2);
         set_global_var(GVAR_FARGO_JOB, 4);
         reset_caravan_status;
      end

      // Was a Crimson Caravans job:
      else if crimson_arrived then begin
         if not(is_loading_game) then set_self_visible;
         PaymentAmount := 600;
         set_local_var(LVAR_UnloadingReward, 30);
         FullPayment := 600;

         if (caravan_masters_killed > 0) then
            PaymentAmount := PaymentAmount - 100;
         if (caravan_guards_killed > 0) then
            PaymentAmount := PaymentAmount - ((100 / caravan_total_guards) * caravan_guards_killed);
         if (caravan_brahmin_killed > 0) then
            PaymentAmount := PaymentAmount - ((PaymentAmount / caravan_total_brahmin) * caravan_brahmin_killed);
         if (caravan_carts_left == 0) or (PaymentAmount < 0) then
            PaymentAmount := 0;

         set_local_var(LVAR_CaravanStatus, 1);
         set_global_var(GVAR_CC_JOB, 4);
         reset_caravan_status;
      end

      // Caravan lost???
      else if waterm_abandoned then begin
         if not(is_loading_game) then set_self_visible;
         PaymentAmount := 0;
         set_local_var(LVAR_CaravanStatus, 3);
         set_global_var(GVAR_WATER_JOB, 3);
         reset_caravan_status;
      end
      else if fargo_abandoned then begin
         if not(is_loading_game) then set_self_visible;
         PaymentAmount := 0;
         set_local_var(LVAR_CaravanStatus, 3);
         set_global_var(GVAR_FARGO_JOB, 3);
         reset_caravan_status;
      end
      else if crimson_abandoned then begin
         if not(is_loading_game) then set_self_visible;
         set_local_var(LVAR_CaravanStatus, 3);
         set_global_var(GVAR_CC_JOB, 3);
         reset_caravan_status;
      end

      set_local_var(5, 1);
      set_local_var(4, 0);

      WaitForTwoHours := 1;
      spawnDialogue := 1;
      beenPaid := 0;
      if (cur_map_index != MAP_HUBENT) then
         call create_caravan;
   end
   else if ((LVar0 == 0) and crimson_lost or waterm_lost or fargo_lost) then begin
      if crimson_lost then
         set_global_var(GVAR_CC_JOB, 5);
      else if waterm_lost then
         set_global_var(GVAR_WATER_JOB, 5);
      else if fargo_lost then
         set_global_var(GVAR_FARGO_JOB, 5);

      reset_caravan_status;

      if not(is_loading_game) then set_self_visible;
      set_local_var(LVAR_CaravanStatus, 5);
      PaymentAmount := 0;

      set_local_var(5, 1);
      set_local_var(4, 0);

      WaitForTwoHours := 0;
      spawnDialogue := 1;
      beenPaid := 0;
      if (cur_map_index != MAP_HUBENT) then
         call remove_caravan;
   end
   else begin
      WaitForTwoHours := 0;
      if not(is_loading_game) then set_self_invisible;
      set_local_var(5, 0);
      set_local_var(4, 0);
      if (cur_map_index != MAP_HUBENT) then
         call remove_caravan;
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction

   if (local_var(LVAR_CaravanStatus) == 4) then begin
      float_msg(self_obj, mstr(random(143, 145)), FLOAT_MSG_RED);
   end
   else if (CUR_MAP_HUBENT) then begin
      if (local_var(LVAR_CaravanStatus) == CARAVAN_ABANDONED) then
         start_dialog_at_node(HubAbandoned);
      else if (local_var(LVAR_CaravanStatus) == CARAVAN_LOST) then
         start_dialog_at_node(HubLost);
      else
         start_dialog_at_node(CarvLead09);

      gfade_out(1);
      set_local_var(5, 0);
      set_local_var(4, 0);
      if not(is_loading_game) then set_self_invisible;
      WaitForTwoHours := 0;
      gfade_in(1);
   end
   else if (local_var(LVAR_CaravanStatus) == CARAVAN_LOST) then begin
      start_dialog_at_node(Lost);
      gfade_out(1);
      set_local_var(5, 0);
      set_local_var(4, 0);
      WaitForTwoHours := 0;
      if not(is_loading_game) then set_self_invisible;
      gfade_in(1);
   end
   else if (local_var(4) == 0) then begin
      set_local_var(4, 1);
      if (local_var(LVAR_CaravanStatus) == CARAVAN_ABANDONED) then begin
         set_local_var(LVAR_CaravanStatus, 4);
         start_dialog_at_node(Abandoned);
      end
      else
         start_dialog_at_node(CarvLead00);
   end
   else
      start_dialog_at_node(CarvLead07);

   if (UnloadCaravan == 1) then begin
      UnloadCaravan := 0;
      game_time_advance(60 * (60 * 10));
      dude_caps_adjust(local_var(LVAR_UnloadingReward));
      display_msg(mstr(520));
      display_msg(mstr(521) + local_var(LVAR_UnloadingReward) + mstr(522));
      set_local_var(LVAR_UnloadingReward, 0);
   end
end

procedure destroy_p_proc begin
   if (cur_map_index != MAP_HUBENT) then
      call remove_caravan;
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc begin
   if (CUR_MAP_HUBENT) then begin
      if source_is_dude then
         set_global_var(GVAR_ENEMY_HUB, 1);
   end
end

/*
   Player and caravan have arrived at the target location
*/
procedure CarvLead00 begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   LVar0 := PaymentAmount;
   dude_caps_adjust(PaymentAmount);

   if ((caravan_masters_killed > 0) or (caravan_drivers_killed > 0) or (caravan_guards_killed > 0) or (caravan_carts_lost > 0)) then
      Reply(mstr(600) + LVar0 + mstr(601) + FullPayment + mstr(103));
   else
      Reply(mstr(101) + LVar0 + mstr(102) + FullPayment + mstr(103));

   beenPaid := 1;
   NLowOption(104, CarvLead01);
   NOption(105, CarvLead03, 4);
   if (CUR_MAP_JUNKENT) then
      NOption(106, CarvLead04, 4);
   else if (CUR_MAP_LAADYTUM) then
      NOption(107, CarvLead05, 4);
   else if (CUR_MAP_BROHDENT) then
      NOption(108, CarvLead06, 4);
   if (local_var(LVAR_UnloadingReward) != 0) then begin
      NOption(506, CarvLead13, 4);
      NLowOption(507, CarvLead13);
   end
end

procedure CarvLead01 begin
   Reply(109);
   NLowOption(110, CarvLead11);
   NLowOption(111, CarvLead02);
   NLowOption(112, CarvLead12);
end

procedure CarvLead02 begin
   NMessage(113);
end

procedure CarvLead03 begin
   Reply(114);
   NOption(115, CarvLead11, 4);
   NOption(116, CarvLead12, 4);
   if (CUR_MAP_JUNKENT) then
      NOption(117, CarvLead04, 4);
   else if (CUR_MAP_LAADYTUM) then
      NOption(118, CarvLead05, 4);
   else if (CUR_MAP_BROHDENT) then
      NOption(119, CarvLead06, 4);
end

procedure CarvLead04 begin
   Reply(120);
   NOption(121, CarvLead03, 4);
   NOption(122, CarvLead11, 4);
end

procedure CarvLead05 begin
   Reply(123);
   NOption(125, CarvLead03, 4);
   NOption(126, CarvLead11, 4);
end

procedure CarvLead06 begin
   Reply(127);
   NOption(128, CarvLead03, 4);
   NOption(129, CarvLead11, 4);
end

procedure CarvLead07 begin
   variable LVar0 := 0;
   if (beenPaid == 0) then begin
      dude_caps_adjust(PaymentAmount);
      //
      // "CONTACT SDUIBEK IF YOU SEE THIS MESSAGE" fixme todo debug testing
      //
      display_msg(gen_mstr(401));
      display_msg(gen_mstr(401));
      display_msg(gen_mstr(401));
      display_msg(gen_mstr(401));
      display_msg(gen_mstr(401));
      beenPaid := 1;
   end
   Reply(130);
   if (local_var(LVAR_UnloadingReward) != 0) then begin
      NOption(506, CarvLead13, 4);
      NLowOption(507, CarvLead13);
   end
   NLowOption(131, CarvLead12);
   NLowOption(111, CarvLead11);
   NOption(132, CarvLead11, 4);
   NOption(133, CarvLead12, 4);
end

/*********************************************************
   Player and caravan have returned to the Hub
*********************************************************/
procedure CarvLead09 begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   LVar0 := PaymentAmount;

   if (PaymentAmount > 0) then begin
      dude_caps_adjust(PaymentAmount);
      if ((caravan_masters_killed > 0) or (caravan_drivers_killed > 0) or (caravan_guards_killed > 0) or (caravan_carts_lost > 0)) then
         Reply(mstr(600) + LVar0 + mstr(603) + mstr(602));
      else
         Reply(mstr(137) + LVar0 + mstr(138));
   end
   else
      Reply(mstr(604) + mstr(602));

   beenPaid := 1;
   NLowOption(139, CarvLead11);
   NOption(140, CarvLead11, 4);
end

procedure CarvLead10 begin
   variable LVar0 := 0;
   LVar0 := random(1, 2);
   if (LVar0 == 1) then
      float_msg(self_obj, mstr(141), FLOAT_MSG_RED);
   else
      float_msg(self_obj, mstr(142), FLOAT_MSG_RED);
end

procedure CarvLead11 begin
end

/*********************************************************
   Traveling back to the Hub
*********************************************************/
procedure CarvLead12 begin
   variable LVar0 := 0;
   variable Caravan_Carts;
   variable Caravan_Encounters;
   variable Caravan_Guards;

   set_caravan_status(CARAVAN_STATUS_ON_JOB);
   //Caravan_Encounters:=random(1,4);
   Caravan_Encounters := 1;
   set_caravan_encounters(Caravan_Encounters);
   set_caravan_end(CARAVAN_HUB);
   set_caravan_status(CARAVAN_STATUS_ON_JOB);

   set_caravan_masters(1);
   set_caravan_drivers(0); // Fo1 has no wagon drivers
   set_caravan_guards(2);
   set_caravan_carts(2);
   set_caravan_brahmin(caravan_total_carts);

   set_local_var(5, 0);
   set_local_var(4, 0);

   call remove_caravan;
   // Water Merchants:
   if (local_var(LVAR_CaravanStatus) == 0) then begin
      set_global_var(GVAR_WATER_JOB, CARAVAN_ON_JOB);
      set_global_var(GVAR_WATER_MERCHANTS_STATUS, CARAVAN_ON_JOB);
      set_global_var(GVAR_WATER_CARAVAN, HUB_CARAVAN_ON_CARAVAN);

      LVar0 := do_check(dude_obj, STAT_lu, -2);
      if (CUR_MAP_JUNKENT) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_JUNKTOWN);
         if (is_success(LVar0)) then begin
            set_global_var(GVAR_WATER_MERCHANTS_STATUS, CARAVAN_ARRIVED);
            game_time_advance(TRAVEL_TIME_HUB_JNK + (random(60, 600)*ONE_GAME_MINUTE));
            load_map(MAP_HUBENT, 1);
         end
         else
            load_map(MAP_DESCRVN4, 1);
      end
      else if (CUR_MAP_LAADYTUM) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_BONEYARD);
         if (is_success(LVar0)) then begin
            set_global_var(GVAR_WATER_MERCHANTS_STATUS, CARAVAN_ARRIVED);
            game_time_advance(TRAVEL_TIME_HUB_ADY + (random(60, 600)*ONE_GAME_MINUTE));
            load_map(MAP_HUBENT, 1);
         end
         else
            load_map(MAP_MNTCRVN4, 1);
      end
   end
   // Crimson Caravan:
   else if (local_var(LVAR_CaravanStatus) == 1) then begin
      set_global_var(GVAR_CC_JOB, CARAVAN_ON_JOB);
      set_global_var(GVAR_CRIMSON_CARAVANS_STATUS, CARAVAN_ON_JOB);
      set_global_var(GVAR_CRIMSON_CARAVAN, HUB_CARAVAN_ON_CARAVAN);

      if (CUR_MAP_BROHDENT) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_BOS);
         load_map(MAP_DESCRVN3, 1);
      end
      else if (CUR_MAP_JUNKENT) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_JUNKTOWN);
         load_map(MAP_DESCRVN4, 1);
      end
      else if (CUR_MAP_HOTEL) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_NECROPOLIS);
         load_map(MAP_MNTCRVN4, 1);
      end
      else if (CUR_MAP_LAADYTUM) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_BONEYARD);
         load_map(MAP_MNTCRVN3, 1);
      end
   end
   // Fargo Traders:
   else if (local_var(LVAR_CaravanStatus) == 2) then begin
      set_global_var(GVAR_FARGO_JOB, CARAVAN_ON_JOB);
      set_global_var(GVAR_FARGO_TRADERS_STATUS, CARAVAN_ON_JOB);
      set_global_var(GVAR_FARGO_CARAVAN, HUB_CARAVAN_ON_CARAVAN);

      LVar0 := do_check(dude_obj, STAT_lu, -4);

      if (CUR_MAP_JUNKENT) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_JUNKTOWN);
         if (is_success(LVar0)) then begin
            set_global_var(GVAR_FARGO_TRADERS_STATUS, CARAVAN_ARRIVED);
            game_time_advance(TRAVEL_TIME_HUB_JNK + (random(60, 600)*ONE_GAME_MINUTE));
            load_map(MAP_HUBENT, 1);
         end
         else
            load_map(MAP_DESCRVN4, 1);
      end
      else if (CUR_MAP_LAADYTUM) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_BONEYARD);
         if (is_success(LVar0)) then begin
            set_global_var(GVAR_FARGO_TRADERS_STATUS, CARAVAN_ARRIVED);
            game_time_advance(TRAVEL_TIME_HUB_ADY + (random(60, 600)*ONE_GAME_MINUTE));
            load_map(MAP_HUBENT, 1);
         end
         else
            load_map(MAP_MNTCRVN4, 1);
      end
      else if (CUR_MAP_BROHDENT) then begin
         set_global_var(GVAR_CARAVAN_START, CARAVAN_BOS);
         if (is_success(LVar0)) then begin
            set_global_var(GVAR_FARGO_TRADERS_STATUS, CARAVAN_ARRIVED);
            game_time_advance(TRAVEL_TIME_HUB_BOS + (random(60, 600)*ONE_GAME_MINUTE));
            load_map(MAP_HUBENT, 1);
         end
         else
            load_map(MAP_DESCRVN3, 1);
      end
   end
end

procedure Abandoned begin
   BMessage(501);
end

procedure Lost begin
   BMessage(502);
end

procedure HubAbandoned begin
   BMessage(504);
end

procedure HubLost begin
   BMessage(505);
end

procedure CarvLead13 begin
   Reply(mstr(508) + local_var(LVAR_UnloadingReward) + mstr(509));
   NLowOption(510, CarvLead14);
   NLowOption(511, CarvLead11);
   NOption(512, CarvLead14, 4);
   NOption(513, CarvLead11, 4);
end

procedure CarvLead14 begin
   UnloadCaravan := 1;
end

procedure create_caravan begin
   if (local_var(8) == 0) then begin
      set_local_var(8, 1);
      if (CUR_MAP_JUNKENT) then
         call Junktown_Caravan;
      else if (CUR_MAP_BROHDENT) then
         call BOS_Caravan;
      else if (CUR_MAP_HOTEL) then
         call Necropolis_Caravan;
      else if (CUR_MAP_LAADYTUM) then
         call LABone_Caravan;
   end
end

procedure remove_caravan begin
   if (local_var(8) == 1) then begin
      set_local_var(8, 0);
      if (CUR_MAP_JUNKENT) then begin
         Destroy_NS_Caravan(car1_junktown_hex, 0)
         Destroy_NS_Caravan(car2_junktown_hex, 0)
      end
      else if (CUR_MAP_BROHDENT) then begin
         Destroy_EW_Caravan(car1_bos_hex, 0)
         Destroy_EW_Caravan(car2_bos_hex, 0)
      end
      else if (CUR_MAP_HOTEL) then begin
         Destroy_NS_Caravan(car1_necropolis_hex, 1)
         Destroy_NS_Caravan(car2_necropolis_hex, 1)
      end
      else if (CUR_MAP_LAADYTUM) then begin
         Destroy_EW_Caravan(car1_boneyard_hex, 0)
         Destroy_EW_Caravan(car2_boneyard_hex, 0)
      end
   end
end

procedure Junktown_Caravan begin
variable LVar0 := 0;
variable LVar1 := 0;
variable Item := 0;
variable all_elev := 0;
variable all_rotation := 5;

   create_crvn_ns_carts(car1_junktown_hex, car2_junktown_hex)
   create_crvn_master(30511)
   create_crvn_guard(29504)
   create_crvn_guard(31505)
   create_crvn_brahmin(29707, 29502)
   create_crvn_customer(PID_PEASANT_5, 28711);
   create_crvn_customer(PID_LOSER, 28711);
   create_crvn_customer(PID_PEASANT_FEMALE_CUTE, 28711);
end

procedure BOS_Caravan begin
variable LVar0 := 0;
variable LVar1 := 0;
variable Item := 0;
variable all_elev := 0;
variable all_rotation := 0;

   create_crvn_ew_carts(car1_bos_hex, car2_bos_hex)
   create_crvn_master(26713)
   create_crvn_guard(25308)
   create_crvn_guard(25716)
   create_crvn_brahmin(24908, 25708)
   create_crvn_customer(PID_JUNIOR_PALADIN, 25702);
   create_crvn_customer(PID_JOURNEYMAN_KNIGHT, 25702);
   create_crvn_customer(PID_INITIATE_SCRIBE, 25702);
end

procedure Necropolis_Caravan begin
variable LVar0 := 0;
variable LVar1 := 0;
variable Item := 0;
variable all_elev := 1;
variable all_rotation := 5;

   create_crvn_ns_carts(car1_necropolis_hex, car2_necropolis_hex)
   create_crvn_master(26697)
   create_crvn_guard(25891)
   create_crvn_guard(27692)
   create_crvn_brahmin(25894, 25889)
   create_crvn_customer(PID_GHOUL, 24697);
   create_crvn_customer(PID_GHOUL, 24697);
   create_crvn_customer(PID_GHOUL, 24697);
end

procedure LABone_Caravan begin
variable LVar0 := 0;
variable LVar1 := 0;
variable Item := 0;
variable all_elev := 0;
variable all_rotation := 0;

   create_crvn_ew_carts(car1_boneyard_hex, car2_boneyard_hex)
   create_crvn_master(9320)
   create_crvn_guard(10122)
   create_crvn_guard(8518)
   create_crvn_brahmin(9920, 9118)
   create_crvn_customer(PID_PEASANT_GREEN, 9716);
   create_crvn_customer(PID_PEASANT_YELLOW, 9716);
   create_crvn_customer(PID_PEASANT_FEMALE_CUTE, 9716);
end
