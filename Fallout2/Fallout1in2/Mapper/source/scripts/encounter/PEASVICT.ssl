/*

   Random Coast Encounter - Peasants being attacked by Raiders.

*/

#include "define.h"

#define NAME                    SCRIPT_PEASVICT
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

procedure start;
procedure push_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

variable initial := 0;
variable dude_enemy;
variable cap_count;

procedure start begin
   if (not(initial) and map_first_run) then begin
      set_self_ai(global_var(GVAR_MYST_STRANGER_ITEM));
      set_self_team(TEAM_SHADY_SANDS);
      initial := 1;
      float_msg(self_obj, message_str(SCRIPT_PHIL, random(128, 129)), FLOAT_MSG_NORMAL);
   end
end

procedure push_p_proc begin
end

procedure critter_p_proc begin
   dude_enemy := global_var(GVAR_RND_ENC_PEASANTS_HOSTILE);
   if ((dude_enemy >= 3) and (tile_distance_objs(self_obj, dude_obj) < 8)) then begin
      call flee_dude;
   end
   else begin
      if ((global_var(GVAR_RND_ENC_RAIDERS_ALIVE) <= 0) and (dude_enemy < 3) and (global_var(GVAR_RND_ENC_RAIDERS_KILLED) > 0)) then begin
         terminate_combat;
         if (not(global_var(GVAR_RND_ENC_PEASANTS_INIT)) and (self_pid != PID_DOG)) then begin
            cap_count := random(80, 120);

            if (random(0, 1)) then
               float_msg(self_obj, message_str(SCRIPT_PHIL, 130) + cap_count + message_str(SCRIPT_PHIL, 131), FLOAT_MSG_NORMAL);
            else
               float_msg(self_obj, message_str(SCRIPT_PHIL, 132) + cap_count + message_str(SCRIPT_PHIL, 133), FLOAT_MSG_NORMAL);

            dude_caps_adjust(cap_count);
            set_global_var(GVAR_RND_ENC_PEASANTS_INIT, 1);
         end
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      dude_enemy := global_var(GVAR_RND_ENC_PEASANTS_HOSTILE);
      dude_enemy := dude_enemy + 1;
      set_global_var(GVAR_RND_ENC_PEASANTS_HOSTILE, dude_enemy);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure pickup_p_proc begin
   dude_enemy := global_var(GVAR_RND_ENC_PEASANTS_HOSTILE);
   dude_enemy := dude_enemy + 1;
   set_global_var(GVAR_RND_ENC_PEASANTS_HOSTILE, dude_enemy);
end

procedure talk_p_proc begin
   dude_enemy := global_var(GVAR_RND_ENC_PEASANTS_HOSTILE);
   if (self_pid != PID_DOG) then begin
      if (dude_enemy >= 3) then
         float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_NORMAL);
      else
         float_msg(self_obj, message_str(SCRIPT_PHIL, 127), FLOAT_MSG_NORMAL);
   end
end
