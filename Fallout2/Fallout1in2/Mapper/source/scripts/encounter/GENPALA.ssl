/*

   Encounter - Generic BoS Paladin

*/

#include "define.h"
#include "area/brotherhood.h"

#define NAME                    SCRIPT_GENPALA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

variable tmp_hostile;
variable initial := 0;
variable round_counter;
variable scared;

import variable random_seed_1;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_BROTHERHOOD);
      set_self_ai(AI_BROTHERHOOD_PALADIN);
      tmp_hostile := global_var(GVAR_ENEMY_RANDOM_ENC);
      initial := 1;
   end
   art_change_fid_num(self_obj, FID_HAPOWR);
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      round_counter := round_counter + 1;
      if (self_cur_hp < 10) then begin
         scared := 1;
      end
   end
end

procedure critter_p_proc begin
   if (scared) then begin
      if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
         call flee_dude;
      end
   end
   else if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
      random_seed_1 := 1;
   end
end

procedure pickup_p_proc begin
   if (not(scared)) then begin
      tmp_hostile := 1;
      set_global_var(GVAR_ENEMY_RANDOM_ENC, 1);
   end
end

procedure talk_p_proc begin
   dude_look_at_self;

   if (scared) then begin
      float_msg(self_obj, message_str(SCRIPT_GENPALA, 101), FLOAT_MSG_RED);
   end
   else if (REP_ENEMY_BOS or (random_seed_1 == 1)) then begin
      float_msg(self_obj, message_str(SCRIPT_GENPALA, 100), FLOAT_MSG_RED);
      set_global_var(GVAR_ENEMY_RANDOM_ENC, 1);
      tmp_hostile := 1;
   end
   else if joined_bos then begin
      float_msg(self_obj, message_str(SCRIPT_GENPALA, 102), FLOAT_MSG_BLUE);
   end
   else if ((check_general_rep < -20) or (has_rep_berserker) or has_rep_childkiller) then begin
      float_msg(self_obj, message_str(SCRIPT_GENPALA, 104), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, message_str(SCRIPT_GENPALA, 103), FLOAT_MSG_GREEN);
   end
end
