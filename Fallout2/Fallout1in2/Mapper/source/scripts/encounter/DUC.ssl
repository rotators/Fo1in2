/*

	Encounter - Merchant

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_DUC
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"
#include "..\headers\updatmap.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Duc01;
procedure Duc02;
procedure Duc03;
procedure Duc04;
procedure Duc05;
procedure Duc06;
procedure Duc07;
procedure Duc08;
procedure Duc09;
procedure Duc10;
procedure Duc11;
procedure DucEnd;
procedure DucBarter;
procedure BarterDone;

variable known;
variable initial :=  0;
variable Tandi_seed;
variable Tandi_rescued;

variable SrcObj := 0;
variable SrcIsParty := 0;

variable GotReaction := 0;


procedure start begin
	if not(initial) then begin
		initial :=  1;
		set_self_team(TEAM_SHADY_SANDS );
		set_self_ai( AI_PEASANT );
		known := global_var( GVAR_HAS_PLAYER_MET_DUC );
	end
end

procedure critter_p_proc begin
	if ((global_var( GVAR_WATER_THIEF_2 ) == 1) and (tile_distance_objs(dude_obj, self_obj) < 8)) then begin
		call flee_dude;
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_WATER_THIEF_2, 1 );
	end
end

procedure destroy_p_proc begin
	inc_good_critter
	set_global_var( GVAR_IS_DUC_ALIVE, 1 );// Duc Status
	set_global_var( GVAR_WATER_THIEF_2, 1 );
end

procedure look_at_p_proc begin
	script_overrides;
	if (not(known)) then begin
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(101));
	end
end

procedure pickup_p_proc begin
	set_global_var( GVAR_WATER_THIEF_2, 1 );
end

procedure talk_p_proc begin
	dude_look_at_self;
	if (global_var( GVAR_WATER_THIEF_2 ) == 1) then begin
		float_msg( self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_NORMAL );
	end
	else begin
		if not(GotReaction) then begin
			get_reaction
			GotReaction := 1;
		end
		start_gdialog(243, self_obj, 4, -1, -1);
		gsay_start;
		if (tandi_is_kidnapped and (Tandi_seed == 0)) then begin
			call Duc09;
		end
		else begin
			if (get_tandi_returned and (Tandi_rescued == 0)) then begin
				call Duc10;
			end
			else begin
				if (not(known)) then begin
				//	if (local_var(1) > 1) then begin
						call Duc01;
				//	end
				//	else begin
				//		call Duc06;
				//	end
				end
				else begin
				//	if (local_var(1) > 1) then begin
						call Duc07;
				//	end
				//	else begin
				//		call Duc08;
				//	end
				end
			end
		end
		gsay_end;
		end_dialogue;
	end
end

procedure Duc01 begin
	gsay_reply( SCRIPT_DUC, 102 );
	known := 1;
	set_global_var( GVAR_HAS_PLAYER_MET_DUC, known );
	NOption( 103, Duc02, 4 );
	NOption( 104, Duc03, 4 );
	NOption( 105, Duc04, -3 );
end

procedure Duc02 begin
	gsay_message( SCRIPT_DUC, 106, NEUTRAL_REACTION );
	gdialog_mod_barter(0);
	gsay_reply( SCRIPT_MODREACT, 100 );
	NOption( 129, BarterDone, 4 );
	NOption( 131, BarterDone, -3 );
end

procedure DucBarter begin
end

procedure BarterDone begin
end

procedure Duc03 begin
	gsay_reply( SCRIPT_DUC, 107 );

	if not(car_in_any_encounter) then
	   NOption( 108, Duc05, 4 );

	NOption( 109, DucEnd, 4 );
end

procedure Duc04 begin
	gsay_message( SCRIPT_DUC, 128, NEUTRAL_REACTION );
end

procedure Duc05 begin
	gsay_message( SCRIPT_DUC, 111, NEUTRAL_REACTION );
	game_time_advance(game_ticks(86400));
	if (global_var( GVAR_WORLDMAPLIST_SHADY_SANDS ) < 1) then
		set_global_var( GVAR_WORLDMAPLIST_SHADY_SANDS, 1 );
	mark_on_map(AREA_SHADY_SANDS)

	advance_time_travel_to_shady;
end

procedure Duc06 begin
	gsay_reply( SCRIPT_DUC, 113 );
	BOption( 114, Duc11, 4 );
	NOption( message_str(SCRIPT_DUC, 115) + dude_name + message_str(SCRIPT_DUC, 116), Duc08, 4 );
	NOption( message_str(SCRIPT_DUC, 117) + dude_name + message_str(SCRIPT_DUC, 132), Duc08, -3 );
end

procedure Duc07 begin
	gsay_reply( SCRIPT_DUC, 118 );

	if not(car_in_any_encounter) then
	   NOption( 119, Duc05, 4 );

	NOption( 120, Duc02, 4 );
	NOption( 121, DucEnd, 4 );
end

procedure Duc08 begin
	gsay_reply( SCRIPT_DUC, 122 );
	known := 1;
	set_global_var( GVAR_HAS_PLAYER_MET_DUC, known );
	NOption( 124, Duc02, 6 );
	NOption( 125, Duc04, -3 );
	NOption( 123, DucEnd, 6 );
end

procedure Duc09 begin
	gsay_message( SCRIPT_DUC, 126, NEUTRAL_REACTION );
	Tandi_seed := 1;
end

procedure Duc10 begin
	gsay_message( SCRIPT_DUC, 127, GOOD_REACTION );
	Tandi_rescued := 1;
	UpReact
end

procedure Duc11 begin
	gsay_message( SCRIPT_DUC, 110, BAD_REACTION );
end

procedure DucEnd begin
end
