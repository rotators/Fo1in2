/*

   Caravan cow - Used in caravan encounter maps.

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "mapenc.h"

#define NAME                    SCRIPT_CARCOW
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "caravan.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure combat_p_proc;
procedure critter_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;

variable initial := 0;

#define LVAR_CaravanTimer      (0)

procedure start begin
   call map_enter_p_proc;
end

procedure map_enter_p_proc begin
   if not(initial) then begin
      initial := 1;
      set_self_ai(AI_COW);

      if map_is_caravan_escort then
         set_self_team(TEAM_PLAYER);
   end

   if not(is_loading_game) then begin
      if (critter_state(self_obj) bwand DAM_KNOCKED_DOWN) then begin
         self_stand;
         move_to(self_obj, self_tile, self_elevation); // avoid visual offset shifting
      end
   end

   check_map_enter_timer
end

procedure map_update_p_proc begin
   check_critter_timer
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      script_overrides;
      debug_msg("I'm strapped in and won't move.");
   end
end

procedure critter_p_proc begin
end

procedure description_p_proc begin
   display_msg(message_str(SCRIPT_BRAHMIN, 100));
end

procedure talk_p_proc begin
   dude_look_at_self;
   display_msg(message_str(SCRIPT_BRAHMIN, random(102, 105)));
end

procedure use_obj_on_p_proc begin
   variable item := 0;
   if (is_alcohol(obj_being_used_with)) then begin
      script_overrides;
      item := obj_being_used_with;
      rm_obj_from_inven(source_obj, obj_being_used_with);
      destroy_object(item);
      //play_sfx("MABROMAQ");
      if ((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
            reg_anim_animate(self_obj, ANIM_hit_from_front, -1);
            reg_anim_animate(self_obj, ANIM_fall_back, 5);
            reg_anim_animate(self_obj, ANIM_fall_back_sf, -1);
         reg_anim_end();
         critter_injure(self_obj, DAM_KNOCKED_DOWN);
      end
   end
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_SCIENCE) then begin
      script_overrides;
      display_msg(message_str(SCRIPT_BRAHMIN, 101));
   end
   else if (action_being_used == SKILL_STEAL) then begin
      script_overrides;
      display_msg(gen_mstr(211));
   end
end

procedure destroy_p_proc begin
   inc_good_critter

   if CUR_AREA_HUB then
      set_global_var(GVAR_ENEMY_HUB, 1);
   else if CUR_AREA_JUNKTOWN then
      set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
   else if CUR_AREA_SHADY_SANDS then
      set_global_var(GVAR_ENEMY_SHADY_SANDS, 1);

   if map_is_caravan_escort then begin
      rm_caravan_brahmin(1);
      debug("caravan brahmin died! " + caravan_brahmin_left + " brahmin left!");
   end
end
