/*
   Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name : Mysterious Stranger
        Location : He is a perk and shows up in Random combat
        Description : He is a bad ass that helps you fight

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "define.h"
//#include "<TownName.h>"

#define NAME                        SCRIPT_MYSTSTRN
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_)

#include "command.h"
//#include "modreact.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure combat_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

procedure doLoadStuff;

#define MY_LEVEL           global_var(GVAR_MYSTERIOUS_STRANGER_LEVEL)
#define SET_MY_LEVEL(x)    set_global_var(GVAR_MYSTERIOUS_STRANGER_LEVEL, x)

#define WEAPON_PID         (local_var(LVAR_weapon_pid))
#define AMMO_PID           (local_var(LVAR_ammo_pid))

#define SET_WEAPON_PID(x)  set_local_var(LVAR_weapon_pid, x)
#define SET_AMMO_PID(x)    set_local_var(LVAR_ammo_pid, x)

#define WEP_AMMO_QTY 2
#define STIMPAK_QTY  2

// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;

procedure do_cloak;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_weapon_pid                 (7)
#define LVAR_ammo_pid                   (8)
#define LVAR_didCombat                  (9) //added by killap - made it into a local var
/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once := 0;
variable cloaked := 0;
//variable didCombat := 0;

procedure start begin
end

procedure combat_p_proc begin
   variable num;

   set_local_var(LVAR_didCombat, 1); //edit by killap
   //didCombat := 1;
   num := self_item_count(AMMO_PID);

   if (num < WEP_AMMO_QTY) then begin
      add_mult_objs_to_inven(self_obj, create_object(AMMO_PID, self_tile, 0), WEP_AMMO_QTY - num);
   end
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once := 0;
   set_self_team(TEAM_PLAYER);
   set_self_ai(AI_MYSTERIOUS_STRANGER);
   set_local_var(LVAR_didCombat, 0); //added by killap
   set_local_var(LVAR_Hostile, 0); //added by killap

   if not is_loading_game then
      call doLoadStuff;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   //if (didCombat == 1) then begin
   if ( (local_var(LVAR_didCombat)) or (local_var(LVAR_Hostile) == 2) ) then begin //edit by killap
      if (obj_on_screen(self_obj)) then begin
         if (not anim_busy(self_obj)) then begin
            self_run_to_tile(tile_num_in_direction(self_tile, Run_Away_From_Dude_Dir, 5));
         end
      end else begin
         destroy_object(self_obj);
      end
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy, 1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile, 2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
//   Evil_Critter:=0;
//   Slavery_Tolerant:=SLAVE_TOLERANT;
//   Karma_Perception:=KARMA_PERCEPTION1;

//   CheckKarma;

//   GetReaction;

   call Node001;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
   //variable obj;

   //obj := obj_carrying_pid_obj(self_obj, PID_STIMPAK);
   //if (obj) then begin
    //  destroy_object(obj);
   //end
   set_stranger_flag(STRANGER_DEAD);
/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
//   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   floater(random(102, 105)); //added by killap
end

procedure do_cloak begin
end

procedure doLoadStuff begin
   variable levelDiff := 0;
   variable rand;
   variable obj;

   debug_msg("MYSTSTRN : in loadstuff");

   levelDiff := (floor (dude_level * .75)) - MY_LEVEL;
   SET_MY_LEVEL(MY_LEVEL + levelDiff);
   if (MY_LEVEL < 6) then begin // was 10
      rand := Random(1, 3);
      if (rand == 1) then begin
         SET_WEAPON_PID(PID_HUNTING_RIFLE);
         SET_AMMO_PID(PID_223_FMJ);
      end else if (rand == 2) then begin
         SET_WEAPON_PID(PID_SHOTGUN); //was PID_HUNTING_RIFLE - killap
         SET_AMMO_PID(PID_SHOTGUN_SHELLS);
      end else if (rand == 3) then begin
         SET_WEAPON_PID(PID_FN_FAL);
         SET_AMMO_PID(PID_7_62MM_AMMO);
      end
   end else if (MY_LEVEL < 12) then begin // was 15
      rand := Random(1, 2);
      if (rand == 1) then begin
         SET_WEAPON_PID(PID_ASSAULT_RIFLE);
         SET_AMMO_PID(PID_5MM_AP);
      end else if (rand == 2) then begin
         SET_WEAPON_PID(PID_COMBAT_SHOTGUN);
         SET_AMMO_PID(PID_SHOTGUN_SHELLS);
      end
   end else if (MY_LEVEL < 18) then begin // was 20
      rand := Random(1, 2);
      if (rand == 1) then begin
         SET_WEAPON_PID(PID_LASER_RIFLE);
         SET_AMMO_PID(PID_MICRO_FUSION_CELL);
      end else if (rand == 2) then begin
         SET_WEAPON_PID(PID_HK_CAWS);
         SET_AMMO_PID(PID_SHOTGUN_SHELLS);
      end
   end else begin
      SET_WEAPON_PID(PID_HK_G11);
      SET_AMMO_PID(PID_4_7MM_CASELESS);
   end

   debug("stranger weapon: " + WEAPON_PID);

   obj := create_object(WEAPON_PID, self_tile, 0);
   add_obj_to_inven(self_obj, obj);
   add_mult_objs_to_inven(self_obj, create_object(AMMO_PID, self_tile, 0), WEP_AMMO_QTY);
   add_mult_objs_to_inven(self_obj, create_object(PID_STIMPAK, self_tile, 0), STIMPAK_QTY);
   reg_anim_clear(self_obj);
   reg_anim_begin();
   wield_obj(obj);
   reg_anim_end();
end
//xxxxxxxxxxxxxxxxxxxx
