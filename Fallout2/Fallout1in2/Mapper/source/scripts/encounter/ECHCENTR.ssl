/*

   Cave Encounter - Centaurs from Centaur Handler

*/

/* Include Files */
#include "define.h"
#include "maps/vaultbur.h"

#define NAME                    SCRIPT_ECHCENTR

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure timed_event_p_proc;
procedure use_skill_on_p_proc;
procedure destroy_p_proc;

variable tmp_hostile := 0;

procedure start begin
end

procedure map_enter_p_proc begin
   if map_first_run then
      set_self_team(TEAM_RND_MASTERS_ARMY);
   add_timer_event(self_obj, random(1, 5), 0);
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude) then begin
         coc_disguise_check;
         if not(DISGUISED) then begin
            tmp_hostile := 1;
         end
      end
   end
end

procedure timed_event_p_proc begin
   if not(combat_is_initialized) then
      self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
   add_timer_event(self_obj, game_ticks(random(1, 5)), 0);
end

procedure use_skill_on_p_proc begin
end

procedure damage_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure destroy_p_proc begin
   inc_neutral_critter
end

procedure combat_p_proc begin
end
