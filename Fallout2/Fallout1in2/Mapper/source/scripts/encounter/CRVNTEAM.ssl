/*

   Caravan job encounter - Caravan Leader

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_CRVNTEAM
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "caravan.h"
#include "mapenc.h"

/* Standard Script Procedures */
procedure start;
procedure push_p_proc;
procedure timed_event_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
variable Only_Once;

#define LVAR_CaravanTimer  (0)
#define LVAR_Called_Clear  (10)

#define TIMER_ALL_CLEAR 3

procedure start begin
   call map_enter_p_proc;
end

procedure push_p_proc begin
   if not(map_is_caravan_escort) then
      script_overrides;
end

procedure timed_event_p_proc begin
   // Only one survivor will float the msg (MVAR_CARVN_LEAD)
   if (fixed_param == TIMER_ALL_CLEAR) then begin
      if (map_var(MVAR_CARVN_LEAD) == 0) then
         set_map_var(MVAR_CARVN_LEAD, self_obj);

      if (map_var(MVAR_CARVN_LEAD) == self_obj) then begin
         if (not(combat_is_initialized)) then
            float_msg(self_obj, g_mstr(450), FLOAT_COLOR_NORMAL);
         add_timer_event(self_obj, game_ticks(10), TIMER_ALL_CLEAR);
      end
   end
end

procedure map_enter_p_proc begin
   if map_first_run then begin
      variable Item := 0;

      if map_is_caravan_escort then begin
         set_self_team(TEAM_PLAYER);
      end
      set_self_ai(AI_BERZERKER);

      // Caravan Master:
      Item := create_object(PID_SHOTGUN_SHELLS, 0, 0);
      add_obj_to_inven(self_obj, Item);
      Item := create_object(PID_SHOTGUN_SHELLS, 0, 0);
      add_obj_to_inven(self_obj, Item);
      Item := create_object(PID_SHOTGUN_SHELLS, 0, 0);
      add_obj_to_inven(self_obj, Item);
      Item := create_object(PID_SHOTGUN_SHELLS, 0, 0);
      add_obj_to_inven(self_obj, Item);
      Item := create_object(PID_STIMPAK, 0, 0);
      add_obj_to_inven(self_obj, Item);
   end

   check_map_enter_timer
end

procedure map_update_p_proc begin
   debug("MVAR_Hostile_Total: " + map_var(MVAR_Hostile_Total));

   if ((not(is_loading_game)) and (not(combat_is_initialized)) ) then begin
      if map_is_caravan_escort then begin
         if ((local_var(LVAR_Called_Clear) == 0) and (map_var(MVAR_Hostile_Total) <= 0)) then begin
           set_local_var(LVAR_Called_Clear, 1);
           add_timer_event(self_obj, game_ticks(2), TIMER_ALL_CLEAR);
         end
      end
   end
end

procedure talk_p_proc begin
   if map_is_caravan_escort then
      float_msg(self_obj, message_str(SCRIPT_CRVNTEAM, random(103, 115)), FLOAT_MSG_NORMAL);
   else
      float_msg(self_obj, message_str(SCRIPT_CRVNTEAM, random(100, 102)), FLOAT_MSG_NORMAL);
end

procedure critter_p_proc begin
   check_critter_timer
end

procedure destroy_p_proc begin
   if map_is_caravan_escort then rm_caravan_guards(1);
   inc_good_critter
end
