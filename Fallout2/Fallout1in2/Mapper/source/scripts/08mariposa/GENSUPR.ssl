#include "..\headers\define.h"

#define NAME                    SCRIPT_GENSUPR

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure GenSupr00;
procedure GenSupr03;
procedure GenSupr03a;
procedure GenSupr03b;
procedure GenSupr04;
procedure GenSupr05;
procedure GenSupr06;
procedure GenSupr07;
procedure GenSupr08;
procedure GenSuprAlert;
procedure GenSuprxx;

variable initial := 0;
variable tmp_hostile;
variable round_count;
variable home_tile := 7000;
variable waypoint;

import variable removal_ptr;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
      initial := 1;
   end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      round_count := round_count + 1;
   end
   if (round_count > 2) then begin
      call GenSuprAlert;
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      else begin
         if (global_var(GVAR_VATS_ALERT)) then begin
            tmp_hostile := 1;
         end
         else begin
            if (not(map_var(5))) then begin
               if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
                  dialogue_system_enter;
               end
            end
         end
      end
   end
   if (CUR_MAP_MBENT) then begin //  MILITARY BASE - OUTSIDE ENTRANCE
      if (map_var(0)) then begin
         if (not(waypoint)) then begin
            if (self_tile != 22312) then begin
               self_walk_to_tile(22312);
            end
            else begin
               waypoint := 1;
            end
         end
         else if (waypoint == 1) then begin
            if (self_tile != 31944) then begin
               self_walk_to_tile(31944);
            end
            else begin
               waypoint := 2;
            end
         end
         else if (waypoint == 2) then begin
            removal_ptr := self_obj;
         end
      end
   end
end

procedure destroy_p_proc begin
   inc_evil_critter
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (global_var(GVAR_LIEUTENANTS_DEAD)) then begin
      call GenSupr08;
   end
   else begin
      if (dude_is_armed) then begin
         if (random(0, 5) == 5) then begin
            call GenSupr00;
         end
         else begin
            tmp_hostile := 1;
         end
      end
      else begin
         start_gdialog(433, self_obj, 4, -1, -1);
         gsay_start;
         call GenSupr03;
         gsay_end;
         end_dialogue;
      end
   end
end

procedure GenSupr00 begin
   float_msg(self_obj, mstr(random(101, 103)), FLOAT_MSG_RED);
   tmp_hostile := 1;
end

procedure GenSupr03 begin
   Reply(random(104, 106));
   giq_option(-3, NAME, 107, GenSupr04, NEUTRAL_REACTION);
   giq_option(4, NAME, 108, GenSupr04, NEUTRAL_REACTION);
   giq_option(5, NAME, 109, GenSupr05, NEUTRAL_REACTION);
   giq_option(6, NAME, 110, GenSupr03a, NEUTRAL_REACTION);
   if dude_wearing_coc_robe then begin
      giq_option(6, NAME, 111, GenSupr03b, NEUTRAL_REACTION);
   end
end

procedure GenSupr03a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call GenSupr07;
   end
   else begin
      call GenSupr06;
   end
end

procedure GenSupr03b begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 25))) then begin
      call GenSupr07;
   end
   else begin
      call GenSupr06;
   end
end

procedure GenSupr04 begin
   tmp_hostile := 1;
   BMessage(random(112, 114));
end

procedure GenSupr05 begin
   Reply(115);
   giq_option(5, NAME, 116, GenSuprxx, NEUTRAL_REACTION);
   giq_option(5, NAME, 117, GenSuprAlert, BAD_REACTION);
end

procedure GenSupr06 begin
   tmp_hostile := 1;
   BMessage(random(118, 120));
end

procedure GenSupr07 begin
   set_map_var(5, 1);
   NMessage(random(121, 123));
end

procedure GenSupr08 begin
   float_msg(self_obj, mstr(random(124, 127)), FLOAT_MSG_RED);
   tmp_hostile := 1;
end

procedure GenSuprAlert begin
   set_global_var(GVAR_VATS_ALERT, 1);
   tmp_hostile := 1;
end

procedure GenSuprxx begin
   set_global_var(GVAR_ENTERING_VATS_HOW, 2);
   //load_map(MAP_MBVATS12, 5);
   advance_time_travel_to_mbase_vats;
end
