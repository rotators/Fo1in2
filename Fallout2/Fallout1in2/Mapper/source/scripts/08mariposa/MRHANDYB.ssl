#include "..\headers\define.h"
#include "..\headers\command.h"

procedure start;
procedure critter_p_proc;

import variable valid_target;

variable tmp_hostile;
variable initial := 0;


///////////////////////////////////////////////////////////////////////
//	For two-armed brainbots in	Military Base, Stronghold (lvls 1 & 2)
///////////////////////////////////////////////////////////////////////


procedure start
begin


	if (global_var(GVAR_DESTROY_VATS_1) == 2) then begin
		set_self_team(TEAM_85);
	end
	if not(initial) then begin
		set_self_team(TEAM_NECROPOLIS_MUTANT);
		set_self_ai(AI_ROBOTS);
		initial := 1;
	end
	else begin
		if (script_action == critter_proc) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
			call critter_p_proc;
		end
	end
end

procedure critter_p_proc
begin
	if (global_var(GVAR_DESTROY_VATS_3) != 0) then begin
		if ((critter_state(self_obj) bwand 64) == 0) then begin
			critter_injure(self_obj, DAM_BLIND);//    DAM_BLIND         64
		end
	end
	if (global_var(GVAR_ROBCTRL_MOVEMENT_HOWMUCH) != 0) then begin
		if ((critter_state(self_obj) bwand 4) == 0) then begin
			critter_injure(self_obj, DAM_CRIP_LEG_LEFT);//    DAM_CRIP_LEG_LEFT      4
		end
	end
	if (global_var(GVAR_ROBCTRL_MOVEMENT_HOWMUCH) == 2) then begin
		if ((critter_state(self_obj) bwand 8) == 0) then begin
			critter_injure(self_obj, DAM_CRIP_LEG_RIGHT);//    DAM_CRIP_LEG_RIGHT     8
		end
	end
	if (global_var(GVAR_VATS_ALERT) == 1) then begin
		if (global_var(GVAR_DESTROY_VATS_3) == 0) then begin
			if (global_var(GVAR_DESTROY_VATS_1) == 0) then begin
				if (self_can_see_dude) then begin
					attack(dude_obj);
				end
			end
			else begin
				if (global_var(GVAR_DESTROY_VATS_1) == 1) then begin
					if (obj_can_see_obj(self_obj, valid_target)) then begin
						attack(valid_target);
					end
				end
				else begin
					if (global_var(GVAR_DESTROY_VATS_1) == 2) then begin
						if (obj_can_see_obj(self_obj, valid_target)) then begin
							attack(valid_target);
						end
					end
					else begin
						if (global_var(GVAR_DESTROY_VATS_1) == 3) then begin
							if (self_can_see_dude) then begin
								attack(dude_obj);
							end
						end
					end
				end
			end
		end
		if (global_var(GVAR_ROBCTRL_MOVEMENT_HOWMUCH) != 2) then begin
			self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
		end
	end
end
