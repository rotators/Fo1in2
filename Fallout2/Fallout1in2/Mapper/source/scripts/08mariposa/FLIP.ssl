/*

   Mariposa - Prisoner?

*/

/* Include Files */
#include "define.h"
#include "area/mbase.h"

#define NAME                    SCRIPT_FLIP
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure Flip00;
procedure Flip01;
procedure Flip02;
procedure Flip03;
procedure Flip03a;
procedure Flip04;
procedure Flip05;
procedure FlipCombat;
procedure transit;
procedure FlipLeave;

variable known;
variable tmp_hostile;
variable initial := 0;
variable round_counter;
variable cell_tile := 23937;
variable home_tile := 14338;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_MUTANTS);
      set_self_ai(AI_GUARD);
   end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      round_counter := round_counter + 1;
   end
   if (round_counter > 2) then begin
      if (global_var(GVAR_VATS_ALERT) == 0) then begin
         set_global_var(GVAR_VATS_ALERT, 1);
         set_map_var(7, 1);
      end
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude and (tile_distance_objs(self_obj, dude_obj) < 12)) then begin
         if (global_var(GVAR_LIEUTENANTS_DEAD)) or bos_invading_mbase then begin
            call Flip00;
         end
         else begin
            coc_disguise_check;
            if (not(DISGUISED) and (local_var(4) == 0)) then begin
               dialogue_system_enter;
            end
         end
      end
      if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
         self_walk_to_tile(cell_tile);
      end
      else begin
         self_walk_to_tile(home_tile);
      end
   end
end

procedure destroy_p_proc begin
   set_map_var(8, 3);
   inc_inv_evil_crit
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(4)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data(tma_source_generic, TMA_MSG_MBASE, -1, -1, 997, 999);

   dude_look_at_self;
   get_reaction
   start_gdialog(367, self_obj, 4, -1, -1);
   gsay_start;

   coc_disguise_check;
   if DISGUISED then begin
      call Flip01;
   end
   else begin
      if (local_var(4) == 1) then begin
         call Flip05;
      end
      else begin
         call Flip02;
      end
   end
   gsay_end;
   end_dialogue;
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      if (self_can_see_dude) then begin
         tmp_hostile := 1;
      end
   end
   else if (fixed_param == 2) then begin
      call Flip00;
   end
end

procedure Flip00 begin
   float_msg(self_obj, mstr(102), FLOAT_MSG_NORMAL);
   call FlipCombat;
end

procedure Flip01 begin
   add_timer_event(self_obj, game_ticks(30), 1);
   NMessage(103);
end

procedure Flip02 begin
   set_local_var(4, 1);
   Reply(104);
   NOption(105, FlipCombat, 4);
   NOption(106, transit, 4);
   NLowOption(107, FlipCombat);
end

procedure Flip03 begin
   Reply(108);
   NOption(109, transit, 4);
   NOption(110, Flip03a, 4);
end

procedure Flip03a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call FlipLeave;
   end
   else begin
      call Flip04;
   end
end

procedure Flip04 begin
   NMessage(111);
   call FlipCombat;
end

procedure Flip05 begin
   NMessage(112);
   call FlipCombat;
end

procedure FlipCombat begin
   tmp_hostile := 1;
end

procedure transit begin
   debug("transit to Lou");
   set_global_var(GVAR_ENTERING_VATS_HOW, 2);

   gfade_out(1);
      override_map_start(71, 116, 1, 0);
   gfade_in(1);
end

procedure FlipLeave begin
   if (self_tile == home_tile) then begin
      self_walk_to_tile(tile_num_in_direction(home_tile, 0, 5));
   end
   else begin
      self_walk_to_tile(tile_num_in_direction(self_tile, 5, 5));
   end
   add_timer_event(self_obj, game_ticks(5), 2);
end
