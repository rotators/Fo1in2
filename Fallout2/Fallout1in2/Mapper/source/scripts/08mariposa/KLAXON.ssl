/*

   Military Base - Klaxon lights

*/

#include "define.h"
#include "mbase.h"

#define NAME                    SCRIPT_COMMAND

#include "command.h"

procedure start;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure map_update_p_proc;
procedure description_p_proc;

procedure check_lights;

#define LVAR_LIGHT_STATUS     (0)

#define light_on              (local_var(LVAR_LIGHT_STATUS) == 1)
#define light_off             (local_var(LVAR_LIGHT_STATUS) == 0)

#define set_light_off         set_local_var(LVAR_LIGHT_STATUS, 0)
#define set_light_on          set_local_var(LVAR_LIGHT_STATUS, 1)

#define FID_EW_ON             (33555350)
#define FID_NS_ON             (33555351)

#define FID_EW_OFF            (33555269)
#define FID_NS_OFF            (33555270)

#define EVENT_ALARM           (1)

procedure start begin
end

procedure map_enter_p_proc begin
   flush_add_timer_event(self_obj, game_ticks(1), EVENT_ALARM);
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_ALARM) then begin
      call check_lights;
   end
   add_timer_event(self_obj, game_ticks(1), EVENT_ALARM);
end

procedure map_update_p_proc begin
   call check_lights;
end

procedure description_p_proc begin
   if (mbase_full_alert and light_on) then begin
      script_overrides;
      display_msg(mstr(100));
   end
end

procedure check_lights begin
   if mbase_full_alert and light_off then begin
      set_light_on;
      if (self_fid == FID_EW_OFF) then
         art_change_fid_num(self_obj, FID_EW_ON);
      else
         art_change_fid_num(self_obj, FID_NS_ON);
   end
   else if (not(mbase_full_alert) and light_on) then begin
      set_light_off;
      if (self_fid == FID_EW_ON) then
         art_change_fid_num(self_obj, FID_EW_OFF);
      else
         art_change_fid_num(self_obj, FID_NS_OFF);
   end
end

