/*

   Military Base - Force Field

*/

#include "define.h"
#include "area/mbase.h"
//#include "maps/mbstrg12.h"

#define NAME                    SCRIPT_FIELDH

#include "command.h"

procedure start;
procedure timed_event_p_proc;
procedure use_p_proc;
procedure map_update_p_proc;
procedure map_enter_p_proc;

procedure turn_field_off;
procedure turn_field_on;
procedure toggle_field;

variable initial := 0;

import variable field_change;
import variable FieldH_Ptr;

procedure start begin
   if (not(initial)) then begin
      FieldH_Ptr := self_obj;
      set_local_var(1, self_tile);
      call use_p_proc;
      initial := 1;
   end
end

procedure timed_event_p_proc begin
   call use_p_proc;
end

procedure use_p_proc begin
   if (global_var(GVAR_FORCE_FIELDS_OFF)) then begin
      field_change := "off";
   end
   if (source_obj != dude_obj) then begin
      if (field_change == "toggle") then begin
         call toggle_field;
      end
      else if (field_change == "off") then begin
         call turn_field_off;
      end
      else if (field_change == "on") then begin
         call turn_field_on;
      end
   end
end

procedure map_update_p_proc begin
   FieldH_Ptr := self_obj;
end

procedure map_enter_p_proc begin
   FieldH_Ptr := self_obj;
end

procedure turn_field_off begin
   if not(is_loading_game) then set_self_invisible;
   set_local_var(0, 1);
end

procedure turn_field_on begin
   if not(is_loading_game) then set_self_visible;
   set_local_var(0, 0);
end

procedure toggle_field begin
   if (local_var(0)) then begin
      call turn_field_on;
   end
   else begin
      call turn_field_off;
   end
end
