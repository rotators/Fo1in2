/*

   Mariposa - Guard Team 3

*/

/* Include Files */
#include "define.h"
#include "area/mbase.h"

#define NAME                    SCRIPT_GENSUPR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure map_enter_p_proc;

procedure GenSupr00;
procedure GenSupr03;
procedure GenSupr03a;
procedure GenSupr03b;
procedure GenSupr04;
procedure GenSupr05;
procedure GenSupr06;
procedure GenSupr07;
procedure GenSupr08;
procedure GenSuprAlert;
procedure GenSuprxx;

procedure set_alert_tile;

#define LVAR_MoveHex       (4)

import variable field_change;
import variable radio_room_attacked;
import variable valid_target;

variable tmp_hostile;
variable round_counter;
variable home_tile;
variable alert_tile;

procedure start begin
end

procedure combat_p_proc begin
   if (not(self_can_see_dude)) then begin
      critter_stop_attacking(self_obj);
   end
   alert_check;
   knock_out_player_check;
end

procedure critter_p_proc begin
   if (tmp_hostile and self_can_see_dude) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude) then begin
         if (global_var(GVAR_VATS_ALERT)) then begin
            tmp_hostile := 1;
         end
         else begin
            if (not(map_var(2))) then begin
               if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
                  dialogue_system_enter;
               end
            end
         end
      end
      if (global_var(GVAR_VATS_ALERT) and radio_room_attacked) then begin
         if (self_elevation == 1) then begin
            if (self_tile != 14520) then begin
               self_walk_to_tile(14520);
            end
            else begin
               move_to(self_obj, 14520, 0);
            end
         end
         else if (self_tile != alert_tile) then begin
            self_walk_to_tile(alert_tile);
         end
      end
      else if (self_tile != local_var(LVAR_MoveHex)) then begin
         self_walk_to_tile(local_var(LVAR_MoveHex));
      end
   end
   if ((global_var(GVAR_ROBCTRL_PESTS_HOWMUCH) >= robo_attack_all_pests) and (global_var(GVAR_ROBCTRL_PESTS_HOWMUCH) <= robo_attack_small_pests)) then begin
      valid_target := self_obj;
   end
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data(tma_source_generic, TMA_MSG_MBASE, -1, -1, 997, 999);

   dude_look_at_self;
   if (global_var(GVAR_LIEUTENANTS_DEAD)) then begin
      call GenSupr08;
   end
   else begin
      if (dude_is_armed) and not(tmp_hostile) then begin
         if (random(0, 5) == 5) then begin
            call GenSupr00;
         end
         else begin
            tmp_hostile := 1;
         end
      end
      else begin
         start_gdialog(433, self_obj, 4, -1, -1);
         gsay_start;
         call GenSupr03;
         gsay_end;
         end_dialogue;
      end
   end
end

procedure map_enter_p_proc begin
   set_self_team(TEAM_NECROPOLIS_MUTANT);
   set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
   call set_alert_tile;
   if (global_var(GVAR_VATS_ALERT) and radio_room_attacked and not(is_loading_game)) then begin
      move_to(self_obj, alert_tile, 0);
   end
end

procedure GenSupr00 begin
   float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(101, 103)), FLOAT_MSG_NORMAL);
   tmp_hostile := 1;
end

procedure GenSupr03 begin
   gsay_reply(SCRIPT_GENSUPR, random(104, 106));
   NLowOption(107, GenSupr04);
   NOption(108, GenSupr04, 4);
   NOption(109, GenSupr05, 5);
   NOption(110, GenSupr03a, 6);
   if dude_wearing_coc_robe then begin
      NOption(111, GenSupr03b, 6);
   end
end

procedure GenSupr03a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call GenSupr07;
   end
   else begin
      call GenSupr06;
   end
end

procedure GenSupr03b begin
   coc_disguise_check;
   if DISGUISED and (dude_skill_success(SKILL_SPEECH, 25)) then begin
      call GenSupr07;
   end
   else begin
      call GenSupr06;
   end
end

procedure GenSupr04 begin
   tmp_hostile := 1;
   gsay_message(SCRIPT_GENSUPR, random(112, 114), NEUTRAL_REACTION);
end

procedure GenSupr05 begin
   gsay_reply(SCRIPT_GENSUPR, 115);
   NOption(116, GenSuprxx, 5);
   NOption(117, GenSuprAlert, 5);
end

procedure GenSupr06 begin
   tmp_hostile := 1;
   gsay_message(SCRIPT_GENSUPR, random(118, 120), NEUTRAL_REACTION);
end

procedure GenSupr07 begin
   set_map_var(2, 1);
   gsay_message(SCRIPT_GENSUPR, random(121, 123), NEUTRAL_REACTION);
end

procedure GenSupr08 begin
   float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(124, 127)), FLOAT_MSG_NORMAL);
   tmp_hostile := 1;
end

procedure GenSuprAlert begin
   set_global_var(GVAR_VATS_ALERT, 1);
   tmp_hostile := 1;
end

procedure GenSuprxx begin
   set_global_var(GVAR_ENTERING_VATS_HOW, 2);
   load_map(MAP_MBVATS12, 5);
end

procedure set_alert_tile begin
   if (not(local_var(LVAR_MoveHex))) then begin
      set_local_var(LVAR_MoveHex, self_tile);
   end
   if (local_var(LVAR_MoveHex) == 23328) then begin
      alert_tile := 25870;
   end
   else if (local_var(LVAR_MoveHex) == 22325) then begin
      alert_tile := 25468;
   end
   else if (local_var(LVAR_MoveHex) == 22329) then begin
      alert_tile := 25070;
   end
end
