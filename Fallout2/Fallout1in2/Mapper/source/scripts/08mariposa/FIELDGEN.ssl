/*

   Military Base - Force Field

*/

#include "define.h"
#include "mbase.h"
#include "maps/mbstrg12.h"

#define NAME                    SCRIPT_FIELDGEN

#include "command.h"

procedure start;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;

procedure turn_field_off;

import variable field_change;

procedure start begin
end

procedure use_obj_on_p_proc begin
variable Item := obj_pid(obj_being_used_with);

   if (Item == PID_MULTI_TOOL or Item == PID_SUPER_TOOL_KIT) then begin
      script_overrides;
      //dude_anim_magic_hands;

      if (is_success(roll_vs_skill(dude_obj, SKILL_REPAIR, 0))) then
         call turn_field_off;
      else
         display_msg(mstr(100));
   end
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_REPAIR) then begin
      script_overrides;
      if (is_success(roll_vs_skill(dude_obj, SKILL_REPAIR, -20))) then
         call turn_field_off;
      else
         display_msg(mstr(100));
   end
end

procedure turn_field_off begin
   field_change := "off";
   display_msg(mstr(101));
   if (CUR_MAP_MBSTRG12) then begin
      if (self_elevation == 0) then begin
         if (self_tile == 15581) then
            use_obj(map_var(MVAR_FieldB1_ptr));
         else if (self_tile == 19124) then
            use_obj(map_var(MVAR_FieldB2_ptr));
         else if (self_tile == 25098) then
            use_obj(map_var(MVAR_FieldB3_ptr));
      end
   end
end
