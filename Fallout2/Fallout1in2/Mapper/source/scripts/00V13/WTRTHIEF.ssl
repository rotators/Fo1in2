/*

   V13 - Water Thief

*/

/* Include Files */
#include "define.h"
#include "vault13.h"

#define NAME                    SCRIPT_WTRTHIEF
#define TOWN_REP_VAR            (GVAR_TOWN_REP_V13)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure WtrThief01;
procedure WtrThief02;
procedure WtrThief03;
procedure WtrThief04;
procedure WtrThief05;
procedure WtrThief06;
procedure WtrThief07;
procedure WtrThief08;
procedure WtrThief09;
procedure WtrThief10;
procedure WtrThief11;
procedure WtrThief11a;

procedure WtrThiefCombat;
procedure finish_quest;
procedure WtrThiefBye;
procedure WtrThiefEnd;

#define LVAR_Move_to       (4)

#define EVENT_MOVE         (1)
#define EVENT_LEAVE_BOX    (2)

import variable Officer_ptr;
import variable VaultBox_ptr;
import variable WtrThief_ptr;

variable tmp_hostile;
variable got_water;
variable on_the_way;
variable searched;
variable scared;

procedure start begin
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      if (Officer_ptr != 0) then begin
         set_team(Officer_ptr, TEAM_PLAYER);
      end
   end
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      scared := 1;
      attack(dude_obj);
   end
   else begin
      if (game_time_hour <= 600) then begin
         if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 1) then begin
            if (not(got_water) and (local_var(7) == 0)) then begin
               if (not(on_the_way)) then begin
                  on_the_way := 1;
                  set_local_var(LVAR_Move_to, 22728);
                  move_to(self_obj, 16912, 2);
                  add_timer_event(self_obj, game_ticks(30), EVENT_MOVE);
                  set_local_var(6, game_time / 10 / 60 / 60);
               end
            end
         end
         if (self_tile != local_var(LVAR_Move_to)) then begin
            self_walk_to_tile(local_var(LVAR_Move_to));
         end
      end
      else begin
         set_local_var(7, 0);
         got_water := 0;
         on_the_way := 0;
         if (self_item_count(PID_WATER_FLASK)) then begin
            destroy_object(self_item(PID_WATER_FLASK));
         end
      end
      if (tile_distance_objs(self_obj, VaultBox_ptr) < 4) then begin
         if (not(got_water)) then begin
            if (anim_busy(self_obj) == 0) then begin
               use_obj(VaultBox_ptr);
               obj_open(VaultBox_ptr);

               got_water := 1;
               LVar0 := create_object(PID_WATER_FLASK, 0, 0);
               add_obj_to_inven(self_obj, LVar0);
               add_timer_event(self_obj, game_ticks(3), EVENT_LEAVE_BOX);
               if (dude_can_see_self) then begin
                  display_msg(mstr(103));
               end
            end
         end
      end
      if (local_var(7) and (self_tile == 16912)) then begin
         move_to(self_obj, 7000, 0);
         set_local_var(LVAR_Move_to, 7000);
      end
      if (((game_time / 10 / 60 / 60) - local_var(6)) >= 7) then begin
         set_local_var(7, 0);
         got_water := 0;
         on_the_way := 0;
         if (self_item_count(PID_WATER_FLASK)) then begin
            destroy_object(self_item(PID_WATER_FLASK));
         end
      end
   end
end

procedure description_p_proc begin
   script_overrides;
   set_local_var(5, 1);
   display_msg(mstr(102));
end

procedure destroy_p_proc begin
   set_global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF, 2);
   if (Officer_ptr != 0) then begin
      set_team(Officer_ptr, TEAM_VAULT13);
   end

   if source_obj == dude_obj then begin
      inc_neutral_critter
      give_xp_msg(EXP_KILLED_WATERTHIEF, mstr(104));
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (not(local_var(5))) then begin
      set_local_var(5, is_success(do_check(self_obj, STAT_pe, dude_perk(PERK_bonus_awareness))));
   end
   if ((tile_distance_objs(self_obj, dude_obj) > 2) and not(local_var(5))) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure map_enter_p_proc begin
   if (v13_invaded) then begin
      invasion_kill_critter_V13;
   end

   WtrThief_ptr := self_obj;
   set_self_team(TEAM_87);
   set_self_ai(AI_BERZERKER);
   if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 0) then begin
      if not(is_loading_game) then set_self_invisible;
   end
   else begin
      if not(is_loading_game) then set_self_visible;
   end
   if (local_var(LVAR_Move_to) != 7000) then begin
      on_the_way := 1;
   end
   if (self_item_count(PID_WATER_FLASK)) then begin
      got_water := 1;
   end
end

procedure map_update_p_proc begin
   if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 0) then begin
      if not(is_loading_game) then set_self_invisible;
   end
   else begin
      if not(is_loading_game) then set_self_visible;
   end
   if ((game_time_hour > 700) and (self_tile != 7000)) then begin
      move_to(self_obj, 7000, 0);
      set_local_var(LVAR_Move_to, 7000);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_VAULT13);

   dude_look_at_self;
   if (scared) then begin
      self_run_to_tile(16912);
      set_local_var(LVAR_Move_to, 16912);
   end
   else begin
      set_local_var(5, 1);
      start_dialog_at_node(WtrThief01);

      if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 2) then begin
         gfade_out(1);
         move_to(self_obj, 7000, 2);
         if (Officer_ptr != 0) then begin
            move_to(Officer_ptr, 22093, 2);
            move_to(dude_obj, 22293, 2);
            dude_rotate(5);
            obj_rotate(Officer_ptr, 2);
            float_msg(Officer_ptr, mstr(105), FLOAT_MSG_NORMAL);
         end
         gfade_in(1);
         give_xp_msg(EXP_CAPTURED_WATERTHIEF, mstr(106));
      end
      if (searched) then begin
         display_msg(mstr(130));
         searched := 0;
      end
   end
end

procedure timed_event_p_proc begin
   set_local_var(7, 1);
   if (fixed_param == EVENT_MOVE) then begin
      if (self_can_see_dude) then begin
         set_local_var(LVAR_Move_to, 16912);
      end
      else begin
         set_local_var(LVAR_Move_to, 20706);
      end
   end
   else if (fixed_param == EVENT_LEAVE_BOX) then begin
      obj_close(VaultBox_ptr);
      set_local_var(LVAR_Move_to, 16912);
   end
end

procedure WtrThief01 begin
   Reply(mstr(107) + dude_name + mstr(108));
   NOption(109, WtrThief02, 4);
   NLowOption(110, WtrThiefEnd);
end

procedure WtrThief02 begin
   Reply(111);
   NOption(112, WtrThiefBye, 4);
   NOption(113, WtrThief03, 4);
   if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 1) then begin
      NOption(114, WtrThief05, 4);
   end
end

procedure WtrThief03 begin
   Reply(115);
   NOption(116, WtrThief04, 4);
   BOption(117, WtrThief05, 4);
end

procedure WtrThief04 begin
   Reply(118);
   NOption(119, WtrThiefBye, 4);
end

procedure WtrThief05 begin
   if (got_water) then begin
      call WtrThief08;
   end
   else begin
      call WtrThief06;
   end
end

procedure WtrThief06 begin
   Reply(120);
   NOption(121, WtrThief07, 4);
end

procedure WtrThief07 begin
   searched := 1;
end

procedure WtrThief08 begin
   Reply(122);
   NOption(123, WtrThiefEnd, 4);
   BOption(124, WtrThief09, 4);
end

procedure WtrThief09 begin
   if (has_rep_berserker) then begin
      call WtrThief10;
   end
   else begin
      call WtrThief11;
   end
end

procedure WtrThief10 begin
   Reply(125);
   NOption(126, finish_quest, 4);
end

procedure WtrThief11 begin
   Reply(127);
   NOption(121, WtrThief11a, 4);
end

procedure WtrThief11a begin
   if (is_success(do_check(dude_obj, STAT_ch, 0))) then begin
      call finish_quest;
   end
   else begin
      call WtrThiefCombat;
   end
end

procedure WtrThiefCombat begin
   tmp_hostile := 1;
end

procedure finish_quest begin
   set_global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF, 2);
end

procedure WtrThiefBye begin
   Reply(128);
   NOption(129, WtrThiefEnd, 4);
end

procedure WtrThiefEnd begin
end
