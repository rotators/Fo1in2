/*

   Vault 13 - vault door

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\vault13.h"
#include "..\headers\maps\v13ent.h"

#define NAME                    SCRIPT_V13DOOR

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;//added from Endgame Mod by Corpse
procedure map_update_p_proc;

import variable vault_door_ptr;

procedure start begin
end

procedure damage_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure map_enter_p_proc begin
   vault_door_ptr := self_obj;

   // Lock the door, so player can't walk through it via Autodoor-script.
   if map_first_run then
      obj_lock(self_obj);

   // Fail-safe. Force the door shut once the endgame scene starts running.
   if (waterchip_returned and military_base_destoryed and cathedral_destroyed and not(is_story_finished)) then begin
      if obj_is_open(self_obj) then begin
         use_obj(self_obj);
      end
   end

   if is_story_finished then begin
      if obj_is_open(self_obj) then begin
         if (map_var(MVAR_Door_Blown) == 0) or (map_var(MVAR_Door_Blown) == 2) then begin
            toggle_v13_door
         end
      end
   end
   else begin
      if obj_is_open(self_obj) then begin
         if (map_var(MVAR_Door_Blown) == 0) or (map_var(MVAR_Door_Blown) == 2) then begin
            toggle_v13_door
         end
      end
      else begin
         if map_var(MVAR_Door_Blown) == 1 then begin
            toggle_v13_door
         end
      end
   end
end

procedure map_update_p_proc begin
   if (global_var(GVAR_OVERSEER_KILLED) == 1) then begin
      if (obj_is_open(vault_door_ptr)) then begin
         toggle_v13_door
      end
      else if (global_var(GVAR_OVERSEER_KILLED) == 2) then begin
         if (obj_is_open(vault_door_ptr)) then begin
            add_timer_event(vault_door_ptr, game_ticks(15), 1);
         end
      end
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      toggle_v13_door
   end
   else if (fixed_param == 2) then begin // this param is called by OVER.ssl
      //add_timer_event(self_obj, game_ticks(8), 3);
   end
   /*else if (fixed_param == 3) then begin
      if obj_is_open(self_obj) then begin
         use_obj(self_obj);
      end
   end*/
end
