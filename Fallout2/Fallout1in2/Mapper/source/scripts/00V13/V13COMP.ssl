/*

   Vault 13 - Computer terminal

*/

/* Include Files */
#include "..\headers\define.h"

#define NAME                    SCRIPT_V13COMP

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure description_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure use_p_proc;

procedure Banished00;
procedure Banished01;
procedure Banished02;

import variable vault_door_ptr;

#define LVAR_VaultAccess      (0)
#define LVAR_DoorStatus       (1)

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      display_msg(mstr(124));
      add_timer_event(self_obj, game_ticks(10), 2);
   end
   else if (fixed_param == 2) then begin
      set_global_var(GVAR_VAULT13_WATER_DAYS_LEFT, 0);
      game_ui_disable;

      gfade_out(1);
      play_gmovie(BOIL3_MOVIE);// <-- Dehydration. Vault drowns, bro ! Oops, opposite of drowning actually. anti-drowns. -- was 6 before
      gfade_in(1);

      game_ui_enable;

      if global_var(GVAR_CAN_PLAY_AFTER_V13_DEAD) < 2 then begin
         signal_end_game;
      end

   end
end

procedure damage_p_proc begin
   script_overrides;
   if (local_var(LVAR_DoorStatus) == 0) then begin
      set_local_var(LVAR_DoorStatus, 1);

      if is_success(roll_vs_skill(dude_obj, SKILL_TRAPS, 0)) then begin
         set_map_var(1, 1);

         display_msg(mstr(121) + mstr(116) + mstr(119));
         display_msg(message_str(SCRIPT_GENCHAT, 503) + 50 + message_str(SCRIPT_GENCHAT, 504));

         give_exp_points(EXP_DESTROYED_DOOR_TERMINAL);

         if not(obj_is_open(vault_door_ptr)) then begin
            use_obj(vault_door_ptr);
         end
      end
      else begin
         display_msg(mstr(122) + mstr(116) + mstr(120));
         set_map_var(1, 2);
         if obj_is_open(vault_door_ptr) then begin
            use_obj(vault_door_ptr);
         end
         if global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) != 2 then begin
            add_timer_event(self_obj, game_ticks(10), 1);
         end
      end

   end
end

procedure look_at_p_proc begin
   if not(local_var(LVAR_DoorStatus) == 0) then begin
      script_overrides;
      display_msg(mstr(103));
   end
   else begin
      script_overrides;
      display_msg(mstr(117));
   end
end

procedure description_p_proc begin
   if local_var(LVAR_DoorStatus) then begin
      script_overrides;
      display_msg(mstr(118));
   end
end

procedure use_p_proc begin
   script_overrides;

   if (local_var(LVAR_DoorStatus) > 0) then begin
      display_msg(mstr(123));
   end
   else begin

      if (get_days_passed < 2) then begin
         if (local_var(LVAR_VaultAccess) == 0) then begin
            display_msg(mstr(101));
            float_msg(self_obj, mstr(102), FLOAT_MSG_NORMAL);//was ", 8" in endgame mod's version
         end
         else begin
            display_msg(mstr(100));
            set_local_var(LVAR_VaultAccess, 1);
         end
         play_sfx("SLDOORSO");
      end
      else begin
         if (global_var(GVAR_GAME_CONTINUES) == 0) then begin
            use_obj(vault_door_ptr);
         end
         else begin
            call Banished00;
         end
      end

   end

end

procedure Banished00 begin
   if (global_var(GVAR_OVERSEER_KILLED) == 1) then begin
      call Banished01;
   end
   else begin
      call Banished02;
   end
end

procedure Banished01 begin
   display_msg(mstr(101));
   float_msg(self_obj, mstr(random(109, 115)), FLOAT_MSG_RED);
   play_sfx("SLDOORSO");
end

procedure Banished02 begin
   display_msg(mstr(101));
   float_msg(self_obj, mstr(random(104, 108)), FLOAT_MSG_YELLOW);
   play_sfx("SLDOORSO");
end
