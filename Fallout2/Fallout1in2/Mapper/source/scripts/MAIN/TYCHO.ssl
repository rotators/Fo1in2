/*

   Tycho - Party Member, Junktown

*/

#include "define.h"

#define NAME                    SCRIPT_TYCHO

#include "command.h"
#include "modreact.h"

#define PARTY_NODE_X                         Node1000
#undef  set_default_party_follow
#define set_default_party_follow             set_follow_close
#undef  def_weapon_use_msg
#define def_weapon_use_msg                   mstr(164)
#undef  def_wait_msg
#define def_wait_msg                         mstr(151)
#undef  def_nowait_msg
#define def_nowait_msg                       mstr(171)
#undef  def_end_msg
#define def_end_msg                          mstr(166)
#undef  def_gear_msg
#define def_gear_msg                         mstr(2000)

procedure start;
procedure push_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure talk_p_proc;
procedure use_obj_on_p_proc;
procedure combat_p_proc;

procedure Tycho01;
procedure Tycho02;
procedure Tycho03;
procedure Tycho04;
procedure Tycho05;
procedure Tycho06;
procedure Tycho07;
procedure Tycho08;
procedure Tycho09;
procedure Tycho10;
procedure Tycho11;
procedure Tycho12;
procedure Tycho13;
procedure Tycho14;
procedure Tycho15;
procedure Tycho16;
procedure Tycho17;
procedure Tycho18;
procedure Tycho19;
procedure Tycho20;
procedure Tycho21;
procedure Tycho22;
procedure Tycho23;
procedure Tycho24;
procedure Tycho25;
procedure Tycho26;
procedure Tycho27;
procedure Tycho28;
procedure Tycho30;
procedure Tycho31;
procedure Tycho32;
procedure Tycho33; // Fo1in2

procedure Node998;
procedure Node999;
procedure Node1000;
procedure Node1001;
procedure Node1002;
procedure Node1003;
procedure Node1004;
procedure Node1005;
procedure Node1006;
procedure Node1007;
procedure Node1008;
procedure Node1009;
procedure Node1010;
procedure Node1100;

procedure TychoJoins;
procedure TychoReJoins;
procedure TychoTactics;
procedure TychoClose;
procedure TychoModerate;
procedure TychoFar;

procedure NodeWait;

variable night_person;
variable tmp_hostile;
variable cola2_ptr;

procedure TychoCombatTactics;
procedure TychoSniper;
procedure TychoNearCombat;
procedure TychoFreeCombat;

procedure TychoHolsterWeapon;
procedure TychoRemoveArmor;

#define LVAR_Sleeping                     (4)
#define LVAR_WAITING                      (10)
#define LVAR_FOLLOW_DISTANCE              (11)
#define LVAR_TEAM                         (12)

#define float_tycho(x)                    float_msg(self_obj, mstr(x), FLOAT_MSG_NORMAL)
#define tycho_joins_party                 party_add_self;                              \
                                          add_timer_event(self_obj, game_ticks(1), 2); \
                                          set_self_team(TEAM_PLAYER)
#define dude_has_free_slot_for_tycho      fo1in2_party_limit_disabled or ((dude_at_max_party_size == false) and (Tycho_In_Party == false))

#define EVENT_FLOAT_JOIN                  (2)
#define EVENT_FLOAT_INJURED               (3)

#define is_junktown_enemy                 (REP_ENEMY_JUNKTOWN_CITIZENS or REP_ENEMY_JUNKTOWN_BOXER or REP_ENEMY_JUNKTOWN)

#define sleep_time      (2200)
#define wake_time       (1600)
#define home_tile       (19690)
#define sleep_tile      (7000)

procedure Node998 begin
end
procedure Node999 begin
end

procedure start begin
end

procedure push_p_proc begin
   if (Tycho_In_Party == false) then begin
      script_overrides;
   end
   else begin
      if (random(0, 1) == 1) then
         float_tycho(random(310, 313));
   end
end

procedure map_enter_p_proc begin
   if (self_item_count(PID_NUKA_COLA) >= 2) then begin
      cola2_ptr := self_item(PID_NUKA_COLA);
   end
   if (global_var(GVAR_TYCHO_HIRELING_STATUS) < 2) then begin
      set_self_team(TEAM_JUNKTOWNER);
   end
   add_timer_event(self_obj, random(25, 40), EVENT_FLOAT_INJURED);
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_FLOAT_JOIN) then begin
      debug("Tycho is joining!");
   end
   else if (fixed_param == EVENT_FLOAT_INJURED) then begin
      if not(combat_is_initialized) and Tycho_In_Party and self_visible then begin
         if (Is_Crippled(self_obj)) then
            float_tycho(3500);
         else if (self_rads) then
            float_tycho(3501);
         else if (get_poison(self_obj)) then
            float_tycho(3502);
      end
      add_timer_event(self_obj, random(50, 80), EVENT_FLOAT_INJURED);
   end
end

// Fallout Fixt to prevent Tycho from slaughtering his homies
procedure combat_p_proc begin
   if fixt_enabled then begin
      if (REP_ENEMY_JUNKTOWN_CITIZENS or REP_ENEMY_JUNKTOWN_BOXER or REP_ENEMY_JUNKTOWN) then begin
         party_remove(self_obj);
         if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
            float_msg(self_obj, mstr(175 + random(0, 5)), FLOAT_MSG_RED);
         end
         attack_setup(self_obj, dude_obj);
         attack(dude_obj);
      end
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_TYCHO_HIRELING_STATUS) < 2) then begin
      sleeping
   end
   else if (Tycho_In_Party) then begin
      party_member_follow_dude
   end
   else if (self_team == TEAM_PLAYER) then begin
      set_self_abandon_party;
   end

   if (tmp_hostile or is_junktown_enemy) then begin
      if (self_can_see_dude /*or self_can_hear_dude*/) then begin
         tmp_hostile := 0;
         set_self_abandon_party;
         attack(dude_obj);
      end
   end
   else begin
      //call map_commentary;
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_TYCHO_HIRELING_STATUS, 3);

   if (source_is_dude and CUR_AREA_JUNKTOWN) then begin
      set_global_var(GVAR_DUDE_ENEMY_JUNK_CITIZEN, 1);
      set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
      dec_general_rep(5);
   end

   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if (global_var(GVAR_TYCHO_HIRELING_STATUS) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure talk_p_proc begin
   set_tma_data(NAME, 1000, -1, -1, 1105, 1105);

   if Tycho_In_Party then
      self_look_at_dude;
   dude_look_at_self;

   get_reaction

   if is_junktown_enemy then begin
      float_msg(self_obj, mstr(180), FLOAT_MSG_RED);
      tmp_hostile := 1;
   end
   else begin

      if Tycho_In_Party or party_is_waiting then begin
         gdialog_set_barter_mod(255);
      end

      if tycho_is_prisoner then begin
         float_tycho(3503);
         set_tycho_prisoner(false);
         tycho_joins_party;
      end
      else if Tycho_In_Party or party_is_waiting then begin
         start_dialog_at_node(Node1000);
      end
      else if (global_var(GVAR_TYCHO_HIRELING_STATUS) == 0) then begin
         start_dialog_at_node(Tycho01);
      end
      else if ((global_var(GVAR_HIRED_BY_GIZMO) == 1) and (global_var(GVAR_HIRED_BY_KILLIAN) == 0)) then begin
         start_dialog_at_node(Tycho19);
      end
      else if bad_critter_reaction then begin
         start_dialog_at_node(Tycho14);
      end
      else begin
         start_dialog_at_node(Tycho15);
      end

      if Tycho_In_Party and (global_var(GVAR_TYCHO_JOINS_GIVE_EXP) == 0) then begin
         give_xp_msg(EXP_HIRED_TYCHO, mstr(173));
         set_global_var(GVAR_TYCHO_JOINS_GIVE_EXP, 1);
      end

   end

   reset_tma_data;
end

procedure use_obj_on_p_proc begin
   // Give item to party member via backpack-icon (right click menu)
   // Remnants from the past. I'm sure nobody ever used this to begin with. Also it allows exploits (give items over the allowed carryweight).
   /*
   if Tycho_In_Party then begin
      item := obj_pid(obj_being_used_with);
      if ((item != PID_STIMPAK) and (item != PID_FIRST_AID_KIT) and (item != PID_DOCTORS_BAG)) then begin
         script_overrides;
         if (obj_item_subtype(obj_being_used_with) != item_type_weapon) then begin
            rm_obj_from_inven(dude_obj, obj_being_used_with);
            add_obj_to_inven(self_obj, obj_being_used_with);
         end
         else if ((item == 8) or (item == 18) or (item == 143) or (item == 10) or (item == 94) or (item == 7) or (item == 21) or (item == 234) or (item == 235) or (item == 24) or (item == 16) or (item == 120) or (item == 242)) then begin
            rm_obj_from_inven(dude_obj, obj_being_used_with);
            add_obj_to_inven(self_obj, obj_being_used_with);
         end
         else begin
            float_msg(self_obj, message_str(SCRIPT_MODREACT, 109), FLOAT_MSG_LIGHT_GREY);
         end
      end
   end
   */
end

procedure Tycho01 begin
   Reply(102);
   NOption(103, Node999, 4);
   NOption(104, Tycho02, 4);
   NLowOption(105, Tycho03);
end

procedure Tycho02 begin
   if (self_item_count(obj_pid(cola2_ptr))) then begin
      rm_obj_from_inven(self_obj, cola2_ptr);
      add_obj_to_inven(dude_obj, cola2_ptr);
   end
   Reply(106);
   NOption(107, Tycho04, 4);
end

procedure Tycho03 begin
   NMessage(108);
end

procedure Tycho04 begin
   set_global_var(GVAR_TYCHO_HIRELING_STATUS, 1);
   Reply(109);
   NOption(110, Tycho05, 4);
   NOption(111, Tycho06, 6);
end

procedure Tycho05 begin
   Reply(112);
   NOption(113, Tycho07, 4);
   NOption(114, Tycho08, 6);
end

procedure Tycho06 begin
   Reply(115);
   if (local_var(5) != 1) then begin
      NOption(116, Tycho11, 4);
   end
   NOption(117, Tycho09, 6);
end

procedure Tycho07 begin
   Reply(118);
   if (local_var(5) != 1) then begin
      NOption(119, Tycho11, 4);
   end
   else begin
      NOption(exit_line, Node999, 4);
   end
end

procedure Tycho08 begin
   Reply(120);
   if (local_var(5) != 1) then begin
      NOption(121, Tycho11, 4);
   end
   else begin
      NOption(exit_line, Node999, 4);
   end
end

procedure Tycho09 begin
   Reply(122);
   NOption(123, Tycho10, 4);
   if (local_var(5) != 1) then begin
      NOption(116, Tycho11, 4);
   end
end

procedure Tycho10 begin
   Reply(124);
   NOption(125, Tycho17, 4);
   if (local_var(5) != 1) then begin
      NOption(126, Tycho11, 4);
   end
end

procedure Tycho11 begin
   set_local_var(5, 1);
   Reply(127);
   NOption(mstr(128) + dude_name + mstr(129), Tycho12, 4);
   NOption(130, Tycho13, 4);
end

procedure Tycho12 begin
   UpReact
   NMessage(131);
end

procedure Tycho13 begin
   DownReact
   Reply(132);
   NOption(133, Node998, 4);
   NOption(134, Node999, 4);
end

procedure Tycho14 begin
   Reply(135);
   NOption(136, Tycho13, 4);
   NOption(137, Tycho08, 4);
   NOption(138, Tycho16, 4);
end

procedure Tycho15 begin
   Reply(139);
   NOption(137, Tycho08, 4);
   NOption(138, Tycho16, 4);
   if (global_var(GVAR_HIRED_BY_KILLIAN)) then begin
      NOption(140, Tycho18, 4);
   end
end

procedure Tycho16 begin
   Reply(141);
   NOption(exit_line, Node999, 4);
end

procedure Tycho17 begin
   gfade_out(1);

   critter_mod_skill(dude_obj, SKILL_OUTDOORSMAN, 5);
   game_time_advance(game_ticks(120));
   Reply(142);
   gfade_in(1);

   if (local_var(5) != 1) then begin
      NOption(exit_line, Tycho11, 4);
   end
   else begin
      NOption(exit_line, Node999, 4);
   end

   display_msg(gen_mstr(116) + 5 + gen_mstr(134));
end

procedure Tycho18 begin
   if (dude_is_male) then begin
      GMessage(168);
   end
   else begin
      GMessage(169);
   end
   call TychoJoins;
end

procedure Tycho19 begin
   Reply(144);
   NOption(145, Tycho20, 4);
   NOption(146, Tycho21, 4);
   NLowOption(105, Node999);
end

procedure Tycho20 begin
   NMessage(147);
end

procedure Tycho21 begin
   DownReact
   NMessage(148);
end

// Primary node
procedure Tycho22 begin
   Reply(149);
   if (CUR_MAP_FOOT) then begin
      GOption(150, Tycho23, 4);
   end
   if (CUR_MAP_GLOWENT) then begin //  THE GLOW - ENTRANCE (CRATER)
      GOption(150, Tycho25, 4);
   end
   if (CUR_MAP_DETHCLAW) then begin//  THE HUB - DEATHCLAW CAVE
      GOption(150, Tycho26, 4);
   end
   if (CUR_MAP_HALLDED) then begin //  NECROPOLIS - HALLS OF THE DEAD
      GOption(150, Tycho30, 4);
   end
   if (CUR_MAP_MBSTRG12) then begin //  MILITARY BASE - STRONGHOLD (LEVELS 1 AND 2)
      GOption(150, Tycho31, 4);
   end
   if (CUR_MAP_MBVATS12) then begin //  MILITARY BASE -  VATS  (LEVELS 3 AND 4)
      GOption(150, Tycho32, 4);
   end
   GOption(151, Tycho24, 4);
   GOption(164, Tycho27, 4);
   NOption(159, TychoTactics, 4);
   NOption(2000, TychoCombatTactics, 4);
   NOption(166, Node999, 4);
   giq_option(-3, SCRIPT_CARVLEAD, 139, Node999, NEUTRAL_REACTION);
end

procedure Tycho23 begin
   GMessage(152);
   //call Tycho22;
   call Node1000;
end

// Wait here node
procedure Tycho24 begin
   GMessage(153);
   NOption(2511, NodeWait, 004);
end

procedure NodeWait begin
   set_global_var(GVAR_TYCHO_HIRELING_STATUS, 3);
   set_party_waiting;
end

procedure Tycho25 begin
   GMessage(154);
   //call Tycho22;
   call Node1000;
end

procedure Tycho26 begin
   GMessage(155);
   //call Tycho22;
   call Node1000;
end

procedure Tycho27 begin
   GMessage(165);
   call Tycho22;
end

procedure Tycho28 begin
   Reply(170);
   GOption(171, TychoReJoins, 4);
   NOption(172, Node999, 4);
   GLowOption(174, TychoReJoins);
   NLowOption(105, Node999);
end

procedure Tycho30 begin
   GMessage(158);
   //call Tycho22;
   call Node1000;
end

procedure Tycho31 begin
   GMessage(157);
   //call Tycho22;
   call Node1000;
end

procedure Tycho32 begin
   GMessage(156);
   //call Tycho22;
   call Node1000;
end

procedure Tycho33 begin
   GMessage(2200);
   call Node1000;
end

procedure TychoJoins begin
   if dude_has_free_slot_for_tycho then begin
      set_global_var(GVAR_TYCHO_HIRELING_STATUS, 2);
      set_global_var(GVAR_TYCHO_DISTANCE_VAL, 4);
      tycho_joins_party;
   end
   else begin
      Reply(2500);
      NOption(2510, Node999, 004);
   end
end

procedure TychoReJoins begin
   if dude_has_free_slot_for_tycho then begin
      set_global_var(GVAR_TYCHO_HIRELING_STATUS, 2);
      set_global_var(GVAR_TYCHO_DISTANCE_VAL, 4);
      end_party_waiting;
   end
   else begin
      Reply(2500);
      NOption(2510, Node999, 004);
   end
end

procedure TychoTactics begin
   Reply(160);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_CLOSE) then
      NOption(mstr(161), TychoClose, 4);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_MEDIUM) then
      NOption(mstr(162), TychoModerate, 4);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_FAR) then
      NOption(mstr(163), TychoFar, 4);
end

procedure TychoClose begin
   set_follow_close;

   /*set_local_var(6, 0);
   set_local_var(7, 0);
   set_local_var(8, 0);
   set_global_var(GVAR_TYCHO_DISTANCE_VAL, 2);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Tycho22;
end

procedure TychoModerate begin
   set_follow_medium;

   /*set_local_var(6, 0);
   set_local_var(7, 0);
   set_local_var(8, 0);
   set_global_var(GVAR_TYCHO_DISTANCE_VAL, 4);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Tycho22;
end

procedure TychoFar begin
   set_follow_far;

/* set_local_var(6, 0);
   set_local_var(7, 0);
   set_local_var(8, 0);
   set_global_var(GVAR_TYCHO_DISTANCE_VAL, 6);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Tycho22;
end

procedure TychoCombatTactics begin
   Reply(2001);

   if (self_is_armed) then
      GOption(2010, TychoHolsterWeapon, 4);

   if (self_wearing_armor) then
      GOption(2011, TychoRemoveArmor, 4);

   GOption(2012, TychoSniper, 4);
   GOption(2013, TychoNearCombat, 4);
   GOption(2014, TychoFreeCombat, 4);
   GOption(2015, Tycho22, 4);
end

procedure TychoHolsterWeapon begin
   inven_unwield(self_obj);
   GMessage(2100);
   call TychoCombatTactics;
end

procedure TychoRemoveArmor begin
   remove_armor(self_obj)
   GMessage(2100);
   call TychoCombatTactics;
end

procedure TychoSniper begin
   GMessage(2100);
   //set_self_ai( AI_PARTY_TYCHO_AGGRESSIVE );
   call TychoCombatTactics;
end

procedure TychoNearCombat begin
   GMessage(2100);
   //set_self_ai( AI_PARTY_TYCHO_BERSERK );
   call TychoCombatTactics;
end

procedure TychoFreeCombat begin
   GMessage(2100);
   //set_self_ai( AI_TYCHO );
   call TychoCombatTactics;
end

// party stuff
procedure Node1000 begin
   if party_is_waiting then begin
      Reply(170);
   end
   else begin
      Reply(mstr(149));
      if (CUR_AREA_JUNKTOWN and is_gizmo_alive) then begin
         NOption(150, Tycho33, 004);
      end
      else if (CUR_MAP_FOOT) then begin
         GOption(150, Tycho23, 4);
      end
      else if (CUR_MAP_GLOWENT) then begin
         GOption(150, Tycho25, 4);
      end
      else if (CUR_MAP_DETHCLAW) then begin
         GOption(150, Tycho26, 4);
      end
      else if (CUR_MAP_HALLDED) then begin
         GOption(150, Tycho30, 4);
      end
      else if (CUR_MAP_MBSTRG12) then begin
         GOption(150, Tycho31, 4);
      end
      else if (CUR_MAP_MBVATS12) then begin
         GOption(150, Tycho32, 4);
      end
   end
   party_member_default_options;
end

procedure Node1001 begin
   // heal yourself
   obj_heal(self_obj)
   global_temp := 0;
   if (party_healed_max) then begin
      Reply(300);
   end else if (party_healed_good) then begin
      Reply(301);
   end else if (party_healed_hurt) then begin
      Reply(302);
   end else begin
      Reply(303);
   end

   party_member_default_options;
end
procedure Node1002 begin
   set_global_var(GVAR_TYCHO_HIRELING_STATUS, 3);

   // wait here
   set_party_waiting;
   Reply(153);
   party_member_default_options;
end
procedure Node1003 begin
   // put your weapon away
   inven_unwield(self_obj);
   Reply(random(2100, 2101));
   party_member_default_options;
end
procedure Node1004 begin
   // follow close
   set_follow_close;
   Reply(random(2100, 2101));
   party_member_default_options;
end
procedure Node1005 begin
   // follow medium
   set_follow_medium;
   Reply(random(2100, 2101));
   party_member_default_options;
end
procedure Node1006 begin
   // follow far
   set_follow_far;
   Reply(random(2100, 2101));
   party_member_default_options;
end
procedure Node1007 begin
   // distance
   Reply(160);
   party_member_def_follow_options;
end
procedure Node1008 begin
   // gear
   Reply(170);
   party_member_def_gear_options;
end
procedure Node1009 begin
   // remove armor
   remove_armor(self_obj)
   Reply(random(2100, 2101));
   party_member_default_options;
end
procedure Node1010 begin
   // weapons that can be used
   Reply(165);
   party_member_default_options;
end
procedure Node1100 begin
   // join me
   if fo1in2_party_limit_disabled or not(dude_at_max_party_size) then begin
      set_global_var(GVAR_TYCHO_HIRELING_STATUS, 2);
      end_party_waiting;
      Reply(2102);
      party_member_default_options;
   end
   else begin
      set_self_abandon_party;
      Reply(2500);
      NOption(g_mstr(10007), Node999, 001);
   end
end
