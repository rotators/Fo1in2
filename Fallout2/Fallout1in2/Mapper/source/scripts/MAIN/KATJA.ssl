/*

   Katja - Party Member, Boneyard

*/

#include "define.h"

#define NAME                    SCRIPT_KATJA

#include "command.h"
#include "modreact.h"

#define PARTY_NODE_X                         Node1000
#undef  set_default_party_follow
#define set_default_party_follow             set_follow_close
#undef  def_weapon_use_msg
#define def_weapon_use_msg                   mstr(177)
#undef  def_wait_msg
#define def_wait_msg                         mstr(167)
#undef  def_nowait_msg
#define def_nowait_msg                       mstr(182)
#undef  def_end_msg
#define def_end_msg                          mstr(2015)
#undef  def_gear_msg
#define def_gear_msg                         mstr(2000)

procedure start;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;
procedure use_obj_on_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;
procedure push_p_proc;

procedure Katja01;
procedure Katja02;
procedure Katja03;
procedure Katja04;
procedure Katja05;
procedure Katja06;
procedure Katja07;
procedure Katja08;
procedure Katja09;
procedure Katja10;
procedure Katja11;
procedure Katja12;
procedure Katja13;
procedure Katja14;
procedure Katja15;
procedure Katja16;
procedure Katja17;
procedure Katja18;
procedure Katja19;
procedure Katja20;
procedure Katja21;
procedure Katja22;
procedure Katja23;
procedure Katja24;
procedure Katja25;
procedure Katja26;
procedure Katja28;

procedure Node998;
procedure Node999;
procedure Node1000;
procedure Node1001;
procedure Node1002;
procedure Node1003;
procedure Node1004;
procedure Node1005;
procedure Node1006;
procedure Node1007;
procedure Node1008;
procedure Node1009;
procedure Node1010;
procedure Node1100;

procedure KatjaLeave;
procedure KatjaTactics;
procedure KatjaClose;
procedure KatjaModerate;
procedure KatjaFar;
procedure KatjaExit;

procedure map_commentary;
procedure pick_lock;
procedure set_lock;
procedure KatjaCombatTactics;

procedure KatjaSniper;
procedure KatjaNearCombat;
procedure KatjaFreeCombat;

procedure KatjaHolsterWeapon;
procedure KatjaRemoveArmor;

variable tmp_hostile;
variable item;
variable lock;

variable line173flag;
variable line174flag;
variable line176flag;
variable line184flag;
variable line186flag;
variable line187flag;
variable line188flag;

#define LVAR_WAITING                      (10)
#define LVAR_FOLLOW_DISTANCE              (11)
#define LVAR_TEAM                         (12)

#define float_katja(x)                    float_msg(self_obj, mstr(x), FLOAT_MSG_NORMAL)
#define katja_joins_party                 party_add_self;                              \
                                          add_timer_event(self_obj, game_ticks(1), 2); \
                                          set_self_team(TEAM_PLAYER)
#define dude_has_free_slot_for_katja      fo1in2_party_limit_disabled or ((dude_at_max_party_size == false) and (Katja_In_Party == false))

#define EVENT_FLOAT_JOIN                  (2)
#define EVENT_FLOAT_INJURED               (3)

procedure Node998 begin
end
procedure Node999 begin
end

procedure start begin
end

procedure push_p_proc begin
   if (Katja_In_Party == false) then begin
      script_overrides;
   end
   else begin
      if (random(0, 1) == 1) then
         float_katja(random(310, 313));
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_FLOAT_JOIN) then begin
      debug("Katja is joining!");
   end if (fixed_param == EVENT_FLOAT_INJURED) then begin
      if not(combat_is_initialized) and self_visible then begin
         if (Katja_ptr != 0) then begin
            if (Is_Crippled(self_obj)) then
               float_katja(3500);
            /*else if (self_rads) then
               float_katja(3501);
            else if (get_poison(self_obj)) then
               float_katja(3502);*/
         end
      end
      add_timer_event(self_obj, random(50, 80), EVENT_FLOAT_INJURED);
   end
end

procedure critter_p_proc begin
   if (Katja_In_Party) then begin
      party_member_follow_dude
   end
   else if (self_team == TEAM_PLAYER) then begin
      set_self_abandon_party;
   end

   if (self_can_see_dude) and (global_var(GVAR_ENEMY_FOLLOWERS)) then begin
      tmp_hostile := 1;
   end

   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_self_abandon_party;
      attack(dude_obj);
   end
   else begin
      call map_commentary;
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_KATJA_HIRELING_STATUS, 3);
   inc_good_critter
end

procedure map_enter_p_proc begin
   if (global_var(GVAR_KATJA_HIRELING_STATUS) < 2) then begin
      set_self_team(TEAM_LA_ADYTOWNER);
   end
   add_timer_event(self_obj, random(25, 40), EVENT_FLOAT_INJURED);
end

procedure use_obj_on_p_proc begin
   // Give item to party member via backpack-icon (right click menu)
   // Remnants from the past. I'm sure nobody ever used this to begin with. Also it allows exploits (give items over the allowed carryweight).
   /*
   if Katja_In_Party then begin
      item := obj_pid(obj_being_used_with);
      if ((item != PID_STIMPAK) and (item != PID_FIRST_AID_KIT) and (item != PID_DOCTORS_BAG)) then begin
         script_overrides;
         if (obj_item_subtype(obj_being_used_with) != item_type_weapon) then begin
            rm_obj_from_inven(dude_obj, obj_being_used_with);
            add_obj_to_inven(self_obj, obj_being_used_with);
         end
         else if ((item == 4) or (item == 9) or (item == 22) or (item == 45) or (item == 7) or (item == 159) or (item == 25) or (item == 26) or (item == 27) or (item == 19) or (item == 21) or (item == 234) or (item == 235) or (item == 116) or (item == 236) or (item == 241)) then begin
            rm_obj_from_inven(dude_obj, obj_being_used_with);
            add_obj_to_inven(self_obj, obj_being_used_with);
         end
         else begin
            float_msg(self_obj, message_str(SCRIPT_MODREACT, 109), FLOAT_MSG_PURPLE);
         end
      end
   end
   */
end

procedure look_at_p_proc begin
   script_overrides;
   if (global_var(GVAR_KATJA_HIRELING_STATUS) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end


procedure talk_p_proc begin
   set_tma_data(NAME, 1000, -1, -1, TMA_MSG_DONTKNOW_START, TMA_MSG_DONTKNOW_END);

   if Katja_In_Party then
      self_look_at_dude;
   dude_look_at_self;

   start_gdialog(623, self_obj, 4, -1, -1);

   if (Katja_In_Party or party_is_waiting) then begin
      gdialog_set_barter_mod(255);
   end

   gsay_start;

   if katja_is_prisoner then begin
      float_katja(3503);
      set_katja_prisoner(false);
      katja_joins_party;
   end
   else if (Katja_In_Party or party_is_waiting) then begin
      //call Katja24;
      call Node1000;
   end
   else if (global_var(GVAR_KATJA_HIRELING_STATUS) == 1) then begin
      call Katja25;
   end
   else begin
      call Katja01;
   end

   gsay_end;
   end_dialogue;

   if (lock) then begin
      call pick_lock;
   end

   if Katja_In_Party then begin
      if (global_var(GVAR_KATJA_JOINS_GIVE_EXP) == 0) then begin
         set_global_var(GVAR_KATJA_JOINS_GIVE_EXP, 1);
         give_xp_msg(EXP_HIRED_KATJA, mstr(180));
      end
   end

   reset_tma_data;
end

procedure Katja01 begin
   Reply(102);
   NOption(mstr(103) + dude_name + mstr(104), Katja02, 4);
   NOption(105, Katja02, 4);
   NOption(106, Katja05, 5);
   NLowOption(189, KatjaExit);
end

procedure Katja02 begin
   Reply(107);
   NOption(108, Katja03, 5);
   NOption(109, Katja04, 5);
   NOption(110, Katja07, 4);
   if looking_for_waterchip then begin
      NOption(111, Katja06, 4);
   end
end

procedure Katja03 begin
   Reply(112);
   NOption(109, Katja04, 5);
   NOption(110, Katja07, 4);
   if looking_for_waterchip then begin
      NOption(111, Katja06, 4);
   end
end

procedure Katja04 begin
   set_global_var(GVAR_KATJA_HIRELING_STATUS, 1);
   Reply(113);
   NOption(114, Katja21, 5);
   NOption(116, Katja09, 4);
   NOption(115, Node999, 4);
end

procedure Katja05 begin
   Reply(117);
   NOption(mstr(103) + dude_name + mstr(104), Katja02, 4);
   NOption(118, Katja07, 4);
   NOption(115, Node999, 4);
end

procedure Katja06 begin
   Reply(119);
   NOption(121, Katja11, 5);
   NOption(122, Katja10, 4);
   NOption(120, Node999, 4);
end

procedure Katja07 begin
   Reply(123);
   NOption(124, Katja06, 4);
   NOption(125, Katja08, 4);
end

procedure Katja08 begin
   BMessage(126);
end

procedure Katja09 begin
   Reply(127);
   if looking_for_waterchip then begin
      NOption(128, Katja06, 4);
   end
   NOption(129, Katja10, 4);
   NOption(130, Katja17, 4);
end

procedure Katja10 begin
   NOption(131, Katja13, 4);
   NOption(133, Katja15, 4);
   NOption(134, Katja16, 4);
   NOption(135, Katja12, 4);
   NOption(130, Katja17, 4);
   NOption(136, Node999, 4);
end

procedure Katja11 begin
   Reply(137);
   NOption(135, Katja12, 4);
   NOption(138, Node999, 4);
end

procedure Katja12 begin
   Reply(139);
   call Katja10;
end

procedure Katja13 begin
   Reply(140);
   call Katja10;
end

procedure Katja14 begin
   Reply(141);
   call Katja10;
end

procedure Katja15 begin
   Reply(142);
   call Katja10;
end

procedure Katja16 begin
   Reply(143);
   call Katja10;
end

procedure Katja17 begin
   mark_on_map(AREA_CATHEDRAL)

   Reply(144);
   NOption(145, Katja18, 4);
   NOption(147, Katja10, 4);
   NOption(146, Node999, 4);
end

procedure Katja18 begin
   Reply(148);
   NOption(149, Katja20, 4);
   NOption(151, Katja10, 4);
   NOption(150, Node999, 4);
end

procedure Katja19 begin
   Reply(152);
   call Katja10;
end

procedure Katja20 begin
   if dude_has_free_slot_for_katja then begin
      set_global_var(GVAR_KATJA_DISTANCE_VAL, 4);

      katja_joins_party;

      set_self_team(TEAM_PLAYER);
      if (global_var(GVAR_KATJA_HIRELING_STATUS) == 0) then begin
         set_global_var(GVAR_KATJA_HIRELING_STATUS, 2);
         NMessage(2200);
      end
      else begin
         set_global_var(GVAR_KATJA_HIRELING_STATUS, 2);
         NMessage(153);
      end
   end
   else begin
      Reply(2500);
      NOption(2510, Node999, 004);
   end
end

procedure Katja21 begin
   Reply(154);
   NOption(156, Katja22, 4);
   NOption(157, Katja23, 6);
   NOption(155, Node999, 4);
end

procedure Katja22 begin
   Reply(158);
   call Katja10;
end

procedure Katja23 begin
   Reply(159);
   NOption(160, Katja09, 4);
   if looking_for_waterchip then begin
      NOption(161, Katja06, 4);
   end
   NOption(162, Node999, 4);
end

procedure Katja24 begin
   Reply(163);
   if (CUR_MAP_MBENT) then begin
      if (map_var(3)) then begin
         NOption(164, set_lock, 4);
      end
   end
   if ((CUR_MAP_JUNKENT) and (dude_elevation == 1)) then begin
      if (map_var(4)) then begin
         NOption(164, set_lock, 4);
      end
   end
   NOption(167, KatjaLeave, 4);
   NOption(177, Katja26, 4);
   giq_option(4, SCRIPT_TYCHO, 159, KatjaTactics, GOOD_REACTION);
   NOption(2000, KatjaCombatTactics, 4);
   NOption(168, Node999, 4);
   NLowOption(189, KatjaExit);
end

procedure Katja25 begin
   Reply(169);
   NOption(171, Katja10, 4);
   NOption(170, Node999, 4);
   NLowOption(189, KatjaExit);
end

procedure Katja26 begin
   NMessage(178);
   call Katja24;
end

procedure Katja28 begin
   Reply(181);
   NOption(182, Katja20, 4);
   NOption(183, Node999, 4);
   NLowOption(gen_mstr(196), Katja20);
   NLowOption(189, KatjaExit);
end

procedure KatjaLeave begin
   set_global_var(GVAR_KATJA_HIRELING_STATUS, 3);
   //party_remove(self_obj);
   set_party_waiting;
end

procedure KatjaTactics begin
   gsay_reply(SCRIPT_TYCHO, 160);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_CLOSE) then
      giq_option(4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 161), KatjaClose, NEUTRAL_REACTION);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_MEDIUM) then
      giq_option(4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 162), KatjaModerate, NEUTRAL_REACTION);

   if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_FAR) then
      giq_option(4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 163), KatjaFar, NEUTRAL_REACTION);
end

procedure KatjaClose begin
   set_follow_close;

   /*set_local_var(1, 0);
   set_local_var(2, 0);
   set_local_var(3, 0);
   set_global_var(GVAR_KATJA_DISTANCE_VAL, 2);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Katja24;
end

procedure KatjaModerate begin
   set_follow_medium;

   /*set_local_var(1, 0);
   set_local_var(2, 0);
   set_local_var(3, 0);
   set_global_var(GVAR_KATJA_DISTANCE_VAL, 4);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Katja24;
end

procedure KatjaFar begin
   set_follow_far;

   /*set_local_var(1, 0);
   set_local_var(2, 0);
   set_local_var(3, 0);
   set_global_var(GVAR_KATJA_DISTANCE_VAL, 6);*/
   gsay_message(SCRIPT_IAN, 201, NEUTRAL_REACTION);
   call Katja24;
end

procedure KatjaExit begin
   NMessage(191);
end

procedure map_commentary begin
   if (CUR_MAP_MSTRLR12) then begin
      if ((tile_distance(self_tile, 27317) < 4) and (line173flag == 0)) then begin
         float_msg(self_obj, mstr(173), FLOAT_MSG_PURPLE);
         line173flag := 1;
      end
   end
   else if (CUR_MAP_VAULT13) then begin
      if (self_elevation == 1) then begin
         if (tile_distance(self_tile, 19111) < 4) then begin
            if ((line174flag == 0) and (global_var(GVAR_QUEST_VAULT13_1_REBELS) != 2) and (Ian_ptr != 0)) then begin
               float_msg(self_obj, mstr(172), FLOAT_MSG_PURPLE);
               line174flag := 1;
            end
         end
      end
   end
   else if (CUR_MAP_HUBDWNTN) then begin
      if ((tile_distance(self_tile, 28104) < 5) and (line176flag == 0)) then begin
         float_msg(self_obj, mstr(176), FLOAT_MSG_PURPLE);
         if (global_var(GVAR_TYCHO_HIRELING_STATUS) == 2) then begin
            float_msg(Tycho_ptr, message_str(SCRIPT_TYCHO, 167), FLOAT_MSG_LIGHT_GREY);
         end
         line176flag := 1;
      end
   end
   else if (CUR_MAP_MBVATS12) then begin
      if (line184flag == 0) then begin
         float_msg(self_obj, mstr(184), FLOAT_MSG_PURPLE);
         if (Ian_In_Party) then begin
            float_msg(Ian_ptr, message_str(SCRIPT_IAN, 175), FLOAT_MSG_NORMAL);
         end
         line184flag := 1;
      end
   end
   else if (CUR_MAP_MSTRLR34) then begin
      if (line186flag == 0) then begin
         float_msg(self_obj, mstr(186), FLOAT_MSG_PURPLE);
         line186flag := 1;
      end
   end
   else if (CUR_MAP_HALLDED) then begin
      if ((tile_distance(self_tile, 17274) < 4) and (line187flag == 0)) then begin
         float_msg(self_obj, mstr(187), FLOAT_MSG_PURPLE);
         line187flag := 1;
      end
   end
   else if (CUR_MAP_LARIPPER) then begin
      if (line188flag == 0) then begin
         float_msg(self_obj, mstr(188), FLOAT_MSG_PURPLE);
         line188flag := 1;
      end
   end
end

procedure pick_lock begin
   reg_anim_clear(self_obj);
   reg_anim_begin();
   reg_anim_obj_move_to_obj(self_obj, lock, -1);
   reg_anim_animate(self_obj, ANIM_magic_hands_middle, -1);
   reg_anim_end();
   use_obj(lock);
   obj_unlock(lock);
   lock := 0;
end

procedure set_lock begin
   if (CUR_MAP_MBENT) then begin //  MILITARY BASE - OUTSIDE ENTRANCE
      lock := map_var(3);
   end
   else if (CUR_MAP_JUNKENT) then begin //  JUNKTOWN - ENTRANCE, MORBID, LARS
      lock := map_var(4);
   end
end

procedure KatjaCombatTactics begin
   Reply(2001);

   if (self_is_armed) then
      GOption(2010, KatjaHolsterWeapon, 4);

   if (self_wearing_armor) then
      GOption(2011, KatjaRemoveArmor, 4);

   GOption(2012, KatjaSniper, 4);
   GOption(2013, KatjaNearCombat, 4);
   GOption(2014, KatjaFreeCombat, 4);
   GOption(2015, Katja24, 4);
end

procedure KatjaHolsterWeapon begin
   inven_unwield(self_obj);
   Reply(2100);
   call KatjaCombatTactics;
end

procedure KatjaRemoveArmor begin
   remove_armor(self_obj)
   Reply(2100);
   call KatjaCombatTactics;
end

procedure KatjaSniper begin
   GMessage(2100);
   //set_self_ai( AI_PARTY_KATJA_AGGRESSIVE );
   call KatjaCombatTactics;
end

procedure KatjaNearCombat begin
   GMessage(2100);
   //set_self_ai( AI_PARTY_KATJA_BERSERK );
   call KatjaCombatTactics;
end

procedure KatjaFreeCombat begin
   GMessage(2100);
   //set_self_ai( AI_GUARD );
   call KatjaCombatTactics;
end


// party stuff
procedure Node1000 begin
   if party_is_waiting then begin
      Reply(181);
   end
   else begin
      Reply(mstr(163));
      // Lockpick
      if (CUR_MAP_MBENT) then begin //  MILITARY BASE - OUTSIDE ENTRANCE
         if (map_var(3)) then begin
            NOption(164, set_lock, 4);
         end
      end
      if ((CUR_MAP_JUNKENT) and (dude_elevation == 1)) then begin //  JUNKTOWN - ENTRANCE, MORBID, LARS
         if (map_var(4)) then begin
            NOption(164, set_lock, 4);
         end
      end
   end
   party_member_default_options;
end

procedure Node1001 begin
   // heal yourself
   obj_heal(self_obj)
   global_temp := 0;
   if (party_healed_max) then begin
      Reply(300);
   end else if (party_healed_good) then begin
      Reply(301);
   end else if (party_healed_hurt) then begin
      Reply(302);
   end else begin
      Reply(303);
   end

   party_member_default_options;
end
procedure Node1002 begin
   set_global_var(GVAR_KATJA_HIRELING_STATUS, 3);

   // wait here
   set_party_waiting;
   Reply(2100);
   party_member_default_options;
end
procedure Node1003 begin
   // put your weapon away
   inven_unwield(self_obj);
   Reply(179);
   party_member_default_options;
end
procedure Node1004 begin
   // follow close
   set_follow_close;
   Reply(2100);
   party_member_default_options;
end
procedure Node1005 begin
   // follow medium
   set_follow_medium;
   Reply(2100);
   party_member_default_options;
end
procedure Node1006 begin
   // follow far
   set_follow_far;
   Reply(2100);
   party_member_default_options;
end
procedure Node1007 begin
   // distance
   Reply(2100);
   party_member_def_follow_options;
end
procedure Node1008 begin
   // gear
   Reply(2001);
   party_member_def_gear_options;
end
procedure Node1009 begin
   // remove armor
   remove_armor(self_obj)
   Reply(2100);
   party_member_default_options;
end
procedure Node1010 begin
   // weapons that can be used
   Reply(178);
   party_member_default_options;
end
procedure Node1100 begin
   // join me
   if fo1in2_party_limit_disabled or not(dude_at_max_party_size) then begin
      set_global_var(GVAR_KATJA_HIRELING_STATUS, 2);
      end_party_waiting;
      Reply(153);
      party_member_default_options;
   end
   else begin
      set_self_abandon_party;
      Reply(2500);
      NOption(g_mstr(10007), Node999, 001);
   end
end
