/*

	Raiders - Generic raider guard

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_RAIDGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure timed_event_p_proc;
procedure description_p_proc;

import variable killing_women;
procedure guard00;
procedure guard01;
procedure guard02;
procedure guard03;
procedure guard04;
procedure guard05;
procedure guard06;
procedure guard07;
procedure guard07a;
procedure guard07b;
procedure guard08;
procedure guard09;
procedure guard10;
procedure guard11;
procedure guard12;
procedure guard13;
procedure guard14;
procedure guard15;
procedure combat;
procedure PickDeadBodyType;

variable tmp_hostile;
variable initial := 0;
variable rndz;
variable Weapons;

variable DeathType := 56;
variable LastTime;

procedure start begin
	if shady_invaded then begin
		if (CUR_MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	if not(initial) then begin
		initial := 1;
		set_self_team(TEAM_RAIDERS);
		set_self_ai(AI_RAIDER_GUARD);
		debug_msg("RAIDGRD init complete.");
	end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust(random(1, dude_luck * 2));
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_GENRAIDR, 101));
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure description_p_proc
begin
	script_overrides;
	if (self_is_male) then begin// MALE raider
		display_msg(message_str(SCRIPT_GENRAIDR, random(200, 202)));
	end
	else begin
		display_msg(message_str(SCRIPT_GENRAIDR, random(203, 205)));// FEMALE raider
	end
end

procedure talk_p_proc begin
	if (local_var(5) == 1) then begin
		display_msg(message_str(SCRIPT_GENRAIDR, 100));
	end
	else begin
		if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
			call guard00;
		end
		else begin
			if ((Weapons == 1) and (killing_women == 0) and (global_var(GVAR_GARLS_FRIEND) != 1)) then begin
				call guard02;
			end
			else begin
				set_local_var(3, 1);
				get_reaction
				start_gdialog(136, self_obj, 4, -1, -1);
				gsay_start;
				if (local_var(4) == 0) then begin
					set_local_var(4, 1);
					call guard04;
				end
				else begin
					call guard15;
				end
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure guard00
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
end

procedure guard01
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(107, 111)), FLOAT_MSG_YELLOW);
end

procedure guard02
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(112, 115)), FLOAT_MSG_YELLOW);
	add_timer_event(self_obj, game_ticks(9), 1);
end

procedure guard03
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(116, 118)), FLOAT_MSG_YELLOW);
	add_timer_event(self_obj, game_ticks(10), 2);
end

procedure guard04
begin
	gsay_reply(SCRIPT_GENRAIDR, 119);
	if (tandi_quest_active and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
		giq_option(4, SCRIPT_GENRAIDR, 120, guard07, NEUTRAL_REACTION);
	end
	giq_option(4, SCRIPT_GENRAIDR, 121, guard06, NEUTRAL_REACTION);
	giq_option(-3, SCRIPT_GENRAIDR, 122, guard05, NEUTRAL_REACTION);
end

procedure guard05
begin
	gsay_message(SCRIPT_GENRAIDR, 123, NEUTRAL_REACTION);
end

procedure guard06
begin
	gsay_message(SCRIPT_GENRAIDR, 124, NEUTRAL_REACTION);
	DownReact
end

procedure guard07
begin
	gsay_reply(SCRIPT_GENRAIDR, 125);
	giq_option(4, SCRIPT_GENRAIDR, 126, guard08, NEUTRAL_REACTION);
	giq_option(6, SCRIPT_GENRAIDR, 127, guard07a, NEUTRAL_REACTION);
	if (is_success(do_check(dude_obj, STAT_pe, 0))) then begin
		giq_option(4, SCRIPT_GENRAIDR, 128, guard07b, NEUTRAL_REACTION);
	end
end

procedure guard07a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call guard09;
	end
	else begin
		call guard11;
	end
end

procedure guard07b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20))) then begin
		call guard12;
	end
	else begin
		call guard13;
	end
end

procedure guard08
begin
	gsay_message(SCRIPT_GENRAIDR, 129, NEUTRAL_REACTION);
end

procedure guard09
begin
	gsay_reply(SCRIPT_GENRAIDR, 130);
	giq_option(6, SCRIPT_GENRAIDR, 131, guard10, NEUTRAL_REACTION);
end

procedure guard10
begin
	gsay_message(SCRIPT_GENRAIDR, 132, NEUTRAL_REACTION);
end

procedure guard11
begin
	gsay_message(SCRIPT_GENRAIDR, 133, NEUTRAL_REACTION);
end

procedure guard12
begin
	gsay_message(SCRIPT_GENRAIDR, 134, NEUTRAL_REACTION);
	set_local_var(5, 1);
end

procedure guard13
begin
	gsay_message(SCRIPT_GENRAIDR, 135, NEUTRAL_REACTION);
	call combat;
end

procedure guard14
begin
	gsay_message(SCRIPT_GENRAIDR, 136, NEUTRAL_REACTION);
	add_timer_event(self_obj, game_ticks(12), 3);
end

procedure guard15
begin
	gsay_message(SCRIPT_GENRAIDR, 137, NEUTRAL_REACTION);
end

procedure combat
begin
	if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
		set_self_team(TEAM_87);
	end
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		set_global_var(GVAR_ENEMY_RAIDERS, 1);
		attack(dude_obj);
	end
	else begin
		if get_tandi_returned or global_var(GVAR_TANDI_ESCAPE) or (map_var(2) == 1) or global_var(GVAR_RAID_LOOTING) then begin
			set_global_var(GVAR_ENEMY_RAIDERS, 1);
		end
	end
	if global_var(GVAR_MISTAKEN_ID) then begin//Raiders think you are Garl's father
		set_global_var(GVAR_ENEMY_RAIDERS, 0);
		if self_can_see_dude then begin
			if (local_var(7) == 0) then begin
				set_local_var(7, 1);
				LastTime := game_time;
				float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
			end
			else begin
				if (anim_busy(self_obj) == 0) and (game_time - LastTime > 100) then begin
					rndz := random(0, 20);
					if (tile_distance_objs(self_obj, dude_obj) < 6) and (rndz == 1) then begin
						LastTime := game_time;
						float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
					end
				end
			end
		end
	end
	else begin
		if self_can_see_dude then begin
			if global_var(GVAR_ENEMY_RAIDERS) then begin
				tmp_hostile := 1;
			end
			else begin
				if (global_var(GVAR_GARLS_FRIEND) != 1) then begin
					if (dude_is_armed) and ((Weapons == 0) and (killing_women == 0)) then begin
						Weapons := 1;
						dialogue_system_enter;
					end
				end
			end
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var(GVAR_ENEMY_RAIDERS, 1);
		end
	end
end

procedure timed_event_p_proc begin
	if (fixed_param == 1) then begin
		if (dude_is_armed) then begin
			call combat;
		end
	end
	else if (fixed_param == 2) then begin
		if (tile_distance_objs(self_obj, dude_obj) < 6) then begin
			call combat;
		end
	end
	else if (fixed_param == 3) then begin
		if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
			call combat;
		end
	end
end

procedure destroy_p_proc begin
	set_global_var(GVAR_TOTAL_RAIDERS, (get_raiders_alive - 1));// Number of raiders left
	//if (source_obj == dude_obj) then begin
		set_global_var(GVAR_ENEMY_RAIDERS, 1);
		set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND
		inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
	//end
	rm_timer_event(self_obj);
	inc_inv_evil_crit
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
