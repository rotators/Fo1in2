/*

   Raiders - Diana

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_DIANA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;

procedure Diana00;
procedure Diana01;
procedure Diana02;
procedure Diana03;
procedure Diana04;
procedure Diana05;
procedure Diana10;
procedure Diana11;
procedure Diana12;
procedure Dianaend;
procedure combat;

#define LVAR_Herebefore       (4)

variable tmp_hostile;
variable Only_Once := 1;
variable rndz;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_RAIDER_GUARD);
      debug_msg("DIANA init.");
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure Diana00 begin
   Reply(101);
   if (tandi_quest_active and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(102, Diana01, 4);
   end
   NOption(103, Dianaend, 4);
   if (dude_is_male) then begin
      NOption(104, Diana02, 4);
   end
   NOption(105, Diana04, 4);
   if (local_var(5) == 0) then begin
      NOption(106, Diana05, 7);
   end
   NLowOption(107, Diana10);
end

procedure Diana01 begin
   Reply(108);
   NOption(109, Dianaend, 4);
   NOption(110, Diana03, 4);
end

procedure Diana02 begin
   NMessage(111);
end

procedure Diana03 begin
   NMessage(112);
end

procedure Diana04 begin
   Reply(113);
   NOption(114, Diana03, 4);
   if (local_var(5) == 0) then begin
      NOption(115, Diana05, 7);
   end
end

procedure Diana05 begin
   Reply(116);
   set_local_var(5, 1);
   if (tandi_quest_active and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(117, Diana01, 4);
   end
   NOption(118, Diana03, 4);
end

procedure Diana10 begin
   NMessage(119);
end

procedure Diana11 begin
   Reply(120);
   NOption(104, Diana10, 4);
   NOption(118, Diana12, 4);
   NLowOption(107, Diana10);
end

procedure Diana12 begin
   NMessage(121);
end

procedure Dianaend begin
end

procedure combat begin
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      set_self_team(TEAM_87);
   end
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_RAIDERS);

   dude_look_at_self;
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
   end
   else begin
      set_local_var(7, 1);
      set_local_var(3, 1);

      get_reaction
      if (local_var(LVAR_Herebefore)) then begin
         start_dialog_at_node(Diana10);
      end
      else if (global_var(GVAR_GARLS_FRIEND) == 1) then begin
         start_dialog_at_node(Diana11);
      end
      else begin
         set_local_var(LVAR_Herebefore, 1);
         start_dialog_at_node(Diana00);
      end
   end
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   rndz := random(0, 500);
// set_local_var(10, game_time);
// set_local_var(11, game_time + 5);
   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 0);
         if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
            LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
            if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
               if (rndz == 0) then begin
                  float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
               end
               self_walk_to_tile(LVar0);
            end
         end
      end
      else begin
         if global_var(GVAR_TANDI_ESCAPE) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
         if (self_can_see_dude) then begin
            if (global_var(GVAR_RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
               set_global_var(GVAR_ENEMY_RAIDERS, 1);
            end
         end
         if (map_var(2) == 1) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
      end
   end
   if (REP_ENEMY_RAIDERS and self_can_see_dude) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      attack(dude_obj);
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_TOTAL_RAIDERS);
   //if source_is_dude then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND
      inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
   //end
   inc_inv_evil_crit
end

procedure description_p_proc begin
   if not(local_var(7)) then begin
      script_overrides;
      display_msg(mstr(200));
   end
   set_local_var(7, 1);
end

procedure look_at_p_proc begin
   script_overrides;
   if not(local_var(7)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(200));
   end
end
