/*

   Raiders - Alya

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_ALYA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;

procedure alya00;
procedure alya01;
procedure alya02;
procedure alya03;
procedure alya04;
procedure alya05;
procedure alya06;
procedure alya07;
procedure alya08;
procedure alya09;
procedure alya10;
procedure alyaend;

procedure combat;

variable tmp_hostile;
variable initial := 0;
variable rndz;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_RAIDER_GUARD);
      debug_msg("ALYA init.");
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end
end

procedure timed_event_p_proc begin
   if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
      call combat;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure alya00 begin
   Reply(101);
   if (tandi_quest_active and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(102, alya01, 4);
   end
   if (dude_is_male) then begin
      NOption(103, alya02, 4);
   end
   NOption(104, alya03, 4);
   NOption(105, alyaend, 4);
   NLowOption(106, alya05);
end

procedure alya01 begin
   NMessage(107);
end

procedure alya02 begin
   NMessage(108);
   call combat;
end

procedure alya03 begin
   Reply(109);
   NOption(110, alya04, 4);
   NOption(111, alya09, 4);
   NOption(112, alyaend, 4);
end

procedure alya04 begin
   Reply(113);
   NOption(114, alya06, 4);
   NOption(115, alya08, 4);
   NOption(116, alyaend, 4);
end

procedure alya05 begin
   NMessage(117);
end

procedure alya06 begin
   Reply(118);
   NOption(119, alya07, 4);
   NOption(120, combat, 4);
   NOption(121, alyaend, 4);
end

procedure alya07 begin
   NMessage(122);
   call combat;
end

procedure alya08 begin
   NMessage(123);
   add_timer_event(self_obj, game_ticks(15), 1);
end

procedure alya09 begin
   NMessage(124);
end

procedure alya10 begin
   NMessage(125);
end

procedure alyaend begin
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_RAIDERS);

   dude_look_at_self;
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
   end
   else begin
      set_local_var(7, 1);
      set_local_var(3, 1);
      get_reaction
      start_gdialog(143, self_obj, 4, -1, -1);
      gsay_start;
      if (local_var(4) == 5) then begin
         call alya10;
      end
      else begin
         set_local_var(4, local_var(4) + 1);
         call alya00;
      end
      gsay_end;
      end_dialogue;
   end
end

procedure combat begin
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      set_self_team(TEAM_RAIDERS_OTHER);
   end
   tmp_hostile := 1;
end

procedure critter_p_proc begin
// variable LVar0 := 0;
// rndz := random(0, 500);
// set_local_var(10, game_time);
// set_local_var(11, game_time + 5);

   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
         variable LVar0 := 0;
         rndz := random(0, 500);
         set_global_var(GVAR_ENEMY_RAIDERS, 0);
         if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
            LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
            if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
               if (rndz == 0) then begin
                  float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
               end
               self_walk_to_tile(LVar0);
            end
         end
      end
      else begin
         if global_var(GVAR_TANDI_ESCAPE) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
         if (self_can_see_dude) then begin
            if (global_var(GVAR_RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
               set_global_var(GVAR_ENEMY_RAIDERS, 1);
            end
         end
         if (map_var(2) == 1) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
      end
   end
   if (REP_ENEMY_RAIDERS and self_can_see_dude) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      attack(dude_obj);
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_TOTAL_RAIDERS);
   //if source_is_dude then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND

      inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
   //end
   inc_inv_evil_crit
end

procedure description_p_proc begin
   if not(local_var(7)) then begin
      script_overrides;
      display_msg(mstr(200));
   end
   set_local_var(7, 1);
end

procedure look_at_p_proc begin
   script_overrides;
   if not(local_var(7)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(200));
   end
end
