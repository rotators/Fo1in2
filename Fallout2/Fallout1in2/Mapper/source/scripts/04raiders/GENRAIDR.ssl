/*

   Raiders - Generic raider

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_GENRAIDR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure timed_event_p_proc;
procedure description_p_proc;

import variable killing_women;
procedure guard00;
procedure guard01;
procedure guard02;
procedure guard03;
procedure guard04;
procedure guard05;
procedure guard06;
procedure guard07;
procedure guard07a;
procedure guard07b;
procedure guard08;
procedure guard09;
procedure guard10;
procedure guard11;
procedure guard12;
procedure guard13;
procedure guard14;
procedure guard15;
procedure combat;

procedure do_flee_dude;

variable tmp_hostile;
variable initial := 0;
variable rndz;
variable Weapons;
variable LastTime;

import variable Jack_Ptr;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_GENERIC_RAIDER);
      debug_msg("GENRAIDR init complete.");
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end

   if map_first_run then
      self_caps_adjust(random(1, dude_luck * 2));
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure description_p_proc begin
   script_overrides;
   if (self_is_male) then begin// MALE raider
      display_msg(mstr(random(200, 202)));
   end
   else begin
      display_msg(mstr(random(203, 205)));// FEMALE raider
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_RAIDERS);

   if (local_var(5) == 1) then begin
      display_msg(mstr(100));
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
         call guard00;
      end
      else begin
         if ((Weapons == 1) and (killing_women == 0) and (global_var(GVAR_GARLS_FRIEND) != 1)) then begin
            call guard02;
         end
         else begin
            set_local_var(3, 1);
            get_reaction
            start_gdialog(136, self_obj, 4, -1, -1);
            gsay_start;
            if (local_var(4) == 0) then begin
               set_local_var(4, 1);
               call guard04;
            end
            else begin
               call guard15;
            end
            gsay_end;
            end_dialogue;
         end
      end
   end
end

procedure guard00 begin
   float_msg(self_obj, mstr(random(102, 106)), FLOAT_MSG_YELLOW);
end

procedure guard01 begin
   NMessage(random(107, 111));
end

procedure guard02 begin
   float_msg(self_obj, mstr(random(112, 115)), FLOAT_MSG_YELLOW);
   add_timer_event(self_obj, game_ticks(9), 1);
end

procedure guard03 begin
   NMessage(random(116, 118));
   add_timer_event(self_obj, game_ticks(10), 2);
end

procedure guard04 begin
   Reply(119);
   if (tandi_quest_active and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(120, guard07, 4);
   end
   NOption(121, guard06, 4);
   NLowOption(122, guard05);
end

procedure guard05 begin
   NMessage(123);
end

procedure guard06 begin
   NMessage(124);
   DownReact
end

procedure guard07 begin
   Reply(125);
   NOption(126, guard08, 4);
   NOption(127, guard07a, 5);
   if (dude_stat_success(STAT_pe, 0)) then begin
      NOption(128, guard07b, 4);
   end
end

procedure guard07a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call guard09;
   end
   else begin
      call guard11;
   end
end

procedure guard07b begin
   if (dude_skill_success(SKILL_SPEECH, -20)) then begin
      call guard12;
   end
   else begin
      call guard13;
   end
end

procedure guard08 begin
   NMessage(129);
end

procedure guard09 begin
   Reply(130);
   NOption(131, guard10, 6);
end

procedure guard10 begin
   NMessage(132);
end

procedure guard11 begin
   NMessage(133);
end

procedure guard12 begin
   NMessage(134);
   set_local_var(5, 1);
end

procedure guard13 begin
   NMessage(135);
   call combat;
end

procedure guard14 begin
   NMessage(136);
   add_timer_event(self_obj, game_ticks(12), 3);
end

procedure guard15 begin
   NMessage(137);
end

procedure combat begin
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      set_self_team(TEAM_RAIDERS_OTHER);
   end
   tmp_hostile := 1;
end

procedure do_flee_dude begin
   variable Msg := 102;
   variable Run := 0;
   variable Distance := 0;
   variable Direction := 0;
   variable DistanceBetween := 0;
   Msg := random(102, 106);
   float_msg(self_obj, mstr(Msg), FLOAT_MSG_YELLOW);
   if (Msg == 105) or (Msg == 106) then begin
      Run := 1;
   end
   while(Direction < 5) do begin
      if (tile_distance(dude_tile, tile_num_in_direction(self_tile, Direction, (3 + Run))) > DistanceBetween) then begin
         Distance := tile_num_in_direction(self_tile, Direction, (3 + Run));
         DistanceBetween := tile_distance(dude_tile, Distance);
      end
      Direction := (Direction + 1);
   end
   animate_move_obj_to_tile(self_obj, Distance, Run);
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      attack(dude_obj);
   end
   else begin
      if get_tandi_returned or global_var(GVAR_TANDI_ESCAPE) or (map_var(2) == 1) or global_var(GVAR_RAID_LOOTING) then begin
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end

   if global_var(GVAR_MISTAKEN_ID) then begin // Raiders think you are Garl's father
      set_global_var(GVAR_ENEMY_RAIDERS, 0);
      if (tile_distance_objs(self_obj, dude_obj) < 5) then begin
         if (local_var(7) == 0) then begin
            set_local_var(7, 1);
            LastTime := game_time;
            call do_flee_dude;
         end
         else begin
            if (anim_busy(self_obj) == 0) and (game_time - LastTime > 100) then begin
               rndz := random(0, 20);
               if (rndz == 1) then begin
                  LastTime := game_time;
                  call do_flee_dude;
               end
            end
         end
      end
   end
   else begin
      if self_can_see_dude then begin
         if REP_ENEMY_RAIDERS then begin
            tmp_hostile := 1;
         end
         else begin
            if (global_var(GVAR_GARLS_FRIEND) != 1) then begin
               if (dude_is_armed) and ((Weapons == 0) and (killing_women == 0)) then begin
                  Weapons := 1;
                  dialogue_system_enter;
               end
            end
         end
      end
   end
   rndz := random(0, (30 + (global_var(GVAR_MISTAKEN_ID) * 10)));
   if (rndz == 1) then begin
      if (anim_busy(self_obj) == 0) then begin
         self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 2)));
      end
   end

   // If guard sees Jack, the prisoner, attack him for running away
   if (Jack_Ptr > 0) then begin
      if obj_can_see_obj(self_obj, Jack_Ptr) and not(in_prison_cell) then begin
         attack(Jack_Ptr);
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      if (dude_is_armed) then begin
         call combat;
      end
   end
   else if (fixed_param == 2) then begin
      if (tile_distance_objs(self_obj, dude_obj) < 6) then begin
         call combat;
      end
   end
   else if (fixed_param == 3) then begin
      if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
         call combat;
      end
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_TOTAL_RAIDERS);
   //if source_is_dude then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND
      inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
   //end
   inc_inv_evil_crit
end
