/*

   Raiders - Prisoner, Jack from Junktown (cut-content)

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_RAIDPRIS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure prisoner00;
procedure prisoner01;
procedure prisoner02;
procedure prisoner03;
procedure prisoner04;
procedure prisonerend;
procedure Critter_Action;
procedure escape;

import variable Jack_Ptr;

#define LVAR_Herebefore       (4)
#define LVAR_giveXP           (5)

variable tmp_hostile;
variable Only_Once := 1;

#define exit_hex        (10284)

procedure start begin
   Jack_Ptr := self_obj;

   if not(fo1in2_cut_characters_enabled) then destroy_object(self_obj);

   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_PRISONERS);
      set_self_ai(AI_GENERIC_RAIDER);
   end
end

procedure map_enter_p_proc begin
   // Prisoner only shows up if Tandi isn't around
   if is_visible(Tandi_ptr) then destroy_object(self_obj);

   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end

   if (self_tile == exit_hex) then begin
      destroy_object(self_obj);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure critter_p_proc begin
   if (tmp_hostile and (tile_distance_objs(self_obj, dude_obj) > 12)) then tmp_hostile := 0;
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end

   call escape;

   if ((self_tile == exit_hex) and (local_var(LVAR_giveXP) == 0)) then begin
      set_local_var(LVAR_giveXP, 1);
      give_xp_msg(EXP_RESCUE_JACK, mstr(200));
      set_self_invisible;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction

   if in_prison_cell then begin
      if local_var(LVAR_Herebefore) then begin
         start_dialog_at_node(prisoner03);
      end
      else begin
         set_local_var(LVAR_Herebefore, 1);
         start_dialog_at_node(prisoner00);
      end
   end
end

procedure prisoner00 begin
   Reply(101);
   NOption(102, prisoner01, 4);
   NOption(103, prisoner02, 6);
   NLowOption(104, prisoner01);
end

procedure prisoner01 begin
   set_local_var(LVAR_Herebefore, 1);
   NMessage(105);
end

procedure prisoner02 begin
   Reply(106);
   NOption(107, prisonerend, 4);
   NOption(108, prisonerend, 4);
end

procedure prisoner03 begin
   NMessage(109);
end

procedure prisoner04 begin
   NMessage(110);
end

procedure prisonerend begin
end

procedure damage_p_proc begin
   tmp_hostile := 1;
   if (global_var(GVAR_MISTAKEN_ID) == 0) then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
end

procedure destroy_p_proc begin
   Jack_Ptr := 0;
   inc_inv_good_crit
end

procedure escape begin
   self_run_to_tile(exit_hex);
end
