/*

   Raiders - Tolya

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_TOLYA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure tolya00;
procedure tolya01;
procedure tolya02;
procedure tolya03;
procedure tolya04;
procedure tolya05;
procedure tolya06;
procedure tolya07;
procedure tolya08;
procedure tolya09;
procedure tolya10;
procedure tolyaend;

procedure combat;

variable rndz;
variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_GENERIC_RAIDER);
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end
end

procedure look_at_p_proc begin
   display_msg(mstr(100));
   script_overrides;
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure tolya00 begin
   Reply(101);
   NOption(mstr(102) + dude_name + mstr(103), tolya01, 4);
   NOption(104, tolya02, 5);
   NLowOption(105, tolya03);
end

procedure tolya01 begin
   Reply(106);
   NOption(107, tolya04, 4);
   NOption(108, tolyaend, 4);
   NOption(109, tolya05, 4);
end

procedure tolya02 begin
   Reply(110);
   NOption(mstr(111) + dude_name + mstr(112), tolya07, 4);
   NOption(113, combat, 4);
end

procedure tolya03 begin
   NMessage(114);
end

procedure tolya04 begin
   Reply(115);
   NOption(116, tolya06, 4);
   NOption(117, tolyaend, 4);
end

procedure tolya05 begin
   Reply(118);
   NOption(119, tolya08, 4);
   NOption(120, tolya09, 4);
end

procedure tolya06 begin
   NMessage(121);
   call combat;
end

procedure tolya07 begin
   NMessage(122);
end

procedure tolya08 begin
   set_local_var(6, 1);
   if (local_var(5) == 0) then begin
      Reply(123);
      NOption(124, tolya09, 4);
   end
   else begin
      NMessage(123);
      call tolyaend;
   end
end

procedure tolya09 begin
   set_local_var(5, 1);
   if (local_var(6) == 0) then begin
      Reply(125);
      NOption(126, tolya08, 4);
   end
   else begin
      NMessage(125);
      call tolyaend;
   end
end

procedure tolya10 begin
   NMessage(127);
end

procedure tolyaend begin
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_RAIDERS);

   dude_look_at_self;
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
   end
   else begin
      set_local_var(3, 1);
      get_reaction
      start_gdialog(140, self_obj, 4, -1, -1);
      gsay_start;
      if (local_var(4) == 5) then begin
         call tolya10;
      end
      else begin
         set_local_var(4, local_var(4) + 1);
         call tolya00;
      end
      gsay_end;
      end_dialogue;
   end
end

procedure combat begin
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      set_self_team(TEAM_RAIDERS_OTHER);
   end
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   rndz := random(0, 500);
// set_local_var(10, game_time);
// set_local_var(11, game_time + 5);
   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 0);
         if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
            LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
            if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
               if (rndz == 0) then begin
                  float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
               end
               self_walk_to_tile(LVar0);
            end
         end
      end
      else begin
         if global_var(GVAR_TANDI_ESCAPE) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
         if (self_can_see_dude) then begin
            if (global_var(GVAR_RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
               set_global_var(GVAR_ENEMY_RAIDERS, 1);
            end
         end
         if (map_var(2) == 1) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
      end
   end
   if (REP_ENEMY_RAIDERS and self_can_see_dude) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      attack(dude_obj);
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_TOTAL_RAIDERS);
   //if source_is_dude then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND
      inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
   //end
   inc_inv_evil_crit
end
