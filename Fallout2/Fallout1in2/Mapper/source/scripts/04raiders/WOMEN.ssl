/*

   Raiders - Woman prisoners

*/

/* Include Files */

#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_WOMEN
#define TOWN_REP_VAR            GVAR_TOWN_REP_RAIDERS

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

import variable women_killed;
import variable signal_women;

variable tmp_hostile;
variable Only_Once := 1;
variable message;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_BROTHERHOOD);
      set_self_ai(AI_LOSER);
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if ((global_var(GVAR_GARL_DEAD) == 1) and (get_raiders_alive <= 12) or (get_raiders_alive <= 6)) then begin
      if (local_var(0) == 0) then begin
         set_local_var(0, 1);
         set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep + 1);

         display_msg(message_str(SCRIPT_RAIDMAP, 100) + EXP_BEFRIEND_KHANS + message_str(SCRIPT_RAIDMAP, 103));
         give_exp_points(EXP_BEFRIEND_KHANS);
      end
   end
   else begin
      if (signal_women == 2) then begin
         signal_women := signal_women - 1;
         float_msg(self_obj, mstr(101), FLOAT_MSG_RED);
      end
      else begin
         if (signal_women == 1) then begin
            signal_women := signal_women - 1;
            float_msg(self_obj, mstr(102), FLOAT_MSG_RED);
         end
      end
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(0) == 1) then begin
      float_msg(self_obj, mstr(104), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(random(102, 103)), FLOAT_MSG_RED);
   end
end

procedure destroy_p_proc begin
   women_killed := women_killed + 1;
   if (women_killed > 1) then begin
      set_global_var(GVAR_PLAYER_REPUTATION, (check_general_rep - 1));
   end

   inc_inv_good_crit
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end
