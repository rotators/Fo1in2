/*

   Raiders - Garl, leader of the Khans

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_GARL
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;

import variable women_killed;
import variable signal_women;
import variable killing_women;
import variable Garls_Inven_Ptr;
import variable Cell_Door_Ptr;

procedure garl00;
procedure garl01;
procedure garl01a;
procedure garl01b;
procedure garl01c;
procedure garl01d;
procedure garl02;
procedure garl03;
procedure garl03a;
procedure garl04;
procedure garl04a;
procedure garl04b;
procedure garl04c;
procedure garl05;
procedure garl06;
procedure garl06a;
procedure garl07;
procedure garl08;
procedure garl09;
procedure garl10;
procedure garl11;
procedure garl12;
procedure garl12a;
procedure garl12b;
procedure garl15;
procedure garl15a;
procedure garl16;
procedure garl17;
procedure garl17a;
procedure garl18;
procedure garl19;
procedure garl20;
procedure garl21;
procedure garl22;
procedure garl23;
procedure garl24;
procedure garl25;
procedure garl26;
procedure garl27;
procedure garl28;
procedure garl29;
procedure garl30;
procedure garl31;
procedure garl32;
procedure garl33;
procedure garl34;
procedure garl35;
procedure garl36;
procedure garl37;
procedure garl37a;
procedure garl38;
procedure garl38a;
procedure garl39;
procedure garl39a;
procedure garl40;
procedure garl41;
procedure garl42;
procedure garl43;
procedure garlx;
procedure garlend;
procedure first;
procedure notfirst;
procedure freetandi;
procedure garlcbt;
procedure honorcbt;
procedure honorarea;
procedure garlbarter;
procedure return_to_map;
procedure PickDeadBodyType;

procedure generate_party_IDs;

variable tmp_hostile;
variable Only_Once := 1;
variable temp;
variable tandi_pid_ptr;
variable counter := 0;
variable TANDI := PID_TANDI;
variable Tandi_flag;
variable DOGMEAT := PID_DOGMEAT;
variable Dogmeat_flag;
variable IAN := PID_IAN;
variable Ian_flag;
variable TYCHO := PID_TYCHO;
variable Tycho_flag;
variable KATJA := PID_KATJA;
variable Katja_flag;
variable DeathType := 56;

procedure start begin
   if shady_invaded then begin
      if (CUR_MAP_RAIDERS) then begin //  RAIDERS - KHANS
         if (local_var(4) != 1) then begin
            set_local_var(4, 1);
            call PickDeadBodyType;
            kill_critter(self_obj, DeathType);
         end
      end
   end

   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_RAIDER_LEADER);
   end
end

procedure map_enter_p_proc begin
variable Item;

   // Remove Garl's armor if the armor destroy mod is enabled:
   if not(fo1in2_destroy_armor_disabled) then begin
      if critter_wearing_armor(self_obj) then begin
         Item := self_armor;
         rm_obj_from_inven(self_obj, Item);
      end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      if (global_var(GVAR_GARLS_FRIEND) != 1) then begin
         dialogue_system_enter;
      end
   end
   else if (fixed_param == 2) then begin
      call freetandi;
   end
   else if (fixed_param == 3) then begin
      call honorarea;
      float_msg(self_obj, mstr(209), FLOAT_MSG_NORMAL);
   end
   else if (fixed_param == 4) then begin
      if (local_var(18) > 0) then begin
         if not(local_var(8)) then begin
            call talk_p_proc;
         end
         call return_to_map;
         call freetandi;
      end
      else begin
         add_timer_event(self_obj, 1, 4);
         tmp_hostile := 1;
      end
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (self_elevation == 1) then begin
      self_look_at_dude;

      start_gdialog(137, self_obj, 4, -1, -1);
      gsay_start;
      NMessage(208);
      gsay_end;
      end_dialogue;

      add_timer_event(self_obj, 1, 4);
      set_local_var(8, 1);
   end
   else begin
      if (killing_women) then begin
         call garl39a;
      end
      else begin
         set_local_var(3, 1);
         get_reaction
         start_gdialog(137, self_obj, 4, -1, -1);
         gsay_start;
         if (women_killed > 1) then begin
            call garl11;
         end
         else begin
            if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
               call garl01;
            end
            else begin
               if (local_var(4) < 1) then begin
                  call first;
                  set_local_var(4, 1);
               end
               else begin
                  call notfirst;
               end
            end
         end
         gsay_end;
         end_dialogue;
      end
   end

   if (dude_item(PID_BARTER_TANDI)) then begin
      rm_obj_from_inven(dude_obj, tandi_pid_ptr);
      destroy_object(tandi_pid_ptr);
      set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);
      set_map_var(2, 2);
      obj_unlock(Cell_Door_Ptr);
      add_timer_event(self_obj, 1, 2);
   end
   else if (self_item(PID_BARTER_TANDI)) then begin
      rm_obj_from_inven(self_obj, tandi_pid_ptr);
      destroy_object(tandi_pid_ptr);
      call garl20;
   end
end

procedure return_to_map begin
   move_to(dude_obj, local_var(6), 0);
   move_obj_inven_to_obj(Garls_Inven_Ptr, dude_obj);
   set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);
   set_map_var(2, 2);
   obj_unlock(Cell_Door_Ptr);
end

procedure critter_p_proc
begin
   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 0);
      end
      else begin
         if global_var(GVAR_TANDI_ESCAPE) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
         if (self_can_see_dude) then begin
            if (global_var(GVAR_RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
               set_global_var(GVAR_ENEMY_RAIDERS, 1);
            end
         end
      end
   end
   if (global_var(GVAR_ENEMY_RAIDERS) and self_can_see_dude) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   if (local_var(18) > 0) and not(combat_is_initialized) and (self_elevation == 1) and not(local_var(9)) then begin
      set_local_var(9, 1);
      add_timer_event(self_obj, 1, 4);
   end
end

procedure damage_p_proc begin
   if source_obj == dude_obj then begin
      if (local_var(7) == 0) then begin
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
      else begin
         set_local_var(18, 1);
      end
   end
end

procedure destroy_p_proc begin
   inc_raiders_rep(REP_BONUS_GARL_KILLED);

   set_global_var(GVAR_GARL_DEAD, 1);// Is Garl dead?
   set_global_var(GVAR_TOTAL_RAIDERS, (get_raiders_alive - 1));// Number of Raiders left

   if (local_var(7) == 1) then begin
      move_to(dude_obj, local_var(6), 0);
      move_obj_inven_to_obj(Garls_Inven_Ptr, dude_obj);
      set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);
      set_map_var(2, 2);
      obj_unlock(Cell_Door_Ptr);
   end
   else if (local_var(7) == 0) then begin
      //if (source_obj == dude_obj) then begin
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
         set_global_var(GVAR_GARLS_FRIEND, 0);//GARLS_FRIEND
      //end
   end
   rm_timer_event(self_obj);
   inc_inv_evil_crit
end

procedure freetandi begin
   set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);
   set_map_var(2, 2);
   obj_unlock(Cell_Door_Ptr);
end

procedure garlcbt begin
   tmp_hostile := 1;
end

procedure honorcbt begin
   add_timer_event(self_obj, 1, 3);
end

procedure honorarea begin
   variable LVar0 := 0;
   set_local_var(6, dude_tile);
   set_local_var(7, 1);

   move_to(dude_obj, 20102, 1);
   move_to(self_obj, 20301, 1);

   if dude_armor then begin
      LVar0 := dude_armor;
   end
   rm_obj_from_inven(dude_obj, LVar0);

   move_obj_inven_to_obj(dude_obj, Garls_Inven_Ptr);
   move_obj_inven_to_obj(self_obj, Garls_Inven_Ptr);

   if (LVar0 > 0) then begin
      add_obj_to_inven(dude_obj, LVar0);
      wield_obj_critter(dude_obj, LVar0);
   end

   call generate_party_IDs;

   if (party_member_obj(IAN) != 0) then begin
      move_to(party_member_obj(IAN), tile_num_in_direction(dude_tile, 0, 10), 1);
   end
   if (party_member_obj(KATJA) != 0) then begin
      move_to(party_member_obj(KATJA), tile_num_in_direction(dude_tile, 1, 10), 1);
   end
   if (party_member_obj(TYCHO) != 0) then begin
      move_to(party_member_obj(TYCHO), tile_num_in_direction(dude_tile, 2, 10), 1);
   end
   if (party_member_obj(DOGMEAT) != 0) then begin
      move_to(party_member_obj(DOGMEAT), tile_num_in_direction(dude_tile, 4, 10), 1);
   end
   if (party_member_obj(TANDI) != 0) then begin
      move_to(party_member_obj(TANDI), tile_num_in_direction(dude_tile, 5, 10), 1);
   end

   dude_look_at_self;
   set_self_team(TEAM_87);
   set_self_ai(AI_GARL_HONOR);
   call garlcbt;
end

procedure generate_party_IDs begin
   if (party_member_obj(IAN) == 0) and (Ian_In_Party) then begin
      counter := 16777529;
      while(counter != 16777556) do begin//  to 16777555
         if (party_member_obj(counter) != 0) then begin
            IAN := counter;
         end
         counter := (counter + 1);
      end
   end
   counter := 0;
   if (party_member_obj(TYCHO) == 0) and (Tycho_In_Party) then begin
      counter := 16777556;
      while(counter != 16777583) do begin//  to 16777582
         if (party_member_obj(counter) != 0) then begin
            TYCHO := counter;
         end
         counter := (counter + 1);
      end
   end
   counter := 0;
   if (party_member_obj(KATJA) == 0) and (Katja_In_Party) then begin
      counter := 16777583;
      while(counter != 16777610) do begin//  to 16777609
         if (party_member_obj(counter) != 0) then begin
            KATJA := counter;
         end
         counter := (counter + 1);
      end
   end
end

procedure first begin
   if (global_var(GVAR_TANDI_HIRELING_STATUS) > 1) then begin
      call garl43;
   end
   else begin
      if (global_var(GVAR_TANDI_HIRELING_STATUS) > 0) then begin
         call garl12;
      end
      else begin
         call garl32;
      end
   end
end

procedure notfirst begin
   if (global_var(GVAR_TANDI_HIRELING_STATUS) > 1) then begin
      call garl43;
   end
   else begin
      if (global_var(GVAR_TANDI_HIRELING_STATUS) > 0) then begin
         call garl22;
      end
      else begin
         call garl41;
      end
   end
end

procedure garl00 begin
   NMessage(101);
   call garlcbt;
end

procedure garl01 begin
   Reply(102);
   giq_option(-3, NAME, 207, garl09, NEUTRAL_REACTION);
   giq_option(4, NAME, 103, garl01a, NEUTRAL_REACTION);
   giq_option(5, NAME, 104, garl01b, NEUTRAL_REACTION);
   giq_option(6, NAME, 105, garl01c, NEUTRAL_REACTION);
end

procedure garl01a begin
   call garl02;
end

procedure garl01b begin
   call garl03;
end

procedure garl01c begin
   call garl04;
end

procedure garl01d begin
   Reply(106);
   giq_option(4, NAME, 107, garl02, NEUTRAL_REACTION);
   giq_option(5, NAME, 108, garl03, NEUTRAL_REACTION);
   giq_option(6, NAME, 109, garl04, NEUTRAL_REACTION);
end

procedure garl02 begin
   NMessage(110);
   set_global_var(GVAR_MISTAKEN_ID, 0);//Raiders no longer think you are Garl's father
   call garlcbt;
end

procedure garl03 begin
   Reply(111);
   giq_option(8, NAME, 112, garl03a, NEUTRAL_REACTION);
end

procedure garl03a begin
   set_global_var(GVAR_MISTAKEN_ID, 0);//Raiders no longer think you are Garl's father
   call garlcbt;
end

procedure garl04 begin
   Reply(113);
   giq_option(6, NAME, 114, garl04a, NEUTRAL_REACTION);
   giq_option(6, NAME, 115, garl04b, NEUTRAL_REACTION);
end

procedure garl04a begin
   call garl06;
end

procedure garl04b begin
   call garl05;
end

procedure garl04c begin
   Reply(116);
   giq_option(6, NAME, 117, garl06, NEUTRAL_REACTION);
   giq_option(6, NAME, 118, garl05, NEUTRAL_REACTION);
end

procedure garl05 begin
   NMessage(119);
   set_global_var(GVAR_MISTAKEN_ID, 0);//Raiders no longer think you are Garl's father
   call garlcbt;
end

procedure garl06 begin
   Reply(120);
   if tandi_is_kidnapped then begin
      giq_option(6, NAME, 121, garl06a, NEUTRAL_REACTION);
   end
   giq_option(6, NAME, 122, garl10, NEUTRAL_REACTION);
   giq_option(6, NAME, 123, garl10, NEUTRAL_REACTION);
end

procedure garl06a
begin
   if (is_success(do_check(dude_obj, STAT_ch, 0)) or is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 10))) then begin
      call garl08;
   end
   else begin
      call garl09;
   end
end

procedure garl07
begin
   NMessage(124);
   giq_option(6, NAME, 125, garl08, NEUTRAL_REACTION);
end

procedure garl08
begin
   NMessage(126);
   set_global_var(GVAR_MISTAKEN_ID, 0);//Raiders no longer think you are Garl's father
   call freetandi;
end

procedure garl09
begin
   NMessage(127);
   call garlcbt;
end

procedure garl10
begin
   NMessage(128);
   set_global_var(GVAR_MISTAKEN_ID, 0);//Raiders no longer think you are Garl's father
   call garlcbt;
end

procedure garl11
begin
   if (women_killed > 1) then begin
      women_killed := 0;
      NMessage(129);
      call garlcbt;
   end
end

procedure garl12
begin
   Reply(130);
   if (global_var(GVAR_TANDI_TALKED_ABOUT) == 1) then begin
      giq_option(4, NAME, 131, garl12b, NEUTRAL_REACTION);
      giq_option(5, NAME, 133, garl15, NEUTRAL_REACTION);
   end
   else begin
      giq_option(5, NAME, 200, garl12a, NEUTRAL_REACTION);
   end
   giq_option(4, NAME, 132, garlcbt, NEUTRAL_REACTION);
   giq_option(-3, NAME, 134, garl17, NEUTRAL_REACTION);
end

procedure garl12a
begin
   NMessage(201);
end

procedure garl12b
begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 10)) or is_success(do_check(dude_obj, STAT_ch, 0))) then begin
      call garl15;
   end
   else begin
      call garl10;
   end
end

procedure garl15
begin
   Reply(140);
   if (has_skill(dude_obj, SKILL_SPEECH) >= 45) then begin
      giq_option(6, NAME, 141, garl15a, NEUTRAL_REACTION);
   end
   giq_option(6, NAME, 142, garl18, NEUTRAL_REACTION);
   giq_option(6, NAME, 143, garl21, NEUTRAL_REACTION);
   giq_option(5, NAME, 203, garl28, NEUTRAL_REACTION);
end

procedure garl15a
begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 10)) or is_success(do_check(dude_obj, STAT_ch, 0))) then begin
      call garl16;
   end
   else begin
      call garl17;
   end
end

procedure garl16
begin
   NMessage(145);
   call freetandi;
end

procedure garl17
begin
   NMessage(146);
   call garlcbt;
end

procedure garl17a
begin
   NMessage(204);
   call garlcbt;
end

procedure garl18
begin
   tandi_pid_ptr := create_object(PID_BARTER_TANDI, 0, 0);
   add_obj_to_inven(self_obj, tandi_pid_ptr);
   NMessage(147);
   call garlbarter;
end

procedure garl19 begin
   float_msg(self_obj, mstr(148), FLOAT_MSG_RED);
   call freetandi;
end

procedure garl20 begin
   if (dude_is_male) then begin
      float_msg(self_obj, mstr(149), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(205), FLOAT_MSG_RED);
   end
   call garlcbt;
end

procedure garl21 begin
   NMessage(150);
   call garlcbt;
end

procedure garl22 begin
   Reply(151);
   giq_option(4, NAME, 152, garl23, NEUTRAL_REACTION);
   giq_option(4, NAME, 153, garl24, NEUTRAL_REACTION);
   giq_option(-3, NAME, 207, garl17, NEUTRAL_REACTION);
end

procedure garl23 begin
   NMessage(155);
end

procedure garl24 begin
   Reply(156);
   giq_option(4, NAME, 157, garl25, NEUTRAL_REACTION);
   giq_option(6, NAME, 158, garl18, NEUTRAL_REACTION);
   giq_option(6, NAME, 159, garl15a, NEUTRAL_REACTION);
end

procedure garl25 begin
   Reply(160);
   giq_option(4, NAME, 161, garl26, NEUTRAL_REACTION);
   giq_option(4, NAME, 162, garl27, NEUTRAL_REACTION);
end

procedure garl26 begin
   NMessage(163);
   call garlcbt;
end

procedure garl27 begin
   Reply(164);
   giq_option(4, NAME, 165, garl26, NEUTRAL_REACTION);
   giq_option(4, NAME, 166, garl28, NEUTRAL_REACTION);
   giq_option(6, NAME, 167, garl29, NEUTRAL_REACTION);
end

procedure garl28 begin
   Reply(168);
   giq_option(4, NAME, 169, honorcbt, NEUTRAL_REACTION);
end

procedure garl29 begin
   Reply(170);
   giq_option(4, NAME, 171, garlcbt, NEUTRAL_REACTION);
end

procedure garl30 begin
   NMessage(172);
   call freetandi;
end

procedure garl31 begin
   NMessage(173);
end

procedure garl32 begin
   Reply(174);
   giq_option(4, NAME, 175, garl33, NEUTRAL_REACTION);
   giq_option(6, NAME, 176, garl34, NEUTRAL_REACTION);
   giq_option(-3, NAME, 154, garl23, NEUTRAL_REACTION);
end

procedure garl33 begin
   if (dude_is_male) then begin
      NMessage(177);
   end
   else begin
      NMessage(206);
   end
   call garlcbt;
end

procedure garl34 begin
   Reply(178);
   giq_option(6, NAME, 179, garl35, NEUTRAL_REACTION);
   giq_option(6, NAME, 180, garl33, NEUTRAL_REACTION);
end

procedure garl35 begin
   Reply(181);
   giq_option(4, NAME, 182, garl37, NEUTRAL_REACTION);
   giq_option(4, NAME, 183, garl36, NEUTRAL_REACTION);
end

procedure garl36 begin
   NMessage(184);
end

procedure garl37 begin
   Reply(185);
   giq_option(4, NAME, 186, garl38, NEUTRAL_REACTION);
   giq_option(4, NAME, 187, garl37a, NEUTRAL_REACTION);
end

procedure garl37a begin
   signal_women := 2;
   killing_women := 1;
   add_timer_event(self_obj, game_ticks(15), 1);
end

procedure garl38 begin
   women_killed := 0;
   if (dude_is_male) then begin
      NMessage(188);
   end
   else begin
      NMessage(189);
   end
   call garlcbt;
end

procedure garl38a begin
   women_killed := 0;
   if (dude_is_male) then begin
      float_msg(self_obj, mstr(188), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(189), FLOAT_MSG_RED);
   end
   call garlcbt;
end

procedure garl39 begin
   NMessage(190);
   call garlcbt;
end

procedure garl39a begin
   killing_women := 0;
   if (women_killed > 1) then begin
      call garl40;
   end
   else begin
      call garl38a;
   end
end

procedure garl40 begin
   women_killed := 0;
   TopReact
   float_msg(self_obj, mstr(191), FLOAT_MSG_BLACK);
   temp := 500;
   display_msg(message_str(SCRIPT_RAIDMAP, 100) + temp + message_str(SCRIPT_RAIDMAP, 102));
   give_exp_points(temp);
   set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep + -2);
   set_global_var(GVAR_GARLS_FRIEND, 1);
end

procedure garl41 begin
   Reply(192);
   giq_option(4, NAME, 193, garl23, NEUTRAL_REACTION);
   giq_option(6, NAME, 194, garl42, NEUTRAL_REACTION);
   giq_option(-3, NAME, 195, garl23, NEUTRAL_REACTION);
end

procedure garl42 begin
   NMessage(196);
end

procedure garl43 begin
   if (dude_is_male) then begin
      NMessage(197);
   end
   else begin
      NMessage(198);
   end
   call garlcbt;
end

procedure garlx begin
end

procedure garlend begin
end

procedure garlbarter begin
   gdialog_mod_barter(0);
   NMessage("");
end

procedure PickDeadBodyType begin
   pick_dead_body_type
end
