/*

	Raiders - Gwen

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_GWEN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;

procedure gwen00;
procedure gwen01;
procedure gwen02;
procedure gwen03;
procedure gwen04;
procedure gwen05;
procedure gwen06;
procedure gwen07;
procedure gwen08;
procedure gwen10;
procedure gwenend;
procedure combat;

procedure PickDeadBodyType;
variable DeathType := 56;
variable tmp_hostile;
variable Only_Once := 1;
variable rndz;

procedure start begin
	if shady_invaded then begin
		if (CUR_MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_RAIDERS );
		set_self_ai( AI_GENERIC_RAIDER );
		debug_msg("GWEN init.");
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure gwen00
begin
	set_local_var(7, 1);
	gsay_reply( NAME, 101 );
	giq_option( 4, NAME, message_str(NAME, 102) + dude_name + message_str(NAME, 103), gwen01, NEUTRAL_REACTION );
	giq_option( 7, NAME, 104, gwen02, NEUTRAL_REACTION );
	giq_option( -3, NAME, 105, gwen03, NEUTRAL_REACTION );
	if (dude_is_male) then begin
		giq_option( 4, NAME, 106, gwen07, NEUTRAL_REACTION );
	end
	set_local_var(7, 1);
end

procedure gwen01
begin
	gsay_reply( NAME, 107 );
	giq_option( 4, NAME, 108, gwen04, NEUTRAL_REACTION );
	giq_option( 4, NAME, 109, gwen08, NEUTRAL_REACTION );
	giq_option( 4, NAME, 110, gwen05, NEUTRAL_REACTION );
	giq_option( 4, NAME, 111, gwen06, NEUTRAL_REACTION );
end

procedure gwen02
begin
	gsay_message( NAME, 112, NEUTRAL_REACTION );
end

procedure gwen03
begin
	gsay_message( NAME, 113, NEUTRAL_REACTION );
end

procedure gwen04
begin
	gsay_reply( NAME, 114 );
	giq_option( 4, NAME, 115, gwenend, NEUTRAL_REACTION );
	giq_option( 4, NAME, 116, combat, NEUTRAL_REACTION );
	giq_option( 4, NAME, 117, combat, NEUTRAL_REACTION );
end

procedure gwen05
begin
	gsay_message( NAME, 118, NEUTRAL_REACTION );
	call combat;
end

procedure gwen06
begin
	gsay_message( NAME, 119, NEUTRAL_REACTION );
	call combat;
end

procedure gwen07
begin
	gsay_message( NAME, 120, NEUTRAL_REACTION );
	call combat;
end

procedure gwen08
begin
	gsay_message( NAME, 121, NEUTRAL_REACTION );
	call combat;
end

procedure gwen10
begin
	gsay_message( NAME, 122, NEUTRAL_REACTION );
end

procedure gwenend
begin
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		float_msg( self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW );
	end
	else begin
		set_local_var(3, 1);
		get_reaction
		start_gdialog(141, self_obj, 4, -1, -1);
		gsay_start;
		if (local_var(4) == 5) then begin
			call gwen10;
		end
		else begin
			set_local_var(4, local_var(4) + 1);
			call gwen00;
		end
		gsay_end;
		end_dialogue;
	end
end

procedure combat
begin
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		set_self_team( TEAM_87 );
	end
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if get_tandi_returned then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );
	end
	else begin
		if (global_var( GVAR_MISTAKEN_ID )) then begin//Raiders think you are Garl's father
			variable LVar0 := 0;
			rndz := random(0, 500);
			set_global_var( GVAR_ENEMY_RAIDERS, 0 );
			if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
				LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
				if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
					if (rndz == 0) then begin
						float_msg( self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW );
					end
					self_walk_to_tile( LVar0 );
				end
			end
		end
		else begin
			if global_var( GVAR_TANDI_ESCAPE ) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );
			end
			if (self_can_see_dude) then begin
				if (global_var( GVAR_RAID_LOOTING )) then begin//  "RAID_LOOTING" (are you stealing from them)
					set_global_var( GVAR_ENEMY_RAIDERS, 1 );
				end
			end
			if (map_var(2) == 1) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );
			end
		end
	end
	if (global_var( GVAR_ENEMY_RAIDERS ) and self_can_see_dude) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );
		attack(dude_obj);
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		if (global_var( GVAR_MISTAKEN_ID ) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var( GVAR_ENEMY_RAIDERS, 1 );
		end
	end
end

procedure description_p_proc begin
	if local_var(7) then begin
		script_overrides;
		display_msg(message_str(NAME, 200));
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if not(local_var(7)) then begin
		display_msg(message_str(NAME, 100));
	end
	else begin
		display_msg(message_str(NAME, 200));
	end
end

procedure destroy_p_proc begin
	set_global_var( GVAR_TOTAL_RAIDERS, (get_raiders_alive - 1) );// Number of raiders left
	//if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );
		set_global_var( GVAR_GARLS_FRIEND, 0 );

		inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
	//end
	inc_inv_evil_crit
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
