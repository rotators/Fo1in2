/*

   Raiders - Gwen

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_GWEN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;

procedure gwen00;
procedure gwen01;
procedure gwen02;
procedure gwen03;
procedure gwen04;
procedure gwen05;
procedure gwen06;
procedure gwen07;
procedure gwen08;
procedure gwen10;
procedure gwenend;
procedure combat;

variable tmp_hostile;
variable Only_Once := 1;
variable rndz;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_RAIDERS);
      set_self_ai(AI_GENERIC_RAIDER);
      debug_msg("GWEN init.");
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_MAP_RAIDERS then begin
      invasion_kill_critter;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure gwen00 begin
   set_local_var(7, 1);
   Reply(101);
   NOption(mstr(102) + dude_name + mstr(103), gwen01, 4);
   NOption(104, gwen02, 7);
   NLowOption(105, gwen03);
   if (dude_is_male) then begin
      NOption(106, gwen07, 4);
   end
   set_local_var(7, 1);
end

procedure gwen01 begin
   Reply(107);
   NOption(108, gwen04, 4);
   NOption(109, gwen08, 4);
   NOption(110, gwen05, 4);
   NOption(111, gwen06, 4);
end

procedure gwen02 begin
   NMessage(112);
end

procedure gwen03 begin
   NMessage(113);
end

procedure gwen04 begin
   Reply(114);
   NOption(115, gwenend, 4);
   NOption(116, combat, 4);
   NOption(117, combat, 4);
end

procedure gwen05 begin
   NMessage(118);
   call combat;
end

procedure gwen06 begin
   NMessage(119);
   call combat;
end

procedure gwen07 begin
   NMessage(120);
   call combat;
end

procedure gwen08 begin
   NMessage(121);
   call combat;
end

procedure gwen10 begin
   NMessage(122);
end

procedure gwenend begin
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_RAIDERS);

   dude_look_at_self;
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
   end
   else begin
      set_local_var(3, 1);
      get_reaction
      start_gdialog(141, self_obj, 4, -1, -1);
      gsay_start;
      if (local_var(4) == 5) then begin
         call gwen10;
      end
      else begin
         set_local_var(4, local_var(4) + 1);
         call gwen00;
      end
      gsay_end;
      end_dialogue;
   end
end

procedure combat begin
   if (global_var(GVAR_MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
      set_self_team(TEAM_87);
   end
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if (global_var(GVAR_MISTAKEN_ID)) then begin//Raiders think you are Garl's father
         variable LVar0 := 0;
         rndz := random(0, 500);
         set_global_var(GVAR_ENEMY_RAIDERS, 0);
         if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
            LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
            if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
               if (rndz == 0) then begin
                  float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), FLOAT_MSG_YELLOW);
               end
               self_walk_to_tile(LVar0);
            end
         end
      end
      else begin
         if global_var(GVAR_TANDI_ESCAPE) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
         if (self_can_see_dude) then begin
            if (global_var(GVAR_RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
               set_global_var(GVAR_ENEMY_RAIDERS, 1);
            end
         end
         if (map_var(2) == 1) then begin
            set_global_var(GVAR_ENEMY_RAIDERS, 1);
         end
      end
   end
   if (global_var(GVAR_ENEMY_RAIDERS) and self_can_see_dude) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      attack(dude_obj);
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
end

procedure description_p_proc begin
   if local_var(7) then begin
      script_overrides;
      display_msg(mstr(200));
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if not(local_var(7)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(200));
   end
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_TOTAL_RAIDERS);
   //if source_is_dude then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
      set_global_var(GVAR_GARLS_FRIEND, 0);

      inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
   //end
   inc_inv_evil_crit
end
