/*

   Boneyard, Gun Runners - Gabriel the boss

*/

/* Include Files */
#include "define.h"
//#include "maps/laadytum.h"
#include "boneyard.h"

#define NAME                    SCRIPT_GABRIEL
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure Gab01;
procedure Gab02;
procedure Gab03;
procedure Gab04;
procedure Gab05;
procedure Gab06;
procedure Gab07;
procedure Gab08;
procedure Gab09;
procedure Gab09a;
procedure Gab11;
procedure Gab12;
procedure Gab13;
procedure Gab14;
procedure Gab15;
procedure Gab16;
procedure Gab17;
procedure Gab18;
procedure Gab19;
procedure Gab20;
procedure Gab21;
procedure Gab22;
procedure Gab23;
procedure Gab24;
procedure Gab25;
procedure Gab26;
procedure Gab27;
procedure Gab28;
procedure GabBarter;
procedure GabEnd;

import variable JonPtr;
variable Only_Once := 1;

#define LVAR_Here_Before         (4)

procedure Start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_LA_GUN_RUNNERS);
      set_self_ai(AI_GUNRUNNERS);
   end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust(random(50, 150));
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then
      display_msg(mstr(101));
   else
      display_msg(mstr(100));
end

procedure description_p_proc begin
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_GUNRUNNR_LVL2);

   dude_look_at_self;

   if ((global_var(GVAR_DUDE_ENEMY_BLADES) == 1) or (dude_iq < 4)) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      get_reaction
      start_gdialog(156, self_obj, -1, -1, -1);
      gsay_start;
      if (local_var(LVAR_Here_Before) == 0) then begin
         if get_all_deathclaws_killed then begin
            call Gab17;
         end
         else begin
            call Gab01;
         end
      end
      else begin

         if (local_var(5) == 1) then begin
            if ((local_var(8) == 0) and get_blades_attack_finished) then begin
               set_local_var(8, 1);
               call Gab25;
            end
            else begin
               call Gab27;
            end
         end
         else if (get_quest_kill_deathclaws and get_all_deathclaws_killed) then begin
            call Gab20;
         end
         else if get_quest_kill_deathclaws then begin
            call Gab13;
         end
         else if ((local_var(6) == 1) or (local_var(7) == 1)) then begin
            call Gab24;
         end
         else begin
            call Gab12;
         end

      end
      gsay_end;
      end_dialogue;
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_DUDE_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_DUDE_ENEMY_BLADES, 1);
      inc_good_critter
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (global_var(GVAR_DUDE_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_DUDE_ENEMY_BLADES, 1);
end

procedure Gab01 begin
   Reply(102);
   NOption(103, Gab02, 1);
   NOption(104, Gab04, 1);

   if debug_mode then
      NOption("***CHEAT *** The deathclaw are kill!", Gab20, 001);
end

procedure Gab02 begin
   Reply(105);
   NOption(106, Gab03, 1);
   set_local_var(LVAR_Here_Before, 1);
end

procedure Gab03 begin
   Reply(107);
   NOption(108, Gab08, 1);
end

procedure Gab04 begin
   Reply(109);
   NOption(110, Gab05, 1);
end

procedure Gab05 begin
   Reply(111);
   NOption(112, Gab06, 1);
end

procedure Gab06 begin
   Reply(113);
   if get_all_deathclaws_killed then begin
      NOption(114, Gab19, 1);
   end
   else begin
      NOption(114, Gab07, 1);
   end
   NOption(115, Gab08, 1);
end

procedure Gab07 begin
   Reply(116);
   NOption(117, Gab03, 1);
end

procedure Gab08 begin
   Reply(118);
   NOption(119, Gab09, 1);
   if get_arming_blades then begin
      if (dude_is_male) then begin
         NOption(170, Gab11, 1);
      end
      else begin
         NOption(171, Gab11, 1);
      end
   end
end

procedure Gab09 begin
   Reply(121);
   NOption(122, Gab09a, 1);
   NOption(123, GabEnd, 1);
end

procedure Gab09a begin
   set_quest_kill_deathclaws;
   set_momclaw_quest_active;
end

procedure Gab11 begin
   Reply(125);
   NOption(126, Gab09a, 1);
   NOption(127, GabEnd, 1);
end

procedure Gab12 begin
   Reply(128);
   NOption(129, Gab04, 1);
   NOption(130, GabEnd, 1);
end

procedure Gab13 begin
   if (dude_is_male) then begin
      Reply(172);
   end
   else begin
      Reply(173);
   end
   NOption(132, Gab15, 1);

   if not(get_gunrunners_armed_blades) and not(get_gunrunners_armed_dude) then
      NOption(133, Gab14, 1);

   if debug_mode then
      NOption("***CHEAT *** The deathclaw are kill!", Gab20, 001);
end

procedure Gab14 begin
   Reply(134);
   NOption(135, GabEnd, 1);
end

procedure Gab15 begin
   if not(get_gunrunners_armed_blades) and not(get_gunrunners_armed_dude) then
      Reply(136);
   else
      Reply(300);
   NOption(137, GabEnd, 1);
end

procedure Gab16 begin
   Reply(138);
   NOption(139, GabEnd, 1);
end

procedure Gab17 begin
   Reply(140);
   NOption(141, Gab18, 1);
   NOption(142, Gab04, 1);
end

procedure Gab18 begin
   Reply(143);
   NOption(144, Gab20, 1);
   set_local_var(LVAR_Here_Before, 1);
end

procedure Gab19 begin
   Reply(145);
   NOption(146, Gab20, 1);
end

procedure Gab20 begin
   if (dude_is_male) then begin
      Reply(174);
   end
   else begin
      Reply(175);
   end
   NOption(148, Gab21, 1);
   if get_arming_blades then begin
      if (dude_is_male) then begin
         NOption(170, Gab22, 1);
      end
      else begin
         NOption(171, Gab22, 1);
      end
   end
   NOption(150, Gab23, 1);
end

// Player wants the weapons for himself:
procedure Gab21 begin
   Reply(151);
   if (dude_is_male) then begin
      NOption(176, GabEnd, 1);
   end
   else begin
      NOption(177, GabEnd, 1);
   end
   if not(get_gunrunners_armed_dude) then begin
      set_gunrunners_armed_dude;
      set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep + 1);

      give_xp(EXP_QUEST_KILLED_DCLAWS);
      set_local_var(6, 1);
   end
end

// Player wants the weapons for the Blades to attack Adytum:
procedure Gab22 begin
   if (dude_is_male) then begin
      Reply(178);
   end
   else begin
      Reply(179);
   end
   NOption(154, GabEnd, 1);
   if not(get_gunrunners_armed_blades) then begin
      set_gunrunners_armed_blades;
      set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep + 1);
      give_xp(EXP_QUEST_KILLED_DCLAWS);
      set_local_var(5, 1);
   end
end

procedure Gab23 begin
   if (dude_is_male) then begin
      Reply(180);
   end
   else begin
      Reply(181);
   end
   NOption(156, GabEnd, 1);
   UpReactLevel
end

procedure Gab24 begin
   if (dude_is_male) then begin
      NMessage(182);
   end
   else begin
      NMessage(183);
   end
end

procedure Gab25 begin
   if (dude_is_male) then begin
      Reply(186);
   end
   else begin
      Reply(187);
   end
   NOption(159, Gab26, 1);
   NOption(160, GabBarter, 1);
   NOption(161, GabEnd, 1);
end

procedure Gab26 begin
   if (dude_is_male) then begin
      Reply(184);
   end
   else begin
      Reply(185);
   end
   NOption(163, GabBarter, 1);
   NOption(164, GabEnd, 1);
end

procedure Gab27 begin
   if (dude_is_male) then begin
      Reply(186);
   end
   else begin
      Reply(187);
   end
   NOption(166, GabBarter, 1);
   NOption(167, GabEnd, 1);
end

procedure Gab28
begin
   NMessage(168);
end

procedure GabBarter
begin
   NMessage(188);
end

procedure GabEnd
begin
end
