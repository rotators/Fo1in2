/*

	Boneyard - Generic gunrunner guarding the weapons

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\maps\laadytum.h"

#define NAME                    SCRIPT_GUNRNR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure timed_event_p_proc;
procedure pickup_p_proc;

variable Only_Once := 1;
variable PsstTime;
variable In_Timed_Event;

#define EVENT_MOVE 	(1)

procedure Start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_LA_GUN_RUNNERS );
		set_self_ai( AI_GUNRUNNERS );
	end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust( random(2, 20) );
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(NAME, 116));
	script_overrides;
end

procedure description_p_proc begin
end

procedure talk_p_proc begin
	dude_look_at_self;
	if (global_var( GVAR_DUDE_ENEMY_BLADES ) == 1) then begin
		float_msg( self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED );
	end
	else begin
		float_msg( self_obj, message_str(NAME, random(100, 115)), FLOAT_MSG_NORMAL );
	end
end

procedure critter_p_proc begin
	variable LVar0 := 0;
	LVar0 := global_var( GVAR_GUNRNR_SOMETHING_0 );

	if ((((game_time / 10) - LVar0) >= 10) and (tile_distance_objs(self_obj, dude_obj) <= 4) and (global_var( GVAR_DUDE_ENEMY_BLADES ) == 0)) then begin
		float_msg( self_obj, message_str(NAME, random(100, 115)), FLOAT_MSG_NORMAL );
		LVar0 := game_time / 10;
		set_global_var( GVAR_GUNRNR_SOMETHING_0, LVar0 );
	end

	if ((global_var( GVAR_WATER_CHIP_8 ) == 2) and (In_Timed_Event == 0)) then begin
		In_Timed_Event := 1;
		add_timer_event(self_obj, game_ticks(random(1, 3)), EVENT_MOVE);
	end

	if (self_can_see_dude) then begin
		if (global_var( GVAR_DUDE_ENEMY_BLADES ) == 1) then begin
			attack(dude_obj);
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_DUDE_ENEMY_BLADES, 1 );
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_DUDE_ENEMY_BLADES, 1 );
	end
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure timed_event_p_proc begin
	if (fixed_param == EVENT_MOVE) then begin
		self_walk_to_tile( tile_num_in_direction(self_tile, random(0, 5), random(1, 5)) );
		//add_timer_event(self_obj, game_ticks(random(1, 3)), EVENT_MOVE);
	end
	In_Timed_Event := 0;
end

procedure pickup_p_proc begin
	set_global_var( GVAR_DUDE_ENEMY_BLADES, 1 );
end
