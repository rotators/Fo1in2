/*

   Boneyard - Zack, at Gun Runners

*/

/* Include Files */
#include "define.h"
#include "maps/lagunrun.h"
#include "area/boneyard.h"
#include "sfall/main.h"

#define NAME                    SCRIPT_ZACK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "upgweap.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure Zack00;
procedure Zack01;
procedure Zack02;
procedure Zack03;
procedure Zack04;
procedure Zack05;
procedure ZackYes;
procedure ZackNo;
procedure ZackBarter;
procedure ZackEnd;

procedure Node1000;
procedure Node1100;
procedure Node1200;
procedure Node1300;
procedure Node1400;

procedure Get_Stuff;
procedure Put_Stuff;

// Weapon Upgrades
procedure NodeUpgrade;
procedure NodeUpgrade_Done;
procedure NodeUpgrade_Pistols;
procedure NodeUpgrade_Rifles;
procedure NodeUpgrade_BigGunsA;
procedure NodeUpgrade_Melee;
procedure NodeUpgrade_Revolver;
procedure NodeUpgrade_DesertEagle;
procedure NodeUpgrade_LaserPistol;
procedure NodeUpgrade_HRifle;
procedure NodeUpgrade_ARifle;
procedure NodeUpgrade_LaserRifle;
procedure NodeUpgrade_FNFAL;
procedure NodeUpgrade_Flamer;
procedure NodeUpgrade_CattleProd;

import variable Locker_Ptr;

variable Only_Once := 1;
variable BarterMod := 0;
variable DisplayMessage;

#define LVAR_Here_Before         (4)
#define LVAR_Discount            (5)
#define LVAR_Restock_Timer       (6)
#define LVAR_Caps_Amount         (7)
#define LVAR_SpecialPayment      (8)

#define RESTOCK_TIME             (GAME_TIME_IN_DAYS + 5)

#ifndef barter_mod_initial
   #define barter_mod_initial       (25)    // vanilla : -10
#endif
#ifndef barter_mod_discount_1
   #define barter_mod_discount_1    (15)    // vanilla : 5
#endif
#ifndef barter_mod_discount_2
   #define barter_mod_discount_2    (5)     // vanilla : 15
#endif

#define caps_amount                 floor((3000 + random(0, 1000)) * caps_modifier)

#define upgrade_cost(base)          ((base * (25000/(160 + has_skill(dude_obj, SKILL_BARTER))))/100)
#define MCOST                       upgrade_cost(400) // old: 2000
#define DECOST                      upgrade_cost(1500) // old: 3000
#define LPCOST                      upgrade_cost(1600)
#define HRCOST                      upgrade_cost(1750) // old: 4000
#define ARCOST                      upgrade_cost(2000) // old: 6000
#define FNCOST                      upgrade_cost(4000) // old: 12000
#define LRCOST                      upgrade_cost(5000)
#define CPCOST                      upgrade_cost(1500) // old: 3000
#define FLAMCOST                    upgrade_cost(4000)

#define give_money(x)               dude_caps_adjust(-x);  \
                                    self_caps_adjust(x)

procedure Start begin
   if Only_Once then begin
      set_self_team(TEAM_LA_GUN_RUNNERS);
      set_self_ai(AI_GUNRUNNERS);
      Only_Once := 0;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_GUNRUNNR_LVL3);

   dude_look_at_self;
   if ((global_var(GVAR_ENEMY_GUN_RUNNERS) == 1) or (dude_iq < 4)) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      call Get_Stuff;
      fo1_caps_restock;
      get_reaction

      if (local_var(LVAR_Discount) == 2) then
         BarterMod := barter_mod_discount_2;
      else if (local_var(LVAR_Discount) == 1) then
         BarterMod := barter_mod_discount_1;
      else
         BarterMod := barter_mod_initial;
      gdialog_set_barter_mod(BarterMod);

      if (local_var(LVAR_Here_Before) == 0) then
         start_dialog_at_node(Zack00);
      else
         start_dialog_at_node(Zack01);

      set_local_var(LVAR_Caps_Amount, self_caps);
      self_caps_adjust(-1 * local_var(LVAR_Caps_Amount));

      call Put_Stuff;
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_GUN_RUNNERS, 1);
   end
end

procedure destroy_p_proc begin
   // There is nobody else but dude_obj to kill her!
   // This will also prevent the dynamite-exploit.
   //if source_is_dude then begin
      inc_good_critter
      set_global_var(GVAR_ENEMY_GUN_RUNNERS, 1);
   //end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_GUN_RUNNERS) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_GUN_RUNNERS, 1);
end

procedure Zack00 begin
   set_local_var(LVAR_Here_Before, 1);

   Reply(102);
   NOption(104, ZackBarter, 4);
   NOption(103, Zack02, 4);
   if (local_var(LVAR_Discount) == 0) then begin
      DisplayMessage := 117;
      NOption(105, Zack04, 4);
   end
   else if (local_var(LVAR_Discount) == 1) then begin
      DisplayMessage := 123;
      NOption(122, Zack05, 4);
   end

   if fo1in2_weapon_upgrades_enabled then
      NOption(1999, NodeUpgrade, 4);

   NOption(107, ZackEnd, 4);
end

procedure Zack01 begin
   Reply(106);
   NOption(104, ZackBarter, 4);
   NOption(103, Zack02, 4);
   if (local_var(LVAR_Discount) == 0) then begin
      DisplayMessage := 117;
      NOption(105, Zack04, 4);
   end
   else if (local_var(LVAR_Discount) == 1) then begin
      DisplayMessage := 123;
      NOption(122, Zack05, 4);
   end

   if fo1in2_merch_restock_enabled and (get_gunrunners_armed_dude or get_gunrunners_armed_blades) and (global_var(GVAR_GUN_RUNNERS_BONUS) == 0) then begin
      if (local_var(LVAR_SpecialPayment) == 0) then
         NOption(998, Node1000, 4);
      else
         NOption(999, Node1100, 4);
   end

   if fo1in2_weapon_upgrades_enabled then
      NOption(1999, NodeUpgrade, 4);

   NOption(107, ZackEnd, 4);
end

procedure Zack02 begin
   Reply(114);
   NOption(115, Zack03, 4);
end

procedure Zack03 begin
   Reply(111);
   NOption(113, ZackBarter, 4);
end

procedure Zack04 begin
   if get_all_deathclaws_killed then begin
      BarterMod := barter_mod_discount_2;
      set_local_var(LVAR_Discount, 2);
      call ZackYes;
   end
   else if get_quest_kill_deathclaws then begin
      BarterMod := barter_mod_discount_1;
      set_local_var(LVAR_Discount, 1);
      call ZackYes;
   end
   else begin
      BarterMod := barter_mod_initial;
      call ZackNo;
   end
end

procedure Zack05 begin
   if get_all_deathclaws_killed then begin
      BarterMod := barter_mod_discount_2;
      set_local_var(LVAR_Discount, 2);
      call ZackYes;
   end
   else begin
      call ZackNo;
   end
end

procedure ZackYes begin
   Reply(116);
   NOption(120, ZackBarter, 4);
   NOption(121, ZackEnd, 4);
end

procedure ZackNo begin
   Reply(DisplayMessage);
   NOption(119, ZackBarter, 4);
   NOption(107, ZackEnd, 4);
end

procedure ZackBarter begin
   gdialog_mod_barter(BarterMod);
   NMessage(108);
end

procedure ZackEnd begin
end

procedure Get_Stuff begin
   get_barter_inven(Locker_Ptr);
end

procedure Put_Stuff begin
   put_barter_inven(Locker_Ptr);
end

procedure Node1000 begin
   set_local_var(LVAR_SpecialPayment, 1);
   Reply(1001);
   NOption(1010, Node1100, 4);
end

procedure Node1100 begin
   Reply(1100);
   BOption(1110, Node1200, 4);
   if (dude_caps >= 10000) then
      GOption(1120, Node1300, 4);
   NOption(1130, Node1400, 4);
end

procedure Node1200 begin
   NMessage(1200);
end

procedure Node1300 begin
   dude_caps_adjust(-10000);
   set_global_var(GVAR_GUN_RUNNERS_BONUS, 1);
   NMessage(1300);
end

procedure Node1400 begin
   NMessage(1400);
end

// Upgrade weapons
procedure NodeUpgrade begin
variable has_pistol := false;
variable has_rifle := false;
variable has_other := false;
variable has_flamer := false;

   if ((dude_item_count(PID_DESERT_EAGLE)) or (dude_item_count(PID_44_MAGNUM_REVOLVER)) or (dude_item_count(PID_LASER_PISTOL))) then begin
      has_pistol := true;
   end
   if ((dude_item_count(PID_HUNTING_RIFLE)) or (dude_item_count(PID_ASSAULT_RIFLE)) or (dude_item_count(PID_FN_FAL)) or (dude_item_count(PID_LASER_RIFLE))) then begin
      has_rifle := true;
   end
   if (dude_item_count(PID_CATTLE_PROD)) then begin
      has_other := true;
   end
   if (dude_item_count(PID_FLAMER)) then begin
      has_flamer := true;
   end

   if ((has_pistol) or (has_rifle) or (has_other) or (has_flamer)) then begin
      Reply(2000);
      if (has_pistol) then
         NOption(2010, NodeUpgrade_Pistols, 4);
      if (has_rifle) then
         NOption(2020, NodeUpgrade_Rifles, 4);
      if (has_flamer) then
         NOption(2025, NodeUpgrade_BigGunsA, 4);
      if (has_other) then
         NOption(2030, NodeUpgrade_Melee, 4);
   end
   else begin
      Reply(2001);
   end
   NOption(2040, Zack01, 4);
   NOption(2050, ZackEnd, 4);
end

procedure NodeUpgrade_Done begin
   NMessage(2600);

   gfade_out(1);
   game_time_advance(ONE_GAME_HOUR * 2);
   gfade_in(1);

   Reply(2601);
   NOption(2110, NodeUpgrade, 4);
   NOption(g_bye, ZackEnd, 4);
end

procedure NodeUpgrade_Pistols begin
variable temp_msg := "";
variable afford := false;
   if (dude_item_count(PID_44_MAGNUM_REVOLVER)) then begin
      temp_msg += mstr(2200) + MCOST + mstr(2202) + " ";
   end
   if (dude_item_count(PID_DESERT_EAGLE)) then begin
      temp_msg += mstr(2201) + DECOST + mstr(2202);
   end
   if (dude_item_count(PID_LASER_PISTOL)) then begin
      temp_msg += mstr(2203) + DECOST + mstr(2202);
   end

   Reply(temp_msg);
   if ((dude_item_count(PID_44_MAGNUM_REVOLVER)) and (dude_caps >= MCOST)) then begin
      NOption(2210, NodeUpgrade_Revolver, 004);
      afford := true;
   end
   if ((dude_item_count(PID_DESERT_EAGLE)) and (dude_caps >= DECOST)) then begin
      NOption(2220, NodeUpgrade_DesertEagle, 004);
      afford := true;
   end
   if ((dude_item_count(PID_LASER_PISTOL)) and (dude_caps >= LPCOST)) then begin
      NOption(2230, NodeUpgrade_LaserPistol, 004);
      afford := true;
   end
   if (afford == false) then begin
      NOption(2140, ZackEnd, 004);
   end
   NOption(2110, NodeUpgrade, 4);
   NOption(2120, Zack01, 4);
   NOption(2130, ZackEnd, 4);
end

procedure NodeUpgrade_Rifles begin
variable temp_msg;
variable afford := false;
   temp_msg := "";
   if (dude_item_count(PID_HUNTING_RIFLE)) then begin
      temp_msg += mstr(2300) + HRCOST + mstr(2202) + " ";
   end
   if (dude_item_count(PID_ASSAULT_RIFLE)) then begin
      temp_msg += mstr(2301) + ARCOST + mstr(2202) + " ";
   end
   if (dude_item_count(PID_FN_FAL)) then begin
      temp_msg += mstr(2302) + FNCOST + mstr(2202);
   end
   if (dude_item_count(PID_LASER_RIFLE)) then begin
      temp_msg += mstr(2303) + FNCOST + mstr(2202);
   end

   Reply(temp_msg);
   if ((dude_item_count(PID_HUNTING_RIFLE)) and (dude_caps >= HRCOST)) then begin
      NOption(2310, NodeUpgrade_HRifle, 004);
      afford := true;
   end
   if ((dude_item_count(PID_ASSAULT_RIFLE)) and (dude_caps >= ARCOST)) then begin
      NOption(2320, NodeUpgrade_ARifle, 004);
      afford := true;
   end
   if ((dude_item_count(PID_FN_FAL)) and (dude_caps >= FNCOST)) then begin
      NOption(2330, NodeUpgrade_FNFAL, 004);
      afford := true;
   end
   if ((dude_item_count(PID_LASER_RIFLE)) and (dude_caps >= LRCOST)) then begin
      NOption(2340, NodeUpgrade_LaserRifle, 004);
      afford := true;
   end
   if (afford == false) then begin
      NOption(2140, ZackEnd, 004);
   end
   NOption(2110, NodeUpgrade, 4);
   NOption(2120, Zack01, 4);
   NOption(2130, ZackEnd, 4);
end

procedure NodeUpgrade_BigGunsA begin
   Reply(mstr(2500) + FLAMCOST + mstr(2202));
   if (dude_caps >= FLAMCOST) then begin
      NOption(2510, NodeUpgrade_Flamer, 004);
   end else begin
      NOption(2140, ZackEnd, 004);
   end
   NOption(2110, NodeUpgrade, 4);
   NOption(2120, Zack01, 4);
   NOption(2130, ZackEnd, 4);
end

procedure NodeUpgrade_Melee begin
   Reply(mstr(2400) + CPCOST + mstr(2202));
   if (dude_caps >= CPCOST) then begin
      NOption(2410, NodeUpgrade_CattleProd, 004);
   end else begin
      NOption(2140, ZackEnd, 004);
   end
   NOption(2110, NodeUpgrade, 4);
   NOption(2120, Zack01, 4);
   NOption(2130, ZackEnd, 4);
end

procedure NodeUpgrade_Revolver begin
   give_money(MCOST);
   call upgrade_weapon(PID_44_MAGNUM_REVOLVER, PID_44_MAGNUM_SPEEDLOADER);
   call NodeUpgrade_Done;
end
procedure NodeUpgrade_DesertEagle begin
   give_money(DECOST);
   call upgrade_weapon(PID_DESERT_EAGLE, PID_DESERT_EAGLE_EXT_MAG);
   call NodeUpgrade_Done;
end
procedure NodeUpgrade_LaserPistol begin
   give_money(LPCOST);
   call upgrade_weapon(PID_LASER_PISTOL, PID_MAGNETO_LASER_PISTOL);
   call NodeUpgrade_Done;
end

procedure NodeUpgrade_HRifle begin
   give_money(HRCOST);
   call upgrade_weapon(PID_HUNTING_RIFLE, PID_SCOPED_HUNTING_RIFLE);
   call NodeUpgrade_Done;
end
procedure NodeUpgrade_ARifle begin
   give_money(ARCOST);
   call upgrade_weapon(PID_ASSAULT_RIFLE, PID_ASSAULT_RIFLE_EXT_MAG);
   call NodeUpgrade_Done;
end
procedure NodeUpgrade_FNFAL begin
   give_money(FNCOST);
   call upgrade_weapon(PID_FN_FAL, PID_FN_FAL_NIGHT_SCOPE);
   call NodeUpgrade_Done;
end
procedure NodeUpgrade_LaserRifle begin
   give_money(LRCOST);
   call upgrade_weapon(PID_LASER_RIFLE, PID_LASER_RIFLE_EXT_CAP);
   call NodeUpgrade_Done;
end

procedure NodeUpgrade_Flamer begin
   give_money(FLAMCOST);
   call upgrade_weapon(PID_FLAMER, PID_IMPROVED_FLAMETHROWER);
   call NodeUpgrade_Done;
end

procedure NodeUpgrade_CattleProd begin
   give_money(CPCOST);
   call upgrade_weapon(PID_CATTLE_PROD, PID_SUPER_CATTLE_PROD);
   call NodeUpgrade_Done;
end
