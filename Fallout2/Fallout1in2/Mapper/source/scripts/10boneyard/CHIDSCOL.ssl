/*

   Boneyard - CoC Scholar, found in the Library after the mutant invasion

*/

/* Include Files */
#include "define.h"
#include "maps/lafollwr.h"

#define NAME                    SCRIPT_CHIDSCOL
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;

#define LVAR_AssignName       (0)

variable initial := 0;
variable in_timed_event;
variable tmp_hostile;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_CATHEDRAL);
      set_self_ai(AI_BRAINWASHED_CHILDREN);

      if (local_var(LVAR_AssignName) == 0) then begin
         set_local_var(LVAR_AssignName, (100 + map_var(MVAR_COC_SCHOLARS)));
         inc_map_var(MVAR_COC_SCHOLARS);
      end

      self_rotate(random(0, 5));
   end
end

procedure destroy_p_proc begin
   inc_evil_critter
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_FOLL_INVADERS, 1);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(local_var(LVAR_AssignName)));
end

procedure talk_p_proc begin
   script_overrides;
   dude_look_at_self;
   float_msg(self_obj, mstr(random(110, 120)), FLOAT_MSG_NORMAL);
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then
      self_rotate(random(0, 5));
   else
      self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
   in_timed_event := 0;
end

procedure critter_p_proc begin
   if (global_var(GVAR_ENEMY_FOLL_INVADERS)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile and self_can_see_dude) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else if (not(in_timed_event)) then begin
      in_timed_event := 1;
      add_timer_event(self_obj, game_ticks(random(6, 10)), random(1, 3));
   end
end
