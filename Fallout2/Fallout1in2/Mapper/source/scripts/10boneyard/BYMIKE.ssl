#include "define.h"
#include "boneyard.h"

#define NAME                    SCRIPT_BYMIKE

#include "command.h"
#include "modreact.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure BYMike02;
procedure BYMike03;
procedure BYMike04;
procedure BYMike05;
procedure BYMike06;
procedure BYMike07;
procedure BYMike08;
procedure BYMikeEnd;

variable Only_Once := 1;
variable DisplayMessage := 100;

procedure Start begin
   if Only_Once then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      Only_Once := 0;
   end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust(random(10, 100));
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_BLADES_LVL2);

   dude_look_at_self;
   if ((global_var(GVAR_ENEMY_BLADES) == 1) or (dude_iq < 4)) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      if ((local_var(5) == 2) or (map_var(0) == 1)) then begin
         float_msg(self_obj, mstr(116), FLOAT_MSG_NORMAL);
      end
      else begin
         get_reaction
         start_gdialog(923, self_obj, -1, -1, -1);
         gsay_start;
         if get_blades_attack_finished then begin
            call BYMike06;
         end
         else begin
            if (local_var(4) == 0) then begin
               DisplayMessage := 102;
               call BYMike02;
            end
            else begin
               DisplayMessage := 103;
               call BYMike02;
            end
         end
         gsay_end;
         end_dialogue;
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_BLADES, 1);
end

procedure BYMike02 begin
   Reply(DisplayMessage);
   if (local_var(4) == 0) then begin
      NOption(105, BYMike04, 1);
   end
   NOption(104, BYMike03, 1);
   NOption(106, BYMike05, 1);
   NOption(107, BYMikeEnd, 1);
end

procedure BYMike03
begin
   DisplayMessage := 108;
   call BYMike02;
end

procedure BYMike04
begin
   DisplayMessage := 109;
   set_local_var(4, 1);
   call BYMike02;
end

procedure BYMike05
begin
   DisplayMessage := 110;
   call BYMike02;
end

procedure BYMike06
begin
   Reply(111);
   NOption(112, BYMike08, 1);
   NOption(113, BYMike07, 1);
end

procedure BYMike07
begin
   NMessage(115);
   set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep + 1);
   call BYMike08;
end

procedure BYMike08
begin
   variable LVar0 := 0;
   NMessage(115);
   set_local_var(5, 1);
   LVar0 := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, LVar0, 4);
   dude_caps_adjust(random(10, 100));
   set_map_var(0, 1);
end

procedure BYMikeEnd
begin
end
