/*

   Boneyard - Michelle, Blades member

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_MICHELLE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure timed_event_p_proc;
procedure talk_p_proc;

procedure Michelle00;
procedure Michelle01;
procedure Michelle02;
procedure Michelle03;
procedure Michelle04;
procedure Michelle05;
procedure Michelle06;
procedure Michelle07;
procedure Michelle08;
procedure Michelle09;
procedure Michelle10;
procedure Michelle11;
procedure Michelle12;
procedure Michelle13;
procedure Michelle14;
procedure Michelle15;
procedure Michelle16;
procedure Michelle17;
procedure MichelleCombat;
procedure MichelleEnd;

procedure show_true_name;
procedure show_false_name;

variable tmp_hostile;
variable initial := 0;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude and (global_var(GVAR_BLADES_LEFT) < 41)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_BLADES_LEFT, (global_var(GVAR_BLADES_LEFT) - 1));
   inc_good_critter
end

procedure look_at_p_proc begin
   if (((global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) or dude_luck) > 6) then begin
      call show_true_name;
   end
   else if (dude_luck < 4) then begin
      call show_false_name;
   end
   else if (dude_iq < 5) then begin
      call show_false_name;
   end
   else begin
      if (random(0, 1)) then begin
         call show_false_name;
      end
      else begin
         call show_true_name;
      end
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure timed_event_p_proc begin
   if (self_can_see_dude) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   start_gdialog(283, self_obj, 4, -1, -1);
   gsay_start;
   set_local_var(0, 1);
   if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
      call Michelle15;
   end
   else begin
      if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 1) then begin
         call Michelle16;
      end
      else if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) then begin
         call Michelle17;
      end
      else begin
         call Michelle00;
      end
   end
   gsay_end;
   end_dialogue;
end

procedure show_true_name begin
   script_overrides;
   if (local_var(0)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(103));
   end
end

procedure show_false_name begin
   script_overrides;
   display_msg(mstr(random(104, 110)));
end

procedure Michelle00 begin
   Reply(111);
   NLowOption(112, Michelle01);
   NOption(113, Michelle02, 4);
   NOption(114, Michelle09, 5);
   NOption(115, MichelleCombat, 4);
end

procedure Michelle01 begin
   Reply(116);
   NLowOption(117, MichelleCombat);
   NLowOption(118, MichelleEnd);
end

procedure Michelle02 begin
   Reply(119);
   NOption(120, Michelle03, 4);
   NOption(121, Michelle04, 5);
   NOption(122, Michelle08, 5);
   NOption(123, MichelleCombat, 4);
end

procedure Michelle03 begin
   NMessage(124);
   call MichelleCombat;
end

procedure Michelle04 begin
   Reply(125);
   NOption(126, Michelle05, 5);
   NOption(127, Michelle06, 5);
   NOption(128, Michelle07, 7);
end

procedure Michelle05 begin
   Reply(129);
   NOption(130, MichelleEnd, 5);
   NOption(131, MichelleEnd, 5);
end

procedure Michelle06 begin
   NMessage(132);
   call MichelleCombat;
end

procedure Michelle07 begin
   NMessage(133);
   call MichelleCombat;
end

procedure Michelle08 begin
   NMessage(134);
   call MichelleCombat;
end

procedure Michelle09 begin
   Reply(135);
   NOption(136, Michelle10, 5);
   NOption(137, Michelle11, 5);
   NOption(138, Michelle04, 5);
   NOption(139, Michelle12, 5);
   NOption(140, MichelleCombat, 5);
end

procedure Michelle10 begin
   NMessage(141);
   call MichelleCombat;
end

procedure Michelle11 begin
   NMessage(mstr(142) + " " + mstr(143));
   call MichelleCombat;
end

procedure Michelle12 begin
   Reply(144);
   NOption(145, Michelle13, 5);
   NOption(146, Michelle14, 5);
   NOption(147, MichelleCombat, 5);
end

procedure Michelle13 begin
   NMessage(148);
end

procedure Michelle14 begin
   NMessage(149);
   call MichelleCombat;
end

procedure Michelle15 begin
   NMessage(150);
   add_timer_event(self_obj, game_ticks(10), 1);
end

procedure Michelle16 begin
   NMessage(151);
end

procedure Michelle17 begin
   NMessage(152);
end

procedure MichelleCombat begin
   tmp_hostile := 1;
end

procedure MichelleEnd begin
end
