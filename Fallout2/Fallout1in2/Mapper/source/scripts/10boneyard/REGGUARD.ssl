/*

	Boneyard, Adytum - Regulator Guards

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\laadytum.h"
#include "..\headers\boneyard.h"

#define NAME                    SCRIPT_REGGUARD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

procedure DialogWeapon;
procedure DialogFirstTime;
procedure DialogSubsequent;
procedure DialogExit;
procedure DialogMain1;
procedure DialogMain2;
procedure DialogMain3;
procedure DialogMain4;
procedure DialogMain5;
procedure DialogMain6;

import variable RegGuard1;
import variable RegGuard2;

variable Only_Once := 1;
variable tmp_hostile;

procedure Start begin
	if Only_Once then begin
		set_self_team( TEAM_LA_REGULATORS );
		set_self_ai( AI_REGULATORS );

		if (local_var(6) == 0) then begin
			set_map_var(MVAR_RegulatorCount, map_var(MVAR_RegulatorCount) + 1);
			set_local_var(6, 1);
		end

		if get_blades_attacking then begin
			variable LVar0;
			LVar0 := global_var( GVAR_WATER_CHIP_11 );
			if (LVar0 == 0) then begin
				RegGuard1 := self_obj;
			end
			else if (LVar0 == 1) then begin
				RegGuard2 := self_obj;
			end
			LVar0 := LVar0 + 1;
			set_global_var( GVAR_WATER_CHIP_11, LVar0 );
		end
		else begin
			RegGuard1 := 0;
			RegGuard2 := 0;
		end
		Only_Once := 0;
	end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust( random(2, 20) );
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_REGULATR, 100));
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_REGULATR, 100));
end

procedure talk_p_proc begin
	dude_look_at_self;
	if ((global_var( GVAR_ENEMY_ADYTUM ) == 1) or (global_var( GVAR_DUDE_ENEMY_REGULATORS ) == 1) or get_blades_attacking) then begin
		float_msg( self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED );
	end
	else begin
		get_reaction
		start_gdialog(252, self_obj, -1, -1, -1);
		gsay_start;
		if (dude_is_armed) then begin
			call DialogWeapon;
		end
		else begin
			if (local_var(4) == 0) then begin
				set_local_var(4, 1);
				call DialogFirstTime;
			end
			else begin
				call DialogSubsequent;
			end
		end
		gsay_end;
		end_dialogue;
	end
end

procedure critter_p_proc begin
	if (self_can_see_dude) then begin
		if (get_blades_attacking and (local_var(5) == 0)) then begin
			attack(dude_obj);
			set_global_var( GVAR_DUDE_ENEMY_REGULATORS, 1 );
		end
		else if ((global_var( GVAR_ENEMY_ADYTUM ) == 1) or (global_var( GVAR_DUDE_ENEMY_REGULATORS ) == 1)) then begin
			attack(dude_obj);
		end
		else if not(get_blades_attacking) and (map_var(0) == 0) then begin
			set_map_var(0, 1);
			dialogue_system_enter;
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_DUDE_ENEMY_REGULATORS, 1 );
	end
end

procedure destroy_p_proc begin
	set_map_var(MVAR_RegulatorCount, (map_var(MVAR_RegulatorCount) - 1));
	if (source_obj == dude_obj) then begin
		inc_evil_critter
		set_global_var( GVAR_DUDE_ENEMY_REGULATORS, 1 );
	end
	if (map_var(MVAR_RegulatorCount) == 0) then begin
		terminate_combat;
	end
end

procedure pickup_p_proc begin
	set_global_var( GVAR_DUDE_ENEMY_REGULATORS, 1 );
end

procedure DialogWeapon begin
	gsay_message( SCRIPT_REGULATR, 131, NEUTRAL_REACTION );
end

procedure DialogFirstTime begin
	gsay_reply( SCRIPT_REGULATR, 103 );
	giq_option( -3, SCRIPT_REGULATR, 106, DialogMain1, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_REGULATR, 104, DialogMain1, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_REGULATR, 105, DialogMain2, NEUTRAL_REACTION );
end

procedure DialogSubsequent begin
	gsay_reply( SCRIPT_REGULATR, 111 );
	giq_option( -3, SCRIPT_REGULATR, 106, DialogMain1, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_REGULATR, 104, DialogMain1, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_REGULATR, 105, DialogMain2, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_REGULATR, 114, DialogExit, NEUTRAL_REACTION );
end

procedure DialogExit begin
end

procedure DialogMain1 begin
	if quest_kill_razor_inactive then begin
		gsay_message( SCRIPT_REGULATR, 107, NEUTRAL_REACTION );
	end
	else if quest_kill_razor_active then begin
		gsay_message( SCRIPT_REGULATR, 109, NEUTRAL_REACTION );
	end
	else begin
		gsay_message( SCRIPT_REGULATR, 110, NEUTRAL_REACTION );
	end
end

procedure DialogMain2 begin
	if (local_var(1) < 2) then begin
		gsay_message( SCRIPT_REGULATR, 116, NEUTRAL_REACTION );
	end
	else begin
		gsay_reply( SCRIPT_REGULATR, 117 );
		giq_option( -3, SCRIPT_REGULATR, 106, DialogExit, NEUTRAL_REACTION );
		giq_option( 4, SCRIPT_REGULATR, 118, DialogMain3, NEUTRAL_REACTION );
		giq_option( 4, SCRIPT_REGULATR, 119, DialogMain4, NEUTRAL_REACTION );
		giq_option( 5, SCRIPT_REGULATR, 120, DialogMain5, NEUTRAL_REACTION );
		giq_option( 6, SCRIPT_REGULATR, 121, DialogMain6, NEUTRAL_REACTION );
		giq_option( 4, SCRIPT_REGULATR, 114, DialogExit, NEUTRAL_REACTION );
	end
end

procedure DialogMain3 begin
	gsay_message( SCRIPT_REGULATR, 123, NEUTRAL_REACTION );
	call DialogMain2;
end

procedure DialogMain4 begin
	gsay_message( SCRIPT_REGULATR, 124, NEUTRAL_REACTION );
	call DialogMain2;
end

procedure DialogMain5 begin
	if quest_kill_razor_inactive then begin
		gsay_message( SCRIPT_REGULATR, 125, NEUTRAL_REACTION );
	end
	else if quest_kill_razor_active then begin
		gsay_message( SCRIPT_REGULATR, 126, NEUTRAL_REACTION );
	end
	else begin
		gsay_message( SCRIPT_REGULATR, 127, NEUTRAL_REACTION );
	end
end

procedure DialogMain6 begin
	gsay_message( SCRIPT_REGULATR, 128, NEUTRAL_REACTION );
	gsay_message( SCRIPT_REGULATR, 129, NEUTRAL_REACTION );
	call DialogMain2;
end
