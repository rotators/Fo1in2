#include "..\headers\define.h"

#define NAME                    SCRIPT_BLADE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure timed_event_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;

variable Only_Once := 1;
variable PsstTime;

procedure push_p_proc begin
end

procedure Start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      add_timer_event(self_obj, game_ticks(random(3, 5)), 1);
      if (local_var(4) == 0) then begin
         set_local_var(4, self_tile);
      end
   end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust(random(10, 100));
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_BLADES_STATUS) == 2) then begin
      float_msg(self_obj, mstr(random(101, 115)), FLOAT_MSG_NORMAL);
   end
   else begin
      float_msg(self_obj, mstr(random(101, 111)), FLOAT_MSG_NORMAL);
   end
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   LVar0 := global_var(GVAR_BLADE_SOMETHING_0);
   if ((((game_time / 10) - LVar0) >= 10) and (tile_distance_objs(self_obj, dude_obj) <= 4) and (global_var(GVAR_ENEMY_BLADES) == 0)) then begin
      if (global_var(GVAR_BLADES_STATUS) == 2) then begin
         float_msg(self_obj, mstr(random(101, 115)), FLOAT_MSG_NORMAL);
      end
      else begin
         float_msg(self_obj, mstr(random(101, 111)), FLOAT_MSG_NORMAL);
      end
      LVar0 := game_time / 10;
      set_global_var(GVAR_BLADE_SOMETHING_0, LVar0);
   end
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
   inc_good_critter
end

procedure timed_event_p_proc begin
   variable LVar0 := 0;
   LVar0 := tile_num_in_direction(local_var(4), random(0, 5), random(1, 5));
   self_walk_to_tile(LVar0);
   add_timer_event(self_obj, game_ticks(random(8, 25)), 1); // was random(3, 5)
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_BLADES, 1);
end
