/*

   Boneyard - Julianna

*/

#include "define.h"
#include "area/boneyard.h"

#define NAME                    SCRIPT_JULIANNA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure Julianna01;
procedure Julianna02;
procedure Julianna03;
procedure Julianna04;
procedure Julianna05;
procedure Julianna06;
procedure Julianna07;
procedure Julianna08;
procedure Julianna09;
procedure Julianna10;
procedure JuliannaEnd;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

variable initial := 0;
variable round_counter;

#define LVAR_Sleeping                     (6)

procedure start begin
   if not(initial) then begin
      initial := 1;
      if (get_blades_attacking or get_blades_attacking_wait) then begin
         set_self_team(TEAM_PLAYER);
      end
      else begin
         set_self_team(TEAM_LA_ADYTOWNER);
      end
      sleep_time := 1930;
      wake_time := 830;
      sleep_tile := 23151;
      home_tile := 22749;
   end
end

procedure critter_p_proc begin
   if (local_var(5) and (local_var(6) == 0) and (tile_distance_objs(self_obj, dude_obj) < 8)) then begin
      call flee_dude;
   end
   else begin
      sleeping
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_ADYTUM, 1);
      set_self_team(TEAM_LA_ADYTOWNER);
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_ADYTUM, 1);
      set_self_team(TEAM_LA_ADYTOWNER);
   end
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(4)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure pickup_p_proc begin
   set_local_var(5, 1);
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(5)) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, 102), FLOAT_MSG_NORMAL);
   end
   else begin
      start_gdialog(257, self_obj, 4, -1, -1);
      gsay_start;
      if (global_var(GVAR_QUEST_BONEYARD_2_ROMERO) < 3) then begin
         call Julianna01;
      end
      else begin
         call Julianna10;
      end
      gsay_end;
      end_dialogue;
   end
   if (global_var(GVAR_QUEST_BONEYARD_2_ROMERO) == 2) then begin
      if (local_var(7) == 0) then begin
         set_local_var(7, 1);
         give_xp_msg(250, mstr(126));
      end
   end
end

procedure timed_event_p_proc begin
   float_msg(self_obj, mstr(124), FLOAT_MSG_NORMAL);
   display_msg(mstr(125));
   critter_dmg(self_obj, 200, DMG_normal_dam);
end

procedure Julianna01 begin
   Reply(102);
   NLowOption(103, JuliannaEnd);
   NOption(104, JuliannaEnd, 4);
   if (global_var(GVAR_QUEST_BONEYARD_2_ROMERO) == 1) then begin
      NOption(105, Julianna06, 1);
   end
   if (not(local_var(4))) then begin
      NOption(106, Julianna02, 4);
   end
end

procedure Julianna02 begin
   set_local_var(4, 1);
   Reply(107);
   NOption(108, Julianna03, 4);
   NOption(109, Julianna04, 4);
end

procedure Julianna03 begin
   Reply(110);
   NOption(111, Julianna05, 4);
   Goodbyes;
   NOption(exit_line, JuliannaEnd, 4);
end

procedure Julianna04 begin
   Reply(112);
   Goodbyes;
   NOption(exit_line, JuliannaEnd, 4);
end

procedure Julianna05 begin
   Reply(113);
   Goodbyes;
   NOption(exit_line, JuliannaEnd, 4);
end

procedure Julianna06 begin
   Reply(114);
   NOption(115, Julianna07, 1);
   NOption(116, Julianna08, 1);
   if (dude_item_count(PID_GOLD_LOCKET)) then begin
      NOption(117, Julianna09, 1);
   end
end

procedure Julianna07 begin
   NMessage(118);
   add_timer_event(self_obj, 1, 1);
end

procedure Julianna08 begin
   Reply(119);
   NOption(120, JuliannaEnd, 4);
   if (dude_item_count(PID_GOLD_LOCKET)) then begin
      NOption(121, Julianna09, 4);
   end
end

procedure Julianna09 begin
   variable LVar0 := 0;
   LVar0 := dude_item(PID_GOLD_LOCKET);
   rm_obj_from_inven(dude_obj, dude_item(PID_GOLD_LOCKET));
   add_obj_to_inven(self_obj, LVar0);
   NMessage(122);
   set_global_var(GVAR_QUEST_BONEYARD_2_ROMERO, 2);
end

procedure Julianna10 begin
   NMessage(123);
end

procedure JuliannaEnd begin
end
