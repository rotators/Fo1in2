/*

   Boneyard - Miles the chemist

*/

/* Include Files */
#include "define.h"
#include "maps/laadytum.h"
#include "area/boneyard.h"

#define NAME                    SCRIPT_MILES
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "upgweap.h"
#include "modreact.h"
#include "sfall/sfall.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure Miles01;
procedure Miles02;
procedure Miles03;
procedure Miles04;
procedure Miles05;
procedure Miles06;
procedure Miles07;
procedure Miles08;
procedure Miles09;
procedure Miles11;
procedure Miles12;
procedure Miles13;
procedure Miles14;
procedure Miles15;
procedure Miles16;
procedure Miles17;
procedure Miles17a;
procedure Miles18;
procedure Miles19;
procedure Miles22;
procedure Miles22a;
procedure Miles23;
procedure Miles24;
procedure Miles25;
procedure Miles27;
procedure Miles28;
procedure Miles28a;
procedure Miles29;
procedure MilesEnd;

procedure MilesUpgrade;
procedure NodeMoreHPA1;
procedure NodeMoreHPA2;
procedure NodeMoreHPA2a;
procedure NodeMoreHPA3;
procedure NodeUpgradeFuel1;
procedure NodeUpgradeFuel1a;
procedure NodeUpgradeFuel2;
procedure NodeUpgradeFuel3;
procedure NodeUpgradeFuel4;
procedure NodeUpgradeFuel5;

#define LVAR_Here_Before            (4)
#define LVAR_Miles_Status           (5)
#define LVAR_Home_Tile              (6)
#define LVAR_Home_Rotation          (7)

variable Only_Once := 1;
variable DisplayMessage := 100;
variable Item;
variable FuelBatch := 0;
variable count_fuel;

#define upgrade_cost(base)          ((base * (25000/(160 + has_skill(dude_obj, SKILL_BARTER))))/100)
#define FUELCOST                    upgrade_cost(100)

procedure Start begin
   if Only_Once then begin
      /*if (get_blades_attacking or get_blades_attacking_wait) then begin
         set_self_team(TEAM_PLAYER);
      end
      else begin*/
         set_self_team(TEAM_LA_ADYTOWNER);
      //end
      set_self_ai(AI_PEASANT);
      Only_Once := 0;
   end
end

procedure map_enter_p_proc begin
   if map_first_run then begin
      set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);
      self_caps_adjust(random(500, 750));
   end
   else if (is_loading_game == false) then begin
      critter_attempt_placement(self_obj, local_var(LVAR_Home_Tile), self_elevation);
      self_rotate(local_var(LVAR_Home_Rotation));
   end
end

procedure map_update_p_proc begin
   set_fight_regulators;
end

procedure timed_event_p_proc begin
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_ADYTUM_LVL2);

   dude_look_at_self;
   if REP_ENEMY_LA_ADYTUM then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else if ((local_var(LVAR_Here_Before) == 1) and (dude_iq < 4)) then begin
      float_msg(self_obj, mstr(152), FLOAT_MSG_NORMAL);
   end
   else if (local_var(LVAR_Miles_Status) == 2) then begin
      if ((fo1in2_weapon_upgrades_enabled and dude_item_count(PID_FLAMETHROWER_FUEL)) or (fo1in2_unlimited_hpa_enabled and dude_item_count(PID_POWERED_ARMOR))) then
         start_dialog_at_node(MilesUpgrade);
      else
         float_msg(self_obj, mstr(152), FLOAT_MSG_NORMAL);
   end
   else begin
      get_reaction
      if (local_var(LVAR_Here_Before) == 0) then begin
         start_dialog_at_node(Miles01);
      end
      else if get_quest_farm_inactive then begin
         DisplayMessage := 118;
         start_dialog_at_node(Miles04);
      end
      else if get_quest_farm_active then begin
         start_dialog_at_node(Miles13);
      end
      else if get_given_parts_to_smitty_yet then begin
         start_dialog_at_node(Miles18);
      end
      else if get_parts_fixed then begin
         start_dialog_at_node(Miles19);
      end
      else if get_offer_upgrading_item then begin
         if (local_var(LVAR_Miles_Status) == 0) then begin
            if (dude_item_count(PID_POWERED_ARMOR)) then
               start_dialog_at_node(Miles22);
            else
               start_dialog_at_node(Miles22a);
         end
         else if (local_var(LVAR_Miles_Status) == 9305) then begin
            if (dude_item_count(PID_POWERED_ARMOR)) then
               start_dialog_at_node(Miles24);
            else
               start_dialog_at_node(Miles29);
         end
         else begin
            if (dude_item_count(PID_POWERED_ARMOR)) then
               start_dialog_at_node(Miles28);
            else
               start_dialog_at_node(Miles29);
         end
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_ADYTUM, 1);
      set_self_team(TEAM_LA_ADYTOWNER);
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      inc_good_critter
      set_global_var(GVAR_ENEMY_ADYTUM, 1);
      set_self_team(TEAM_LA_ADYTOWNER);
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if REP_ENEMY_LA_ADYTUM then begin
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_ADYTUM, 1);
end

procedure Miles01 begin
   Reply(102);
   NOption(103, Miles02, 4);
   NOption(104, Miles03, 4);
   NLowOption(105, Miles09);

   if debug_mode and (dude_item_count(PID_POWERED_ARMOR)) then
      NOption("*** CHEAT *** Just upgrade my damn power armor!", Miles28, 4);
end

procedure Miles02 begin
   NMessage(106);
end

procedure Miles03 begin
   DisplayMessage := 107;
   call Miles04;
end

procedure Miles04 begin
   set_local_var(LVAR_Here_Before, 1);

   Reply(DisplayMessage);
   NOption(108, Miles05, 1);
   NOption(109, Miles06, 1);
   NOption(110, Miles07, 1);
   NOption(111, Miles08, 1);
end

procedure Miles05 begin
   Reply(112);
   NOption(113, Miles11, 1);
end

procedure Miles06 begin
   DisplayMessage := 114;
   call Miles04;
end

procedure Miles07 begin
   DisplayMessage := 115;
   call Miles04;
end

procedure Miles08 begin
   NMessage(116);
end

procedure Miles09 begin
   set_local_var(LVAR_Here_Before, 1);
   NMessage(117);
end

procedure Miles11 begin
   Reply(119);
   NOption(120, Miles12, 1);
   NOption(121, MilesEnd, 1);
end

procedure Miles12 begin
   NMessage(122);
   set_quest_farm_active;
end

procedure Miles13 begin
   Reply(123);
   if party_has_item(PID_FARM_PARTS) then
      NOption(124, Miles15, 1);
   else
      NOption(125, Miles14, 1);
end

procedure Miles14 begin
   NMessage(126);
end

procedure Miles15 begin
   Reply(127);
   set_give_parts_to_smitty;
   NOption(128, Miles16, 1);
   NOption(129, MilesEnd, 1);
end

procedure Miles16 begin
   Reply(130);
   NOption(131, Miles17, 1);
   NOption(132, Miles17a, 1);
end

procedure Miles17 begin
   NMessage(133);

   party_remove_item(PID_FARM_PARTS);
   dude_caps_adjust(125);

   Item := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, Item, 3);

   set_offer_upgrading_item;
   NMessage(137);
end

procedure Miles17a begin
   UpReactLevel
   NMessage(133);

   party_remove_item(PID_FARM_PARTS);
   dude_caps_adjust(150);

   Item := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, Item, 4);

   set_offer_upgrading_item;
   NMessage(137);
end

procedure Miles18 begin
   Reply(134);
   NOption(128, Miles16, 1);
   NOption(129, MilesEnd, 1);
end

procedure Miles19 begin
   NMessage(135);
   NMessage(136);

   inc_boneyard_rep(REP_BONUS_FARM_REPAIRED);
   party_remove_item(PID_FARM_PARTS);
   dude_caps_adjust(250);

   Item := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, Item, 6);

   set_offer_upgrading_item;
   NMessage(137);
end

procedure Miles22 begin
   Reply(138);

   set_local_var(LVAR_Miles_Status, 9305);
   set_global_var(GVAR_CHEMISTRY_BOOK, 1);

   NOption(139, MilesEnd, 1);
   NOption(140, Miles23, 1);
end

procedure Miles22a begin
   //NMessage(153);
   Reply(153);
   if (fo1in2_weapon_upgrades_enabled and dude_item_count(PID_FLAMETHROWER_FUEL)) then
      NOption(155, NodeUpgradeFuel1, 004);
   NOption(154, MilesEnd, 1);
end

procedure Miles23 begin
   //NMessage(141);
   Reply(141);
   NOption(154, MilesEnd, 1);
end

procedure Miles24 begin
   Reply(142);
   if (dude_item_count(PID_CHEMISTRY_MANUAL)) then
      NOption(143, Miles25, 1);
   else
      NOption(144, MilesEnd, 1);
end

procedure Miles25 begin
   Reply(145);

   Item := dude_item(PID_CHEMISTRY_MANUAL);
   rm_obj_from_inven(dude_obj, Item);

   add_obj_to_inven(self_obj, Item);
   set_local_var(LVAR_Miles_Status, 9306);

   NOption(146, Miles28, 1);
   NOption(147, Miles27, 1);
end

procedure Miles27 begin
   NMessage(148);
end

procedure Miles28 begin
   NMessage(149);

   gfade_out(1);
      game_time_advance(ONE_GAME_DAY);       // 1 day for bonding to occur
      if (local_var(LVAR_Miles_Status) < 2) then
         game_time_advance(ONE_GAME_HOUR * 6);  // 6 hours to read the books and perform pre-bonding tasks. Skipped when doing additional power armors.
   gfade_in(1);

   call Miles28a;
end

procedure Miles28a begin
   NMessage(150);

   Item := dude_item(PID_POWERED_ARMOR);
   rm_obj_from_inven(dude_obj, Item);

   Item := create_object(PID_HARDENED_POWER_ARMOR, 0, 0);
   add_mult_objs_to_inven(dude_obj, Item, 1);

   set_local_var(LVAR_Miles_Status, 2);
end

procedure Miles29 begin
   NMessage(151);
end

procedure MilesEnd begin
end

procedure MilesUpgrade begin
   if fo1in2_unlimited_hpa_enabled and dude_item_count(PID_POWERED_ARMOR) then begin
      Reply(2000);
      NOption(2030, MilesEnd, 004);
      if (dude_caps >= 1000) then
         NOption(2010, NodeMoreHPA2, 004);
      else
         NOption(2020, NodeMoreHPA3, 004);
   end
   else begin
      Reply(118);
   end

   if (fo1in2_weapon_upgrades_enabled and dude_item_count(PID_FLAMETHROWER_FUEL)) then
      NOption(155, NodeUpgradeFuel1, 004);

   NOption(g_bye, MilesEnd, 004);
end

procedure NodeMoreHPA1 begin // Currently deprecated
   Reply(2000);
   if (dude_caps >= 1000) then
      NOption(2010, NodeMoreHPA2, 004);
   else
      NOption(2020, NodeMoreHPA3, 004);
   NOption(2030, MilesEnd, 004);
end

procedure NodeMoreHPA2 begin
   Reply(2100);
   NOption(2110, NodeMoreHPA2a, 004);
   NOption(2120, Miles27, 004);
end

procedure NodeMoreHPA2a begin
   dude_caps_adjust(-1000);
   call Miles28;
end

procedure NodeMoreHPA3 begin
   NMessage(2200);
end

procedure NodeUpgradeFuel1 begin
   Reply(mstr(3000) + FUELCOST + mstr(3001));
   if (dude_caps < FUELCOST) then begin
      NOption(3030, MilesEnd, 004);
   end
   else
      call NodeUpgradeFuel1a;
   NOption(3040, MilesEnd, 004);
end

procedure NodeUpgradeFuel1a begin
variable tmp_mstr;
   count_fuel := dude_item_count(PID_FLAMETHROWER_FUEL);

   if (dude_caps >= FUELCOST) then begin
      NOption(mstr(3010 + FuelBatch) + FUELCOST + mstr(3023), NodeUpgradeFuel2, 004); // 1
   end
   if (count_fuel > 1 and dude_caps >= (FUELCOST * count_fuel)) then begin
      variable num_text := num_to_num_text(count_fuel);
      NOption(mstr(3020 + FuelBatch) + num_text + mstr(3022) + (FUELCOST * count_fuel) + mstr(3023), NodeUpgradeFuel3, 004); // All
   end

   FuelBatch := 1;
end

procedure NodeUpgradeFuel2 begin
   count_fuel := 1;
   call NodeUpgradeFuel4;
end

procedure NodeUpgradeFuel3 begin
   call NodeUpgradeFuel4;
end

procedure NodeUpgradeFuel4 begin
variable i;

   dude_caps_adjust(-(FUELCOST * count_fuel));
   for (i := 0; (i < count_fuel) ; i++) begin
      call upgrade_weapon(PID_FLAMETHROWER_FUEL, PID_FLAMETHROWER_FUEL_MK_II);
   end

   NMessage(4000);

   gfade_out(1);
   game_time_advance((ONE_GAME_HOUR * 4) * count_fuel);
   gfade_in(1);

   Reply(4001);
   if dude_item_count(PID_FLAMETHROWER_FUEL) then
      call NodeUpgradeFuel1a;
   NOption(4010, MilesEnd, 004);
end

procedure NodeUpgradeFuel5 begin
   NMessage(4100);
end
