/*

   Boneyard, Adytum - Jon Zimmermann

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\laadytum.h"
#include "..\headers\boneyard.h"

#define NAME                    SCRIPT_JON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure JonZimmerman00;
procedure JonZimmerman01;
procedure JonZimmerman02;
procedure JonZimmerman03;
procedure JonZimmerman03a;
procedure JonZimmerman04;
procedure JonZimmerman05;
procedure JonZimmerman06;
procedure JonZimmerman07;
procedure JonZimmerman08;
procedure JonZimmerman08a;
procedure JonZimmerman09;
procedure JonZimmerman10;
procedure JonZimmerman11;
procedure JonZimmerman12;
procedure JonZimmerman13;
procedure JonZimmerman14;
procedure JonZimmerman15;
procedure JonZimmerman16;
procedure JonZimmerman17;
procedure JonZimmerman18;
procedure JonZimmerman19;
procedure JonZimmerman20;
procedure JonZimmerman21;
procedure JonZimmerman22;
procedure JonZimmerman23;
procedure JonZimmerman24;
procedure JonZimmerman25;
procedure JonZimmerman26;
procedure JonZimmerman27;
procedure JonZimmermanEnd;

procedure Cheat001;

import variable JonPtr;

variable Only_Once := 1;
variable DisplayMessage;

#define LVAR_Here_Before            (4)

#define LVAR_Home_Tile              (6)
#define LVAR_Home_Rotation          (7)

procedure Start begin
   if Only_Once then begin
      Only_Once := 0;

      if get_blades_attacking then begin
         if not(get_zimmermann_turned) then begin
            set_self_team(TEAM_LA_REGULATORS);
         end
         else begin
            set_self_team(TEAM_PLAYER);
         end
      end
      else begin
         set_self_team(TEAM_LA_ADYTOWNER);
      end
      set_self_ai(AI_PEASANT);
      JonPtr := self_obj;
   end
end

procedure map_enter_p_proc begin
   if map_first_run then begin
      set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);
      self_caps_adjust(random(50, 150));
   end
   else begin
      critter_attempt_placement(self_obj, local_var(LVAR_Home_Tile), self_elevation);
      self_rotate(local_var(LVAR_Home_Rotation));
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Here_Before) == 1) then
      display_msg(mstr(176));
   else
      display_msg(mstr(175));
end

procedure description_p_proc begin
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_ADYTUM_LVL3);

   dude_look_at_self;

   if (global_var(GVAR_ENEMY_ADYTUM) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else if (map_var(MVAR_RegulatorCount) == 0) and get_zimmermann_turned then begin
      // Added the float message, because in vanilla there seems to be nothing if Zimmermann survives the Blade invasion??
      float_msg(self_obj, mstr(random(183, 187)), FLOAT_COLOR_NORMAL);
   end
   else begin
      if ((local_var(LVAR_Here_Before) == 1) and (dude_iq < 4)) then begin
         float_msg(self_obj, mstr(139), FLOAT_MSG_NORMAL);
      end
      else if get_zimmermann_at_peace then begin
         float_msg(self_obj, mstr(random(183, 187)), FLOAT_MSG_NORMAL);
      end
      else begin
         get_reaction
         DisplayMessage := 100;
         if (local_var(LVAR_Here_Before) == 0) then begin
            start_dialog_at_node(JonZimmerman00);
         end
         else if (global_var(GVAR_ZIMMERMAN_STATUS) == 0) then begin
            start_dialog_at_node(JonZimmerman01);
         end
         else if global_var(GVAR_RECEIVED_HOLODISK_RAZOR) then begin
            if get_task_kill_razor then
               start_dialog_at_node(JonZimmerman24);
            else
               start_dialog_at_node(JonZimmerman27);
         end
         else begin
            if get_zimmermann_insulted then begin
               start_dialog_at_node(JonZimmerman17);
            end
            else if get_task_kill_razor then begin
               start_dialog_at_node(JonZimmerman19);
            end
            else if get_razor_killed then begin
               start_dialog_at_node(JonZimmerman21);
            end
         end
      end
   end
end

procedure damage_p_proc begin
   if source_obj == dude_obj then begin
      if (global_var(GVAR_DUDE_ENEMY_REGULATORS) != 1) then begin
         set_global_var(GVAR_ENEMY_ADYTUM, 1);
         set_self_team(TEAM_LA_ADYTOWNER);
      end
   end
end

procedure destroy_p_proc begin
   if source_obj == dude_obj then begin
      if (global_var(GVAR_DUDE_ENEMY_REGULATORS) != 1) then begin
         set_global_var(GVAR_ENEMY_ADYTUM, 1);
         set_self_team(TEAM_LA_ADYTOWNER);
      end
      inc_good_critter
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (get_blades_attacking and not(get_zimmermann_turned)) then begin
         set_self_team(TEAM_LA_REGULATORS);
         float_msg(self_obj, mstr(174), FLOAT_MSG_RED);
         attack(dude_obj);
      end
      else if (global_var(GVAR_ENEMY_ADYTUM) == 1) then begin
         attack(dude_obj);
      end
   end

   if (global_var(GVAR_DUDE_ENEMY_REGULATORS) == 1) and not(get_zimmermann_turned) then begin
      set_self_team(TEAM_LA_REGULATORS);
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_ADYTUM, 1);
end

procedure JonZimmerman00 begin
   DisplayMessage := 182;
   Reply(100);
   NOption(181, JonZimmerman01, 4);
   NLowOption(104, JonZimmerman13);
   set_local_var(LVAR_Here_Before, 1);
end

procedure JonZimmerman01 begin
   Reply(DisplayMessage);
   NOption(101, JonZimmerman02, 4);
   NOption(102, JonZimmerman14, 4);
   if global_var(GVAR_RECEIVED_HOLODISK_RAZOR) then begin
      NOption(178, JonZimmerman26, 4);
   end
   NOption(103, JonZimmermanEnd, 4);
   NLowOption(104, JonZimmerman13);

   if debug_mode then
      NOption("***CHEAT *** [Speech : 100%] Open your eyes ! The Regulators are evil!", Cheat001, 001);
end

// Attack Adytum with the Blades:
procedure Cheat001 begin
   set_blades_attacking;
   call JonZimmerman26;

   debug("mvar: " + map_var(2));
   debug("invasion: " + global_var(GVAR_BLADES_INVASION_READY));
end

procedure JonZimmerman02 begin
   Reply(105);
   NOption(106, JonZimmerman03, 1);
   NOption(107, JonZimmerman03, 1);
   NOption(108, JonZimmerman04, 1);
end

procedure JonZimmerman03 begin
   Reply(109);
   NOption(110, JonZimmerman04, 1);
   NOption(111, JonZimmerman13, 1);
   NOption(112, JonZimmerman03a, 1);
end

procedure JonZimmerman03a begin
   set_zimmermann_insulted;
end

procedure JonZimmerman04 begin
   Reply(113);
   NOption(114, JonZimmerman07, 1);
   NOption(115, JonZimmerman05, 1);
   NOption(116, JonZimmerman05, 1);
end

procedure JonZimmerman05 begin
   Reply(117);
   NOption(118, JonZimmerman06, 1);
   NOption(119, JonZimmerman07, 1);
end

procedure JonZimmerman06 begin
   Reply(120);
   NOption(121, JonZimmerman07, 1);
   NOption(122, JonZimmerman11, 1);
end

procedure JonZimmerman07 begin
   Reply(123);
   NOption(124, JonZimmerman09, 1);
   NOption(125, JonZimmerman08, 1);
   NOption(126, JonZimmerman10, 1);
end

procedure JonZimmerman08 begin
   Reply(127);
   NOption(128, JonZimmerman09, 1);
   NOption(129, JonZimmerman10, 1);
   NOption(130, JonZimmerman08a, 1);
end

procedure JonZimmerman08a begin
   BottomReact
   set_zimmermann_insulted;
end

procedure JonZimmerman09 begin
   Reply(131);
   NOption(132, JonZimmerman10, 1);
   NOption(133, JonZimmerman08a, 1);
   set_zimmermann_insulted;
end

procedure JonZimmerman10 begin
   NMessage(134);
   set_task_kill_razor;
end

procedure JonZimmerman11 begin
   Reply(135);
   NOption(136, JonZimmerman16, 1);
   NOption(137, JonZimmerman08a, 1);
end

procedure JonZimmerman12 begin
   NMessage(138);
   BottomReact

   set_global_var(GVAR_DUDE_ENEMY_REGULATORS, 1);
   gfade_out(1);
   move_to(dude_obj, 10500, 0);
   gfade_in(1);
end

procedure JonZimmerman13 begin
   NMessage(139);
end

procedure JonZimmerman14 begin
   Reply(140);
   NOption(141, JonZimmerman15, 1);
   NOption(142, JonZimmerman12, 1);
end

procedure JonZimmerman15 begin
   Reply(143);
   NOption(144, JonZimmerman03, 1);
   NOption(145, JonZimmerman03, 1);
   NOption(146, JonZimmerman04, 1);
   NOption(147, JonZimmerman12, 1);
end

procedure JonZimmerman16 begin
   Reply(148);
   NOption(149, JonZimmerman07, 1);
   NOption(150, JonZimmerman07, 1);
   NOption(151, JonZimmerman08a, 1);
end

procedure JonZimmerman17 begin
   Reply(152);
   NOption(153, JonZimmerman18, 1);
   NOption(154, JonZimmerman14, 1);
   NOption(155, JonZimmermanEnd, 1);
end

procedure JonZimmerman18 begin
   NMessage(156);
   set_global_var(GVAR_ZIMMERMAN_STATUS, 9001);
end

procedure JonZimmerman19 begin
   Reply(157);
   NOption(158, JonZimmerman12, 1);
   NOption(159, JonZimmerman20, 1);
   NOption(160, JonZimmerman13, 1);
end

procedure JonZimmerman20 begin
   NMessage(161);
end

procedure JonZimmerman21 begin
   Reply(162);
   NOption(163, JonZimmerman22, 1);
   NOption(164, JonZimmerman23, 1);
   NOption(165, JonZimmerman22, 1);
   set_zimmermann_at_peace;
end

procedure JonZimmerman22 begin
   NMessage(166);
   TopReact
   dude_caps_adjust(2000);
end

procedure JonZimmerman23 begin
   NMessage(167);
   TopReact
   dude_caps_adjust(2500);
end

procedure JonZimmerman24 begin
   Reply(168);
   NOption(169, JonZimmerman25, 1);
   NOption(170, JonZimmerman26, 1);
end

procedure JonZimmerman25 begin
   Reply(171);
   NOption(172, JonZimmerman26, 1);
end

procedure JonZimmerman26 begin
   set_global_var(GVAR_REMOVE_JON_DISK_SON, 1);
   party_remove_item(PID_REGULATOR_HOLODISK)

   NMessage(173);
   set_zimmermann_turned;

   // Will the blades help in the attack on adytown?
   if get_blades_attacking_wait then begin
      set_self_team(TEAM_PLAYER);
      set_blades_attacking;
   end
   else begin
      set_self_team(TEAM_PLAYER);
   end
end

procedure JonZimmerman27 begin
   Reply(177);
   NOption(178, JonZimmerman26, 1);
   NOption(179, JonZimmermanEnd, 1);
end

procedure JonZimmermanEnd begin
end
