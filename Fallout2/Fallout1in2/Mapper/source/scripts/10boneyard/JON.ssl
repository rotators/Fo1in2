/*

	Boneyard, Adytum - Jon Zimmermann

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\laadytum.h"
#include "..\headers\boneyard.h"

#define NAME                    SCRIPT_JON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure JonZimmerman00;
procedure JonZimmerman01;
procedure JonZimmerman02;
procedure JonZimmerman03;
procedure JonZimmerman03a;
procedure JonZimmerman04;
procedure JonZimmerman05;
procedure JonZimmerman06;
procedure JonZimmerman07;
procedure JonZimmerman08;
procedure JonZimmerman08a;
procedure JonZimmerman09;
procedure JonZimmerman10;
procedure JonZimmerman11;
procedure JonZimmerman12;
procedure JonZimmerman13;
procedure JonZimmerman14;
procedure JonZimmerman15;
procedure JonZimmerman16;
procedure JonZimmerman17;
procedure JonZimmerman18;
procedure JonZimmerman19;
procedure JonZimmerman20;
procedure JonZimmerman21;
procedure JonZimmerman22;
procedure JonZimmerman23;
procedure JonZimmerman24;
procedure JonZimmerman25;
procedure JonZimmerman26;
procedure JonZimmerman27;
procedure JonZimmermanEnd;

procedure Cheat001;

import variable JonPtr;

variable Only_Once := 1;
variable DisplayMessage;

#define LVAR_Here_Before            (4)

#define LVAR_Home_Tile              (6)
#define LVAR_Home_Rotation          (7)

procedure Start begin
	if Only_Once then begin
		Only_Once := 0;

		if get_blades_attacking then begin
			if not(get_zimmermann_turned) then begin
				set_self_team(TEAM_LA_REGULATORS);
			end
			else begin
				set_self_team(TEAM_PLAYER);
			end
		end
		else begin
			set_self_team(TEAM_LA_ADYTOWNER);
		end
		set_self_ai(AI_PEASANT);
		JonPtr := self_obj;
	end
end

procedure map_enter_p_proc begin
   if map_first_run then begin
	   set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);
      self_caps_adjust(random(50, 150));
   end
   else begin
      critter_attempt_placement(self_obj, local_var(LVAR_Home_Tile), self_elevation);
      self_rotate(local_var(LVAR_Home_Rotation));
   end
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(LVAR_Here_Before) == 1) then begin
		display_msg(mstr(176));
	end
	else begin
		display_msg(mstr(175));
	end
	script_overrides;
end

procedure description_p_proc begin
end

procedure talk_p_proc begin
	dude_look_at_self;

	if (global_var(GVAR_ENEMY_ADYTUM) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
	end
	else if (map_var(MVAR_RegulatorCount) == 0) and get_zimmermann_turned then begin
		// Added the float message, because in vanilla there seems to be nothing if Zimmermann survives the Blade invasion??
		float_msg(self_obj, mstr(random(183, 187)), FLOAT_COLOR_NORMAL);
	end
	else begin
		if ((local_var(LVAR_Here_Before) == 1) and (dude_iq < 4)) then begin
			float_msg(self_obj, mstr(139), FLOAT_MSG_NORMAL);
		end
		else begin
			if get_zimmermann_at_peace then begin
				float_msg(self_obj, mstr(random(183, 187)), FLOAT_MSG_NORMAL);
			end
			else begin
				get_reaction
				start_gdialog(288, self_obj, -1, -1, -1);
				gsay_start;
				DisplayMessage := 100;
				if (local_var(LVAR_Here_Before) == 0) then begin
					call JonZimmerman00;
				end
				else begin
					if (global_var(GVAR_ZIMMERMAN_STATUS) == 0) then begin
						call JonZimmerman01;
					end
					else begin
						if global_var(GVAR_RECEIVED_HOLODISK_RAZOR) then begin
							if get_task_kill_razor then begin
								call JonZimmerman24;
							end
							else begin
								call JonZimmerman27;
							end
						end
						else begin
							if get_zimmermann_insulted then begin
								call JonZimmerman17;
							end
							else if get_task_kill_razor then begin
								call JonZimmerman19;
							end
							else if get_razor_killed then begin
								call JonZimmerman21;
							end
						end
					end
				end
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure damage_p_proc begin
	if source_obj == dude_obj then begin
		if (global_var(GVAR_DUDE_ENEMY_REGULATORS) != 1) then begin
			set_global_var(GVAR_ENEMY_ADYTUM, 1);
			set_self_team(TEAM_LA_ADYTOWNER);
		end
	end
end

procedure destroy_p_proc begin
	if source_obj == dude_obj then begin
		if (global_var(GVAR_DUDE_ENEMY_REGULATORS) != 1) then begin
			set_global_var(GVAR_ENEMY_ADYTUM, 1);
			set_self_team(TEAM_LA_ADYTOWNER);
		end
		inc_good_critter
	end
end

procedure critter_p_proc begin
	if (self_can_see_dude) then begin
		if (get_blades_attacking and not(get_zimmermann_turned)) then begin
			set_self_team(TEAM_LA_REGULATORS);
			float_msg(self_obj, mstr(174), FLOAT_MSG_RED);
			attack(dude_obj);
		end
		else if (global_var(GVAR_ENEMY_ADYTUM) == 1) then begin
			attack(dude_obj);
		end
	end

	if (global_var(GVAR_DUDE_ENEMY_REGULATORS) == 1) and not(get_zimmermann_turned) then begin
		set_self_team(TEAM_LA_REGULATORS);
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	set_global_var(GVAR_ENEMY_ADYTUM, 1);
end

procedure JonZimmerman00 begin
	DisplayMessage := 182;
	Reply(100);
	NOption(181, JonZimmerman01, 4);
	NOption(104, JonZimmerman13, -3);
	set_local_var(LVAR_Here_Before, 1);
end

procedure JonZimmerman01 begin
	Reply(DisplayMessage);
	NOption(101, JonZimmerman02, 4);
	NOption(102, JonZimmerman14, 4);
	if global_var(GVAR_RECEIVED_HOLODISK_RAZOR) then begin
		NOption(178, JonZimmerman26, 4);
	end
	NOption(103, JonZimmermanEnd, 4);
	NOption(104, JonZimmerman13, -3);

	if debug_mode then
		NOption("***CHEAT *** [Speech : 100%] Open your eyes ! The Regulators are evil!",Cheat001,001);
end

// Attack Adytum with the Blades:
procedure Cheat001 begin
	set_blades_attacking;
	call JonZimmerman26;

	debug("mvar: " + map_var(2));
	debug("invasion: " + global_var(GVAR_BLADES_INVASION_READY));
end

procedure JonZimmerman02 begin
	Reply(105);
	gsay_option(NAME, 106, JonZimmerman03, 50);
	gsay_option(NAME, 107, JonZimmerman03, 50);
	gsay_option(NAME, 108, JonZimmerman04, 50);
end

procedure JonZimmerman03 begin
	Reply(109);
	gsay_option(NAME, 110, JonZimmerman04, 50);
	gsay_option(NAME, 111, JonZimmerman13, 50);
	gsay_option(NAME, 112, JonZimmerman03a, 50);
end

procedure JonZimmerman03a begin
	set_zimmermann_insulted;
end

procedure JonZimmerman04 begin
	Reply(113);
	gsay_option(NAME, 114, JonZimmerman07, 50);
	gsay_option(NAME, 115, JonZimmerman05, 50);
	gsay_option(NAME, 116, JonZimmerman05, 50);
end

procedure JonZimmerman05 begin
	Reply(117);
	gsay_option(NAME, 118, JonZimmerman06, 50);
	gsay_option(NAME, 119, JonZimmerman07, 50);
end

procedure JonZimmerman06 begin
	Reply(120);
	gsay_option(NAME, 121, JonZimmerman07, 50);
	gsay_option(NAME, 122, JonZimmerman11, 50);
end

procedure JonZimmerman07 begin
	Reply(123);
	gsay_option(NAME, 124, JonZimmerman09, 50);
	gsay_option(NAME, 125, JonZimmerman08, 50);
	gsay_option(NAME, 126, JonZimmerman10, 50);
end

procedure JonZimmerman08 begin
	Reply(127);
	gsay_option(NAME, 128, JonZimmerman09, 50);
	gsay_option(NAME, 129, JonZimmerman10, 50);
	gsay_option(NAME, 130, JonZimmerman08a, 50);
end

procedure JonZimmerman08a begin
	BottomReact
	set_zimmermann_insulted;
end

procedure JonZimmerman09 begin
	Reply(131);
	gsay_option(NAME, 132, JonZimmerman10, 50);
	gsay_option(NAME, 133, JonZimmerman08a, 50);
	set_zimmermann_insulted;
end

procedure JonZimmerman10 begin
	NMessage(134);
	set_task_kill_razor;
end

procedure JonZimmerman11 begin
	Reply(135);
	gsay_option(NAME, 136, JonZimmerman16, 50);
	gsay_option(NAME, 137, JonZimmerman08a, 50);
end

procedure JonZimmerman12 begin
	NMessage(138);
	BottomReact

	set_global_var(GVAR_DUDE_ENEMY_REGULATORS, 1);
	gfade_out(1);
	move_to(dude_obj, 10500, 0);
	gfade_in(1);
end

procedure JonZimmerman13 begin
	NMessage(139);
end

procedure JonZimmerman14 begin
	Reply(140);
	gsay_option(NAME, 141, JonZimmerman15, 50);
	gsay_option(NAME, 142, JonZimmerman12, 50);
end

procedure JonZimmerman15 begin
	Reply(143);
	gsay_option(NAME, 144, JonZimmerman03, 50);
	gsay_option(NAME, 145, JonZimmerman03, 50);
	gsay_option(NAME, 146, JonZimmerman04, 50);
	gsay_option(NAME, 147, JonZimmerman12, 50);
end

procedure JonZimmerman16 begin
	Reply(148);
	gsay_option(NAME, 149, JonZimmerman07, 50);
	gsay_option(NAME, 150, JonZimmerman07, 50);
	gsay_option(NAME, 151, JonZimmerman08a, 50);
end

procedure JonZimmerman17 begin
	Reply(152);
	gsay_option(NAME, 153, JonZimmerman18, 50);
	gsay_option(NAME, 154, JonZimmerman14, 50);
	gsay_option(NAME, 155, JonZimmermanEnd, 50);
end

procedure JonZimmerman18 begin
	NMessage(156);
	set_global_var(GVAR_ZIMMERMAN_STATUS, 9001);
end

procedure JonZimmerman19 begin
	Reply(157);
	gsay_option(NAME, 158, JonZimmerman12, 50);
	gsay_option(NAME, 159, JonZimmerman20, 50);
	gsay_option(NAME, 160, JonZimmerman13, 50);
end

procedure JonZimmerman20 begin
	NMessage(161);
end

procedure JonZimmerman21 begin
	Reply(162);
	gsay_option(NAME, 163, JonZimmerman22, 50);
	gsay_option(NAME, 164, JonZimmerman23, 50);
	gsay_option(NAME, 165, JonZimmerman22, 50);
	set_zimmermann_at_peace;
end

procedure JonZimmerman22 begin
	NMessage(166);
	TopReact
	dude_caps_adjust(2000);
end

procedure JonZimmerman23 begin
	NMessage(167);
	TopReact
	dude_caps_adjust(2500);
end

procedure JonZimmerman24 begin
	Reply(168);
	gsay_option(NAME, 169, JonZimmerman25, 50);
	gsay_option(NAME, 170, JonZimmerman26, 50);
end

procedure JonZimmerman25 begin
	Reply(171);
	gsay_option(NAME, 172, JonZimmerman26, 50);
end

procedure JonZimmerman26 begin
	set_global_var(GVAR_REMOVE_JON_DISK_SON, 1);
	party_remove_item(PID_REGULATOR_HOLODISK)

	NMessage(173);
	set_zimmermann_turned;

	// Will the blades help in the attack on adytown?
	if get_blades_attacking_wait then begin
		set_self_team(TEAM_PLAYER);
		set_blades_attacking;
	end
	else begin
		set_self_team(TEAM_PLAYER);
	end
end

procedure JonZimmerman27 begin
	Reply(177);
	gsay_option(NAME, 178, JonZimmerman26, 50);
	gsay_option(NAME, 179, JonZimmermanEnd, 50);
end

procedure JonZimmermanEnd begin
end
