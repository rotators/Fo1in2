#include "..\headers\define.h"

#define NAME                    SCRIPT_BYCHRIS

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure BYChris01;
procedure BYChris02;
procedure BYChris03;
procedure BYChris04;
procedure BYChris05;
procedure BYChris06;
procedure BYChris07;
procedure BYChrisEnd;

variable Only_Once := 1;
variable DisplayMessage := 100;

procedure Start begin
	if Only_Once then begin
		set_self_team(TEAM_LA_BLADES );
		set_self_ai( AI_BLADES );
		Only_Once := 0;
	end
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust( random(10, 100) );
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(NAME, 101));
	end
	else begin
		display_msg(message_str(NAME, 100));
	end
end

procedure description_p_proc
begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(NAME, 101));
	end
	else begin
		display_msg(message_str(NAME, 100));
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (global_var( GVAR_ENEMY_BLADES ) == 1) then begin
		float_msg( self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED );
	end
	else begin
		if (local_var(5) == 2) then begin
			float_msg( self_obj, message_str(NAME, 116), FLOAT_MSG_NORMAL );
		end
		else begin
			get_reaction
			start_gdialog(924, self_obj, -1, -1, -1);
			gsay_start;
			DisplayMessage := 102;
			call BYChris01;
			gsay_end;
			end_dialogue;
		end
	end
end

procedure damage_p_proc
begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BLADES, 1 );
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BLADES, 1 );
		if (has_rep_berserker) then begin
			set_global_var( GVAR_BERSERKER_REPUTATION, 1 );
			set_global_var( GVAR_CHAMPION_REPUTATION, 0 );
		end
		if (has_rep_champion) then begin
			set_global_var( GVAR_CHAMPION_REPUTATION, 1 );
			set_global_var( GVAR_BERSERKER_REPUTATION, 0 );
		end
		set_global_var( GVAR_GOOD_MONSTER, global_var( GVAR_GOOD_MONSTER ) + 1 );// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var( GVAR_GOOD_MONSTER ) % 2) == 0) then begin
			set_global_var( GVAR_PLAYER_REPUTATION, (check_general_rep - 1) );
		end
	end
end

procedure critter_p_proc
begin
	if (self_can_see_dude) then begin
		if (global_var( GVAR_ENEMY_BLADES ) == 1) then begin
			attack(dude_obj);
		end
	end
end

procedure pickup_p_proc
begin
	set_global_var( GVAR_ENEMY_BLADES, 1 );
end

procedure BYChris01
begin
	gsay_reply( NAME, DisplayMessage );
	giq_option( 4, NAME, 103, BYChris02, NEUTRAL_REACTION );
	if (local_var(4) == 0) then begin
		giq_option( 4, NAME, 104, BYChris03, NEUTRAL_REACTION );
	end
	giq_option( 4, NAME, 105, BYChris04, NEUTRAL_REACTION );
	giq_option( 7, NAME, 111, BYChris05, NEUTRAL_REACTION );
	giq_option( 4, NAME, 106, BYChrisEnd, NEUTRAL_REACTION );
	giq_option( -3, NAME, 107, BYChris07, NEUTRAL_REACTION );
end

procedure BYChris02
begin
	DisplayMessage := 108;
	call BYChris01;
end

procedure BYChris03
begin
	DisplayMessage := 109;
	set_local_var(4, 1);
	call BYChris01;
end

procedure BYChris04
begin
	DisplayMessage := 110;
	call BYChris01;
end

procedure BYChris05
begin
	gsay_reply( NAME, 112 );
	gsay_option( NAME, 113, BYChris06, 50 );
end

procedure BYChris06
begin
	gsay_reply( NAME, 114 );
	DisplayMessage := 116;
	gsay_option( NAME, 115, BYChris01, 50 );
end

procedure BYChris07
begin
	gsay_message( NAME, 117, NEUTRAL_REACTION );
end

procedure BYChrisEnd
begin
end
