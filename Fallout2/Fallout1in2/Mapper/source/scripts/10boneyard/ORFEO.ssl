/*

   Cathedral - Senior member Orfeo

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_ORFEO
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Orfeo0;
procedure Orfeo1;
procedure Orfeo2;
procedure Orfeo3;
procedure Orfeoend;
procedure combat;
procedure to_master;

#define LVAR_Herebefore          (0)
#define LVAR_Initial             (1)

variable initial := 0;
variable tmp_hostile;
variable weaponPtr;

import variable Orfeo_ptr;

procedure start begin
   if not(initial) then begin
      Orfeo_ptr := self_obj;
      set_self_team(TEAM_CATHEDRAL);
      set_self_ai(AI_BRAINWASHED_CHILDREN);
      self_rotate(2);
      if (local_var(LVAR_Initial) == 0) then begin
         set_local_var(LVAR_Initial, 1);
         // Fo1in2: Changed laser pistol to pulse pistol for more uniqueness.
         //weaponPtr := create_object(PID_LASER_PISTOL, 0, 0);
         weaponPtr := create_object(PID_YK32_PULSE_PISTOL, 0, 0);

         add_obj_to_inven(self_obj, weaponPtr);
         wield_obj_critter(self_obj, weaponPtr);
         add_obj_to_inven(self_obj, create_object(PID_SMALL_ENERGY_CELL, 0, 0));
      end
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_ENEMY_FOLL_INVADERS)) or (cathedral_destroyed and military_base_destroyed) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile and self_can_see_dude) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (self_can_see_dude and (tile_distance_objs(self_obj, dude_obj) < 6)) then begin
         if (local_var(LVAR_Herebefore) == 0) then begin
            dialogue_system_enter;
         end
      end
   end
end

procedure destroy_p_proc begin
   Orfeo_ptr := 0;
   if source_is_dude then begin
      inc_evil_critter
      set_global_var(GVAR_ENEMY_FOLL_INVADERS, 1);
   end
end

procedure look_at_p_proc begin
   if (local_var(LVAR_Herebefore)) then begin
      script_overrides;
      display_msg(mstr(100));
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data(tma_source_generic, TMA_MSG_MBASE, -1, -1, 997, 999);

   dude_look_at_self;
   if (local_var(LVAR_Herebefore)) then begin
      call Orfeo3;
   end
   else begin
      set_local_var(LVAR_Herebefore, 1);
      start_dialog_at_node(Orfeo0);
   end
end

procedure Orfeo0 begin
   Reply(101);
   NLowOption(102, combat);
   NOption(103, Orfeo1, 4);
   NOption(104, Orfeo2, 4);
end

procedure Orfeo1 begin
   NMessage(105);
   call combat;
end

procedure Orfeo2 begin
   NMessage(106);
   call to_master;
end

procedure Orfeo3 begin
   float_msg(self_obj, mstr(107), FLOAT_MSG_NORMAL);
   call combat;
end

procedure Orfeoend begin
end

procedure combat begin
   tmp_hostile := tmp_hostile + 1;
end

procedure to_master begin
   set_global_var(GVAR_PREVENT_REVULSION_DAMAGE, 1);
   //load_map("mstrlr34.map", 12);
   if not(cathedral_destroyed) then begin
      advance_time_travel_to_master;
   end
   else begin
      advance_time_travel_to_mbase_vats;
   end
end
