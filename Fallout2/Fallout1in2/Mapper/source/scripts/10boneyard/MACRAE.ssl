/*

   Boneyard - Mac Rae, Blades member, at the door outside of the house

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_MACRAE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure MacRae01;
procedure MacRae02;
procedure MacRae03;
procedure MacRae04;
procedure MacRae05;
procedure MacRae06;
procedure MacRae07;
procedure MacRae08;
procedure MacRae09;
procedure MacRae10;
procedure MacRae11;
procedure MacRae12;
procedure MacRae13;
procedure MacRae14;
procedure MacRae15;
procedure MacRae16;
procedure MacRaeEnd;

variable Only_Once := 1;
variable DisplayMessage := 100;

procedure Start begin
   if Only_Once then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      Only_Once := 0;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_BLADES_LVL2);

   dude_look_at_self;
   if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      if (local_var(5) == 2) then begin
         float_msg(self_obj, mstr(141), FLOAT_MSG_NORMAL);
      end
      else begin
         get_reaction
         start_gdialog(918, self_obj, -1, -1, -1);
         gsay_start;
         if (global_var(GVAR_BLADES_STATUS) == 2) then begin
            call MacRae12;
         end
         else begin
            if (local_var(4) == 0) then begin
               if (dude_is_male) then begin
                  DisplayMessage := 102;
               end
               else begin
                  DisplayMessage := 103;
               end
               call MacRae02;
            end
            else begin
               DisplayMessage := 104;
               call MacRae02;
            end
         end
         gsay_end;
         end_dialogue;
      end
   end
end

procedure damage_p_proc
begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
   inc_good_critter
end

procedure critter_p_proc
begin
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc
begin
   set_global_var(GVAR_ENEMY_BLADES, 1);
end

procedure MacRae01
begin
   DisplayMessage := 104;
   call MacRae02;
end

procedure MacRae02
begin
   Reply(DisplayMessage);
   if (local_var(4) == 0) then begin
      NOption(105, MacRae03, 4);
   end
   NOption(106, MacRae11, 4);
   NOption(107, MacRaeEnd, 4);
   NLowOption(141, MacRaeEnd);
end

procedure MacRae03
begin
   Reply(108);
   NOption(109, MacRae04, 1);
   NOption(110, MacRae08, 1);
   NOption(111, MacRae09, 1);
   NOption(112, MacRae01, 1);
end

procedure MacRae04
begin
   Reply(113);
   NOption(114, MacRae05, 1);
   NOption(115, MacRae06, 1);
   NOption(116, MacRae01, 1);
end

procedure MacRae05
begin
   NMessage(117);
   DownReactLevel
end

procedure MacRae06 begin
   if dude_is_male then
      DisplayMessage := 118;
   else
      DisplayMessage := 1180;
   set_local_var(5, 1);
   call MacRae07;
end

procedure MacRae07 begin
   Reply(DisplayMessage);
   NOption(119, MacRae01, 1);
   NOption(120, MacRaeEnd, 1);
end

procedure MacRae08
begin
   DisplayMessage := 121;
   call MacRae07;
end

procedure MacRae09
begin
   Reply(122);
   NOption(123, MacRae10, 1);
   NOption(124, MacRae01, 1);
   NOption(125, MacRaeEnd, 1);
end

procedure MacRae10
begin
   DisplayMessage := 126;
   call MacRae07;
end

procedure MacRae11
begin
   DisplayMessage := 127;
   call MacRae07;
end

procedure MacRae12
begin
   Reply(128);
   if (local_var(5 == 1)) then begin
      NOption(129, MacRae15, 1);
   end
   if (local_var(4) == 1) then begin
      NOption(130, MacRae13, 1);
   end
   else begin
      NOption(131, MacRae16, 1);
   end
   NOption(132, MacRaeEnd, 1);
end

procedure MacRae13
begin
   Reply(133);
   NOption(134, MacRae14, 1);
   NOption(135, MacRaeEnd, 1);
end

procedure MacRae14
begin
   Reply(113);
   NOption(136, MacRae15, 1);
   NOption(137, MacRaeEnd, 1);
end

procedure MacRae15
begin
//
// Adds five points into Unarmed and Melee.
// originally added to Melee Damage but the game engine cannot support that.
// Five points should give double if one of those skills is tagged, but need to test this.
// --Sduibek
//
   variable LVar0 := 5;// Skill points to add
   variable LVar1 := 5;// Skill points to add
   variable LVar2 := 24;// Hours it takes to learn
   NMessage(138);
   gfade_out(1);
// if (get_critter_stat(dude_obj, 4) >= 8) or has_trait(dude_obj, 0, #) or has_trait(dude_obj, 0, #) then begin
   game_time_advance(game_ticks(60 * 60 * LVar2));
// if get_critter_stat(dude_obj, 4) <=
   gfade_in(1);
   critter_mod_skill(dude_obj, SKILL_UNARMED_COMBAT, LVar0);
   critter_mod_skill(dude_obj, SKILL_MELEE, LVar1);
   display_msg(gen_mstr(116) + LVar0 + gen_mstr(120));
   display_msg(gen_mstr(116) + LVar1 + gen_mstr(121));

   give_xp_msg(EXP_LEARN_BRAWLING, mstr(200));

   set_local_var(5, 2);
   NMessage(139);
end

procedure MacRae16
begin
   Reply(140);
   NOption(109, MacRae15, 1);
   NOption(137, MacRaeEnd, 1);
end

procedure MacRaeEnd
begin
end
