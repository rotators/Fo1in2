/*

   Boneyard - Blade in the building (outside, south west)

*/

/* Include Files */
#include "define.h"

#define NAME                    SCRIPT_BYGREG

#include "command.h"
#include "modreact.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

procedure BYGreg03;
procedure BYGreg04;
procedure BYGreg05;
procedure BYGreg06;
procedure BYGreg07;
procedure BYGregEnd;

#define LVAR_Herebefore              (4)

variable Only_Once := 1;
variable PsstTime;
variable DisplayMessage := 100;

procedure Start begin
   if Only_Once then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      Only_Once := 0;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_BLADES_LVL1);

   dude_look_at_self;
   if ((global_var(GVAR_ENEMY_BLADES) == 1) or (dude_iq < 4)) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      if ((local_var(5) == 1) and (dude_iq < 4)) then begin
         float_msg(self_obj, mstr(random(115, 119)), FLOAT_MSG_NORMAL);
      end
      else begin
         get_reaction
         start_gdialog(925, self_obj, -1, -1, -1);
         gsay_start;
         if (local_var(LVAR_Herebefore) == 0) then begin
            DisplayMessage := 102;
         end
         else begin
            DisplayMessage := 103;
         end
         call BYGreg03;
         gsay_end;
         end_dialogue;
      end
   end
end

procedure critter_p_proc begin
   if ((((game_time / 10) - PsstTime) >= 30) and (tile_distance_objs(self_obj, dude_obj) <= 4) and (local_var(5) == 1)) then begin
      float_msg(self_obj, mstr(random(115, 119)), FLOAT_MSG_NORMAL);
      PsstTime := game_time / 10;
   end
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_BLADES, 1);
end

procedure BYGreg03 begin
   Reply(DisplayMessage);
   if (local_var(LVAR_Herebefore) == 0) then begin
      NOption(105, BYGreg04, 1);
   end
   NOption(106, BYGreg05, 1);
   NOption(107, BYGreg07, 1);
   NOption(108, BYGregEnd, 1);
end

procedure BYGreg04 begin
   DisplayMessage := 109;
   set_local_var(LVAR_Herebefore, 1);
   call BYGreg03;
end

procedure BYGreg05 begin
   Reply(110);
   NOption(111, BYGreg06, 1);
   NOption(112, BYGregEnd, 1);
end

procedure BYGreg06 begin
   Reply(113);
   set_local_var(5, 1);
end

procedure BYGreg07 begin
   Reply(114);
   set_local_var(5, 1);
end

procedure BYGregEnd begin
end
