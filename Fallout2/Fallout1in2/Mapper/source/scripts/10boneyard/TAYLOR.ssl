/*

   Boneyard - Tylor

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\maps\laadytum.h"

#define NAME                    SCRIPT_TAYLOR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Tine01;
procedure Tine02;
procedure Tine03;
procedure Tine04;
procedure Tine05;
procedure Tine06;
procedure Tine07;
procedure Tine08;
procedure TineBarter;
procedure Tine_barter1;
procedure Tine_barter2;
procedure TineEnd;

variable tmp_hostile;
variable initial := 0;
variable round_counter;
variable barter_mod := 0;

import variable Tine_barter;
import variable AdyStor1_ptr;
import variable AdyStor2_ptr;

#define LVAR_Herebefore         (4)

#define barter_mod_initial       (35)    // vanilla : 0

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_LA_ADYTOWNER);
      set_self_ai(AI_ADYTOWNER);
      initial := 1;
   end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      if (self_elevation == 0) then begin
         round_counter := round_counter + 1;
         if (round_counter > 3) then begin
            if (global_var(GVAR_ENEMY_ADYTUM) == 0) then begin
               set_global_var(GVAR_ENEMY_ADYTUM, 1);
               set_global_var(GVAR_PLAYER_REPUTATION, check_general_rep - 5);
            end
         end
      end
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (Tine_barter != 0) then begin
         dialogue_system_enter;
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(5, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_ADYTUM, 1);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore)) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   variable LVar0 := 0;
   dude_look_at_self;
   get_reaction
   LVar0 := random(0, 1);
   if (Tine_barter == 0) then begin
      if (LVar0 == 1) then begin
         move_obj_inven_to_obj(AdyStor1_ptr, self_obj);
      end
      else begin
         move_obj_inven_to_obj(AdyStor2_ptr, self_obj);
      end
   end

   barter_mod := barter_mod_initial;
   gdialog_set_barter_mod(barter_mod);

   if (Tine_barter == -1) then begin
      float_msg(self_obj, message_str(SCRIPT_JTGENGRD, 136), FLOAT_MSG_RED);
      tmp_hostile := 1;
   end
   else if ((Tine_barter != 0) and ((local_var(5) == 1) or (global_var(GVAR_ENEMY_ADYTUM) == 1))) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, 104), FLOAT_MSG_RED);
   end
   else begin
      if (Tine_barter == 1) then begin
         start_dialog_at_node(Tine_barter1);
      end
      else if (Tine_barter == 2) then begin
         start_dialog_at_node(Tine_barter2);
      end
      else begin
         if (local_var(LVAR_Herebefore)) then begin
            start_dialog_at_node(Tine07);
         end
         else begin
            start_dialog_at_node(Tine01);
         end
      end
   end

   if (Tine_barter == 1) then begin
      move_obj_inven_to_obj(self_obj, AdyStor1_ptr);
   end
   else if (Tine_barter == 2) then begin
      move_obj_inven_to_obj(self_obj, AdyStor2_ptr);
   end
   else if (LVar0 == 1) then begin
      move_obj_inven_to_obj(self_obj, AdyStor1_ptr);
   end
   else begin
      move_obj_inven_to_obj(self_obj, AdyStor2_ptr);
   end
   Tine_barter := 0;
end

procedure Tine01 begin
   if (local_var(1) < 2) then begin
      Reply(102);
   end
   else begin
      Reply(103);
   end
   call Tine02;
end

procedure Tine02 begin
   if (not(local_var(LVAR_Herebefore))) then begin
      NOption(mstr(104) + dude_name + mstr(105), Tine04, 4);
   end
   NOption(106, Tine03, 4);
   NOption(107, Tine05, 4);
   NOption(108, Tine08, 4);
   NOption(109, Tine06, 0);
end

procedure Tine03 begin
   if (local_var(1) < 2) then begin
      Reply(110);
   end
   else begin
      Reply(111);
   end
   call Tine02;
end

procedure Tine04 begin
   set_local_var(LVAR_Herebefore, 1);
   if (local_var(1) < 2) then begin
      Reply(112);
   end
   else begin
      Reply(113);
   end
   call Tine02;
end

procedure Tine05 begin
   gdialog_mod_barter(barter_mod);
   NMessage(114);
end

procedure Tine06 begin
   if (local_var(1) < 2) then begin
      NMessage(115);
   end
   else begin
      NMessage(116);
   end
end

procedure Tine07 begin
   if (local_var(1) < 2) then begin
      Reply(117);
   end
   else begin
      Reply(118);
   end
   call Tine02;
end

procedure Tine08 begin
   if (local_var(0) < 2) then begin
      Reply(119);
   end
   else begin
      Reply(120);
   end
   call Tine02;
end

procedure TineBarter begin
   gdialog_mod_barter(barter_mod);
   gsay_message(SCRIPT_MODREACT, 100, NEUTRAL_REACTION);
end

procedure Tine_barter1 begin
   move_obj_inven_to_obj(AdyStor1_ptr, self_obj);
   gsay_reply(SCRIPT_HBETH, 293);
   giq_option(0, SCRIPT_MODREACT, 106, TineBarter, NEUTRAL_REACTION);
end

procedure Tine_barter2 begin
   move_obj_inven_to_obj(AdyStor2_ptr, self_obj);
   gsay_reply(SCRIPT_HBETH, 293);
   giq_option(0, SCRIPT_MODREACT, 106, TineBarter, NEUTRAL_REACTION);
end

procedure TineEnd begin
end
