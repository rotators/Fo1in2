/*

   Hub - door to the CoC storage room, water merchants map.

*/

#include "define.h"
#include "command.h"
#include "maps/hubwater.h"

#define NAME                     SCRIPT_DOOR

procedure start;
procedure damage_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure map_update_p_proc;
procedure use_p_proc;
procedure description_p_proc;

#define LVAR_DoorState              (0)
#define LVAR_DoorDestroyed          (1)

procedure start begin
end

procedure damage_p_proc begin
   if (weapon_dmg_type(target_obj) == DMG_explosion) then begin
      if not(is_loading_game) then set_self_invisible;
      set_local_var(LVAR_DoorDestroyed, 1);
      set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
   end
   else begin
      display_msg(mstr(193));
   end
end

procedure use_skill_on_p_proc begin
   variable LVar0 := 0;
   if (local_var(LVAR_DoorState) == 0) then begin
      if (action_being_used == SKILL_LOCKPICK) then begin
         script_overrides;
         if (map_var(MVAR_GUARD_DEAD) == 0) then begin
            if (dude_is_sneaking != 1) then begin
               set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
            end
            else begin
               LVar0 := roll_vs_skill(source_obj, SKILL_SNEAK, 0);
               if (is_success(LVar0) != 1) then begin
                  set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
               end
               else begin
                  LVar0 := roll_vs_skill(source_obj, SKILL_LOCKPICK, 0);
                  if (is_success(LVar0)) then begin
                     set_local_var(LVAR_DoorState, 1);
                     if source_is_dude then
                        display_msg(mstr(100));
                     else
                        display_msg(obj_name(source_obj) + message_str(SCRIPT_JUNKJAIL, 200));
                  end
                  else begin
                     if source_is_dude then
                        display_msg(mstr(103));
                     else
                        display_msg(obj_name(source_obj) + message_str(SCRIPT_JUNKJAIL, 202));
                  end
               end
            end
         end
         else begin
            LVar0 := roll_vs_skill(source_obj, SKILL_LOCKPICK, 0);
            if (is_success(LVar0)) then begin
               set_local_var(LVAR_DoorState, 1);
               if source_is_dude then
                  display_msg(mstr(100));
               else
                  display_msg(obj_name(source_obj) + message_str(SCRIPT_JUNKJAIL, 200));
            end
            else begin
               if source_is_dude then
                  display_msg(mstr(103));
               else
                  display_msg(obj_name(source_obj) + message_str(SCRIPT_JUNKJAIL, 202));
            end
         end
      end
   end
   else begin
      display_msg(mstr(109));//{109}{}{The door is already unlocked.}
   end
end

procedure use_obj_on_p_proc begin
   variable LVar0 := 0;
   variable test := 0;
   variable item := obj_being_used_with;

   if (obj_pid(item) == PID_LOCKPICKS or obj_pid(item) == PID_EXP_LOCKPICK_SET) then begin
      script_overrides;
      if (map_var(MVAR_GUARD_DEAD) == 0) then begin
         if (dude_is_sneaking != 1) then begin
            set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
         end
         else begin
            test := roll_vs_skill(dude_obj, SKILL_SNEAK, 0);
            if (is_success(test) != 1) then begin
               set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
            end
            else begin
               if (obj_pid(item) == PID_EXP_LOCKPICK_SET) then
                  test := roll_vs_skill(dude_obj, SKILL_LOCKPICK, 25);
               else
                  test := roll_vs_skill(dude_obj, SKILL_LOCKPICK, 20);

               if (is_success(test)) then begin
                  set_local_var(LVAR_DoorState, 1);
                  display_msg(mstr(100));//{100}{}{You unlock the door.}
               end
               else if (is_critical(test)) then begin
                  rm_obj_from_inven(dude_obj, item);
                  destroy_object(item);
                  display_msg(mstr(101));//{101}{}{You broke your lockpicks!}
               end
            end
         end
      end
      else begin
         test := roll_vs_skill(dude_obj, SKILL_LOCKPICK, 20);
         if (is_success(test)) then begin
            set_local_var(LVAR_DoorState, 1);
            display_msg(mstr(100));//{100}{}{You unlock the door.}
         end
         else if (is_critical(test)) then begin
            rm_obj_from_inven(dude_obj, item);
            destroy_object(item);
            display_msg(mstr(101));//{101}{}{You broke your lockpicks!}
         end
      end
   end
   else begin
      display_msg(mstr(103));//{103}{}{You were not able to pick the lock.}
   end
end

procedure map_update_p_proc begin
   if (local_var(LVAR_DoorState) == 0) then begin
      obj_lock(self_obj);
   end
   else begin
      obj_unlock(self_obj);
   end
end

procedure use_p_proc begin
   variable LVar0 := 0;
   if (local_var(LVAR_DoorState) == 0) then begin
      obj_lock(self_obj);
   end
   else begin
      if (map_var(MVAR_GUARD_DEAD) == 0) then begin
         if (dude_is_sneaking != 1) then begin
            set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
         end
         else begin
            LVar0 := roll_vs_skill(dude_obj, SKILL_SNEAK, 0);
            if (is_success(LVar0)) then begin
               obj_unlock(self_obj);
            end
            else begin
               set_map_var(MVAR_DOOR_UNLOCK_FAILED, 1);
            end
         end
      end
      else begin
         obj_unlock(self_obj);
      end
   end
   if ((local_var(LVAR_DoorState) == 0) and source_is_dude) then begin
      script_overrides;
      display_msg(mstr(104));//{104}{}{The door is locked.}
   end
end

procedure description_p_proc begin
   if (local_var(LVAR_DoorState) == 0) then begin
      display_msg(mstr(104));//{104}{}{The door is locked.}
   end
end
