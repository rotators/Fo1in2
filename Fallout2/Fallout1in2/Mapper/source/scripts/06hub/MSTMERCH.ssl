/*

	Hub - Water Merchant leader

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_MSTMERCH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure MasterMerch00;
procedure MasterMerch01;
procedure MasterMerch02;
procedure MasterMerch03;
procedure MasterMerch04;
procedure MasterMerch05;
procedure MasterMerch06;
procedure MasterMerch07;
procedure MasterMerch08;
procedure MasterMerch09;
procedure MasterMerch10;
procedure MasterMerch11;
procedure MasterMerch12;
procedure MasterMerch13;
procedure MasterMerch14;
procedure MasterMerch15;
procedure MasterMerch16;
procedure MasterMerch17;
procedure MasterMerch18;
procedure MasterMerch19;
procedure MasterMerch20;
procedure MasterMerchEnd;
procedure Lost;
procedure Abandoned;

procedure Node998;
procedure Node999;

variable tmp_hostile;
variable Only_Once := 1;

#define float(x)    						float_msg(self_obj, message_str(NAME, x), FLOAT_MSG_RED)

procedure Node998 begin
	tmp_hostile := 1;
end

procedure Node999 begin
end

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_WATER_MERCHANTS);
		set_self_ai(AI_MERCHANT);
	end
end

procedure critter_p_proc begin
	if (self_can_see_dude and tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	call MasterMerch00;
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_self;

	get_reaction

	if (map_var(2) == 0) then begin
		float(156);
	end
	else if (global_var( GVAR_WATER_JOB ) == 5) then begin
		start_gdialog(598, self_obj, 4, -1, -1);
		gsay_start;
		call Lost;
		gsay_end;
		end_dialogue;
	end
	else if (global_var( GVAR_WATER_JOB ) == 3) then begin
		start_gdialog(598, self_obj, 4, -1, -1);
		gsay_start;
		call Abandoned;
		gsay_end;
		end_dialogue;
	end
	else if (local_var(4) == 0) then begin
		set_local_var(4, 1);
		start_dialog_at_node(MasterMerch01);
	end
	else if ((local_var(5) > 0) and (global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) < 1)) then begin
		start_dialog_at_node(MasterMerch19);
	end
	else begin
		start_dialog_at_node(MasterMerch10);
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_HUB, 1 );
	end
end

procedure MasterMerch00 begin
	float_msg( self_obj, message_str(SCRIPT_MSTMERCH, 101), FLOAT_MSG_RED );
	call Node998;
end

procedure MasterMerch01 begin
	gsay_reply( SCRIPT_MSTMERCH, 102 );
	NOption( 103, MasterMerch02, 4 );
	if ((global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 2) or ((global_var( GVAR_NECROP_WATER_CHIP_TAKEN ) == 1) and (global_var( GVAR_NECROP_WATER_PUMP_FIXED ) == 0))) then begin
		NOption( 104, MasterMerch04, 4 );
	end
	NOption( 105, MasterMerchEnd, 4 );
	NOption( 106, MasterMerch03, -3 );
end

procedure MasterMerch02 begin
	set_map_var(1, 1);
	gsay_reply( SCRIPT_MSTMERCH, 107 );
	NOption( 108, MasterMerchEnd, 4 );
	if ((global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 2) or ((global_var( GVAR_NECROP_WATER_CHIP_TAKEN ) == 1) and (global_var( GVAR_NECROP_WATER_PUMP_FIXED ) == 0))) then begin
		NOption( 109, MasterMerch04, 4 );
	end
	NOption( 110, MasterMerch03, -3 );
end

procedure MasterMerch03 begin
	gsay_message( SCRIPT_MSTMERCH, 111, NEUTRAL_REACTION );
end

procedure MasterMerch04 begin
	if (global_var( GVAR_WORLDMAPLIST_NECROPOLIS ) == 0) then
		set_global_var( GVAR_WORLDMAPLIST_NECROPOLIS, 1 );
	mark_on_map(AREA_NECROPOLIS)

	gsay_reply( SCRIPT_MSTMERCH, 112 );
	NOption( 113, MasterMerch13, 4 );
	NOption( 114, MasterMerch05, 4 );
	NOption( 115, MasterMerch06, 4 );
	NOption( 116, MasterMerch07, 4 );
end

procedure MasterMerch05 begin
	set_local_var(6, 1);
	gsay_reply( SCRIPT_MSTMERCH, 117 );
	NOption( 118, MasterMerch14, 4 );
	NOption( 119, MasterMerch08, 4 );
	NOption( 120, MasterMerch07, 4 );
end

procedure MasterMerch06 begin
	gsay_message( SCRIPT_MSTMERCH, 121, NEUTRAL_REACTION );
end

procedure MasterMerch07 begin
	gsay_message( SCRIPT_MSTMERCH, 122, NEUTRAL_REACTION );
end

procedure MasterMerch08 begin
	gsay_reply( SCRIPT_MSTMERCH, 123 );
	NOption( 124, MasterMerch14, 4 );
	NOption( 125, MasterMerch09, 4 );
end

procedure MasterMerch09 begin
	gsay_message( SCRIPT_MSTMERCH, 126, NEUTRAL_REACTION );
end

procedure MasterMerch10 begin
	gsay_reply( SCRIPT_MSTMERCH, 127 );
	if ((global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 2) and (global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 1) and (local_var(6) == 0)) then begin
		NOption( 128, MasterMerch04, 4 );
	end
	else begin
		if ((global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 2) and (global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 1) and (local_var(6) == 1)) then begin
			NOption( 129, MasterMerch12, 4 );
		end
	end
	NOption( 130, MasterMerch11, 4 );
	NOption( 131, MasterMerchEnd, 4 );
	NOption( 106, MasterMerch03, -3 );
end

procedure MasterMerch11 begin
	set_map_var(1, 1);
	gsay_message( SCRIPT_MSTMERCH, 132, NEUTRAL_REACTION );
end

procedure MasterMerch12 begin
	gsay_reply( SCRIPT_MSTMERCH, 133 );
	NOption( 134, MasterMerch13, 4 );
	NOption( 135, MasterMerch14, 4 );
	NOption( 136, MasterMerchEnd, 4 );
end

procedure MasterMerch13 begin
	gsay_reply( SCRIPT_MSTMERCH, 137 );
	NOption( 138, MasterMerch14, 4 );
	NOption( 139, MasterMerch07, 4 );
end

procedure MasterMerch14 begin
	gsay_reply( SCRIPT_MSTMERCH, 140 );
	call MasterMerch15;
end

procedure MasterMerch15 begin
	if (local_var(5) == 0) then begin
		set_local_var(5, 2000);
	end
	NOption( 141, MasterMerch16, 4 );
	if (local_var(7) == 0) then begin
		NOption( 142, MasterMerch17, 4 );
	end
	if (local_var(7) == 0) then begin
		NOption( 143, MasterMerch18, 4 );
	end
	NOption( 144, MasterMerchEnd, 4 );
end

procedure MasterMerch16 begin
	if (dude_caps < local_var(5)) then begin
		gsay_message( SCRIPT_MSTMERCH, message_str(SCRIPT_MSTMERCH, 145) + local_var(5) + message_str(SCRIPT_MSTMERCH, 146), NEUTRAL_REACTION );
		call MasterMerchEnd;
	end
	else begin
		gsay_reply( SCRIPT_MSTMERCH, 147 );
		NOption( 148, MasterMerchEnd, 4 );
		if (global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) != 2) then begin
			NOption( 149, MasterMerch20, 4 );
		end
	end
end

procedure MasterMerch17 begin
	set_local_var(7, 1);
	if (is_success(roll_vs_skill(dude_obj, SKILL_BARTER, -15))) then begin
		set_local_var(5, 1000);
		gsay_message( SCRIPT_MSTMERCH, 150, NEUTRAL_REACTION );
		call MasterMerch16;
	end
	else begin
		gsay_reply( SCRIPT_MSTMERCH, 151 );
		call MasterMerch15;
	end
end

procedure MasterMerch18 begin
	set_local_var(7, 1);
	if (is_success(roll_vs_skill(dude_obj, SKILL_BARTER, -30))) then begin
		set_local_var(5, 500);
		gsay_message( SCRIPT_MSTMERCH, 152, NEUTRAL_REACTION );
		call MasterMerch16;
	end
	else begin
		gsay_reply( SCRIPT_MSTMERCH, 153 );
		call MasterMerch15;
	end
end

procedure MasterMerch19 begin
	gsay_reply( SCRIPT_MSTMERCH, message_str(SCRIPT_MSTMERCH, 157) + local_var(5) + message_str(SCRIPT_MSTMERCH, 158) );
	call MasterMerch15;
end

procedure MasterMerch20 begin
	variable LVar0 := 0;
	dude_caps_adjust( -1 * local_var(5) );

	set_global_var( GVAR_QUEST_VAULT13_4_WATERCHIP, 1 );
	set_global_var( GVAR_VAULT13_WATER_DAYS_LEFT, global_var( GVAR_VAULT13_WATER_DAYS_LEFT ) + 100 );

	if get_v13_days_left >= 100 then begin
		set_v13_days_left(-90);
	end

	give_exp_points(EXP_BOUGHT_WATER_CARAVAN);
	display_msg(message_str(SCRIPT_GENCHAT, 103) + 1000 + message_str(SCRIPT_GENCHAT, 104));
	gsay_message( SCRIPT_MSTMERCH, 155, NEUTRAL_REACTION );
end

procedure MasterMerchEnd
begin
end

procedure Lost begin
	set_map_var(2, 0);
	set_global_var( GVAR_WATER_JOB, 6 );
	gsay_message( SCRIPT_MSTMERCH, 200, NEUTRAL_REACTION );
end

procedure Abandoned begin
	gsay_message( SCRIPT_MSTMERCH, 201, NEUTRAL_REACTION );
end
