/*

   Hub - Vance

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\sfall\main.h"

#define NAME                    SCRIPT_VANCE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;

procedure Vance00;
procedure Vance01;
procedure Vance02;
procedure Vance02a;
procedure Vance03;
procedure Vance04;
procedure Vance05;
procedure Vance06;
procedure Vance07;
procedure Vance08;
procedure Vance09;
procedure Vance10;
procedure Vance11;
procedure Vance11a;
procedure Vance12;
procedure Vance13;
procedure Vance14;
procedure Vance15;
procedure Vance16;
procedure VanceEnd;
procedure VanceKnowsPC;
procedure Get_Stuff;
procedure Put_Stuff;
procedure Barter;

import variable Vance_Box_Ptr;


variable tmp_hostile;
variable tmp_box;
variable Only_Once := 1;
variable kickOut;
variable initiateDialogue := 1;
variable killed := 0;

#define LVAR_Here_Before      (4)
#define LVAR_Been_Introduced  (5)
#define LVAR_Barter_Mod       (6)

#define barter_mod_initial          (-20)
#define barter_mod_dude_known       (-20)
#define barter_mod_introduced       (-15) // Knows Vance via Jacob or Lemmy

procedure start begin
   if Only_Once then begin
      set_local_var(LVAR_Barter_Mod, barter_mod_initial);
      set_self_team(TEAM_HUB_VANCE);
      set_self_ai(AI_GUARD);
      //call Put_Stuff;
      Only_Once := 0;
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if ((initiateDialogue == 1) and (tile_distance_objs(self_obj, dude_obj) <= 4)) then begin
         initiateDialogue := 0;
         dialogue_system_enter;
      end
      else begin
         if (kickOut == 1) then begin
            kickOut := 0;
            initiateDialogue := 1;
            gfade_out(1);
            move_to(dude_obj, 16265, 0);
            gfade_in(1);
         end
         if (tile_distance_objs(self_obj, dude_obj) >= 5) then begin
            initiateDialogue := 1;
         end
      end
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure look_at_p_proc begin
   if (global_var(GVAR_VANCE_KNOWS_PLAYER) == 1) or (local_var(LVAR_Been_Introduced) == 1) then begin
   end
   else begin
      script_overrides;
      display_msg(mstr(100));
   end
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(119));
end

procedure talk_p_proc begin
   dude_look_at_self;

   if (global_var(GVAR_VANCE_KNOWS_PLAYER) == 1) then begin
      set_enable_barter;
   end
   else begin
      set_disable_barter;
   end

   get_reaction
   call Get_Stuff;

   // TODO: Fixt addition. Clean this up at some point.
   if (global_var(GVAR_VANCE_KNOWS_PLAYER) == 1) then begin// Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
      set_local_var(LVAR_Barter_Mod, barter_mod_introduced);
   end
   else if (local_var(LVAR_Been_Introduced) == 1) then begin
      set_local_var(LVAR_Barter_Mod, barter_mod_dude_known);
   end
   else begin
      set_local_var(LVAR_Barter_Mod, barter_mod_initial);
   end
   gdialog_set_barter_mod(local_var(LVAR_Barter_Mod));

   if (global_var(GVAR_ENEMY_HUB) == 1) or (local_var(1) == 1) or (local_var(0) <= 25) then begin// gvar248 = ENEMY_HUB
      start_dialog_at_node(Vance12);
   end
   else begin
      if (global_var(GVAR_VANCE_KNOWS_PLAYER) == 1) or (local_var(LVAR_Been_Introduced) >= 1) then begin // Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
         if (local_var(LVAR_Been_Introduced) != 1) then begin
            start_dialog_at_node(VanceKnowsPC);
            set_local_var(LVAR_Been_Introduced, 1);
         end
         else begin
            start_dialog_at_node(Vance06);
         end
      end
      else begin
         if (local_var(1) == 1) then begin
            start_dialog_at_node(Vance11);
            set_local_var(LVAR_Been_Introduced, 1);
         end
         else begin
            if ((local_var(LVAR_Here_Before) == 0) or (global_var(GVAR_VANCE_KNOWS_PLAYER) == 0)) then begin// Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
               set_local_var(LVAR_Here_Before, 1);
               start_dialog_at_node(Vance01);
               set_local_var(LVAR_Been_Introduced, 1);
            end
            else begin
               start_dialog_at_node(Vance06);
               set_local_var(LVAR_Been_Introduced, 1);
            end
         end
      end
   end
   call Put_Stuff;
end

procedure VanceKnowsPC begin
   Reply(101);
   giq_option(4, NAME, 105, Barter, BAD_REACTION);
   giq_option(4, NAME, 104, Vance03, NEUTRAL_REACTION);
   giq_option(4, NAME, 106, Vance04, BAD_REACTION);
   giq_option(-3, NAME, 161, Vance05, NEUTRAL_REACTION);
   giq_option(-3, NAME, 162, VanceEnd, BAD_REACTION);
end

procedure Vance00 begin
   Reply(mstr(157) + mstr(107));
   giq_option(4, NAME, 152, Vance16, NEUTRAL_REACTION);
   giq_option(4, NAME, 110, Vance02a, NEUTRAL_REACTION);
   giq_option(4, NAME, 111, Vance04, NEUTRAL_REACTION);

end

procedure Vance01 begin
   Reply(153);// was 101
   giq_option(4, NAME, 103, Vance02, NEUTRAL_REACTION);
   giq_option(4, NAME, 104, Vance03, NEUTRAL_REACTION);
   giq_option(4, NAME, 106, Vance04, NEUTRAL_REACTION);
   giq_option(-3, NAME, 102, Vance16, BAD_REACTION);
   giq_option(-3, NAME, mstr(154) + dude_name + mstr(155), Vance00, NEUTRAL_REACTION);
   giq_option(-3, NAME, mstr(154) + dude_name + mstr(156), Vance00, BAD_REACTION);

end

procedure Vance02 begin
   Reply(107);
   giq_option(4, NAME, 108, Vance03, NEUTRAL_REACTION);
   giq_option(4, NAME, 109, Vance02a, NEUTRAL_REACTION);
   giq_option(4, NAME, 111, Vance04, NEUTRAL_REACTION);
end

procedure Vance02a begin
   if ((is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -30)) == 1) or is_success(do_check(dude_obj, STAT_ch, -3))) then begin
      call Vance05;
   end
   else begin
      call Vance03;
   end
end

procedure Vance03 begin
   Reply(112);
   giq_option(4, NAME, 113, Vance13, BAD_REACTION);
   giq_option(4, NAME, 114, Vance14, NEUTRAL_REACTION);
end

procedure Vance04 begin
   Reply(115);
   giq_option(4, NAME, 116, Vance13, BAD_REACTION);
   giq_option(4, NAME, 117, Vance14, NEUTRAL_REACTION);
end

procedure Vance05 begin
   //set_global_var(GVAR_VANCE_KNOWS_PLAYER, 1);
   set_local_var(LVAR_Been_Introduced, 1);
   set_enable_barter;
   call Barter;
end

procedure Vance06 begin
   Reply(120);
   giq_option(-3, NAME, 121, Barter, NEUTRAL_REACTION);
   giq_option(4, NAME, 122, Barter, NEUTRAL_REACTION);
   giq_option(4, NAME, 123, Vance07, NEUTRAL_REACTION);
   giq_option(4, NAME, 124, Vance08, NEUTRAL_REACTION);
   giq_option(4, NAME, 125, Vance09, NEUTRAL_REACTION);
   giq_option(4, NAME, 126, Vance15, NEUTRAL_REACTION);
end

procedure Vance07 begin
   Reply(127);
   giq_option(4, NAME, 128, Vance13, BAD_REACTION);
   giq_option(4, NAME, 129, Vance14, NEUTRAL_REACTION);
end

procedure Vance08 begin
   Reply(130);
   giq_option(4, NAME, 131, Vance13, BAD_REACTION);
   giq_option(4, NAME, 132, Vance10, NEUTRAL_REACTION);
   giq_option(4, NAME, 133, Vance15, NEUTRAL_REACTION);
end

procedure Vance09 begin
   Reply(134);
   giq_option(4, NAME, 135, Barter, NEUTRAL_REACTION);
   giq_option(4, NAME, 136, Vance07, BAD_REACTION);
   giq_option(4, NAME, 137, Vance15, NEUTRAL_REACTION);
end

procedure Vance10 begin
   Reply(138);
   giq_option(4, NAME, 139, Barter, NEUTRAL_REACTION);
   giq_option(4, NAME, 140, Vance15, NEUTRAL_REACTION);
end

procedure Vance11 begin
   Reply(141);
   giq_option(4, NAME, 142, Vance13, BAD_REACTION);
   giq_option(4, NAME, 143, Vance14, NEUTRAL_REACTION);
   giq_option(4, NAME, 144, Vance11a, GOOD_REACTION);
end

procedure Vance11a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20)) == 1) then begin
      call Vance02;
   end
   else begin
      call Vance13;
   end
end

procedure Vance12 begin
   Reply(145);
   giq_option(4, NAME, 148, Vance13, BAD_REACTION);
   giq_option(4, NAME, 149, Vance14, NEUTRAL_REACTION);
   giq_option(-3, NAME, 146, Vance13, BAD_REACTION);
   giq_option(-3, NAME, 147, Vance14, NEUTRAL_REACTION);
end

procedure Vance13 begin
   BottomReact
   call combat;
   float_msg(self_obj, mstr(160), FLOAT_MSG_RED);
end

procedure Vance14 begin
   kickOut := 1;
end

procedure Vance15 begin
end

procedure Vance16 begin
   Reply(158);
   giq_option(-3, NAME, 159, VanceEnd, NEUTRAL_REACTION);
end

procedure VanceEnd begin
end

procedure Barter begin
   GMessage(118);
   gdialog_mod_barter(local_var(LVAR_Barter_Mod));
   Reply(150);
   giq_option(4, NAME, 151, VanceEnd, NEUTRAL_REACTION);
   giq_option(-3, NAME, 152, VanceEnd, NEUTRAL_REACTION);
end

procedure destroy_p_proc begin
   inc_good_critter
   move_obj_inven_to_obj(Vance_Box_Ptr, self_obj);
end

procedure Get_Stuff begin
   get_barter_inven(Vance_Box_Ptr);
end

procedure Put_Stuff begin
   put_barter_inven(Vance_Box_Ptr);
end
