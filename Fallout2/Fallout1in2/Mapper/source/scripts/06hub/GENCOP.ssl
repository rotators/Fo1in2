/*

   Hub Police

*/

/* Include Files */
#include "..\headers\define.h"

#define NAME                    SCRIPT_GENCOP
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure damage_p_proc;
procedure combat_p_proc;
procedure look_at_p_proc;

procedure Cop00;
procedure Cop01;
procedure Cop02;
procedure spawn_items;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
end

procedure map_enter_p_proc begin
   if (map_first_run) then begin
      set_self_team(TEAM_HUB_POLICE);
      set_self_ai(AI_HUB_POLICE);
      call spawn_items;
   end
end

procedure spawn_items begin
   variable Item := 0;
   if (self_pid == (PID_WOMAN_YELLOW or PID_GUARD_NFMETL)) then begin
      if (self_item_count(PID_STIMPAK) <= 0) then begin
         Item := create_object(PID_STIMPAK, 0, 0);
         add_obj_to_inven(self_obj, Item);
         Item := create_object(PID_STIMPAK, 0, 0);
         add_obj_to_inven(self_obj, Item);
      end
      if (self_item_count(PID_SPIKED_KNUCKLES) <= 0) then begin
         Item := create_object(PID_SPIKED_KNUCKLES, 0, 0);
         add_obj_to_inven(self_obj, Item);
      end
      if (self_item_count(PID_COMBAT_KNIFE) <= 0) then begin
         Item := create_object(PID_COMBAT_KNIFE, 0, 0);
         add_obj_to_inven(self_obj, Item);
      end
   end
   else if (self_item_count(PID_STIMPAK) <= 0) then begin
      Item := create_object(PID_STIMPAK, 0, 0);
      add_obj_to_inven(self_obj, Item);
   end
   if (self_pid == (PID_MALE_GUARD_METAL_BLACK or PID_GUARD_HMBMET)) then begin
      if (self_item_count(PID_BRASS_KNUCKLES) <= 0) then begin
         Item := create_object(PID_BRASS_KNUCKLES, 0, 0);
         add_obj_to_inven(self_obj, Item);
      end
      if (self_item_count(PID_10MM_PISTOL) <= 0) and (self_item_count(PID_DESERT_EAGLE) <= 0) then begin
         if (random(0, 1)) then begin
            if (self_item_count(PID_10MM_PISTOL) <= 0) then begin
               Item := create_object(PID_10MM_PISTOL, 0, 0);
               add_obj_to_inven(self_obj, Item);
               if (random(0, 1)) then begin
                  if (self_item_count(PID_10MM_JHP) <= 0) then begin
                     Item := create_object(PID_10MM_JHP, 0, 0);
                     add_obj_to_inven(self_obj, Item);
                  end
               end
               else if (self_item_count(PID_10MM_AP) <= 0) then begin
                  Item := create_object(PID_10MM_AP, 0, 0);
                  add_obj_to_inven(self_obj, Item);
               end
            end
         end
         else if (self_item_count(PID_10MM_PISTOL) <= 0) and (self_item_count(PID_DESERT_EAGLE) <= 0) then begin
            if (self_item_count(PID_DESERT_EAGLE) <= 0) then begin
               Item := create_object(PID_DESERT_EAGLE, 0, 0);
               add_obj_to_inven(self_obj, Item);
               if (random(0, 1)) then begin
                  if (self_item_count(PID_44_MAGNUM_JHP) <= 0) then begin
                     Item := create_object(PID_44_MAGNUM_JHP, 0, 0);
                     add_obj_to_inven(self_obj, Item);
                  end
               end
               else if (self_item_count(PID_44_FMJ_MAGNUM) <= 0) then begin
                  Item := create_object(PID_44_FMJ_MAGNUM, 0, 0);
                  add_obj_to_inven(self_obj, Item);
               end
            end
         end
      end
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (global_var(GVAR_ENEMY_HUB) == 1) then begin
         tmp_hostile := 1;
         attack(dude_obj);
      end
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;

   get_reaction

   if (CUR_MAP_HUBENT) then begin
      call Cop01;
   end
   else if (CUR_MAP_HUBDWNTN) then begin
      call Cop00;
   end
   else begin
      call Cop02;
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_HUB, 1);
      tmp_hostile := 1;
   end
end

procedure combat_p_proc begin
   if (global_var(GVAR_ENEMY_HUB) == 1) then begin
      tmp_hostile := 1;
      attack(dude_obj);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Cop00 begin
   if (random(0, 3) == 1) then begin
      float_msg(self_obj, mstr(random(101, 106)), FLOAT_MSG_RED);
   end
   else if has_rep_childkiller then begin
      float_msg(self_obj, mstr(random(113, 116)), FLOAT_MSG_RED);
   end
   else if (has_rep_berserker) then begin
      if (dude_is_male) then begin
         float_msg(self_obj, mstr(random(110, 112)), FLOAT_MSG_RED);
      end
      else begin
         float_msg(self_obj, mstr(random(110, 111)), FLOAT_MSG_RED);
      end
   end
   else if (has_rep_champion) then begin
      float_msg(self_obj, mstr(random(117, 118)), FLOAT_MSG_RED);
   end
   else begin
      if (dude_is_armed) then begin
         float_msg(self_obj, mstr(random(107, 109)), FLOAT_MSG_RED);
      end
      else begin
         if (global_var(GVAR_KILL_DEATHCLAW) == 2) or (global_var(GVAR_MISSING_CARAVANS_STATUS) == 5) then begin
            if random(0, 1) == 1 then begin
               float_msg(self_obj, mstr(119), FLOAT_MSG_GREEN);
            end
            else begin
               float_msg(self_obj, mstr(random(103, 106)), FLOAT_MSG_NORMAL);
            end
         end
         else begin
            float_msg(self_obj, mstr(random(101, 106)), FLOAT_MSG_RED);
         end
      end
   end
end

procedure Cop01 begin
   if (random(0, 3) == 1) then begin
      float_msg(self_obj, mstr(random(120, 129)), FLOAT_MSG_NORMAL);
   end
   else if (dude_is_armed) then begin
      float_msg(self_obj, mstr(random(130, 133)), FLOAT_MSG_RED);
   end
   else if dude_wearing_bos_power_armor then begin
      float_msg(self_obj, mstr(134), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_PC_WANTED) == 1) then begin
      float_msg(self_obj, mstr(random(135, 137)), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_FARGO_TRADERS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(138), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_WATER_MERCHANTS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(139), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_CRIMSON_CARAVANS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(140), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_UNDERGROUND_STATUS) == 1) then begin
      float_msg(self_obj, mstr(random(141, 143)), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(random(120, 129)), FLOAT_MSG_NORMAL);
   end
end

procedure Cop02 begin
   variable LVar0 := 0;
   if (random(0, 3) == 1) then begin
      float_msg(self_obj, mstr(144), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_PC_WANTED) == 1) then begin
      float_msg(self_obj, mstr(random(155, 157)), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_FARGO_TRADERS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(158), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_WATER_MERCHANTS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(159), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_CRIMSON_CARAVANS_STATUS) == 1) then begin
      float_msg(self_obj, mstr(160), FLOAT_MSG_RED);
   end
   else if (global_var(GVAR_UNDERGROUND_STATUS) == 1) then begin
      float_msg(self_obj, mstr(random(161, 163)), FLOAT_MSG_RED);
   end
   else begin
      LVar0 := random(144, 154);
      if ((LVar0 == 145) or (LVar0 == 146)) then begin
         if (dude_is_male) then begin
            LVar0 := 145;
         end
         else begin
            LVar0 := 146;
         end
      end
      float_msg(self_obj, mstr(LVar0), FLOAT_MSG_RED);
   end
end
