/*

   Hub - Hightower

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_HIGHTOWR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;
procedure combat_p_proc;

procedure Daren01;
procedure Daren02;
procedure Daren03;
procedure Daren04;
procedure Daren05;
procedure Daren05a;
procedure Daren06;
procedure Daren07;
procedure DarenCombat;
procedure DarenEndLeon;

variable tmp_hostile;
variable Only_Once := 1;
variable visible := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      if (global_var(GVAR_KILL_MERCHANT) == 5) then begin
         if not(is_loading_game) then set_self_invisible;
         visible := 0;
      end
      set_self_team(TEAM_HUB_HEIGHTS);
      set_self_ai(AI_LOSER);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc
begin
   if (visible == 0) then begin
      script_overrides;
   end
   else begin
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      if (map_var(5) == 1) then begin
         if (/*(self_can_hear_dude) or*/ (self_can_see_dude)) then begin
            call combat;
         end
      end
      else begin
         if (morning or (day)) then begin
            if (self_tile != 25125) then begin
               self_walk_to_tile(25125);
            end
            else if ((dude_tile == 24522) or self_can_see_dude and (map_var(1) == 0)) then begin
               dialogue_system_enter;
            end
         end
         else begin
            if (self_tile != 24708) then begin
               self_walk_to_tile(24708);
            end
         end
      end
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_HUBHEIGT);

   dude_look_at_self;
   get_reaction
   if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
      float_msg(self_obj, mstr(127), FLOAT_MSG_RED);
      call combat;
   end
   else begin
      if (global_var(GVAR_ENEMY_HUB) == 1) then begin
         float_msg(self_obj, mstr(127), FLOAT_MSG_RED);
         call combat;
      end
      else begin
         if (local_var(4) == 0) then begin
            set_local_var(4, 1);
            start_gdialog(582, self_obj, 4, -1, -1);
            gsay_start;
            call Daren01;
            gsay_end;
            end_dialogue;
         end
         else begin
            float_msg(self_obj, mstr(127), FLOAT_MSG_RED);
            call combat;
         end
      end
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   inc_hub_rep(REP_BONUS_KILLED_HIGHTOWER);
   set_map_var(0, map_var(0) + 1);
   if (map_var(0) > 1) then begin
      set_global_var(GVAR_KILL_MERCHANT, 2);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc begin
   set_map_var(5, 1);
   set_global_var(GVAR_KILL_MERCHANT, 5);
end

procedure combat_p_proc begin
   critter_set_flee_state(self_obj, 1);
end

procedure Daren01 begin
   Reply(101);
   NOption(102, Daren02, 4);
   if (global_var(GVAR_KILL_MERCHANT) == 1) then begin
      NOption(103, Daren03, 4);
   end
   NOption(mstr(104) + dude_name + mstr(105), Daren04, 4);
   NOption(106, Daren07, 4);
   NOption(107, Daren06, 4);
   NLowOption(108, Daren06);
end

procedure Daren02 begin
   Reply(109);
   if (global_var(GVAR_KILL_MERCHANT) == 1) then begin
      BOption(110, DarenCombat, 4);
   end
   NOption(111, Daren05, 4);
   NOption(112, Daren06, 4);
   NOption(113, Daren06, 4);
end

procedure Daren03 begin
   Reply(114);
   NOption(115, Daren06, 4);
   NOption(116, Daren07, 4);
   BOption(117, DarenCombat, 4);
   NOption(118, Daren06, 4);
end

procedure Daren04 begin
   BMessage(mstr(119) + dude_name + mstr(120));
   call DarenEndLeon;
end

procedure Daren05 begin
   Reply(121);
   NOption(122, Daren07, 4);
   if (dude_caps >= 100) then begin
      NOption(123, Daren05a, 4);
   end
   NOption(124, Daren06, 4);
end

procedure Daren05a begin
   variable LVar0 := 0;
   dude_caps_adjust(-100);
   BMessage(125);
   call DarenEndLeon;
end

procedure Daren06 begin
   BMessage(126);
   call DarenEndLeon;
end

procedure Daren07 begin
   BMessage(127);
   call DarenCombat;
end

procedure DarenCombat begin
   call combat;
end

procedure DarenEndLeon begin
   set_map_var(1, 1);
end
