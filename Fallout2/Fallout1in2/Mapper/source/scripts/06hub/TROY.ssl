/*

   Hub - Super Mutant who approaches the player on map_enter, after the Hub has been invaded.

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_TROY
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Troy00;
procedure Troy01;
procedure Troy02;
procedure Troy03;
procedure Troy04;
procedure Troy05;
procedure Troy06;
procedure Troy07;
procedure Troy08;

#define LVAR_Herebefore          (4)
#define LVAR_Initial             (5)

variable tmp_hostile;
variable Only_Once := 1;
variable TalkToPlayer;

variable weaponPtr;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
      reg_anim_clear(self_obj);
      if (local_var(LVAR_Initial) == 0) then begin
         set_local_var(LVAR_Initial, 1);
         weaponPtr := create_object(PID_MINIGUN, 0, 0);
         add_obj_to_inven(self_obj, weaponPtr);
         wield_obj_critter(self_obj, weaponPtr);
         add_obj_to_inven(self_obj, create_object(PID_5MM_JHP, 0, 0));
      end
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   if ((local_var(LVAR_Herebefore) == 0) and ((tile_distance_objs(self_obj, dude_obj) <= 5) or self_can_see_dude)) then begin
      set_local_var(LVAR_Herebefore, 1);
      if (cathedral_destroyed and military_base_destroyed) then
         tmp_hostile := 1;
      else
         dialogue_system_enter;
   end
   if (local_var(LVAR_Herebefore) == 0) then begin
      self_walk_to_tile(tile_num_in_direction(dude_tile, random(0, 5), 1));
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   if (local_var(LVAR_Herebefore) == 1) then
      start_dialog_at_node(Troy00);
   else
      display_msg(mstr(132));
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Troy00 begin
   Reply(101);
   NOption(102, Troy01, 7);
   NOption(103, Troy01, 4);
   NOption(104, Troy02, 4);
   BOption(105, Troy03, 4);
   BOption(106, Troy04, 4);
   NLowOption(107, Troy05);
end

procedure Troy01 begin
   Reply(108);
   NOption(109, Troy06, 4);
   NOption(110, Troy02, 4);
   BOption(111, Troy03, 4);
   BOption(112, Troy07, 4);
end

procedure Troy02 begin
   NMessage(113);
   set_global_var(GVAR_ENTERING_VATS_HOW, 1);
   //game_time_advance(game_ticks(60 * 60 * 24 * 14));
   //load_map(MAP_MBVATS12, 5);
   if not(military_base_destroyed) then begin
      advance_time_travel_to_mbase_vats;
   end
   else begin
      advance_time_travel_to_master;
   end
end

procedure Troy03 begin
   set_global_var(GVAR_ENEMY_CATHEDRAL, 1);
   NMessage(114);
   call combat;
end

procedure Troy04
begin
   Reply(115);
   NOption(116, Troy02, 4);
   BOption(117, Troy03, 4);
   BOption(118, Troy08, 4);
   BOption(119, Troy07, 4);
end

procedure Troy05 begin
   Reply(120);
   NLowOption(121, Troy02);
   NLowOption(122, Troy03);
end

procedure Troy06 begin
   Reply(123);
   NOption(124, Troy02, 4);
   BOption(125, Troy03, 4);
   BOption(126, Troy07, 4);
end

procedure Troy07 begin
   set_global_var(GVAR_ENEMY_CATHEDRAL, 1);
   BMessage(127);
   call combat;
end

procedure Troy08 begin
   Reply(128);
   NOption(129, Troy02, 4);
   BOption(120, Troy03, 4);
   BOption(121, Troy07, 4);
end
