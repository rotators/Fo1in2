/*

   Hub - Hightower / Rick

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_HTWRRICK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure Rick01;
procedure Rick02;
procedure Rick03;
procedure Rick04;
procedure Rick05;
procedure Rick06;
procedure Rick07;
procedure Rick08;
procedure Rick09;
procedure Rick10;
procedure RickEnd;
procedure RickCombat;

variable tmp_hostile;
variable Only_Once := 1;
variable nightWarned;
variable visible := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      if (global_var(GVAR_KILL_MERCHANT) == 5) then begin
         if not(is_loading_game) then set_self_invisible;
         visible := 0;
      end
      set_self_team(TEAM_42);
      set_self_ai(AI_GUARD_SADISTIC);
   end
end

procedure combat
begin
   tmp_hostile := 1;
end

procedure critter_p_proc
begin
   if (visible == 0) then begin
      script_overrides;
   end
   else begin
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      if (map_var(5) == 1) then begin
         if ((self_can_hear_dude) or (self_can_see_dude)) then begin
            call combat;
         end
      end
      else begin
         if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
            if ((tile_distance_objs(self_obj, dude_obj) <= 7) and (nightWarned == 0)) then begin
               nightWarned := 1;
               dialogue_system_enter;
            end
            if (self_tile != 19713) then begin
               self_walk_to_tile(19713);
            end
         end
         else begin
            if (self_tile != 17892) then begin
               self_walk_to_tile(17892);
            end
         end
      end
   end
end

procedure pickup_p_proc
begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc
begin
   dude_look_at_self;
   get_reaction
   if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
      start_gdialog(871, self_obj, 4, -1, -1);
      gsay_start;
      call Rick09;
      gsay_end;
      end_dialogue;
   end
   else begin
      start_gdialog(871, self_obj, 4, -1, -1);
      gsay_start;
      call Rick01;
      gsay_end;
      end_dialogue;
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc
begin
   set_map_var(5, 1);
end

procedure Rick01
begin
   Reply(101);
   NOption(103, Rick03, 4);
   NOption(104, Rick04, 4);
   if (has_rep_berserker) then begin
      BOption(105, Rick08, 4);
   end
   NLowOption(102, Rick02);
end

procedure Rick02
begin
   Reply(106);
   NLowOption(107, Rick10);
   NLowOption(108, RickEnd);
end

procedure Rick03
begin
   Reply(109);
   NOption(110, Rick05, 4);
   NOption(111, Rick06, 4);
   NOption(112, RickEnd, 4);
end

procedure Rick04
begin
   Reply(113);
   BOption(114, RickCombat, 4);
   NOption(115, Rick08, 4);
end

procedure Rick05
begin
   Reply(116);
   NOption(117, Rick06, 4);
   NOption(118, Rick07, 4);
   NOption(119, RickEnd, 4);
end

procedure Rick06
begin
   Reply(120);
   BOption(121, RickCombat, 4);
   NOption(122, Rick05, 4);
   NOption(123, RickEnd, 4);
end

procedure Rick07
begin
   BMessage(124);
end

procedure Rick08
begin
   Reply(125);
   NOption(126, RickEnd, 4);
   BOption(127, RickCombat, 4);
end

procedure Rick09
begin
   Reply(128);
   BOption(131, RickCombat, 4);
   NOption(132, RickEnd, 4);
   NLowOption(129, RickEnd);
   BLowOption(130, RickCombat);
end

procedure Rick10
begin
   BMessage(133);
end

procedure RickEnd
begin
end

procedure RickCombat
begin
   call combat;
end
