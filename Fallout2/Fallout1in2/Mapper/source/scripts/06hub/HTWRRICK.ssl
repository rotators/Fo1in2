/*

	Hub - Hightower / Rick

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_HTWRRICK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure Rick01;
procedure Rick02;
procedure Rick03;
procedure Rick04;
procedure Rick05;
procedure Rick06;
procedure Rick07;
procedure Rick08;
procedure Rick09;
procedure Rick10;
procedure RickEnd;
procedure RickCombat;

variable tmp_hostile;
variable Only_Once := 1;
variable nightWarned;
variable visible := 1;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		if (global_var( GVAR_KILL_MERCHANT ) == 5) then begin
			if not(is_loading_game) then begin    set_self_invisible;    end
			visible := 0;
		end
		set_self_team( TEAM_42 );
		set_self_ai( AI_GUARD_SADISTIC );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((self_can_hear_dude) or (self_can_see_dude)) then begin
				call combat;
			end
		end
		else begin
			if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
				if ((tile_distance_objs(self_obj, dude_obj) <= 7) and (nightWarned == 0)) then begin
					nightWarned := 1;
					dialogue_system_enter;
				end
				if (self_tile != 19713) then begin
					self_walk_to_tile( 19713 );
				end
			end
			else begin
				if (self_tile != 17892) then begin
					self_walk_to_tile( 17892 );
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	get_reaction
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		start_gdialog(871, self_obj, 4, -1, -1);
		gsay_start;
		call Rick09;
		gsay_end;
		end_dialogue;
	end
	else begin
		start_gdialog(871, self_obj, 4, -1, -1);
		gsay_start;
		call Rick01;
		gsay_end;
		end_dialogue;
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(NAME, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
end

procedure Rick01
begin
	gsay_reply( NAME, 101 );
	giq_option( 4, NAME, 103, Rick03, NEUTRAL_REACTION );
	giq_option( 4, NAME, 104, Rick04, NEUTRAL_REACTION );
	if (has_rep_berserker) then begin
		giq_option( 4, NAME, 105, Rick08, BAD_REACTION );
	end
	giq_option( -3, NAME, 102, Rick02, NEUTRAL_REACTION );
end

procedure Rick02
begin
	gsay_reply( NAME, 106 );
	giq_option( -3, NAME, 107, Rick10, NEUTRAL_REACTION );
	giq_option( -3, NAME, 108, RickEnd, NEUTRAL_REACTION );
end

procedure Rick03
begin
	gsay_reply( NAME, 109 );
	giq_option( 4, NAME, 110, Rick05, NEUTRAL_REACTION );
	giq_option( 4, NAME, 111, Rick06, NEUTRAL_REACTION );
	giq_option( 4, NAME, 112, RickEnd, NEUTRAL_REACTION );
end

procedure Rick04
begin
	gsay_reply( NAME, 113 );
	giq_option( 4, NAME, 114, RickCombat, BAD_REACTION );
	giq_option( 4, NAME, 115, Rick08, NEUTRAL_REACTION );
end

procedure Rick05
begin
	gsay_reply( NAME, 116 );
	giq_option( 4, NAME, 117, Rick06, NEUTRAL_REACTION );
	giq_option( 4, NAME, 118, Rick07, NEUTRAL_REACTION );
	giq_option( 4, NAME, 119, RickEnd, NEUTRAL_REACTION );
end

procedure Rick06
begin
	gsay_reply( NAME, 120 );
	giq_option( 4, NAME, 121, RickCombat, BAD_REACTION );
	giq_option( 4, NAME, 122, Rick05, NEUTRAL_REACTION );
	giq_option( 4, NAME, 123, RickEnd, NEUTRAL_REACTION );
end

procedure Rick07
begin
	gsay_message( NAME, 124, BAD_REACTION );
end

procedure Rick08
begin
	gsay_reply( NAME, 125 );
	giq_option( 4, NAME, 126, RickEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 127, RickCombat, BAD_REACTION );
end

procedure Rick09
begin
	gsay_reply( NAME, 128 );
	giq_option( 4, NAME, 131, RickCombat, BAD_REACTION );
	giq_option( 4, NAME, 132, RickEnd, NEUTRAL_REACTION );
	giq_option( -3, NAME, 129, RickEnd, NEUTRAL_REACTION );
	giq_option( -3, NAME, 130, RickCombat, BAD_REACTION );
end

procedure Rick10
begin
	gsay_message( NAME, 133, BAD_REACTION );
end

procedure RickEnd
begin
end

procedure RickCombat
begin
	call combat;
end
