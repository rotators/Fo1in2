/*

   Hub - Hightower / Leon

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_HTWRLEON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure Leon01;
procedure Leon01a;
procedure Leon02;
procedure Leon03;
procedure Leon04;
procedure Leon04a;
procedure Leon05;
procedure Leon06;
procedure Leon07;
procedure Leon08;
procedure Leon08a;
procedure Leon09;
procedure LeonEnd;
procedure LeonKickOut;
procedure LeonPerformDump;
procedure LeonCombat;

variable tmp_hostile;
variable Only_Once := 1;
variable visible := 1;

import variable Front_Door_Ptr;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      if (global_var(GVAR_KILL_MERCHANT) == 5) then begin
         if not(is_loading_game) then set_self_invisible;
         visible := 0;
      end
      set_self_team(TEAM_42);
      set_self_ai(AI_GUARD_SADISTIC);
   end
end

procedure combat
begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (visible == 0) then begin
      script_overrides;
   end
   else begin
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      if (map_var(5) == 1) then begin
         if (/*(self_can_hear_dude) or*/ (self_can_see_dude)) then begin
            call combat;
         end
      end
      else begin
         if (map_var(1) == 1) then begin
            call LeonPerformDump;
         end
      end
      if (morning or (day)) then begin
         if (self_tile != 22120) then begin
            self_walk_to_tile(22120);
         end
      end
      else begin
         if (self_tile != 16519) then begin
            self_walk_to_tile(16519);
         end
      end
      if (tile_distance_objs(self_obj, dude_obj) < 6) then begin
         if ((local_var(5) == 0) and (self_tile == 22120)) then begin
            set_local_var(5, 1);
            dialogue_system_enter;
         end
      end
   end
end

procedure pickup_p_proc
begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc
begin
   dude_look_at_self;
   get_reaction
   if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
      float_msg(self_obj, mstr(141), FLOAT_MSG_RED);
   end
   else begin
      if (global_var(GVAR_ENEMY_HUB) == 1) then begin
         float_msg(self_obj, mstr(142), FLOAT_MSG_RED);
         call combat;
      end
      else begin
         if (local_var(4) == 0) then begin
            set_local_var(4, 1);
            start_gdialog(866, self_obj, 4, -1, -1);
            gsay_start;
            call Leon01;
            gsay_end;
            end_dialogue;
         end
         else begin
            start_gdialog(866, self_obj, 4, -1, -1);
            gsay_start;
            call Leon08;
            gsay_end;
            end_dialogue;
         end
      end
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc
begin
   set_map_var(5, 1);
end

procedure Leon01
begin
   Reply(101);
   NOption(102, Leon01a, 4);
   BOption(103, LeonCombat, 4);
   NOption(104, Leon03, 4);
   if (global_var(GVAR_KILL_MERCHANT) == 1) then begin
      NOption(140, Leon04, 4);
   end
   NLowOption(138, Leon09);
end

procedure Leon01a
begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Leon02;
   end
   else begin
      call Leon04;
   end
end

procedure Leon02
begin
   Reply(105);
   NOption(106, LeonEnd, 4);
   NOption(107, Leon03, 4);
end

procedure Leon03
begin
   Reply(108);
   BOption(109, LeonCombat, 4);
   NOption(110, LeonKickOut, 4);
   BOption(111, LeonCombat, 4);
end

procedure Leon04
begin
   Reply(112);
   NOption(113, Leon03, 4);
   NOption(114, Leon04a, 4);
   if (global_var(GVAR_KILL_MERCHANT) == 1) then begin
      NOption(115, Leon06, 4);
   end
end

procedure Leon04a
begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Leon02;
   end
   else begin
      call Leon05;
   end
end

procedure Leon05
begin
   Reply(116);
   NOption(117, Leon03, 4);
   if (global_var(GVAR_KILL_MERCHANT) == 1) then begin
      NOption(118, Leon06, 4);
   end
   NOption(119, Leon03, 4);
   NOption(120, LeonKickOut, 4);
end

procedure Leon06
begin
   NMessage(121);
   NMessage(122);
   Reply(123);
   NOption(124, Leon07, 4);
   BOption(125, LeonCombat, 4);
   NOption(126, Leon03, 4);
end

procedure Leon07
begin
   Reply(127);
   NOption(128, Leon03, 4);
   NOption(129, LeonKickOut, 4);
end

procedure Leon08
begin
   Reply(130);
   BOption(131, LeonCombat, 4);
   NOption(132, LeonKickOut, 4);
   NOption(133, Leon08a, 4);
end

procedure Leon08a
begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20))) then begin
      call Leon02;
   end
   else begin
      call Leon04;
   end
end

procedure Leon09
begin
   Reply(134);
   BOption(135, LeonCombat, 4);
   NOption(136, LeonKickOut, 4);
   NOption(137, LeonKickOut, 4);
   NLowOption(139, LeonKickOut);
end

procedure LeonEnd
begin
end

procedure LeonKickOut
begin
   set_map_var(1, 1);
end

procedure LeonPerformDump begin
   set_map_var(1, 0);
   set_map_var(3, 0);
   gfade_out(1);
   obj_close(Front_Door_Ptr);
   set_local_var(5, 0);
   move_to(dude_obj, 21931, 0);
   gfade_in(1);
end

procedure LeonCombat
begin
   set_local_var(5, 1);
   call combat;
end
