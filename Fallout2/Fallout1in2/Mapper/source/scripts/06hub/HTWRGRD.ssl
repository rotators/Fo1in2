/*

   Hub - Water Merchants guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_HTWRGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable guardNumber;
variable threatened;
variable visible := 1;

procedure start
begin


   if Only_Once then begin
      Only_Once := 0;
      if (self_tile == 19511) then begin
         guardNumber := 1;
      end
      if (self_tile == 19935) then begin
         guardNumber := 2;
      end
      if (global_var(GVAR_KILL_MERCHANT) == 5) then begin
         if not(is_loading_game) then set_self_invisible;
         visible := 0;
      end
      set_self_team(TEAM_42);
      set_self_ai(AI_GUARD_SADISTIC);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc
begin
   if (visible == 0) then begin
      script_overrides;
   end
   else begin
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      if (map_var(5) == 1) then begin
         if ((self_can_hear_dude) or (self_can_see_dude)) then begin
            call combat;
         end
      end
      else begin
         if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
            if ((self_can_hear_dude) or (self_can_see_dude) and (threatened == 0)) then begin
               dialogue_system_enter;
            end
            if (guardNumber == 1) then begin
               if (self_tile != 17119) then begin
                  self_walk_to_tile(17119);
               end
            end
            else begin
               if (guardNumber == 2) then begin
                  if (self_tile != 16930) then begin
                     self_walk_to_tile(16930);
                  end
               end
            end
         end
         else begin
            if (guardNumber == 1) then begin
               if (self_tile != 19511) then begin
                  self_walk_to_tile(19511);
               end
            end
            else begin
               if (guardNumber == 2) then begin
                  if (self_tile != 19935) then begin
                     self_walk_to_tile(19935);
                  end
               end
            end
         end
      end
   end
end

procedure pickup_p_proc
begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc
begin
   dude_look_at_self;
   get_reaction
   if ((game_time_hour > 700) and (game_time_hour < 1900)) then begin
      float_msg(self_obj, mstr(random(101, 106)), FLOAT_MSG_RED);
   end
   else begin
      threatened := 1;
      float_msg(self_obj, mstr(random(107, 109)), FLOAT_MSG_RED);
      call combat;
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc
begin
   set_map_var(5, 1);
end
