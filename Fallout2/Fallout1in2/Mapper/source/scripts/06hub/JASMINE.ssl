/*

   Hub - Jasmine, member of the Thieves' Circle

*/

/* Include Files */
#include "define.h"
#include "area/hub.h"
#include "maps/huboldtn.h"

#define NAME                    SCRIPT_JASMINE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Jasmine00;
procedure Jasmine01;
procedure Jasmine02;
procedure Jasmine03;
procedure Jasmine04;
procedure Jasmine05;
procedure Jasmine06;
procedure Jasmine07;
procedure Jasmine08;
procedure Jasmine09;
procedure Jasmine10;
procedure Jasmine11;
procedure Jasmine12;
procedure Jasmine13;
procedure Jasmine14;
procedure Jasmine15;
procedure Jasmine16;
procedure Jasmine17;
procedure JasmineEnd;

variable tmp_hostile;
variable Only_Once := 1;
variable float;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_THIEVES_CIRCLE);
      set_self_ai(AI_HUB_THIEF);
   end
end

procedure map_enter_p_proc begin
   float := 0;
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_HUBOLDTN);

   dude_look_at_self;
   get_reaction
   if (map_var(MVAR_Leave_Thieves) == 1) then begin
      call Jasmine01;
   end
   else if (global_var(GVAR_STEAL_NECKLACE) == 0) then begin
      call Jasmine02;
   end
   else if ((global_var(GVAR_STEAL_NECKLACE) == 1) and (local_var(6) == 0)) then begin
      start_dialog_at_node(Jasmine03);
   end
   else if (global_var(GVAR_STEAL_NECKLACE) == 1) then begin
      call Jasmine05;
   end
   else if ((map_var(MVAR_Give_reward) == 1) and (local_var(5) == 0)) then begin
      start_dialog_at_node(Jasmine06);
   end
   else begin
      call Jasmine07;
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Jasmine00 begin
   float_msg(self_obj, mstr(102), FLOAT_MSG_RED);
   call combat;
end

procedure Jasmine01 begin
   float_msg(self_obj, mstr(103), FLOAT_MSG_RED);
end

procedure Jasmine02 begin
   float_msg(self_obj, mstr(104), FLOAT_MSG_RED);
end

procedure Jasmine03 begin
   variable LVar0 := 0;
   LVar0 := create_object(PID_LOCKPICKS, 0, 0);
   add_obj_to_inven(dude_obj, LVar0);
   LVar0 := create_object(PID_FLARE, 0, 0);
   add_mult_objs_to_inven(dude_obj, LVar0, 2);
   LVar0 := create_object(PID_NUKA_COLA, 0, 0);
   add_obj_to_inven(dude_obj, LVar0);
   set_local_var(6, 1);

   Reply(105);
   NOption(106, Jasmine08, 4);
   NOption(107, Jasmine09, 4);

   // This was a cut dialog option
   if (dude_perception > 9) then
      NOption(108, Jasmine10, 4);

   NOption(109, Jasmine11, 4);
   NOption(110, Jasmine12, 4);
   NOption(111, Jasmine13, 4);
   NLowOption(112, Jasmine14);
end

procedure Jasmine04 begin
   if (dude_is_male) then begin
      float_msg(self_obj, mstr(113), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(114), FLOAT_MSG_RED);
   end
end

procedure Jasmine05 begin
   if (float) then begin
      call Jasmine04;
   end
   else begin
      float := 1;
      float_msg(self_obj, mstr(117), FLOAT_MSG_RED);
   end
end

procedure Jasmine06 begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   set_local_var(5, 1);
   dude_caps_adjust(3000);
   LVar1 := create_object(PID_ELECTRONIC_LOCKPICKS, 0, 0);
   add_obj_to_inven(dude_obj, LVar1);
   if (dude_is_male) then begin
      NMessage(118);
   end
   else begin
      NMessage(119);
   end
   NMessage(121);
   NMessage(135);
end

procedure Jasmine07 begin
   float_msg(self_obj, mstr(122), FLOAT_MSG_RED);
end

procedure Jasmine08 begin
   NMessage(123);
end

procedure Jasmine09 begin
   if (dude_is_male) then begin
      NMessage(124);
   end
   else begin
      NMessage(134);
   end
end

procedure Jasmine10 begin
   NMessage(125);
end

procedure Jasmine11 begin
   NMessage(126);
end

procedure Jasmine12 begin
   NMessage(127);
end

procedure Jasmine13 begin
   NMessage(128);
end

procedure Jasmine14 begin
   NMessage(129);
end

procedure Jasmine15 begin
   float_msg(self_obj, mstr(130), FLOAT_MSG_RED);
end

procedure Jasmine16 begin
   float_msg(self_obj, mstr(131), FLOAT_MSG_RED);
end

procedure Jasmine17 begin
   float_msg(self_obj, mstr(132), FLOAT_MSG_RED);
end

procedure JasmineEnd begin
end
