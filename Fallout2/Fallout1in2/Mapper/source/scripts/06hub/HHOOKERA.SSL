/*

   Hub - Hooker A

*/

/* Include Files */
#include "define.h"
//#include "hub.h"

#define NAME                    SCRIPT_HHOOKERA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure map_enter_p_proc;

procedure hookerend;
procedure hooker00;
procedure hooker01;
procedure hooker02;
procedure hooker03;
procedure hooker04;
procedure hooker05;
procedure hooker06;
procedure hooker07;
procedure hooker08;
procedure hooker09;
procedure hookendPoor;
procedure hooker10;
procedure HookGROUP;
procedure hookdone;
procedure hookdoneGROUP;
procedure enddialog;
procedure enddialogGROUP;
procedure hookdoneStupid;

variable tmp_hostile;
variable Only_Once := 1;

variable FloatWhenDone := 1;
variable FloatMsg;
variable AskedHowMuch;
//
// local_var(4) == LineToDisplay
// local_var(5) == SexCost
// local_var(6) == SexOnceBefore
// local_var(7) == DumbAndPoor


procedure start begin
   if not(global_var(GVAR_ENABLE_HOOKERS)) or fixt_disabled then begin
      destroy_object(self_obj);
   end

   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_UNDERGROUND);
      set_self_ai(AI_BOUNCER);
      set_local_var(4, 200);
   end
end

procedure combat_p_proc begin
   //tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   if (local_var(7) and (dude_caps < 150)) then begin
      script_overrides;
      float_msg(self_obj, mstr(263), FLOAT_MSG_RED);
   end
   else begin
      set_local_var(4, (random(200, 209) + (dude_gender * 10)));
      start_dialog_at_node(hooker00);

      if FloatWhenDone then begin
         FloatMsg := (random(206, 209) + (dude_gender * 10));
         while(FloatMsg == local_var(4)) do
         begin
            FloatMsg := (random(206, 209) + (dude_gender * 10));
         end
         float_msg(self_obj, mstr(FloatMsg), FLOAT_MSG_RED);
      end

      if (dude_is_stupid) then begin
         float_msg(self_obj, mstr(262), FLOAT_MSG_RED);
      end
   end
//
// OLD CODE:
//    if ((local_var(4) < 200) or (local_var(4) > 208)) then begin
//       set_local_var(4, 200);
//    end
//    else begin
//       set_local_var(4, (local_var(4) + 1));
//    end
end

procedure hooker00 begin
   AskedHowMuch := 0;
   Reply(local_var(4));
   NOption(220, hooker02, 4);
   if ((Ian_In_Party) or (global_var(GVAR_TYCHO_HIRELING_STATUS) == 2) or (global_var(GVAR_KATJA_HIRELING_STATUS) == 2)) then begin
      NOption(225, hooker03, 4);
   end
   NOption(222, hooker01, 4);
   NOption(223, hookerend, 4);
   BLowOption(232, hooker05);
end

procedure hooker05// stupid characters
begin
   Reply(233);
   NLowOption(235, hooker07);
   NLowOption(238, hooker06);
end

procedure hooker06// stupid characters
begin
   Reply(239);
   NLowOption(240, enddialog);
end

procedure hooker07// stupid characters
begin
   Reply(405);
   NLowOption(243, hooker08);
end

procedure hooker08// stupid characters
begin
   Reply(244);
   if (dude_caps >= 150) then begin
      NLowOption(257, hooker09);
   end
   else begin
      BLowOption(258, hookendPoor);
      set_local_var(7, 1);
   end
   NLowOption(259, enddialog);
end

procedure hooker09// stupid characters
begin
   Reply(260);
   NLowOption(264, hookdoneStupid);
   dude_caps_adjust(-150);
end

procedure hookdoneStupid// stupid characters
begin
   gfade_out(1);
   game_time_advance(random(1, ((10 * 60 * 2 * dude_strength) + (10 * 60 * 7 * dude_endurance))));// random between 1 minute and (2*STR)+(7*END)
   gfade_in(1);
   Reply(261);
   BLowOption(265, enddialog);
   FloatWhenDone := 0;
end

procedure hooker01 begin
   Reply(227);
   if AskedHowMuch then begin
      NOption(246, hooker10, 4);// "Just sex"
      NOption(237, hooker10, 4);// "Anything and everything ! Surprise me..."
      NOption(245, hooker10, 4);// "I don't care, let's just get this over with."
   end
   else begin
      NOption(246, hooker04, 4);// "Just sex"
      NOption(237, hooker04, 4);// "Anything and everything ! Surprise me..."
      NOption(245, hooker04, 4);// "I don't care, let's just get this over with."
   end
// giq_option(4, 708, 234, hooker10, 50);
// giq_option(4, 708, 230, hooker10, 50);
// giq_option(4, 708, 231, hooker10, 50);
   NOption(250, hookerend, 4);
end

procedure hooker02// just player
begin
//Charisma
   AskedHowMuch := 1;
   if (dude_charisma < 4) then begin
      set_local_var(5, 30);
      Reply(mstr(247) + local_var(5) + mstr(300));
   end
   else begin
      if (dude_charisma > 7) then begin
         set_local_var(5, 10);
         Reply(mstr(248) + local_var(5) + mstr(300));
      end
      else begin
         set_local_var(5, 20);
         Reply(221);
      end
   end
   if (dude_caps >= local_var(5)) then begin
      NOption(249, hooker01, 4);
   end
   else begin
      BOption(252, hookendPoor, 4);
   end
   NOption(223, hookerend, 4);
end

procedure hooker03// group action
begin
//Charisma
   if (dude_charisma < 4) then begin
      set_local_var(5, 125);
      Reply(mstr(247) + local_var(5) + mstr(300));
   end
   else begin
      if (dude_charisma > 7) then begin
         set_local_var(5, 75);
         Reply(mstr(248) + local_var(5) + mstr(300));
      end
      else begin
         set_local_var(5, 100);
         Reply(226);
      end
   end
   if (dude_caps >= local_var(5)) then begin
      NOption(249, HookGROUP, 4);
   end
   else begin
      BOption(252, hookendPoor, 4);
   end
   NOption(223, hookerend, 4);
end

procedure hooker04// just player, didn't start by asking "How much?"
begin
//Charisma
   if (dude_charisma < 4) then begin
      set_local_var(5, 30);
      Reply(mstr(247) + local_var(5) + mstr(300));
   end
   else begin
      if (dude_charisma > 7) then begin
         set_local_var(5, 10);
         Reply(mstr(248) + local_var(5) + mstr(300));
      end
      else begin
         set_local_var(5, 20);
         Reply(221);
      end
   end
   if (dude_caps >= local_var(5)) then begin
      NOption(249, hooker10, 4);
   end
   else begin
      BOption(252, hookendPoor, 4);
   end
   NOption(223, hookerend, 4);
end

procedure hooker10 begin
   dude_caps_adjust(-local_var(5));
   Reply(228);
   NOption(254, hookdone, 4);
end

procedure HookGROUP begin
   dude_caps_adjust(-local_var(5));
   Reply(242);
   NOption(254, hookdoneGROUP, 4);
end

procedure hookendPoor begin
   FloatWhenDone := 0;
   BMessage(253);// "get out!"
end

procedure hookerend begin
   FloatWhenDone := 1;
   NMessage(251);// "suit yourself"
end

procedure hookdone begin
   gfade_out(ONE_GAME_SECOND);
   do_default_sex;
   gfade_IN(ONE_GAME_SECOND);

   if local_var(6) then begin// does she know player
      Reply(256);
   end
   else begin
      Reply(229);
      set_local_var(6, 1);
   end
   NOption(224, enddialog, 4);
   NOption(255, enddialog, 4);
end

procedure hookdoneGROUP begin
   gfade_out(1);
   game_time_advance(random(1, (((10 * 60 * 2 * dude_strength) + (10 * 60 * 7 * dude_endurance)) * 2)));// random between 1 minute and (2*STR)+(7*END), times 2 for being a group
   gfade_in(1);
   if local_var(6) then begin// does she know player
      Reply(256);
   end
   else begin
      Reply(229);
      set_local_var(6, 1);
   end
   NOption(224, enddialogGROUP, 4);
   NOption(255, enddialogGROUP, 4);
end

procedure enddialog begin
   FloatWhenDone := 0;
end

procedure enddialogGROUP begin
   FloatWhenDone := 0;
   if (Ian_In_Party) and (Ian_ptr != 0) then begin
      float_msg(Ian_ptr, mstr(400), FLOAT_MSG_NORMAL);
   end
   if (global_var(GVAR_DOGMEAT_HIRELING_STATUS)) and (Dog_ptr != 0) then begin
      float_msg(Dog_ptr, message_str(SCRIPT_ALLDOGS, 117), FLOAT_MSG_GREEN);
   end
   if (global_var(GVAR_TYCHO_HIRELING_STATUS) == 2) and (Tycho_ptr != 0) then begin
      float_msg(Tycho_ptr, mstr(402), FLOAT_MSG_LIGHT_GREY);
   end
   if (global_var(GVAR_KATJA_HIRELING_STATUS) == 2) and (Katja_ptr != 0) then begin
      float_msg(Katja_ptr, mstr(403), FLOAT_MSG_PURPLE);
   end
   if (global_var(GVAR_TANDI_HIRELING_STATUS) == 5) and (Tandi_ptr != 0) then begin
      float_msg(Tandi_ptr, mstr(404), FLOAT_MSG_SEQUENTIAL);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(302));
end

procedure map_enter_p_proc begin
   variable LVar0 := 0;
   LVar0 := self_item(PID_BOTTLE_CAPS);
   if (LVar0 <= 50) then begin
      variable LVar1 := 0;
      LVar1 := create_object(PID_BOTTLE_CAPS, 0, 0);
      add_mult_objs_to_inven(self_obj, LVar1, random(10, 40) + random(10, 40) + random(10, 40));
   end
end
