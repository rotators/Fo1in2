/*

	The Glow - Power terminal(s)

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\glow.h"

#define NAME                    SCRIPT_GPWRTERM

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure use_p_proc;
procedure talk_p_proc;
procedure use_skill_on_p_proc;

procedure Term01;
procedure Term02;
procedure Term03;
procedure Term03a;
procedure Term04;
procedure Term05;
procedure Term06;
procedure Term06a;
procedure Term07;
procedure Term08;
procedure Term09;
procedure Term09a;
procedure Term10;
procedure Term11;
procedure Term11a;
procedure Term12;
procedure Term13;
procedure Term14;
procedure Term14a;
procedure Term15;
procedure TermEnd;

procedure start begin
end

procedure use_p_proc begin
	script_overrides;
	dialogue_system_enter;
end

procedure talk_p_proc begin
	dude_look_at_self;
	start_gdialog(828, self_obj, 4, -1, -1);
	gsay_start;
	if glow_power_offline then begin
		call Term14;
	end
	else if glow_power_emergency then begin
		call Term01;
	end
	else begin
		call Term09;
	end
	gsay_end;
	end_dialogue;
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_SCIENCE) then begin
		script_overrides;
		dialogue_system_enter;
	end
end

procedure Term01 begin
	Reply(100);
	giq_option(4, NAME, 101, Term02, NEUTRAL_REACTION);
	giq_option(-3, NAME, 102, Term10, NEUTRAL_REACTION);
	giq_option(0, NAME, 103, TermEnd, NEUTRAL_REACTION);
end

procedure Term02 begin
	Reply(104);
	giq_option(4, NAME, 105, Term03, NEUTRAL_REACTION);
	giq_option(4, NAME, 106, Term06, NEUTRAL_REACTION);
	giq_option(4, NAME, 107, Term01, NEUTRAL_REACTION);
end

procedure Term03 begin
	Reply(108);
	giq_option(4, NAME, 109, Term03a, NEUTRAL_REACTION);
	giq_option(4, NAME, 110, Term02, NEUTRAL_REACTION);
end

procedure Term03a begin
	if not(glow_quest_power_restored) then begin
		call Term04;
	end
	else begin
		set_glow_power_online;
		play_sfx("ELEVATO1");
	end
end

procedure Term04 begin
	Reply(111);
	giq_option(8, NAME, 112, Term05, NEUTRAL_REACTION);
	giq_option(4, NAME, 113, Term02, NEUTRAL_REACTION);
end

procedure Term05 begin
	Reply(114);
	giq_option(4, NAME, 115, Term02, NEUTRAL_REACTION);
end

procedure Term06 begin
	Reply(116);
	giq_option(4, NAME, 117, Term06a, NEUTRAL_REACTION);
	giq_option(4, NAME, 118, Term02, NEUTRAL_REACTION);
end

procedure Term06a begin
	set_glow_power_offline;
end

procedure Term07 begin
	Reply(119);
	giq_option(4, NAME, 120, Term08, NEUTRAL_REACTION);
	giq_option(0, NAME, 121, TermEnd, NEUTRAL_REACTION);
end

procedure Term08 begin
	Reply(122);
	giq_option(4, NAME, 123, Term09a, NEUTRAL_REACTION);
	giq_option(4, NAME, 124, Term07, NEUTRAL_REACTION);
end

procedure Term09 begin
	Reply(125);
	giq_option(-3, NAME, 126, Term09a, NEUTRAL_REACTION);
	giq_option(4, NAME, 127, Term09a, NEUTRAL_REACTION);
	giq_option(0, NAME, 128, TermEnd, NEUTRAL_REACTION);
end

procedure Term09a begin
	set_glow_power_emergency;
	play_sfx("CLANK1");
end

procedure Term10 begin
	Reply(129);
	giq_option(-3, NAME, 130, Term11, NEUTRAL_REACTION);
	giq_option(-3, NAME, 131, Term13, NEUTRAL_REACTION);
	giq_option(-3, NAME, 132, Term01, NEUTRAL_REACTION);
end

procedure Term11 begin
	Reply(133);
	giq_option(-3, NAME, 134, Term11a, NEUTRAL_REACTION);
	giq_option(-3, NAME, 135, Term10, NEUTRAL_REACTION);
end

procedure Term11a begin
	if not(glow_quest_power_restored) then begin
		call Term12;
	end
	else begin
		set_glow_power_online;
	end
end

procedure Term12 begin
	Reply(136);
	giq_option(-3, NAME, 137, Term10, NEUTRAL_REACTION);
end

procedure Term13 begin
	Reply(138);
	giq_option(-3, NAME, 148, Term06a, NEUTRAL_REACTION);
	giq_option(-3, NAME, 137, Term10, NEUTRAL_REACTION);
end

procedure Term14 begin
	Reply(139);
	giq_option(4, NAME, 140, Term14a, NEUTRAL_REACTION);
	giq_option(-3, NAME, 141, Term15, NEUTRAL_REACTION);
	giq_option(-3, NAME, 142, Term15, NEUTRAL_REACTION);
	giq_option(-3, NAME, 143, Term14a, NEUTRAL_REACTION);
	giq_option(-3, NAME, 144, Term15, NEUTRAL_REACTION);
	giq_option(0, NAME, 145, TermEnd, NEUTRAL_REACTION);
end

procedure Term14a begin
	set_glow_power_emergency;
	if glow_quest_power_restored then begin
		call Term01;
	end
end

procedure Term15 begin
	Reply(146);
	giq_option(0, NAME, 147, TermEnd, NEUTRAL_REACTION);
end

procedure TermEnd begin
end
