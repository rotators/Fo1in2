/*

   Glow - security robots

*/

/* Include Files */
#include "define.h"
#include "area/glow.h"

#define NAME                    SCRIPT_GSENROB

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure use_skill_on_p_proc;
procedure combat_p_proc;
procedure pickup_p_proc;

#define LVAR_RobotExp               (0)
#define LVAR_AnimStatus             (1)

#define robot_offline                     (local_var(LVAR_AnimStatus) == 0)
#define robot_online                      (local_var(LVAR_AnimStatus) == 1)
#define set_robot_offline                 set_local_var(LVAR_AnimStatus, 0)
#define set_robot_online                  set_local_var(LVAR_AnimStatus, 1)

procedure start begin
end

procedure map_update_p_proc begin
   if glow_power_online and glow_traps_active then begin
      if robot_offline then begin
         set_robot_online;
         reg_anim_clear(self_obj);
         reg_anim_begin();
         reg_anim_animate(self_obj, ANIM_back_to_standing, -1);
         reg_anim_end();
      end
   end
end

procedure critter_p_proc begin
   if glow_power_online and glow_traps_active then begin
      if self_can_see_dude then begin
         attack(dude_obj);
      end
   end
   else begin
      if robot_offline then begin
         anim(self_obj, ANIM_fall_back_sf, 0);
      end
      else if robot_online then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         reg_anim_animate(self_obj, ANIM_fall_back, -1);
         reg_anim_end();
         set_robot_offline;
      end
   end
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_SCIENCE) then begin
      script_overrides;
      if (robot_offline and (dude_skill_success(SKILL_SCIENCE, -10)) or (global_var(GVAR_GLOW_ROBOT_EXP) == 1)) then begin
         display_msg(gen_mstr(173));
         if (global_var(GVAR_GLOW_ROBOT_EXP) == 0) then begin
            set_global_var(GVAR_GLOW_ROBOT_EXP, 1);
            give_xp(EXP_INVESTIGATE_ROBOTS);
         end
      end
      else begin
         display_msg(gen_mstr(174));
      end
   end
end

procedure combat_p_proc begin
   //if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      if (not(glow_power_online) or glow_traps_inactive) then begin
         script_overrides;
         anim(self_obj, ANIM_fall_back_sf, 0);
      end
   //end
end

procedure pickup_p_proc begin
   if (not(glow_power_online) or glow_traps_inactive) then begin
      script_overrides;
   end
end
