/*

   Necropolis - Hotel

*/

#include "define.h"
#include "updatmap.h"
#include "command.h"
#include "necropolis.h"
#include "sfall/lib.arrays.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure map_lightning;
procedure Invasion;

//#define default_start_hex     (0)
#define bike_start_hex        (24681)
#define demo_start_hex        (25523)

export variable Use_Manhole_Top;
export variable Use_Manhole_Bottom;
export variable Use_Manhole_Middle;
export variable Manhole_Pointer_Top;
export variable Manhole_Pointer_Middle;
export variable Manhole_Pointer_Bottom;

variable fugee_ptr;
variable fugee_hex;

procedure start begin
end

procedure map_enter_p_proc begin
   if (global_var(GVAR_LOAD_MAP_INDEX) == 69) then begin
      override_map_start_hex(25523, 1, 0);
      gfade_in(ONE_GAME_SECOND);
      invisible_on_map(AREA_SCRAPHEAP)
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 3) then begin
      override_map_start(94, 136, 1, 5);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 12) then begin
      override_map_start(143, 64, 0, 2);
   end
   else begin
      override_map_start(94, 136, 1, 5);
   end

   if get_car_from_worldmap then begin
      override_map_start_hex(bike_start_hex, 1, 3);
   end

   if (map_first_run) then begin
      display_msg(message_str(SCRIPT_SHADYWST, 106));
   end

   set_global_var(GVAR_MARK_NECROP_1, 1);//Mark Hotel on the worldmap button list / town selection screen

   call map_lightning;
   call Invasion;

   if ((global_var(GVAR_NECROP_WATER_CHIP_TAKEN) == 1) and not(is_loading_game)) then begin
      if (((GAME_TIME_IN_DAYS) - global_var(GVAR_TIME_CHIP_GONE)) > 7) then begin
         if not(necropolis_wpump_fixed) then begin
            kill_critter_type(PID_ZOMBIE_GUARD, 2);
            kill_critter_type(PID_MINDLESS_GHOUL, 2);
            kill_critter_type(PID_GHOUL, 2);
            kill_critter_type(PID_GLOWING_ONE, 2);
         end
      end
   end

   if (global_var(GVAR_SEWER_KING_POS) == SEWER_POS_HOTEL) then
      call spawn_sewer_king;

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   call map_lightning;

   if (Use_Manhole_Top) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Top);
   end
   if (Use_Manhole_Middle) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Middle);
   end
   if (Use_Manhole_Bottom) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Bottom);
   end
end

procedure map_lightning begin
   if (dude_elevation == 0) then
      Darkness;
   else
      Lighting;
end

procedure Invasion begin
   if (necropolis_invaded and not(is_loading_game) and (map_var(10) == 0)) then begin
      set_map_var(10, 1);

      kill_critter_type(PID_ZOMBIE_GUARD, 1);
      kill_critter_type(PID_MINDLESS_GHOUL, 1);
      kill_critter_type(PID_GHOUL, 1);
      kill_critter_type(PID_GLOWING_ONE, 1);

      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 25717;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);

      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 24516;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);

      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 24927;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);

      // Any player party member was waiting in the map before?
      check_invasion_party_waiting;
   end
end
