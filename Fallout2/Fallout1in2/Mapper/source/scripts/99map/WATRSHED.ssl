#include "define.h"
#include "command.h"
#include "updatmap.h"
#include "area/necropolis.h"
#include "maps/watrshd.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure map_lightning;
procedure Invasion;
procedure set_critter_state;

variable party_elevation;
variable dude_start_hex;

variable fugee_ptr;
variable fugee_hex;

export variable SuperLeave;
export variable SuperLeft;
export variable ghoul_door_open;
export variable Harry_Pointer;
export variable Manhole_Pointer_Top;
export variable Manhole_Pointer_Middle;
export variable Manhole_Pointer_Bottom;
export variable Use_Manhole_Top;
export variable Use_Manhole_Bottom;
export variable Use_Manhole_Middle;

export variable Griffith_Ptr;

procedure start begin
end

procedure map_enter_p_proc begin
   set_global_var(GVAR_MARK_NECROP_3, 1);

   if (map_first_run) then begin
      // Add motorcycle
      if fo1in2_motorcycle_enabled then begin
         Create_Moto1(CAR_UNCLAIMED_HEX, CAR_UNCLAIMED_ELEV)
         car_give_gas(-80000); // start with 0 fuel!
      end
   end

   // If player has the motorcycle, delete the initial bike on the unused hex
   else if get_car_used then begin
      if (dude_has_car and tile_contains_obj_pid(CAR_UNCLAIMED_HEX, CAR_UNCLAIMED_ELEV, PID_DRIVABLE_MOTO1)) then begin
         Dest_Car(CAR_UNCLAIMED_HEX, CAR_UNCLAIMED_ELEV)
      end
   end

   // If Set sends the player to fix the water pump, Garret will be with him and block his exit.
   if (global_var(GVAR_NECROP_WATER_PUMP_GUARD)) then begin
      override_map_start_hex(9871, 1, 0);
   end
   else begin
      if (global_var(GVAR_LOAD_MAP_INDEX) == 1) then
         override_map_start_hex(11324, 1, 5);
      else if (global_var(GVAR_LOAD_MAP_INDEX) == 12) then // Teleport from Vault back to Watershed
         override_map_start_hex(19653, 0, 5);
      else
         override_map_start_hex(11324, 1, 5);
   end

   if get_car_from_worldmap then begin
      override_map_start_hex(9725, 1, 2);
   end

   if (map_var(MVAR_MUTANTS_HOSTILE) == 2) then begin
      kill_critter_type(PID_SUPER_MUTANT, 0);
      kill_critter_type(PID_HARRY, 0);
   end
   if (global_var(GVAR_NECROP_MUTANTS_KILLED) == 2) then begin
      // The GVAR-condition is a bit stupid, because it's never set to anything else but 0?
      // I guess it is supposed to remove the CoC robed guys 1 day after Harry and Friends are gone.
      //if (((GAME_TIME_IN_DAYS) - global_var(GVAR_NECRO_MUTANTS_TIME_LEFT)) > 1) then begin
      if (days_since_visited > 1) then begin
         kill_critter_type(PID_PEASANT_ROBE, 0);
      end
   end

   if not(is_loading_game) then begin
      call set_critter_state;
   end
   call Invasion;
   call map_lightning;

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   call map_lightning;

   if (Use_Manhole_Top) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Top);
   end
   if (Use_Manhole_Middle) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Middle);
   end
   if (Use_Manhole_Bottom) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Bottom);
   end
end

procedure map_lightning begin
   if (dude_elevation == 0) then
      Darkness;
   else
      Lighting;
end

procedure Invasion begin
variable Item;

   if (necropolis_invaded and not(is_loading_game) and (map_var(MVAR_INVADED) == 0)) then begin
      set_map_var(MVAR_INVADED, 1);

      kill_critter_type(PID_GARRET, 0);
      kill_critter_type(PID_MINDLESS_GHOUL, 1);
      kill_critter_type(PID_GHOUL, 1);
      kill_critter_type(PID_ZOMBIE_GUARD, 1);
      kill_critter_type(PID_GLOWING_ONE, 1);

      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 12910;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);
      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 13910;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);
      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 13516;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);

      // Any player party member was waiting in the map before?
      check_invasion_party_waiting;
   end
end

procedure set_critter_state begin
   // Is Garret (Set's guard) still around?
   if (global_var(GVAR_GARRET_STATUS) == 3) then begin
      kill_critter_type(PID_GARRET, 0);
   end

   // Also kill everyone in the vault
   if (global_var(GVAR_NECROP_WATER_CHIP_TAKEN) == 1) then begin
      if (((GAME_TIME_IN_DAYS) - global_var(GVAR_TIME_CHIP_GONE)) > 7) then begin
         if not(necropolis_wpump_fixed) then begin
            kill_critter_type(PID_GARRET, 0);
            kill_critter_type(PID_MINDLESS_GHOUL, 2);
            kill_critter_type(PID_GHOUL, 2);
            kill_critter_type(PID_ZOMBIE_GUARD, 2);
            kill_critter_type(PID_GLOWING_ONE, 2);
            kill_critter_type(PID_SUPER_MUTANT, 0);
            kill_critter_type(PID_HARRY, 0);
         end
      end
   end
end
