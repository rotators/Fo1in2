/*

   Mariposa - Vats

*/

#include "define.h"
#include "updatmap.h"
#include "command.h"
#include "area/mbase.h"
#include "maps/mbvats12.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure combat_p_proc;

export variable Lt_ptr;
export variable Team9_Count;
export variable ignoring_dude;
export variable VWeapLocker_ptr;
export variable valid_target;
export variable field_change;
export variable removal_ptr;

export variable Field1c_Ptr;
//export variable Field1d_Ptr;
export variable Field2c_Ptr;
export variable FieldH_Ptr;

variable move_party;

procedure start begin
end

procedure map_enter_p_proc begin
   if (map_first_run) then begin
      field_change := "off";
   end
   if (global_var(GVAR_VATS_ALERT)) then begin
      field_change := "on";
   end
   if (global_var(GVAR_FORCE_FIELDS_OFF)) then begin
      field_change := "off";
   end

   if (global_var(GVAR_LOAD_MAP_INDEX) == 0) then
      override_map_start(144, 64, 0, 3);
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 1) then
      override_map_start(120, 122, 0, 3);
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 2) then
      override_map_start(92, 122, 0, 2);
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 3) then
      override_map_start(120, 122, 1, 3);
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 4) then
      override_map_start(92, 122, 1, 2);
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 5) then
      override_map_start(71, 116, 1, 0);
   else
      override_map_start(144, 64, 0, 3);

   //if ((map_first_run) and global_var(GVAR_ENTERING_VATS_HOW)) then begin
   if (dude_elevation == 1) and (global_var(GVAR_ENTER_MBVATS_ELEV_1) == 0) and global_var(GVAR_ENTERING_VATS_HOW) then begin
      set_global_var(GVAR_ENTER_MBVATS_ELEV_1, 1);
      move_party := 1;
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   if (global_var(GVAR_VATS_ALERT) == 0) then begin
      if (dude_elevation == 0) then begin
         set_global_var(GVAR_MARK_BASE_4, 1);
      end
      else begin
         set_global_var(GVAR_MARK_BASE_5, 1);
         if ((map_var(MVAR_sneak_in_award) == 0) and not(global_var(GVAR_ENTERING_VATS_HOW))) then begin
            give_xp_msg(EXP_SNEAK_SUCCESS, message_str(SCRIPT_RADIO, 140));
            set_map_var(MVAR_sneak_in_award, 1);
         end
      end
   end
   else begin
      if (map_var(MVAR_Warned_Player2) == 0) then begin
         display_msg(gen_mstr(186));
         set_map_var(MVAR_Warned_Player2, 1);
      end
      set_global_var(GVAR_MARK_BASE_4, 0);
      set_global_var(GVAR_MARK_BASE_5, 0);
   end

   if (removal_ptr != 0) then begin
      destroy_object(removal_ptr);
      removal_ptr := 0;
   end

   // Move party members back into their cell if the player comes from elevation 1 (via elevator)
   if (dude_elevation == 0) and (global_var(GVAR_ENTER_MBVATS_ELEV_0) == 0) and global_var(GVAR_ENTERING_VATS_HOW) then begin
      set_global_var(GVAR_ENTER_MBVATS_ELEV_0, 1);
      move_party := 1;
   end
   if (move_party == 1) then begin
      move_party := 0;
      if (Ian_In_Party and not(Ian_ptr == 0)) then begin
         move_to(Ian_ptr, 21147, 0);
         obj_rotate(Ian_ptr, random(0, 5));
         set_ian_prisoner(true);
      end
      if (Dog_In_Party and not(Dog_ptr == 0)) then begin
         move_to(Dog_ptr, 21142, 0);
         obj_rotate(Dog_ptr, 1);
         set_dog_prisoner(true);
      end
      if (Tycho_In_Party and not(Tycho_ptr == 0)) then begin
         move_to(Tycho_ptr, 22945, 0);
         obj_rotate(Tycho_ptr, random(0, 5));
         set_tycho_prisoner(true);
      end
      if (Katja_In_Party and not(Katja_ptr == 0)) then begin
         move_to(Katja_ptr, 22749, 0);
         obj_rotate(Katja_ptr, random(0, 5));
         set_katja_prisoner(true);
      end
      if (Tandi_In_Party and not(Tandi_ptr == 0)) then begin
         move_to(Tandi_ptr, 21349, 0);
         obj_rotate(Tandi_ptr, random(0, 5));
         set_tandi_prisoner(true);
      end
      if (MrHandyC_In_Party and not(Handy_ptr == 0)) then begin
         move_to(Handy_ptr, 14347, 0);
         obj_rotate(Handy_ptr, 2);
         set_mrhandy_prisoner(true);
      end
      if (Eyebot_In_Party and not(Eyebot_ptr == 0)) then begin
         move_to(Eyebot_ptr, 14345, 0);
         obj_rotate(Eyebot_ptr, 0);
         set_eyebot_prisoner(true);
      end
      if (Vasquez_In_Party and not(Vasquez_ptr == 0)) then begin
         move_to(Vasquez_ptr, 22946, 0);
         obj_rotate(Vasquez_ptr, 0);
         set_vasquez_prisoner(true);
      end
   end
end

procedure combat_p_proc begin
   // TODO: Figure out the purpose of this procedure here. Doesn't really seem to make sense.
   if get_robo_attack_all_pests then begin
      script_overrides;
      set_robo_attack_unauth_pests;
      set_global_var(GVAR_ENTERING_VATS_HOW, 3);
      load_map(MAP_MBVATS12, 5);
   end
end
