/*

   Boneyard - Rippers / old Cinema

*/

#include "define.h"
#include "command.h"
#include "updatmap.h"
#include "maps/laripper.h"
#include "boneyard.h"
#include "sfall/sfall.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure map_exit_p_proc;

procedure map_lightning;
procedure PlaceCritter;

procedure start begin
end

procedure map_enter_p_proc begin
   variable rnd;
   set_global_var(GVAR_MARK_LA_5, 1);

   call map_lightning;

   if not(is_loading_game) then begin
      set_rest_mode(0);
      if ((map_var(MVAR_IsMommyDead) == 0) and (((game_time / 10) - global_var(GVAR_WATER_CHIP_13)) > (60 * 60))) then begin
         set_global_var(GVAR_WATER_CHIP_13, game_time / 10);
         if (map_var(MVAR_RoamingCount) == 0) then begin
            rnd := random(2, 3);
            set_map_var(MVAR_RoamingCount, rnd);
            set_map_var(MVAR_NewCount, rnd);
            call PlaceCritter;
         end
         else if (map_var(MVAR_RoamingCount) == 1) then begin
            rnd := random(1, 2);
            set_map_var(MVAR_RoamingCount, rnd + 1);
            set_map_var(MVAR_NewCount, rnd);
            call PlaceCritter;
         end
         else if (map_var(MVAR_RoamingCount) == 2) then begin
            if (random(0, 1)) then begin
               set_map_var(MVAR_RoamingCount, 3);
               set_map_var(MVAR_NewCount, 1);
               call PlaceCritter;
            end
         end
      end
      else
         set_rest_mode(RESTMODE_STRICT); // Can't rest here as long as MOMCLAW is still alive.
   end

   override_map_start(96, 121, 0, 0);

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   call map_lightning;
end

procedure map_exit_p_proc begin
   if ((map_var(MVAR_IsMommyDead) == 1) and (map_var(MVAR_RoamingCount) == 0) and (map_var(MVAR_EggCount) == 0)) then begin
      if (global_var(GVAR_WATER_CHIP_8) != 2) then begin
         set_all_deathclaws_killed;
         inc_boneyard_rep(REP_BONUS_DEATHCLAWS_DEAD);
      end
   end
end

procedure map_lightning begin
   if (dude_elevation == 1) then
      Darkness;
   else
      Lighting;
end

procedure PlaceCritter begin
   variable
      critter := 0,
      hex := 0,
      rnd := 0,
      count := 0;

   count := map_var(MVAR_NewCount);
   while(count > 0) do begin
      rnd := random(1, 7);

      if (rnd == 1) then
         hex := 16890;
      else if (rnd == 2) then
         hex := 17269;
      else if (rnd == 3) then
         hex := 23492;
      else if (rnd == 4) then
         hex := 15885;
      else if (rnd == 5) then
         hex := 12287;
      else if (rnd == 6) then
         hex := 17511;
      else if (rnd == 7) then
         hex := 15475;

      critter := create_object_sid(PID_DEATHCLAW, 0, 0, SCRIPT_ROAMCLAW);
      critter_attempt_placement(critter, hex, 0);
      count := count - 1;
   end
end
