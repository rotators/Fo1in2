/*

   Cathedral - Master's Lair #2

*/

#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\maps\mstrlr34.h"

#include "..\headers\updatmap.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure map_exit_p_proc;

export variable Skill_Used;
export variable Key_Used;
export variable Master_Has_Armed;
export variable Bomb_Armed;
export variable Master_ptr;
export variable signal_mutants;
export variable Master_Has_Activated;

variable mutan_ptr;
variable stuff;

#define spawn_mutant(pid, hex, wpn, team)    mutan_ptr := create_object_sid(pid, 0, 0, SCRIPT_GENMUTAN); \
                                             critter_attempt_placement(mutan_ptr, hex, 0);               \
                                             set_team(mutan_ptr, team);                                  \
                                             stuff := create_object(wpn, 0, 0);                          \
                                             add_obj_to_inven(mutan_ptr, stuff);                         \
                                             stuff := create_object(PID_MICRO_FUSION_CELL, 0, 0);        \
                                             add_obj_to_inven(mutan_ptr, stuff);                         \
                                             stuff := create_object(PID_STIMPAK, 0, 0);                  \
                                             add_obj_to_inven(mutan_ptr, stuff)

procedure start begin
end

procedure map_enter_p_proc begin
   Darkness;

   if (dude_elevation == 0) then begin
      if (day and not(is_loading_game)) then begin
         if (map_var(MVAR_SPAWN_MUTANTS) == 0) then begin
            set_map_var(MVAR_SPAWN_MUTANTS, 1);

            spawn_mutant(PID_SUPER_NIGHTKIN, 20532, PID_PLASMA_RIFLE, TEAM_NECROPOLIS_MUTANT);
            spawn_mutant(PID_SUPER_NIGHTKIN, 19337, PID_GATLING_LASER, TEAM_NECROPOLIS_MUTANT);
            spawn_mutant(PID_SUPER_NIGHTKIN, 18740, PID_GATLING_LASER, TEAM_NECROPOLIS_MUTANT);
            spawn_mutant(PID_SUPER_NIGHTKIN, 21339, PID_GATLING_LASER, TEAM_NECROPOLIS_MUTANT);
         end
      end
   end

   if (global_var(GVAR_LOAD_MAP_INDEX) == 2) then begin
      override_map_start(112, 84, 0, 3);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 3) then begin
      override_map_start(140, 78, 1, 4);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 5) then begin
      override_map_start(140, 78, 0, 4);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 12) then begin
      override_map_start(55, 68, 0, 5);
      if (not(is_loading_game) and (global_var(GVAR_MORPHEUS_DELIVERS_PLAYER) == 100)) then begin
         set_global_var(GVAR_MORPHEUS_DELIVERS_PLAYER, 1);
         create_object_sid(PID_MORPHEUS, 13854, 0, SCRIPT_MORPH);

         spawn_mutant(PID_MASTER_NIGHTKIN, 15468, PID_PLASMA_RIFLE, TEAM_MASTER_LAIR);
         spawn_mutant(PID_MASTER_NIGHTKIN, 15450, PID_PLASMA_RIFLE, TEAM_MASTER_LAIR);
      end
   end
   else begin
      override_map_start(112, 84, 0, 3);
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   Darkness;
end

procedure map_exit_p_proc begin
   if (map_var(MVAR_SPAWN_MUTANTS) == 1) then begin
      set_map_var(MVAR_SPAWN_MUTANTS, 0);

      // Moved to GENMUTAN.ssl because we don't want to destroy *all* of them.
      //kill_critter_type(PID_SUPER_NIGHTKIN, 0);
   end
end
