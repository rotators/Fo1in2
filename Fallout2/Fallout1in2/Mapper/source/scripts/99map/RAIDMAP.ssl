/*

   Raiders / Khans

*/

#include "define.h"
#include "command.h"
#include "updatmap.h"

#include "area/shadysands.h"
#include "maps/raiders.h"
#include "sfall/main.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure map_exit_p_proc;

export variable women_killed;
export variable signal_women;
export variable killing_women;
export variable Garls_Inven_Ptr;
export variable Cell_Door_Ptr;
export variable Jack_Ptr;

variable ExpReward;

procedure start begin
end

procedure map_enter_p_proc begin
   Lighting;

   override_map_start(96, 133, 0, 5);

   if (map_first_run) then begin
      display_msg(message_str(SCRIPT_SHADYWST, 103));
      if (dude_is_male) then begin
         if ((dude_luck > 8) and random(0, 1)) then begin
            set_global_var(GVAR_MISTAKEN_ID, 1); // Raiders think you are Garl's father
         end
      end
   end

   // Garl is dead and the Raiders left
   if not(is_loading_game) then begin
      if (days_since_visited > 0) and ((not(is_garl_alive) and (get_raiders_alive <= 8)) or (get_raiders_alive <= 4)) then begin
         variable critter;
         foreach critter in list_as_array(LIST_CRITTERS) begin
            if (elevation(critter) == 0) and (critter != dude_obj) and (party_member_obj(critter) == false) and (critter_state(critter) != CRITTER_IS_DEAD) then begin
               // When removing raiders, the kill counter increases.
               // We revert it here as workaround, except for when the slaves and prisoners get removed.
               if ((obj_pid(critter) != PID_TANDI) and (obj_pid(critter) != PID_MERCHANT) and (obj_pid(critter) != PID_PEASANT_FEMALE_CUTE)) then begin
                  set_global_var(GVAR_TOTAL_RAIDERS, global_var(GVAR_TOTAL_RAIDERS) + 1);
               end
               destroy_object(critter);
            end
         end
      end
      debug("get_raiders_alive: " + get_raiders_alive);
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   Lighting;
end

procedure map_exit_p_proc begin
   if ((global_var(GVAR_TANDI_HIRELING_STATUS) == 5) and (map_var(MVAR_GAVE_EXP) != 1)) then begin
      set_map_var(MVAR_GAVE_EXP, 1);
      set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);

      set_tandi_quest_completed;

      ExpReward := (16 - get_raiders_alive) * 50;
      if (ExpReward < 500) then
         ExpReward := 500 - ExpReward;
      else
         ExpReward := 0;

      inc_general_rep(1);
      if (ExpReward > 0) then give_xp_msg(ExpReward, message_str(SCRIPT_RAIDMAP, 101));
   end
end
