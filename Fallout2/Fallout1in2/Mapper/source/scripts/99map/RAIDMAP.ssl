#include "define.h"
#include "updatmap.h"
#include "command.h"
#include "area/shadysands.h"
#include "maps/raiders.h"

procedure start;

export variable women_killed;
export variable signal_women;
export variable killing_women;
export variable Garls_Inven_Ptr;
export variable Cell_Door_Ptr;

variable ExpReward;
variable party_elevation;
variable dude_start_hex;

#define REMOVE_RAIDERS \
   kill_critter_type(PID_MAN_JEANS, 0);               \
   kill_critter_type(PID_LEATHER_JACKET_WOMAN, 0);    \
   kill_critter_type(PID_LEATHER_JACKET_MAN, 0);      \
   kill_critter_type(PID_PEASANT_FEMALE_CUTE, 0);     \
   kill_critter_type(PID_CITIZEN_PUNK, 0);            \
   kill_critter_type(PID_MALE_GUARD_METAL_BLACK, 0);  \
   kill_critter_type(PID_PEASANT_GREEN, 0);           \
   kill_critter_type(PID_LEATHER_ARMOR_MALE, 0);      \
   kill_critter_type(PID_TOWNSWOMAN_METAL, 0);        \
   kill_critter_type(PID_WOMAN_YELLOW, 0);            \
   kill_critter_type(PID_TOWNSMAN_LEATHER, 0)

procedure start begin
end

procedure map_enter_p_proc begin
   Lighting;

   set_global_var(GVAR_WORLDMAPLIST_RAIDERS, 2);
   set_global_var(GVAR_MARK_RAIDERS_1, 1);

   override_map_start(96, 133, 0, 5);

   if (map_first_run) then begin
      display_msg(message_str(SCRIPT_SHADYWST, 103));
      if (dude_is_male) then begin
         if ((dude_luck > 8) and random(0, 1)) then begin
            set_global_var(GVAR_MISTAKEN_ID, 1);//Raiders think you are Garl's father
         end
      end
   end

   if not(is_loading_game) then begin
      if (((global_var(GVAR_GARL_DEAD) == 1) and (get_raiders_alive <= 12)) or (get_raiders_alive <= 6)) then begin//GARL_DEAD, RAIDERS_LEFT
         REMOVE_RAIDERS;
      end
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   Lighting;
end

procedure map_exit_p_proc begin
   if ((global_var(GVAR_TANDI_HIRELING_STATUS) == 5) and (map_var(MVAR_GAVE_EXP) != 1)) then begin
      set_map_var(MVAR_GAVE_EXP, 1);
      set_global_var(GVAR_TANDI_HIRELING_STATUS, 5);

      set_tandi_quest_completed;

      ExpReward := (16 - get_raiders_alive) * 50;
      if (ExpReward < 500) then
         ExpReward := 500 - ExpReward;
      else
         ExpReward := 0;

      inc_general_rep(1);
      if (ExpReward > 0) then give_xp_msg(ExpReward, message_str(SCRIPT_RAIDMAP, 101));
   end

   if (((global_var(GVAR_GARL_DEAD) == 1) and (get_raiders_alive <= 8)) or (get_raiders_alive <= 4)) then begin//GARL_DEAD, RAIDERS_LEFT
      REMOVE_RAIDERS;
   end
end
