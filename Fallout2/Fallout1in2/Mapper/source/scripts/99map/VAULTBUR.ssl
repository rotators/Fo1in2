#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\maps\vaultbur.h"

#include "..\headers\updatmap.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

variable Critter;
variable Critter_Tile;
variable Critter_Type;
variable rnd_tile;
variable final_tile;

variable cur_rats;
variable new_rats;

procedure start begin
end

procedure map_enter_p_proc begin
   Darkness;

   // Spawn rats on elevation 2:
   if not(is_loading_game) and (not(map_first_run)) then begin
      if ((map_var(MVAR_RATHOLE_STATE) != STATE_CLOSED) and (game_time > map_var(MVAR_RespawnTimer))) then begin
         set_map_var(MVAR_RespawnTimer, (game_time + RESPAWN_TIME));
         new_rats := random(MIN_RATS, MAX_RATS);
         cur_rats := map_var(MVAR_RoamingCount);

         if (cur_rats < new_rats) then begin
            //debug("!!!! we spawn rats now !!!! So many: " + (new_rats - cur_rats));
            while(cur_rats < new_rats) do begin
               cur_rats := cur_rats + 1;

               rnd_tile := random(1, 5);
               if (rnd_tile == 1) then
                  Critter_Tile := 18899;
               else if (rnd_tile == 2) then
                  Critter_Tile := 21303;
               else if (rnd_tile == 3) then
                  Critter_Tile := 22705;
               else if (rnd_tile == 4) then
                  Critter_Tile := 21117;
               else if (rnd_tile == 5) then
                  Critter_Tile := 21133;

               if (map_var(MVAR_RATHOLE_STATE) == STATE_LARGE) and (random(0, 2) == 1) then
                  Critter_Type := PID_MEAN_PIG_RAT;
               else
                  Critter_Type := PID_RAT_CAVE;

               // Bonus pig rat:
               if (random(0, 5) == 1) then
                  Critter_Type := PID_MEAN_PIG_RAT;

               Critter := create_object_sid(Critter_Type, 0, 2, SCRIPT_WANRATS);
               closest_clear_tile(Critter, Critter_Tile, final_tile)
               critter_attempt_placement(Critter, final_tile, 2);
               obj_rotate(Critter, random(0, 5));
            end
         end

      end
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   Darkness;
   if (dude_elevation == 1) then begin
      if (global_var(GVAR_UNDRBED_STARTED_TIMER) < 3) then begin
         add_timer_event(dude_obj, 0, 13);
      end
   end
   //debug("MVAR_RoamingCount: " + map_var(MVAR_RoamingCount));
end
