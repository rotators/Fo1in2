export variable Set_Pointer;
export variable Garret_ptr;
export variable Fridge_ptr;
export variable Shotgun_ptr;
export variable Shells1_ptr;
export variable Shells2_ptr;
export variable Flare1_ptr;
export variable Flare2_ptr;
export variable Flare3_ptr;
export variable Flare4_ptr;
export variable Cola1_ptr;
export variable Cola2_ptr;
export variable Cola3_ptr;
export variable Cola4_ptr;
export variable Manhole_Pointer_Top;
export variable Manhole_Pointer_Middle;
export variable Manhole_Pointer_Bottom;
export variable Use_Manhole_Top;
export variable Use_Manhole_Bottom;
export variable Use_Manhole_Middle;
export variable set_gone;
export variable set_dead;

variable fugee_ptr;
variable fugee_hex;

#include "..\headers\define.h"
#include "..\headers\updatmap.h"
#include "..\headers\command.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure map_lightning;
procedure Invasion;

variable party_elevation;
variable dude_start_hex;

export variable GhDorGrd_Ptr;

procedure start begin
end

procedure map_enter_p_proc begin
   if (global_var(GVAR_LOAD_MAP_INDEX) == 2) then begin
      override_map_start(153, 100, 1, 0);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 12) then begin
      override_map_start(103, 58, 0, 2);
   end
   else if (global_var(GVAR_LOAD_MAP_INDEX) == 13) then begin
      override_map_start(141, 134, 0, 5);
   end
   //else begin
   // override_map_start(153, 100, 1, 0);
   //end

   set_global_var(GVAR_MARK_NECROP_2, 1);

   call map_lightning;
   call Invasion;

   if ((global_var(GVAR_NECROP_WATER_CHIP_TAKEN) == 1) and not(is_loading_game)) then begin
      if (((GAME_TIME_IN_DAYS) - global_var(GVAR_TIME_CHIP_GONE)) > 7) then begin
         if not(necropolis_wpump_fixed) then begin
            kill_critter_type(PID_SETH, 2);
            kill_critter_type(PID_GARRET, 2);
            kill_critter_type(PID_MINDLESS_GHOUL, 2);
            kill_critter_type(PID_GHOUL, 2);
            kill_critter_type(PID_ZOMBIE_GUARD, 2);
            kill_critter_type(PID_GLOWING_ONE, 2);
         end
      end
   end
   if (global_var(GVAR_GARRET_STATUS) == 3) then begin
      kill_critter_type(PID_GARRET, 0);
   end

   set_global_var(GVAR_LOAD_MAP_INDEX, 0);
end

procedure map_update_p_proc begin
   call map_lightning;

   if (Use_Manhole_Top) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Top);
   end
   if (Use_Manhole_Middle) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Middle);
   end
   if (Use_Manhole_Bottom) then begin
      animate_stand_reverse_obj(Manhole_Pointer_Bottom);
   end
end

procedure map_lightning begin
   if (dude_elevation == 0) then
      Darkness;
   else
      Lighting;
end

procedure Invasion begin
   if (necropolis_invaded and not(is_loading_game) and (map_var(10) == 0)) then begin
      set_map_var(10, 1);
      kill_critter_type(PID_SETH, 1);
      kill_critter_type(PID_GARRET, 1);
      kill_critter_type(PID_MINDLESS_GHOUL, 1);
      kill_critter_type(PID_GHOUL, 1);
      kill_critter_type(PID_ZOMBIE_GUARD, 1);
      kill_critter_type(PID_GLOWING_ONE, 1);
      fugee_ptr := create_object_sid(PID_GUARD_5, 0, 0, SCRIPT_GHREFUGE);
      fugee_hex := 28689;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);
      set_team(fugee_ptr, TEAM_PLAYER);
      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 16886;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);
      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 16084;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);
      fugee_ptr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(fugee_ptr, AI_SUPER_MUTANT_AGGRESSIVE);
      fugee_hex := 20111;
      critter_attempt_placement(fugee_ptr, fugee_hex, 1);

      // Any player party member was waiting in the map before?
      check_invasion_party_waiting
   end
   else begin
      if (global_var(GVAR_WATER_CHIP_6) == 1) then begin
         kill_critter_type(PID_SETH, 1);
         kill_critter_type(PID_GARRET, 1);
      end
   end
end
