/*

   Junktown - Gretch

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_GRETCH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure damage_p_proc;

procedure Gretch00;
procedure Gretch00a;
procedure Gretch00b;
procedure Gretch01;
procedure Gretch01a;
procedure Gretch02;
procedure Gretch02a;
procedure Gretch03;
procedure Gretch03a;
procedure Gretch04;
procedure Gretch05;
procedure Gretch05a;
procedure Gretch06;
procedure Gretch07;
procedure Gretch08;
procedure Gretch09;
procedure Gretch10;
procedure GretchEnd;
procedure GretchCombat;

#define LVAR_Herebefore          (0)

variable tmp_hostile;
variable initial := 0;
variable warned_about_messing;

import variable Gretch_call;
import variable messing_with_Morbid_stuff;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_JUNKTOWN_MORBID);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (messing_with_Morbid_stuff) then begin
         if ((tile_distance_objs(self_obj, dude_obj) < 12) and (self_elevation == dude_elevation)) then begin
            if (warned_about_messing) then begin
               tmp_hostile := 1;
            end
            else begin
               warned_about_messing := 1;
               float_msg(self_obj, mstr(141), FLOAT_MSG_RED);
            end
         end
         messing_with_Morbid_stuff := 0;
      end
      if (Gretch_call) then begin
         Gretch_call := 0;
         tmp_hostile := 1;
      end
   end
   if (global_var(GVAR_IS_MORBID_TEAM_HOSTILE) == 1) then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(LVAR_Herebefore) == 1) then
      start_dialog_at_node(Gretch01);
   else
      start_dialog_at_node(Gretch00);
end

procedure timed_event_p_proc begin
end

procedure Gretch00 begin
   set_local_var(LVAR_Herebefore, 1);

   Reply(102);
   NLowOption(103, Gretch07);
   NOption(104, Gretch00a, 4);
   BOption(105, Gretch00b, 4);
   NOption(106, Gretch02, 4);
   NOption(107, GretchEnd, 4);
end

procedure Gretch00a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch04;
   end
   else begin
      call Gretch02;
   end
end

procedure Gretch00b begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch02;
   end
   else begin
      call Gretch08;
   end
end

procedure Gretch01 begin
   Reply(108);
   NOption(109, Gretch02, 4);
   NOption(110, Gretch01a, 4);
   NOption(111, GretchEnd, 4);
   NLowOption(103, Gretch07);
end

procedure Gretch01a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch05;
   end
   else begin
      call Gretch02;
   end
end

procedure Gretch02 begin
   Reply(112);
   NOption(113, Gretch04, 4);
   NOption(114, Gretch03, 4);
   NOption(115, Gretch02a, 4);
end

procedure Gretch02a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch03;
   end
   else begin
      call Gretch07;
   end
end

procedure Gretch03 begin
   Reply(116);
   NOption(117, Gretch04, 4);
   NOption(118, Gretch03a, 4);
   NOption(119, GretchEnd, 4);
end

procedure Gretch03a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch05;
   end
   else begin
      call Gretch04;
   end
end

procedure Gretch04 begin
   Reply(120);
   NOption(121, Gretch06, 4);
   NOption(122, Gretch05, 4);
   NOption(123, GretchEnd, 4);
end

procedure Gretch05 begin
   set_global_var(GVAR_GRETCH_PRIMECUTS, 1);
   Reply(124);
   NOption(125, Gretch05a, 4);
   BOption(126, GretchCombat, 4);
   NOption(127, GretchEnd, 4);
end

procedure Gretch05a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Gretch09;
   end
   else begin
      call Gretch06;
   end
end

procedure Gretch06 begin
   set_global_var(GVAR_GRETCH_PRIMECUTS, 1);
   Reply(128);
   NOption(129, GretchEnd, 4);
   NOption(130, Gretch09, 4);
   NOption(131, Gretch07, 4);
end

procedure Gretch07 begin
   Reply(132);
   NOption(133, GretchEnd, 4);
   NOption(134, Gretch04, 4);
   BOption(135, Gretch08, 4);
   NLowOption(136, Gretch10);
end

procedure Gretch08 begin
   BMessage(137);
end

procedure Gretch09 begin
   Reply(138);
   NOption(139, GretchEnd, 4);
   BOption(126, GretchCombat, 4);
end

procedure Gretch10 begin
   NMessage(140);
end

procedure GretchEnd begin
end

procedure GretchCombat begin
   tmp_hostile := 1;
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_IS_MORBID_TEAM_HOSTILE, 1);
   end
end
