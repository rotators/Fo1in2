/*

	Junktown - Gretch

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GRETCH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure Gretch00;
procedure Gretch00a;
procedure Gretch00b;
procedure Gretch01;
procedure Gretch01a;
procedure Gretch02;
procedure Gretch02a;
procedure Gretch03;
procedure Gretch03a;
procedure Gretch04;
procedure Gretch05;
procedure Gretch05a;
procedure Gretch06;
procedure Gretch07;
procedure Gretch08;
procedure Gretch09;
procedure Gretch10;
procedure GretchEnd;
procedure GretchCombat;

variable tmp_hostile;
variable initial :=  0;
variable warned_about_messing;

import variable Gretch_call;
import variable messing_with_Morbid_stuff;

procedure damage_p_proc;

procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_JUNKTOWN_MORBID );
		initial :=  1;
	end
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (messing_with_Morbid_stuff) then begin
			if ((tile_distance_objs(self_obj, dude_obj) < 12) and (self_elevation == dude_elevation)) then begin
				if (warned_about_messing) then begin
					tmp_hostile := 1;
				end
				else begin
					warned_about_messing := 1;
					float_msg( self_obj, message_str(NAME, 141), FLOAT_MSG_RED );
				end
			end
			messing_with_Morbid_stuff := 0;
		end
		if (Gretch_call) then begin
			Gretch_call := 0;
			tmp_hostile := 1;
		end
	end
	if (global_var( GVAR_IS_MORBID_TEAM_HOSTILE ) == 1) then begin
		if (self_can_see_dude) then begin
			attack(dude_obj);
		end
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(0) == 0) then begin
		display_msg(message_str(NAME, 100));
	end
	else begin
		display_msg(message_str(NAME, 101));
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	start_gdialog(103, self_obj, 4, -1, -1);
	gsay_start;
	if (local_var(0) == 1) then begin
		call Gretch01;
	end
	else begin
		call Gretch00;
	end
	gsay_end;
	end_dialogue;
end

procedure timed_event_p_proc
begin
end

procedure Gretch00
begin
	gsay_reply( NAME, 102 );
	giq_option( -3, NAME, 103, Gretch07, NEUTRAL_REACTION );
	giq_option( 4, NAME, 104, Gretch00a, NEUTRAL_REACTION );
	giq_option( 4, NAME, 105, Gretch00b, BAD_REACTION );
	giq_option( 4, NAME, 106, Gretch02, NEUTRAL_REACTION );
	giq_option( 4, NAME, 107, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch00a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch04;
	end
	else begin
		call Gretch02;
	end
end

procedure Gretch00b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch02;
	end
	else begin
		call Gretch08;
	end
end

procedure Gretch01
begin
	gsay_reply( NAME, 108 );
	giq_option( 4, NAME, 109, Gretch02, NEUTRAL_REACTION );
	giq_option( 4, NAME, 110, Gretch01a, NEUTRAL_REACTION );
	giq_option( 4, NAME, 111, GretchEnd, NEUTRAL_REACTION );
	giq_option( -3, NAME, 103, Gretch07, NEUTRAL_REACTION );
end

procedure Gretch01a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch05;
	end
	else begin
		call Gretch02;
	end
end

procedure Gretch02
begin
	gsay_reply( NAME, 112 );
	giq_option( 4, NAME, 113, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, NAME, 114, Gretch03, NEUTRAL_REACTION );
	giq_option( 4, NAME, 115, Gretch02a, NEUTRAL_REACTION );
end

procedure Gretch02a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch03;
	end
	else begin
		call Gretch07;
	end
end

procedure Gretch03
begin
	gsay_reply( NAME, 116 );
	giq_option( 4, NAME, 117, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, NAME, 118, Gretch03a, NEUTRAL_REACTION );
	giq_option( 4, NAME, 119, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch03a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch05;
	end
	else begin
		call Gretch04;
	end
end

procedure Gretch04
begin
	gsay_reply( NAME, 120 );
	giq_option( 4, NAME, 121, Gretch06, NEUTRAL_REACTION );
	giq_option( 4, NAME, 122, Gretch05, NEUTRAL_REACTION );
	giq_option( 4, NAME, 123, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch05
begin
	set_global_var( GVAR_GRETCH_PRIMECUTS, 1 );
	gsay_reply( NAME, 124 );
	giq_option( 4, NAME, 125, Gretch05a, NEUTRAL_REACTION );
	giq_option( 4, NAME, 126, GretchCombat, BAD_REACTION );
	giq_option( 4, NAME, 127, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch05a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch09;
	end
	else begin
		call Gretch06;
	end
end

procedure Gretch06
begin
	set_global_var( GVAR_GRETCH_PRIMECUTS, 1 );
	gsay_reply( NAME, 128 );
	giq_option( 4, NAME, 129, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 130, Gretch09, NEUTRAL_REACTION );
	giq_option( 4, NAME, 131, Gretch07, NEUTRAL_REACTION );
end

procedure Gretch07
begin
	gsay_reply( NAME, 132 );
	giq_option( 4, NAME, 133, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 134, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, NAME, 135, Gretch08, BAD_REACTION );
	giq_option( -3, NAME, 136, Gretch10, NEUTRAL_REACTION );
end

procedure Gretch08
begin
	gsay_message( NAME, 137, BAD_REACTION );
end

procedure Gretch09
begin
	gsay_reply( NAME, 138 );
	giq_option( 4, NAME, 139, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 126, GretchCombat, BAD_REACTION );
end

procedure Gretch10
begin
	gsay_message( NAME, 140, NEUTRAL_REACTION );
end

procedure GretchEnd
begin
end

procedure GretchCombat
begin
	tmp_hostile := 1;
end



procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_IS_MORBID_TEAM_HOSTILE, 1 );
	end
end
