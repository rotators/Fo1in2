/*

	Generic gambler

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GENGAMBL
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure damage_p_proc;
procedure pickup_p_proc;

procedure gambler1;
procedure gambler2;
procedure gambler3;
procedure gambler4;
procedure gambler5;
procedure items_held;

variable tmp_hostile;
variable weapon_check;

procedure start begin
end

procedure critter_p_proc begin
	if (self_can_see_dude) then begin
		if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
			if dude_is_armed then begin
				if (not(weapon_check)) then begin
					dialogue_system_enter;
					add_timer_event(self_obj, game_ticks(600), 1);
				end
			end
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end

	// TODO: Should generic gamblers really attack dude if dude killed Killian??? Makes no sense!
	/*
	if (global_var(GVAR_ENEMY_JUNKTOWN) == 1) then begin
		if (self_can_see_dude) then begin
			attack(dude_obj);
		end
	end*/

	if (CUR_MAP_JUNKCSNO) then begin //  JUNKTOWN - CASINO, GIZMO, SKUM PITT, BOXING, DOGMEAT
		if ((global_var(GVAR_CAPTURE_GIZMO) == 2) or not(is_gizmo_alive)) then begin
			destroy_object(self_obj);
		end
	end
	if (CUR_MAP_JUNKCSNO) and (global_var(GVAR_DUDE_ENEMY_JUNK_CITIZEN) == 1) then begin //  GVAR_ENEMY_JUNK_CITIZEN,  MAP == JUNKTOWN - CASINO, GIZMO, SKUM PITT, BOXING, DOGMEAT
		if (self_can_see_dude) then begin
			call flee_dude;
		end
	end
end

procedure destroy_p_proc begin
	if (source_obj == dude_obj) then begin
		if (CUR_MAP_JUNKENT) or (CUR_MAP_JUNKCSNO) or (CUR_MAP_JUNKKILL) then begin //  JUNKTOWN - ANY MAPS
			set_global_var(GVAR_DUDE_ENEMY_JUNK_CITIZEN, 1);// GVAR_ENEMY_JUNK_CITIZEN
			set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
		end
	end
	rm_timer_event(self_obj);
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure map_enter_p_proc begin
   if map_first_run then
      self_caps_adjust(random(0, 200));

	if (local_var(0) == 0) then begin
		set_local_var(0, self_tile);
	end
	if (CUR_MAP_JUNKCSNO) then begin
		set_self_team(TEAM_JUNKTOWNER);
		if ((global_var(GVAR_CAPTURE_GIZMO) == 2) or not(is_gizmo_alive)) then begin
			destroy_object(self_obj);
		end
	end
end

procedure talk_p_proc begin
	script_overrides;
	if dude_is_armed then begin
		call gambler1;
	end
	else begin
		if has_rep_childkiller then begin
			call gambler3;
		end
		else if (global_var(GVAR_KILL_KILLIAN) == 2) then begin
			call gambler4;
		end
		else if (check_general_rep < -10) then begin
			call gambler2;
		end
		else begin
			call gambler5;
		end
	end
end

procedure timed_event_p_proc
begin
	weapon_check := 0;
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
	end
end

procedure gambler1
begin
	variable LVar0 := 0;
	weapon_check := 1;
	LVar0 := random(1, 3);
	if (LVar0 == 1) then begin
		float_msg(self_obj, mstr(101), FLOAT_MSG_NORMAL);
	end
	else begin
		if (LVar0 == 2) then begin
			float_msg(self_obj, mstr(102), FLOAT_MSG_NORMAL);
		end
		else begin
			if (LVar0 == 3) then begin
				if (dude_is_male) then begin
					float_msg(self_obj, mstr(103), FLOAT_MSG_NORMAL);
				end
				else begin
					float_msg(self_obj, mstr(104), FLOAT_MSG_NORMAL);
				end
			end
		end
	end
end

procedure gambler2
begin
	float_msg(self_obj, mstr(random(105, 107)), FLOAT_MSG_NORMAL);
end

procedure gambler3
begin
	float_msg(self_obj, mstr(random(108, 110)), FLOAT_MSG_NORMAL);
end

procedure gambler4
begin
	float_msg(self_obj, mstr(random(111, 113)), FLOAT_MSG_NORMAL);
end

procedure gambler5
begin
	variable LVar0 := 0;
	LVar0 := random(1, 6);
	if (LVar0 == 1) then begin
		if (day) then begin
			float_msg(self_obj, mstr(114), FLOAT_MSG_NORMAL);
		end
		else begin
			float_msg(self_obj, mstr(115), FLOAT_MSG_NORMAL);
		end
	end
	else begin
		float_msg(self_obj, mstr(114 + LVar0), FLOAT_MSG_NORMAL);
	end
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end
