/*

   Junktown - Sherry, Skullz gang member

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_SHERRY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Sherry01;
procedure Sherry02;
procedure Sherry03;
procedure Sherry04;
procedure Sherry05;
procedure Sherry06;
procedure Sherry07;
procedure Sherry08;
procedure Sherry09;
procedure Sherry10;
procedure Sherry11;
procedure Sherry12;
procedure Sherry13;
procedure Sherry14;
procedure Sherry15;
procedure Sherry16;
procedure Sherry17;
procedure Sherry18;
procedure Sherry18a;
procedure Sherry19;
procedure Sherry20;
procedure Sherry21;
procedure Sherry22;
procedure Sherry23;
procedure Sherry24;
procedure Sherry25;
procedure Sherry26;
procedure Sherry27;
procedure Sherry27a;
procedure Sherry28;
procedure Sherry29;
procedure SherryCombat;
procedure SherryCook;
procedure SherryTurns;
procedure SherryEnd;

variable tmp_hostile;
variable initial := 0;
variable feed_dude;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

#define LVAR_Herebefore                   (4)
#define LVAR_Sleeping                     (5)
#define LVAR_LeaveTimer                   (6)
#define LVAR_Ask_GangInfo                 (7)
#define LVAR_Ask_WhatDo                   (8)
#define LVAR_Ask_WhyStay                  (9)
#define LVAR_Ask_Snitch                   (10)

procedure start begin
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (global_var(GVAR_SHERRY_TURNS) == 1) then begin
         if ((game_time - local_var(LVAR_LeaveTimer)) > (ONE_GAME_DAY)) then begin
            call SherryTurns;
         end
      end
      if ((game_time_hour >= 700) and (game_time_hour <= 1930)) then begin
         if (self_tile != home_tile) then begin
            self_walk_to_tile(home_tile);
         end
         else if (self_tile == home_tile) and (self_cur_rot != 2) then begin
            self_rotate(2);
         end
      end
      sleeping
   end

   if ((global_var(GVAR_SHERRY_TURNS) != 2) and REP_ENEMY_JUNKTOWN_SKULZ) then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
   else if ((global_var(GVAR_SHERRY_TURNS) == 2) and REP_ENEMY_JUNKTOWN_CITIZENS) then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      if (global_var(GVAR_SHERRY_TURNS) != 2) then begin
         set_global_var(GVAR_DUDE_ENEMY_SKULZ, 1);
      end
      else begin
         set_global_var(GVAR_DUDE_ENEMY_JUNK_CITIZEN, 1);
         set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
         dec_general_rep(5);
      end
      inc_good_critter
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore)) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

procedure map_enter_p_proc begin
   sleep_time := 2000;
   wake_time := 630;
   if ((global_var(GVAR_BUST_SKULZ) == 2) and (global_var(GVAR_SHERRY_TURNS) != 2)) then begin
      if ((global_var(GVAR_SHERRY_TURNS) == 1) and ((game_time - local_var(LVAR_LeaveTimer)) > (ONE_GAME_DAY))) then begin
         call SherryTurns;
      end
      else begin
         destroy_object(self_obj);
      end
   end
   if (global_var(GVAR_SHERRY_TURNS) != 2) then begin
      set_self_team(TEAM_JUNKTOWN_SKULZ);
      home_tile := 15513;
      sleep_tile := 13494;
   end
   else begin
      set_self_team(TEAM_JUNKTOWNER);
      home_tile := 18125;
      sleep_tile := 17924;
   end
end

procedure pickup_p_proc begin
   float_msg(self_obj, mstr(102), FLOAT_MSG_NORMAL);
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_JUNKKILL);

   dude_look_at_self;

   if (local_var(LVAR_Sleeping) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_GENVAULT, 166), FLOAT_MSG_NORMAL);
   end
   else begin
      get_reaction
      if (global_var(GVAR_SHERRY_TURNS) == 2) then
         start_dialog_at_node(Sherry22);
      else if (local_var(LVAR_Herebefore) == 1) then
         start_dialog_at_node(Sherry15);
      else
         start_dialog_at_node(Sherry01);
   end

   if (feed_dude) then begin
      call SherryCook;
   end
end

procedure Sherry01 begin
   set_local_var(LVAR_Herebefore, 1);
   Reply(103);
   NOption(mstr(104) + dude_name + mstr(105), Sherry02, 4);
   NOption(106, Sherry03, 4);
   NLowOption(107, Sherry04);
end

procedure Sherry02 begin
   Reply(108);
   BOption(109, SherryCombat, 4);
   NOption(110, SherryEnd, 4);
   NOption(111, Sherry05, 5);
   NOption(112, Sherry07, 6);
end

procedure Sherry03 begin
   DownReact
   Reply(113);
   NOption(114, SherryEnd, 4);
   NOption(115, SherryCombat, 4);
end

procedure Sherry04 begin
   NMessage(116);
end

procedure Sherry05 begin
   Reply(117);
   NOption(118, Sherry11, 4);
   NOption(119, SherryEnd, 4);
end

procedure Sherry06 begin
   Reply(120);
   NOption(121, Sherry07, 4);
   NOption(122, Sherry08, 4);
   NOption(123, SherryEnd, 4);
end

procedure Sherry07 begin
   BMessage(124);
end

procedure Sherry08 begin
   Reply(125);
   call Sherry13;
end

procedure Sherry09 begin
   set_local_var(LVAR_Ask_GangInfo, 1);
   Reply(126);
   NOption(127, Sherry10, 4);
   call Sherry13;
end

procedure Sherry10 begin
   Reply(128);
   call Sherry13;
end

procedure Sherry11 begin
   Reply(129);
   call Sherry13;
end

procedure Sherry12 begin
   set_local_var(LVAR_Ask_WhatDo, 1);
   Reply(130);
   NOption(131, Sherry26, 4);
   call Sherry13;
end

procedure Sherry13 begin
   if (not(local_var(LVAR_Ask_GangInfo))) then begin
      NOption(133, Sherry09, 5);
   end
   if (not(local_var(LVAR_Ask_WhatDo))) then begin
      NOption(132, Sherry12, 4);
   end
   if (not(local_var(LVAR_Ask_WhyStay))) then begin
      NOption(134, Sherry14, 6);
   end
   NOption(exit_line, SherryEnd, 4);
end

procedure Sherry14 begin
   set_local_var(LVAR_Ask_WhyStay, 1);
   Reply(135);
   call Sherry13;
end

procedure Sherry15 begin
   Reply(136);
   NOption(137, Sherry16, 4);
   if (local_var(LVAR_LeaveTimer) == 0) then begin
      NOption(138, Sherry17, 5);
   end
   NOption(exit_line, SherryEnd, 4);
end

procedure Sherry16 begin
   BMessage(139);
   call SherryCombat;
end

procedure Sherry17 begin
   Reply(140);
   NOption(141, SherryEnd, 4);
   NOption(142, Sherry18, 6);
end

procedure Sherry18 begin
   Reply(143);
   NOption(144, SherryEnd, 4);
   NOption(145, Sherry18a, 6);
end

procedure Sherry18a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call Sherry19;
   end
   else begin
      call Sherry20;
   end
end

procedure Sherry19 begin
   Reply(146);
   NOption(147, Sherry21, 6);
end

procedure Sherry20 begin
   DownReact
   BMessage(148);
end

procedure Sherry21 begin
   set_local_var(LVAR_LeaveTimer, game_time);
   set_global_var(GVAR_SHERRY_TURNS, 1);
   NMessage(149);
end

procedure Sherry22 begin
   Reply(150);
   NOption(151, SherryEnd, 4);
   NOption(152, Sherry23, 4);
   NOption(153, Sherry24, 4);
   if ((local_var(LVAR_Ask_Snitch) == 0) and (global_var(GVAR_BUST_SKULZ) == 1)) then begin
      NOption(166, Sherry27, 4);
   end
end

procedure Sherry23 begin
   Reply(154);
   NOption(155, Sherry25, 4);
   NOption(156, SherryEnd, 4);
end

procedure Sherry24 begin
   NMessage(157);
end

procedure Sherry25 begin
   feed_dude := 1;
end

procedure Sherry26 begin
   Reply(159);
   call Sherry13;
end

procedure Sherry27 begin
   set_local_var(LVAR_Ask_Snitch, 1);
   Reply(160);
   NOption(161, Sherry27a, 4);
   NOption(162, SherryEnd, 4);
   NOption(163, Sherry28, 6);
end

procedure Sherry27a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call Sherry28;
   end
   else begin
      call Sherry29;
   end
end

procedure Sherry28 begin
   NMessage(164);
   set_global_var(GVAR_SHERRY_TELLS_ON_SKULZ, 1);
end

procedure Sherry29 begin
   NMessage(165);
end

procedure SherryCombat begin
   tmp_hostile := 1;
end

procedure SherryCook begin
   use_obj(dude_obj);
   float_msg(self_obj, mstr(158), FLOAT_MSG_NORMAL);
   critter_heal(dude_obj, 1);
   feed_dude := 0;
end

procedure SherryTurns begin
   set_self_team(TEAM_JUNKTOWNER);
   set_global_var(GVAR_SHERRY_TURNS, 2);
   set_local_var(LVAR_LeaveTimer, 0);
   home_tile := 18125;
   sleep_tile := 17924;
end

procedure SherryEnd begin
end
