/*

   Junktown - Vinnie, Skullz Leader

*/

/* Include Files */
#include "define.h"
#include "area/junktown.h"

#define NAME                    SCRIPT_VINNIE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Vinnie01;
procedure Vinnie01a;
procedure Vinnie02;
procedure Vinnie03;
procedure Vinnie04;
procedure Vinnie05;
procedure Vinnie06;
procedure Vinnie07;
procedure Vinnie08;
procedure Vinnie09;
procedure Vinnie10;
procedure Vinnie14;
procedure Vinnie14a;
procedure Vinnie15;
procedure Vinnie15a;
procedure Vinnie16;
procedure Vinnie17;
procedure Vinnie18;
procedure Vinnie19;
procedure Vinnie20;
procedure Vinnie21;
procedure Vinnie22;
procedure Vinnie23;
procedure Vinnie24;
procedure Vinnie25;
procedure VinnieEnd;

procedure VinnieCombat;
procedure VinnieKillNeal;
procedure set_sleep_time;

import variable Neal_ptr;
import variable removal_ptr;

variable tmp_hostile;
variable line160flag;
variable line162flag;
variable Vinnie_kill_Neal;
variable moving_disabled;
variable sleeping_disabled;
variable sleep_time;
variable wake_time;
variable night_person;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      game_ui_enable;
      if (map_var(1) != 0) then begin
         set_global_var(GVAR_DUDE_ENEMY_SKULZ, 1);
         float_msg(self_obj, mstr(162), FLOAT_MSG_RED);
         attack(dude_obj);
      end
      else begin
         attack(Neal_ptr);
      end
   end
end

procedure critter_p_proc begin
   if REP_ENEMY_JUNKTOWN_SKULZ then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (CUR_MAP_JUNKCSNO) and (line160flag == 0) then begin
         float_msg(self_obj, mstr(160), FLOAT_MSG_RED);
         game_ui_disable;
         add_timer_event(self_obj, game_ticks(2), 1);
         line160flag := 1;
      end
   end
end

procedure destroy_p_proc begin
   if (CUR_MAP_JUNKCSNO) then begin
      dec_map_var(0);
   end

   if source_is_dude then begin
      set_global_var(GVAR_DUDE_ENEMY_SKULZ, 1);
   end

   set_quest_join_skullz_completed;

   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_reaction)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(101));
   end
end

procedure map_enter_p_proc begin
   call set_sleep_time;
   set_self_team(TEAM_JUNKTOWN_SKULZ);
   set_self_ai(AI_SKULZ);
   if ((global_var(GVAR_BUST_SKULZ) == 2) or ((CUR_MAP_JUNKCSNO) and (global_var(GVAR_IS_NEAL_DEAD) == 1))) then begin
      move_to(self_obj, 7000, 0);
      if not(is_loading_game) then set_self_invisible;
      removal_ptr := self_obj;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_JUNKKILL);

   dude_look_at_self;

   if (global_var(GVAR_BUST_NEAL_STATE) == 2) then begin
      float_msg(self_obj, mstr(171), FLOAT_MSG_BLUE);
   end
   else if (global_var(GVAR_IS_NEAL_DEAD) == 1) then begin
      float_msg(self_obj, mstr(170), FLOAT_MSG_RED);
   end
   else begin
      start_gdialog(385, self_obj, 4, -1, -1);
      gsay_start;
      if (global_var(GVAR_BUST_NEAL_STATE) == 1) then begin
         call Vinnie24;
      end
      else if (local_var(LVAR_got_reaction) == BAD) then begin
         call Vinnie19;
      end
      else if (local_var(LVAR_reaction) == BAD) then begin
         call Vinnie10;
      end
      else begin
         call Vinnie01;
      end
      gsay_end;
      end_dialogue;
   end

   if (local_var(LVAR_reaction_level) == BAD) then begin
      give_xp_msg(EXP_STEAL_URN, mstr(163));
      dec_general_rep(1);
      set_local_var(LVAR_reaction_level, NEUTRAL);
      if (global_var(GVAR_IS_NEAL_DEAD) == 0) then begin
         set_global_var(GVAR_BUST_NEAL_TIMER, (GAME_TIME_IN_DAYS) + 2);
      end
   end

   if (Vinnie_kill_Neal) then begin
      Vinnie_kill_Neal := 0;
      load_map(MAP_JUNKCSNO, 7);
   end

   if ((global_var(GVAR_BUST_NEAL_TIMER) > (GAME_TIME_IN_DAYS)) and (global_var(GVAR_IS_NEAL_DEAD) == 0)) then begin
      float_msg(self_obj, mstr(159), FLOAT_MSG_BLUE);
   end
end

procedure Vinnie01 begin
   set_local_var(LVAR_reaction, 1);
   Reply(103);
   NOption(104, Vinnie02, 4);
   NOption(105, Vinnie03, 4);
   if quest_join_skullz_active then begin
      NOption(102, Vinnie01a, 4);
   end
   NLowOption(106, Vinnie04);
end

procedure Vinnie01a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call Vinnie14;
   end
   else begin
      call Vinnie15;
   end
end

procedure Vinnie02 begin
   BMessage(107);
end

procedure Vinnie03 begin
   Reply(108);
   NOption(109, VinnieEnd, 4);
   NOption(110, Vinnie06, 4);
   NOption(111, Vinnie05, 6);
end

procedure Vinnie04 begin
   NMessage(112);
end

procedure Vinnie05 begin
   Reply(113);
   NOption(114, VinnieEnd, 4);
   NOption(115, Vinnie07, 6);
end

procedure Vinnie06 begin
   BMessage(116);
   call VinnieCombat;
end

procedure Vinnie07 begin
   Reply(117);
   NOption(118, VinnieEnd, 4);
   NOption(119, Vinnie08, 6);
end

procedure Vinnie08 begin
   Reply(120);
   NOption(121, VinnieEnd, 4);
   if (global_var(GVAR_HIRED_BY_GIZMO)) then begin
      NOption(122, Vinnie09, 6);
   end
end

procedure Vinnie09 begin
   NMessage(123);
end

procedure Vinnie10 begin
   Reply(126);
   NOption(127, VinnieEnd, 4);
   if quest_join_skullz_active then begin
      NOption(102, Vinnie01a, 4);
   end
end

procedure Vinnie14 begin
   Reply(131);
   if (has_rep_berserker) then begin
      BOption(132, VinnieCombat, 4);
   end
   NOption(133, Vinnie16, 4);
   NOption(134, Vinnie14a, 4);
end

procedure Vinnie14a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call Vinnie16;
   end
   else begin
      call Vinnie15;
   end
end

procedure Vinnie15 begin
   Reply(135);
   NOption(136, Vinnie15a, 4);
   if (has_rep_berserker) then begin
      BOption(137, VinnieCombat, 4);
   end
   NOption(138, VinnieEnd, 4);
   NOption(139, Vinnie03, 4);
end

procedure Vinnie15a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call Vinnie16;
   end
   else begin
      call Vinnie04;
   end
end

procedure Vinnie16 begin
   Reply(140);
   if (global_var(GVAR_IS_NEAL_DEAD) == 1) then begin
      GOption(145, Vinnie22, 4);
   end
   else begin
      if (global_var(GVAR_URN_STOLEN) == 1) then begin
         GOption(144, Vinnie19, 4);
      end
      else begin
         NOption(141, Vinnie17, 4);
      end
   end
   BOption(142, Vinnie18, 4);
   BOption(143, Vinnie04, 4);
end

procedure Vinnie17 begin
   set_local_var(LVAR_got_reaction, BAD);
   GMessage(146);
end

procedure Vinnie18 begin
   BMessage(147);
end

procedure Vinnie19 begin
   Reply(148);
   if (dude_item_count(PID_URN)) then begin
      GOption(149, Vinnie20, 4);
   end
   else begin
      BOption(150, Vinnie21, 4);
   end
   if (global_var(GVAR_IS_NEAL_DEAD) == 1) then begin
      GOption(151, Vinnie22, 4);
   end
end

procedure Vinnie20 begin
   set_local_var(LVAR_got_reaction, NO_REACTION);
   if (local_var(LVAR_reaction_level) == NO_REACTION) then begin
      set_local_var(LVAR_reaction_level, BAD);
   end
   if (global_var(GVAR_IS_NEAL_DEAD) == 1) then begin
      call Vinnie22;
   end
   else begin
      set_global_var(GVAR_BUST_NEAL_TIMER, (GAME_TIME_IN_DAYS) + 2);
      Reply(152);
      BOption(153, Vinnie06, 4);
      GOption(154, VinnieKillNeal, 4);
      NOption(155, Vinnie23, 5);
   end
end

procedure Vinnie21 begin
   BMessage(156);
end

// Dude becomes a skull
procedure Vinnie22 begin
   set_quest_join_skullz_completed;

   set_local_var(LVAR_got_reaction, NO_REACTION);
   set_global_var(GVAR_BUST_NEAL_STATE, 2);
   GMessage(157);
end

procedure Vinnie23 begin
   set_global_var(GVAR_BUST_NEAL_STATE, 1);
   NMessage(158);
end

procedure Vinnie24 begin
   Reply(165);
   GOption(166, VinnieKillNeal, 4);
   NOption(167, Vinnie23, 4);
   BOption(168, Vinnie25, 4);
end

procedure Vinnie25 begin
   BMessage(169);
   call VinnieCombat;
end

procedure VinnieEnd begin
end

procedure VinnieCombat begin
   tmp_hostile := 1;
end

procedure VinnieKillNeal begin
   set_global_var(GVAR_BUST_NEAL_STATE, 2);
   Vinnie_kill_Neal := 1;
end

procedure set_sleep_time begin
   if (CUR_MAP_JUNKKILL) then begin
      sleep_time := random(2200, 2215);
      wake_time := random(800, 815);
   end
   else if (CUR_MAP_JUNKCSNO) then begin
      sleep_time := 300;
      wake_time := 1600;
      night_person := 1;
   end
   else if (CUR_MAP_JUNKENT) then begin
      sleep_time := 2200;
      wake_time := 1000;
   end
end
