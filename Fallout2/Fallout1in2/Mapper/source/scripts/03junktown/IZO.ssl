/*

   Junktown - Izo, Gizmo's guard

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_IZO
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure description_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure map_update_p_proc;
procedure map_enter_p_proc;

procedure ShowToDoor_talk;
procedure PayPlayer_talk;
procedure IzoEnd;

import variable Gizmo_ptr;
import variable Gizmo_is_angry;
import variable show_to_door;
import variable payment;

variable initial := 0;
variable tmp_hostile;
variable line103flag;

#define home_hex        (16520)

procedure start begin
   if (not(initial)) then begin
      initial := 1;
      set_self_team(TEAM_JUNKTOWN_GIZMO);
      if (global_var(GVAR_CAPTURE_GIZMO) == 2) then begin
         kill_critter(self_obj, ANIM_fall_front_blood_sf);
      end
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_ENEMY_GIZMOS_GANG) == 1) then begin
      if (self_can_see_dude) then begin
         tmp_hostile := 1;
         attack(dude_obj);
      end
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (Gizmo_is_angry) then begin
         Gizmo_is_angry := 0;
         attack(dude_obj);
      end
      else begin
         if (show_to_door) then begin
            if (tile_distance_objs(dude_obj, Gizmo_ptr) > 8) then begin
               if (self_tile != home_hex) then begin
                  self_walk_to_tile(home_hex);
               end
               else begin
                  self_rotate(2);
                  line103flag := 0;
                  show_to_door := 0;
               end
            end
            else if (self_tile != 18720) then begin
               self_walk_to_tile(18720);
            end
            else begin
               if (not(line103flag)) then begin
                  line103flag := 1;
                  display_msg(mstr(103));
               end
            end
         end

      end
   end
end

procedure map_enter_p_proc begin
   set_local_var(4, 0);
   //if (self_tile == 27719) then begin
   //   self_run_to_tile(16720);
   //end
   if (self_tile != home_hex) then begin
      critter_attempt_placement(self_obj, home_hex, self_elevation);
      self_rotate(2);
   end
end

procedure map_update_p_proc begin
   if ((self_tile == 16720) and (local_var(4) == 0)) then begin
      set_local_var(4, 1);
      self_rotate(rotation_to_tile(self_tile, 16920));
   end
end

procedure destroy_p_proc begin
   if source_obj == dude_obj then begin
      set_global_var(GVAR_ENEMY_GIZMOS_GANG, 1);
   end
   inc_evil_critter
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   display_msg(mstr(101));
   if ((payment > 0) and (local_var(3) == 0)) then begin
      use_obj(dude_obj);
      dude_caps_adjust(payment);
      start_dialog_at_node(PayPlayer_talk);

      payment := 0;
      set_local_var(3, 1);
   end
end

procedure ShowToDoor_talk begin
   if not(global_var(GVAR_GIZMO_NICE_SHOWDOOR)) then begin
      BMessage(103);
   end
   else begin
      set_global_var(GVAR_GIZMO_NICE_SHOWDOOR, 0);
      NMessage(106);
   end
end

procedure PayPlayer_talk begin
   Reply(102);
   NOption(104, IzoEnd, 4);
   BLowOption(105, IzoEnd);
end

procedure IzoEnd begin
end
