/*

   Junktown - Flash, Doc Morbid guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_FLASH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure destroy_p_proc;
procedure damage_p_proc;

procedure Flash00;
procedure Flash01;
procedure Flash00N;
procedure Flash00Na;
procedure Flash01N;
procedure Flash02N;
procedure Flash03N;
procedure Flash03Na;
procedure Flash04N;
procedure Flash05N;
procedure Flash06N;
procedure Flash07N;
procedure Flash07Na;
procedure Flash08Na;
procedure Flash08Nb;
procedure Flash09N;
procedure Flash10N;
procedure Flash11N;
procedure FlashCombat;
procedure FlashEnd;

import variable fetch_dude;
import variable Morbid_ptr;

variable tmp_hostile;
variable loitering;

variable FuckOff;
variable SeenDude;
variable SeenTime;
variable SeenTime2;

procedure start begin
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      if (SeenDude != 1) then begin
         SeenDude := 1;
         SeenTime := game_time;
      end
      SeenTime2 := game_time;
      if (game_time_hour < 700) then begin
         if ((SeenTime2 - SeenTime) >= 50) then begin
            if (FuckOff != 0) then begin
               float_msg(self_obj, mstr(129), FLOAT_MSG_LIGHT_RED);
               call FlashCombat;
            end
            else begin
               loitering := 1;
               start_dialog_at_node(Flash07N);
               SeenTime := game_time;
               FuckOff := 1;
            end
         end
      end
      if (game_time_hour >= 1800) then begin
         if ((SeenTime2 - SeenTime) >= 50) then begin
            if (FuckOff != 0) then begin
               float_msg(self_obj, mstr(129), FLOAT_MSG_LIGHT_RED);
               call FlashCombat;
            end
            else begin
               loitering := 1;
               start_dialog_at_node(Flash07N);
               SeenTime := game_time;
               FuckOff := 1;
            end
         end
      end
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (fetch_dude) then begin
         fetch_dude := 0;
         tmp_hostile := 1;
      end
   end
   if (global_var(GVAR_IS_MORBID_TEAM_HOSTILE) == 1) then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
end

procedure map_enter_p_proc begin
   if junktown_invaded then begin
      kill_critter(self_obj, ANIM_burned_to_nothing_sf);
   end
   set_self_team(TEAM_JUNKTOWN_MORBID);
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if ((game_time_hour >= 800) and (game_time_hour < 1700)) then begin
      FuckOff := 0;
      if (not(local_var(0))) then begin
         call Flash00;
      end
      else begin
         call Flash01;
      end
   end
   else begin
      if (loitering) then begin
         start_dialog_at_node(Flash07N);
      end
      else if (not(local_var(0))) then begin
         start_dialog_at_node(Flash00N);
      end
      else begin
         start_dialog_at_node(Flash03N);
      end
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      if (self_can_see_dude) then begin
         loitering := 1;
         dialogue_system_enter;
      end
   end
end

procedure destroy_p_proc begin
   rm_timer_event(self_obj);

   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_IS_MORBID_TEAM_HOSTILE, 1);
   end
   inc_good_critter
   rm_timer_event(self_obj);
end

procedure Flash00 begin
   set_local_var(0, 1);
   float_msg(self_obj, mstr(100), FLOAT_MSG_NORMAL);
end

procedure Flash01 begin
   float_msg(self_obj, mstr(106), FLOAT_MSG_NORMAL);
end

procedure Flash00N begin
   set_local_var(0, 1);
   Reply(101);
   NOption(102, Flash01N, 4);
   NOption(103, Flash10N, 4);
   NOption(104, Flash00Na, 5);
   NOption(128, Flash11N, 4);
   NLowOption(105, Flash10N);
end

procedure Flash00Na begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   variable LVar2 := 0;
   LVar1 := dude_max_hp;
   LVar2 := dude_cur_hp;
   if (LVar2 == LVar1) then begin
      call Flash01N;
   end
   else begin
      LVar0 := -20;
      if (LVar2 < 5) then begin
         LVar0 := 0;
      end
      if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, LVar0))) then begin
         call Flash02N;
      end
      else begin
         call Flash01N;
      end
   end
end

procedure Flash01N begin
   float_msg(self_obj, mstr(107), FLOAT_MSG_LIGHT_RED);
   FuckOff := 1;
   SeenTime := game_time;
end

procedure Flash02N begin
   NMessage(108);
end

procedure Flash03N begin
   set_local_var(0, 1);
   Reply(109);
   NOption(110, Flash03Na, 4);
   NOption(111, Flash03Na, 5);
   NLowOption(112, Flash06N);
end

procedure Flash03Na begin
   if (self_elevation == elevation(Morbid_ptr)) then begin
      call Flash05N;
   end
   else begin
      call Flash04N;
   end
end

procedure Flash04N begin
   Reply(113);
   NOption(126, Flash11N, 4);
   add_timer_event(self_obj, game_ticks(10), 1);
end

procedure Flash05N begin
   NMessage(114);
end

procedure Flash06N begin
   NMessage(115);
end

procedure Flash07N begin
   Reply(116);
   NOption(117, FlashCombat, 4);
   NOption(118, Flash07Na, 5);
end

procedure Flash07Na begin
   variable LVar0 := 0;
   if (dude_is_male) then begin
      LVar0 := -15;
   end
   else begin
      LVar0 := -25;
   end
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, LVar0))) then begin
      call Flash09N;
   end
   else begin
      if (dude_is_male) then begin
         call Flash08Nb;
      end
      else begin
         call Flash08Na;
      end
   end
end

procedure Flash08Na begin
   Reply(119);
   NOption(120, FlashCombat, 4);
   NOption(121, FlashEnd, 5);
end

procedure Flash08Nb begin
   Reply(122);
   NOption(123, FlashCombat, 4);
   NOption(124, FlashEnd, 5);
end

procedure Flash09N begin
   NMessage(125);
end

procedure Flash10N begin
   add_timer_event(self_obj, game_ticks(10), 1);
end

procedure Flash11N begin
   NMessage(127);
end

procedure FlashCombat begin
   tmp_hostile := 1;
end

procedure FlashEnd begin
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_IS_MORBID_TEAM_HOSTILE, 1);
   end
end
