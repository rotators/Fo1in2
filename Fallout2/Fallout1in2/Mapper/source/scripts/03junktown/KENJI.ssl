/*

   Kenji, the assassin send after Killian

*/

#include "define.h"
#include "maps/junkkill.h"

#define NAME                    SCRIPT_KENJI

#include "command.h"
#include "modreact.h"


procedure start;
procedure map_exit_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;

variable tmp_hostile;

import variable Killian_ptr;
import variable Killian_store1_ptr;
import variable Kenji_ptr;

#define KENJI_HEX       26281

procedure start begin
   Kenji_ptr := self_obj;
end

procedure map_exit_p_proc begin
   if (Killian_store1_ptr > 0) then begin
      move_obj_inven_to_obj(self_obj, Killian_store1_ptr);
   end
   destroy_object(self_obj);
end

procedure critter_p_proc begin
   if (tile_distance(self_tile, 26281) > 3) then begin
      self_walk_to_tile(26281);
   end
   else begin
      if (obj_on_screen(self_obj)) then begin
         float_msg(self_obj, mstr(103), FLOAT_MSG_RED);
         attack(Killian_ptr);
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj or source_obj == party_member_obj(obj_pid(source_obj))) then begin
      set_map_var(MVAR_helped_Killian, 1);
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj or source_obj == party_member_obj(obj_pid(source_obj))) then begin
      inc_evil_critter
   end

   if (map_var(MVAR_helped_Killian) == 1) then begin
      inc_general_rep(2);
      give_xp_msg(EXP_KILLED_KENJI, mstr(104));
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

procedure talk_p_proc begin
   dude_look_at_self;
   display_msg(mstr(random(100, 101)));
end
