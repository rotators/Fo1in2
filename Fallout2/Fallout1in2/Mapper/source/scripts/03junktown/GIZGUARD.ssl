/*

	Junktown - Gizmo guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_VINNIE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure GizGuard00;
procedure GizGuard01;
procedure GizGuard02;
procedure GizGuard03;
procedure GizGuard04;
procedure GizGuard05;
procedure GizGuard06;
procedure GizGuard07;
procedure GizGuard08;
procedure GizGuardEnd;

variable tmp_hostile;
variable sneak_checked;

import variable removal_ptr;
import variable weapon_checked;

procedure start begin
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		if (self_can_see_dude) then begin
			tmp_hostile := 0;
			attack(dude_obj);
		end
	end
	else begin
		if (self_can_see_dude) then begin
			if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
				if dude_is_armed then begin
					call GizGuard00;//dialogue_system_enter;
				end
				else begin
					if (dude_is_sneaking and not(sneak_checked)) then begin
						call GizGuard01;//dialogue_system_enter;
					end
				end
			end
		end
	end
	if (global_var( GVAR_ENEMY_GIZMOS_GANG ) == 1) then begin// - ENEMY_GIZMOS_GANG
		tmp_hostile := 1;
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_GIZMOS_GANG, 1 );// ENEMY_GIZMOS_GANG
	end
	inc_evil_critter
	rm_timer_event(self_obj);
end

procedure map_enter_p_proc
begin
	if (not(is_loading_game) and self_caps == 0) then begin
		self_caps_adjust( random(0, 10) );
	end
	set_self_team(TEAM_JUNKTOWN_GIZMO );
	if (not(is_gizmo_alive) or (global_var( GVAR_CAPTURE_GIZMO ) == 2)) then begin
		if not(is_loading_game) then begin    set_self_invisible;    end
		move_to(self_obj, 7000, 0);
		removal_ptr := self_obj;
	end
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if dude_is_armed then begin
		call GizGuard00;
	end
	else begin
		if (dude_is_sneaking) then begin
			call GizGuard01;
		end
		else begin
			if (not(local_var(0))) then begin
				start_gdialog(639, self_obj, 4, -1, -1);
				gsay_start;
				call GizGuard02;
				gsay_end;
				end_dialogue;
			end
			else begin
				call GizGuard07;
			end
		end
	end
end

procedure timed_event_p_proc
begin
	if (fixed_param == 1) then begin
		if dude_is_armed then begin
			float_msg( self_obj, message_str(SCRIPT_GIZGUARD, random(100, 103)), FLOAT_MSG_RED );
			tmp_hostile := 1;
		end
		else begin
			weapon_checked := 0;
		end
	end
	else begin
		if (fixed_param == 2) then begin
			if (dude_is_sneaking) and self_can_see_dude then begin
				tmp_hostile := 1;
			end
			else begin
				sneak_checked := 0;
			end
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_GIZMOS_GANG, 1 );// ENEMY_GIZMOS_GANG
	end
end

procedure GizGuard00
begin
	float_msg( self_obj, message_str(SCRIPT_GIZGUARD, random(132, 133)), FLOAT_MSG_RED );//{132}{}{No weapons in here!} OR {133}{}{Hey! You! Put your piece away!}
	weapon_checked := 1;
	add_timer_event(self_obj, game_ticks(10), 1);//ticks was 5
end

procedure GizGuard01
begin
	float_msg( self_obj, message_str(SCRIPT_GIZGUARD, random(104, 106)), FLOAT_MSG_RED );//{104}{}{Quit sneaking around!}, {105}{}{No sneaking around in here!}, {106}{}{Hey, you! Quit trying to hide, or get out!}
	sneak_checked := 1;
	add_timer_event(self_obj, game_ticks(10), 2);//ticks was 5
end

procedure GizGuard02
begin
	set_local_var(0, 1);
	gsay_reply( SCRIPT_GIZGUARD, 107 );//{107}{}{What?}
	giq_option( 4, SCRIPT_GIZGUARD, 108, GizGuard03, NEUTRAL_REACTION );//{108}{}{Hey, can I ask you a few questions?}
	giq_option( 4, SCRIPT_GIZGUARD, 109, GizGuard04, NEUTRAL_REACTION );//{109}{}{Can you spare a few caps?}
	giq_option( 4, SCRIPT_GIZGUARD, 110, GizGuardEnd, NEUTRAL_REACTION );//{110}{}{Nevermind, sorry.}
	giq_option( -3, SCRIPT_GIZGUARD, 111, GizGuard08, BAD_REACTION );//{111}{}{Uuuh?}
end

procedure GizGuard03
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		gsay_reply( SCRIPT_GIZGUARD, 112 );//{112}{}{I'm on duty. Make it quick.}
		giq_option( 4, SCRIPT_GIZGUARD, 114, GizGuard05, NEUTRAL_REACTION );//{114}{}{Can you tell me about your boss?}
		giq_option( 4, SCRIPT_GIZGUARD, 115, GizGuard06, NEUTRAL_REACTION );//{115}{}{What else is there around this town?}
	end
	else begin
		gsay_message( SCRIPT_GIZGUARD, 113, BAD_REACTION );//{113}{}{No. Go away.}
	end
end

procedure GizGuard04
begin
	gsay_message( SCRIPT_GIZGUARD, random(116, 119), NEUTRAL_REACTION );//{116}{}{What are you, stupid? Go away.}, {117}{}{No. Go beg somewhere else.}, {118}{}{Buzz off.}, {119}{}{We don't have a use for paupers here.}
end

procedure GizGuard05
begin
	gsay_message( SCRIPT_GIZGUARD, 120, NEUTRAL_REACTION );//{120}{}{Gizmo's a fair man. Pay's good, work's easy. Don't make trouble and everyone will be happy.}
end

procedure GizGuard06
begin
	gsay_message( SCRIPT_GIZGUARD, random(121, 124), NEUTRAL_REACTION );
//
//{121}{}{Go catch a few drinks at the Skum Pitt. Good grog. Opens at four. Now leave me alone.}, {122}{}{Why don't you go shopping at Killian's? His store is southeast of here. Now beat it.}
//{123}{}{Talk to the dealers.}, {124}{}{Hey, if you wanna have a little fun, stop by the Crash House and ask for Sinthia. She's worth it.}
end

procedure GizGuard07
begin
	float_msg( self_obj, message_str(SCRIPT_GIZGUARD, random(125, 130)), FLOAT_MSG_NORMAL );
/*
{125}{}{No cheating.}
{126}{}{Debtors will be shot.}
{127}{}{Boy, my neck hurts.}
{128}{}{This job bites the Bone.}
{129}{}{I love this place.}
{130}{}{Think I'll visit Sinthia tonight.}
*/
end

procedure GizGuard08
begin
	gsay_message( SCRIPT_GIZGUARD, 131, NEUTRAL_REACTION );//{131}{}{Get outta here, you moron.}
end

procedure GizGuardEnd
begin
end
