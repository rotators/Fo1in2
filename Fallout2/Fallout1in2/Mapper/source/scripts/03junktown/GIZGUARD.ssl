/*

   Junktown - Gizmo guard

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_GIZGUARD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure GizGuard00;
procedure GizGuard01;
procedure GizGuard02;
procedure GizGuard03;
procedure GizGuard04;
procedure GizGuard05;
procedure GizGuard06;
procedure GizGuard07;
procedure GizGuard08;
procedure GizGuardEnd;

variable tmp_hostile;
variable sneak_checked;
variable float_weapon;
variable float_sneak;

import variable removal_ptr;
import variable weapon_checked;

#define EVENT_FLOAT_WEAPON    (1)
#define EVENT_FLOAT_SNEAKING  (2)

procedure start begin
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      if (self_can_see_dude) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
   end
   else begin
      if (self_can_see_dude) then begin
         if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
            if dude_is_armed and (float_weapon == 0) then begin
               call GizGuard00;
            end
            else if (dude_is_sneaking and not(sneak_checked) and float_sneak == 0) then begin
               call GizGuard01;
            end
         end
      end
   end

   if (global_var(GVAR_ENEMY_GIZMOS_GANG) == 1) then begin
      tmp_hostile := 1;
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_GIZMOS_GANG, 1);
   end
   inc_evil_critter
end

procedure map_enter_p_proc begin
   if (not(is_loading_game) and self_caps == 0) then begin
      self_caps_adjust(random(0, 10));
   end
   set_self_team(TEAM_JUNKTOWN_GIZMO);
   if (not(is_gizmo_alive) or (global_var(GVAR_CAPTURE_GIZMO) == 2)) then begin
      if not(is_loading_game) then set_self_invisible;
      move_to(self_obj, 7000, 0);
      removal_ptr := self_obj;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if dude_is_armed then begin
      call GizGuard00;
   end
   else if (dude_is_sneaking) then begin
      call GizGuard01;
   end
   else if (not(local_var(0))) then begin
      start_dialog_at_node(GizGuard02);
   end
   else begin
      call GizGuard07;
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_FLOAT_WEAPON) then begin
      if dude_is_armed then begin
         float_msg(self_obj, mstr(random(100, 103)), FLOAT_MSG_RED);
         tmp_hostile := 1;
      end
      else begin
         float_weapon := 0;
         weapon_checked := 0;
      end
   end
   else if (fixed_param == EVENT_FLOAT_SNEAKING) then begin
      if (dude_is_sneaking) and self_can_see_dude then begin
         tmp_hostile := 1;
      end
      else begin
         float_sneak := 0;
         sneak_checked := 0;
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_GIZMOS_GANG, 1);
   end
end

procedure GizGuard00 begin
   float_weapon := 1;

   float_msg(self_obj, mstr(random(132, 133)), FLOAT_MSG_RED);
   weapon_checked := 1;
   add_timer_event(self_obj, game_ticks(10), EVENT_FLOAT_WEAPON);
end

procedure GizGuard01 begin
   float_sneak := 1;

   float_msg(self_obj, mstr(random(104, 106)), FLOAT_MSG_RED);
   sneak_checked := 1;
   add_timer_event(self_obj, game_ticks(10), EVENT_FLOAT_SNEAKING);//ticks was 5
end

procedure GizGuard02 begin
   set_local_var(0, 1);
   Reply(107);//{107}{}{What?}
   NOption(108, GizGuard03, 4);//{108}{}{Hey, can I ask you a few questions?}
   NOption(109, GizGuard04, 4);//{109}{}{Can you spare a few caps?}
   NOption(110, GizGuardEnd, 4);//{110}{}{Nevermind, sorry.}
   BLowOption(111, GizGuard08);//{111}{}{Uuuh?}
end

procedure GizGuard03 begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      Reply(112);//{112}{}{I'm on duty. Make it quick.}
      NOption(114, GizGuard05, 4);//{114}{}{Can you tell me about your boss?}
      NOption(115, GizGuard06, 4);//{115}{}{What else is there around this town?}
   end
   else begin
      BMessage(113);//{113}{}{No. Go away.}
   end
end

procedure GizGuard04 begin
   NMessage(random(116, 119));//{116}{}{What are you, stupid? Go away.}, {117}{}{No. Go beg somewhere else.}, {118}{}{Buzz off.}, {119}{}{We don't have a use for paupers here.}
end

procedure GizGuard05 begin
   NMessage(120);//{120}{}{Gizmo's a fair man. Pay's good, work's easy. Don't make trouble and everyone will be happy.}
end

procedure GizGuard06 begin
   NMessage(random(121, 124));
//
//{121}{}{Go catch a few drinks at the Skum Pitt. Good grog. Opens at four. Now leave me alone.}, {122}{}{Why don't you go shopping at Killian's? His store is southeast of here. Now beat it.}
//{123}{}{Talk to the dealers.}, {124}{}{Hey, if you wanna have a little fun, stop by the Crash House and ask for Sinthia. She's worth it.}
end

procedure GizGuard07 begin
   float_msg(self_obj, mstr(random(125, 130)), FLOAT_MSG_NORMAL);
/*
{125}{}{No cheating.}
{126}{}{Debtors will be shot.}
{127}{}{Boy, my neck hurts.}
{128}{}{This job bites the Bone.}
{129}{}{I love this place.}
{130}{}{Think I'll visit Sinthia tonight.}
*/
end

procedure GizGuard08 begin
   NMessage(131);//{131}{}{Get outta here, you moron.}
end

procedure GizGuardEnd begin
end
