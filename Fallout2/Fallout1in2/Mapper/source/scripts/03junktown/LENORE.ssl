/*

   Junktown - Lenore, standing at the house near Dogmeat.

*/

/* Include Files */
#include "define.h"
#include "area/junktown.h"
#include "maps/junkcsno.h"

#define NAME                    SCRIPT_LENORE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;
procedure critter_p_proc;

procedure Lenore00;
procedure Lenore01;
procedure Lenore02;
procedure Lenore03;
procedure Lenore04;
procedure go_inside;

#define LVAR_Herebefore         (4)

variable armed;
variable tmp_hostile;
variable home_tile := 29081;

procedure start begin
end

procedure map_enter_p_proc begin
   set_self_team(TEAM_JUNKTOWNER);
end

procedure pickup_p_proc begin
   attack(dude_obj);
end

procedure use_skill_on_p_proc begin
   // TODO: Check what this is.
   if (action_being_used == 14) then begin
      dialogue_system_enter;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 1) then
      display_msg(mstr(108));
   else
      display_msg(mstr(100));
end

procedure critter_p_proc begin
   if REP_ENEMY_JUNKTOWN_CITIZENS then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
   if (map_var(MVAR_growling) == 1) then begin
      call Lenore00;
   end
   if (map_var(MVAR_dog_is_angry) == 0) then begin
      self_walk_to_tile(home_tile);
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_DUDE_ENEMY_JUNK_CITIZEN, 1);
      set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
   end
   inc_good_critter
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_JUNKCSNO);

   set_local_var(LVAR_Herebefore, 1);
   if (global_var(GVAR_DOGMEAT_HIRELING_STATUS)) then begin
      call Lenore02;
   end
   else if (not(map_var(MVAR_dog_is_angry))) then begin
      call Lenore04;
   end
   else if (dude_is_armed) then begin
      call Lenore01;
   end
   else begin
      call Lenore03;
   end
end

procedure go_inside begin
   self_walk_to_tile(home_tile);
end

procedure Lenore00 begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   LVar0 := random(1, 3);
   if (LVar0 == 1) then begin
      LVar1 := message_str(NAME, 101);
   end
   else if (LVar0 == 2) then begin
      LVar1 := message_str(NAME, 102);
   end
   else if (LVar0 == 3) then begin
      set_global_var(GVAR_DOGMEAT_SOMETHING_1, 1);
      LVar1 := message_str(NAME, 103);
   end

   if (dog_is_angry) then begin
      float_msg(self_obj, LVar1, FLOAT_MSG_NORMAL);
   end
   // This will break Phil running away
   //set_map_var(MVAR_growling, 0);
end

procedure Lenore01 begin
   if dog_is_angry then begin
      float_msg(self_obj, message_str(NAME, 104), FLOAT_MSG_NORMAL);
   end
end

procedure Lenore02 begin
   if dog_is_killed then begin
      call Lenore04;
   end
   else begin
      float_msg(self_obj, message_str(NAME, 105), FLOAT_MSG_NORMAL);
   end
end

procedure Lenore03 begin
   float_msg(self_obj, message_str(NAME, 106), FLOAT_MSG_NORMAL);
   set_map_var (MVAR_smartass_1, 1);
end

procedure Lenore04 begin
   float_msg(self_obj, message_str(NAME, 107), FLOAT_MSG_NORMAL);
end
