/*

   Junktown, Spatial script for the Skum Pitt bar.
      Detects if the player is sneaking around inside.

*/

#include "..\headers\define.h"
#include "..\headers\junktown.h"
#include "..\headers\maps\junkcsno.h"

#define NAME                    SCRIPT_NEAL

#include "..\headers\command.h"

procedure start;

#define LVAR_Only_Once     (0)
#define LVAR_NealFloat     (1)

import variable Neal_ptr;

procedure start begin
end

procedure spatial_p_proc begin
   if (source_obj == dude_obj) then begin
      if ((game_time_hour > 409) and (game_time_hour < 1400) and (map_var(MVAR_SKUM_NOISY) == 0)) then begin
         if not(dude_is_sneaking) or not(is_success(roll_vs_skill(dude_obj, SKILL_SNEAK, 40))) then begin
            set_map_var(MVAR_SKUM_NOISY, 1);

            float_msg(Neal_ptr, mstr(201 + local_var(LVAR_NealFloat)), FLOAT_MSG_NORMAL);
            set_local_var(LVAR_NealFloat, 1);

            reg_anim_clear(Neal_ptr);
            animate_move_obj_to_tile(Neal_ptr, check_noise_hex, ANIMATE_WALK);
         end
      end
      else begin
         if (local_var(LVAR_NealFloat) > 0) then
            set_local_var(LVAR_NealFloat, 1);
      end
   end
end
