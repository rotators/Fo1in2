/*

   Check where we are on the worldmap and do stuff to get the scripted encounters working.

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "sfall/sfall.rotators.h"
#include "sfall/lib.math.h"
#include "sfall/lib.arrays.h"

#include "gl_worldmap.h"

#define in_glow_zone       ((worldmap_xpos >= 1150) and (worldmap_xpos <= 1300) and (worldmap_ypos >= 1200) and (worldmap_ypos <= 1350))

#define south_table        (global_var(GVAR_WORLDMAP_TABLE) == 3)
#define vats_table         (global_var(GVAR_WORLDMAP_TABLE) == 11)
#define glow_table         (global_var(GVAR_WORLDMAP_TABLE) == 12)

// some consts
#define  TILE_COUNT        (20)
variable TILE_COUNT_X := 0;
#define  TILE_COUNT_Y      (TILE_COUNT / TILE_COUNT_X)
#define  TILE_ZONES_X      (7)
#define  TILE_ZONES_Y      (6)

#define ZONE_SIZE          (50)
#define ZONE_COUNT_X       (TILE_COUNT_X * TILE_ZONES_X)
#define ZONE_COUNT_Y       (TILE_COUNT_Y * TILE_ZONES_Y)

#define EVENT_RADS         (1)

//
// worldmap zones table
//
// zone := GetZoneInfo( zx, zy );
// zone := GetCurrentZoneInfo();
//
// zx := zone["zx"];           // zone X coordinates (top-left is 0,0)
// zy := zone["zy"];           // zone Y coordinates (top-left is 0,0)
// terrain := zone["terrain"]; // zone terrain; copy of 1st element of [Tile T]->X_Y
// table := zone["table"];     // zone table; copy of 6th (last) element of [Tile T]->X_Y
//
// debug info
// tile := zone["tile"];     // T from [Tile T]->X_Y
// tile_x := zone["tile_x"]; // X from [Tile T]->X_Y
// tile_y := zone["tile_y"]; // Y from [Tile T]->X_Y
//

variable ZONES;
variable tick;

procedure GetZone(variable zx, variable zy) begin
   return ZONES[zx][zy];
end

procedure GetCurrentZone begin
   return GetZone(worldmap_xpos / ZONE_SIZE, worldmap_ypos / ZONE_SIZE);
end

// read [Tile*] sections of WORLDMAP.TXT and save parts of info in ZONES[][]
// TODO it should be called earlier
procedure ReadWorldmapTxt begin
   variable forTile, forX, forY;
   variable counter := get_uptime;

   // must be set before using some TILE_/ZONE_ macros
   TILE_COUNT_X := get_ini_setting("mods\\fo1_base\\data\\WORLDMAP.TXT|Tile Data|num_horizontal_tiles");

   if(ZONES == 0) then
   begin
      // no idea how to create 2d array properly
      variable x;
      ZONES := create_array(ZONE_COUNT_X, 0);
      for( x := 0; x < ZONE_COUNT_Y; x++ )
      begin
         ZONES[x] := create_array(ZONE_COUNT_Y, 0);
      end
   end

   for( forTile := 0; forTile < TILE_COUNT; forTile++ )
   begin
      for( forX := 0; forX < TILE_ZONES_X; forX++ )
      begin
         for( forY := 0; forY < TILE_ZONES_Y; forY++ )
         begin
            variable zx := (forTile % TILE_COUNT_X) * TILE_ZONES_X + forX;
            variable zy := (forTile / TILE_COUNT_X) * TILE_ZONES_Y + forY;
            variable line, cfg, zone := create_lookup_map;

            if(sfall_rotators) then
            begin // TODO cfg := r_get_ini_list(), use db::fastread()
               line := r_get_ini_string("mods\\fo1_base\\data\\WORLDMAP.TXT", "Tile " + forTile, "" + forX + "_" + forY, "");
               cfg := string_split(line, ",");
            end
            else // so slow i'm not sure is it worth keeping
            begin
               line := get_ini_string("mods\\fo1_base\\data\\WORLDMAP.TXT|Tile " + forTile + "|" + forX + "_" + forY);
               cfg := string_split(line, ",");
            end

            zone["zx"] := zx;
            zone["zy"] := zy;
            zone["terrain"] := cfg[0];
            zone["fill"]    := cfg[1];
            zone["table"]   := cfg[5];

            // debug info
            zone["tile"]   := forTile;
            zone["tile_x"] := forX;
            zone["tile_y"] := forY;

            ZONES[zx][zy] := zone;

            if(debug_mode and zone["table"] != "0" and atoi(zone["table"]) == 0) then
               display_msg("BUG BUG BUG : [Tile " + forTile + "]->" + forX + "_" + forY + " : table<" + zone["table"] + "> is not a number");
         end
      end
   end
   debug("ReadWorldmapTxt=" + (get_uptime-counter) + "ms");
end

variable prevTerrain := "";
variable prevTable := -1;
procedure SetWorldmapInfo begin
   variable zone := GetCurrentZone();
   variable terrain := WORLDMAP_TERRAIN_UNKNOWN, table := atoi(zone["table"]); // if table is not a number, we're in trouble

   // get terrain type from string
   switch (zone["terrain"]) begin
      case "City":     terrain := WORLDMAP_TERRAIN_CITY;
      case "Desert":   terrain := WORLDMAP_TERRAIN_DESERT;
      case "Mountain": terrain := WORLDMAP_TERRAIN_MOUNTAIN;
      case "Ocean":    terrain := WORLDMAP_TERRAIN_OCEAN;
      default : display_msg("BUG BUG BUG : unknown terrain<" + zone["terrain"] + ">");
   end
   // spam when changing zone type
   if (debug_mode and (prevTerrain != terrain or prevTable != table)) then
   begin
      if(not (prevTerrain == "" and prevTable == -1)) then
         debug_msg("exit " + prevTerrain + prevTable);

      debug_msg("enter " + zone["terrain"] + table + " (" + terrain + "," + table + ") : " + "zone<" + zone["zx"] + "," + zone["zy"] + "> tile<" + zone["tile"] + ":" + zone["tile_x"] + "_" + zone["tile_y"] + ">");
      prevTerrain := terrain;
      prevTable := table;
   end

   set_global_var(GVAR_WORLDMAP_TABLE, table);
   set_global_var(GVAR_WORLDMAP_TERRAIN, terrain);
end

procedure SetRadiation begin
   if above(game_time, tick) then begin
      tick := game_time + ONE_GAME_HOUR;
      radiation_inc(dude_obj, random(1, 20));
      debug_msg("radiation from wm! " + dude_rads + " rads!");
   end
end

procedure override_terrain_type begin
   // Override zone hotspot text

   // Rewrite zone titles around the Glow
   // Traveling on these zones will radiate the player (see gl_worldmap.ssl)
   set_terrain_name(23, 24, mstr_worldmap(1005));
   set_terrain_name(24, 24, mstr_worldmap(1005));
   set_terrain_name(25, 24, mstr_worldmap(1006));

   set_terrain_name(23, 25, mstr_worldmap(1005));
   set_terrain_name(25, 25, mstr_worldmap(1006));

   set_terrain_name(23, 26, mstr_worldmap(1005));
   set_terrain_name(24, 26, mstr_worldmap(1005));
   set_terrain_name(25, 26, mstr_worldmap(1005));
end

procedure encounter_handler begin
   variable
      event := get_sfall_arg,
      mapID := get_sfall_arg;

   // If the player enters a map on the worldmap, we set it depending on the current terrain type.
   // event 0 is random encounter, 1 is player initiated "encounter"
   if (event == 1 and mapID == MAP_DESERT0) then begin
      if (is_terrain_desert) then begin
         mapID := MAP_RNDDESER;
      end
      else if (is_terrain_mountain) then begin
         mapID := MAP_RNDMOUNT;
      end
      else if (is_terrain_city) then begin
         mapID := MAP_RNDCITY;
      end
      else if (is_terrain_ocean) then begin
         mapID := MAP_RNDCOAST;
      end
      else begin
         mapID := MAP_RNDDESER;
         debug_msg("!!! COULDN'T DETECT TERRAIN TYPE !!!");
      end
      set_sfall_return(mapID);
   end
   else if (event == 0) then begin
      if is_terrain_mountain then begin
         variable
            rnd_cave := random(1, 100),
            Maps_List, Bonus := 0;

         if ((mapID == MAP_MOUNTN1 or mapID == MAP_MOUNTN2) or (mapID >= MAP_CAVE0 and mapID <= MAP_MINE3)) then begin
            if south_table then Bonus := 15;
            else if glow_table then Bonus := 35;
            else Bonus := 0;
            Bonus := Bonus + (has_skill(dude_obj, SKILL_OUTDOORSMAN) / 20);
            debug("Cavern chance is " + rnd_cave + " and cavern bonus is " + bonus + ".");

            if (fo1in2_cave_maps_enabled and (rnd_cave <= (15 + Bonus))) then begin
               if glow_table then Bonus := 35;
               else Bonus := 0;
               Bonus := Bonus + (has_skill(dude_obj, SKILL_OUTDOORSMAN) / 20);

               if (south_table or vats_table or glow_table) and (random(1, 100) <= (5 + Bonus)) then
                  Maps_List := [MAP_MINE1, MAP_MINE2, MAP_MINE3];
               else
                  Maps_List := [MAP_CAVE0, MAP_CAVE1, MAP_CAVE2, MAP_CAVE3, MAP_CAVE4, MAP_CAVE5];
            end
            else
               Maps_List := [MAP_MOUNTN1, MAP_MOUNTN2];

            mapID := array_random_value(Maps_List);
            set_sfall_return(mapID);
         end
      end
   end
end

procedure start begin
   if (game_loaded) then begin
      set_global_script_repeat(10);
      set_global_script_type(1);

      register_hook_proc(HOOK_ENCOUNTER, encounter_handler);

      call ReadWorldmapTxt;
      call SetWorldmapInfo;

      call override_terrain_type;
   end

   if (get_game_mode == WORLDMAP) then begin
      call SetWorldmapInfo;
      if in_glow_zone then call SetRadiation;
   end
   else begin
      if in_glow_zone then begin
         if above(game_time, tick) then begin
            tick := game_time + (ONE_GAME_MINUTE * 10);
            radiation_inc(dude_obj, random(5, 15));
            debug_msg("radiation from zones around Glow! " + dude_rads + " rads!");
         end
      end
   end
end
