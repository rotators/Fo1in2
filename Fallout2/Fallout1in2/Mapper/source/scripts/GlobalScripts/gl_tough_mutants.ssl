/*

   Makes the super mutants a lot tougher with overriding their base hp value.

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure adjust_hp;

#define crit_pid           obj_pid(critter)
#define crit_max_hp        get_max_hp(critter)

#define hp_overkill        global_var(GVAR_ENABLE_TOUGH_MUTANTS)
#define hp_base            get_proto_data(crit_pid, 64)

#define set_hp(critter, hp) \
   if (crit_pid == critter) then begin \
      set_proto_data(crit_pid, PROTO_CR_BONUS_HP, ((hp - hp_base) * hp_overkill));  \
      if map_first_run then critter_heal(critter, crit_max_hp);                     \
      continue;                        \
   end                                 \
   noop

#define inc_plasma_res(x)  set_proto_data(crit_pid, PROTO_AR_DR_PLASMA, x)

procedure start begin
   call adjust_hp;
end

procedure map_enter_p_proc begin
   call adjust_hp;
end

procedure adjust_hp begin
   variable critter;
   variable base;
   if fo1in2_tough_mutants_enabled then begin
      if (cur_map_index > MAP_DESERT0) then begin
         foreach (critter in list_as_array(LIST_CRITTERS)) begin
            set_hp(PID_MASTER, 500); // No change to the Master, he is strong enough.
            set_hp(PID_LOU, 250); // No change to the Lou, he is strong enough with his guards.
            set_hp(PID_HARRY, 120); // No change to Harry for early game balance

            // Don't change the Watershed mutants, for early game balance
            if not(CUR_MAP_WATRSHD) then begin
               set_hp(PID_SUPER_MUTANT, 80); // orig. hp is 40
            end

            // Don't change the Necropolis invasion mutants, for early game balance
            if not(CUR_AREA_NECROPOLIS) then begin
               set_hp(PID_MAD_SUPER_MUTANT, 150); // orig. hp is 100
            end

            set_hp(PID_NIGHTKIN, 100); // orig. hp is 50
            set_hp(PID_NIGHTKIN_GUARD, 125); // orig. hp is 60
            set_hp(PID_TOUGH_SUPER_MUTANT, 115); // orig. hp is 75
            set_hp(PID_SUPER_MUTANT_GUARD, 90); // orig. hp is 50
            set_hp(PID_MEAN_SUPER_MUTANT, 100); // orig. hp is 60
            set_hp(PID_DEADLY_SUPER_MUTANT, 175); // orig. hp is 125
            set_hp(PID_TOUGH_NIGHTKIN, 140); // orig. hp is 70
            set_hp(PID_DEADLY_NIGHTKIN, 160); // orig. hp is 90
            set_hp(PID_SUPER_NIGHTKIN, 180); // orig. hp is 110
            set_hp(PID_MASTER_NIGHTKIN, 200); // orig. hp is 140
         end
      end
   end
end
