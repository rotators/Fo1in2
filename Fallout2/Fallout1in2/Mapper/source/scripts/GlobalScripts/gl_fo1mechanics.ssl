/*

   Adjust Fo2 core mechanics to get them closer to Fo1.

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "updatmap.h"
#include "voodoo.h"
#include "sfall/main.h"
#include "sfall/sfall.rotators.h"

#include "boneyard.h"

/* Standard Script Procedures */
procedure start;

procedure general;
procedure mode_change_handler;
procedure scene_master_destroyed;
procedure scene_vats_destroyed;
procedure night_person;
procedure item_description_handler;
procedure afterhitroll_handler;
procedure useanimobj_handler;
procedure useskillon_handler;
procedure useobjon_handler;
procedure calcapcost_handler;

procedure steal_urn;

variable dude_elev := 0;

#define get_active_hand(slot)  if (active_hand == 0) then slot := INVEN_TYPE_LEFT_HAND; \
                               else slot := INVEN_TYPE_RIGHT_HAND

import variable Neal_urn_ptr;

procedure start begin
   if (game_loaded) then begin
      set_global_script_type(0);
      register_hook_proc(HOOK_GAMEMODECHANGE, mode_change_handler);
      register_hook_proc(HOOK_DESCRIPTIONOBJ, item_description_handler);
      register_hook_proc(HOOK_AFTERHITROLL, afterhitroll_handler);
      register_hook_proc(HOOK_USESKILLON, useskillon_handler);
      register_hook_proc(HOOK_USEOBJON, useobjon_handler);
      register_hook_proc(HOOK_USEANIMOBJ, useanimobj_handler);
      //register_hook_proc(HOOK_CALCAPCOST, calcapcost_handler);
   end

   call general;
end

procedure general begin
   // Max skill is 200%
   set_skill_max(200);

   // Max rad resistance is 100%
   set_pc_stat_max(STAT_rad_resist, 100);

   // Changes the PipBoy rest timer from 08:00 to 06:00
   VOODOO_rest_till_0600;

   // Disable the "You encounter: ..." message
   VOODOO_disable_YouEncounter_message;

   // Fix dogmeat party member dialog
   VOODOO_dogmeat_pm_dialog;

   // Change the skill bonus from survivalist perk to 0.
   // The bonus for all perk ranks is applied via gl_survivalist.ssl script.
   VOODOO_survivalist_bonus;
end

procedure mode_change_handler begin
   call scene_master_destroyed;
   call scene_vats_destroyed;
   call night_person;

   // Activate special radiation death if > 1000 rem. See data/enddeath.txt
   set_global_var(GVAR_SPECIAL_RADIATION_DEATH, dude_rads);
end

procedure scene_master_destroyed begin
   if (get_game_mode == WORLDMAP) then begin
      if master_killed and not(cathedral_destroyed) then begin
         set_cathedral_destroyed;

         inc_cathedral_rep(REP_BONUS_BLOWN_UP_MASTER);

         give_xp_msg(EXP_MASTER_DESTROYED, gen_mstr(702));

         mark_map_entrance_state(MAP_CHILDRN1, 0);
         mark_map_entrance_state(MAP_CHILDRN2, 0);
         mark_map_entrance_state(MAP_MSTRLR12, 0);
         mark_map_entrance_state(MAP_MSTRLR34, 0);
         mark_map_entrance_elev_state(MAP_CHILDRN1, 1, 0); // We need this additionally, otherwise the stupid entry point won't disable itself.
         mark_map_entrance_state(MAP_CHILDEAD, 1);

         play_gmovie(CATHEXPLODE_MOVIE);

         if vats_destruction_started and waterchip_returned then begin
            force_encounter_with_flags(MAP_V13ENT, ENCOUNTER_FLAG_FADEOUT);
         end
      end
   end
end

procedure scene_vats_destroyed begin
   if (get_game_mode == WORLDMAP) then begin
      if vats_destruction_started and not(military_base_destroyed) then begin
         set_military_base_destroyed;

         inc_mbase_rep(REP_BONUS_BLOWN_UP_VATS);

         give_xp_msg(EXP_VATS_DESTROYED, gen_mstr(701));

         mark_map_entrance_state(MAP_MBENT, 0);
         mark_map_entrance_state(MAP_MBSTRG12, 0);
         mark_map_entrance_state(MAP_MBVATS12, 0);
         mark_map_entrance_state(MAP_MBDEAD, 1);

         play_gmovie(VATSEXPLODE_MOVIE);

         if master_killed and waterchip_returned then begin
            force_encounter_with_flags(MAP_V13ENT, ENCOUNTER_FLAG_FADEOUT);
         end
      end
   end
end

procedure night_person begin
   /* Night Person Trait
   As a night-time person, you are more awake when the sun goes down.
   Your Intelligence and Perception are improved at night, but dulled during the day. */
   if (dude_trait(TRAIT_night_person)) then begin
      if (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 0) then begin
         set_global_var(GVAR_NIGHT_PERSON_ACTIVE, 1);
         mod_skill_points_per_level(2);
      end

      if night and (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 1) then begin
         set_global_var(GVAR_NIGHT_PERSON_ACTIVE, 2);
         set_critter_extra_stat(dude_obj, STAT_pe, get_critter_extra_stat(dude_obj, STAT_pe) + 2);
         set_critter_extra_stat(dude_obj, STAT_iq, get_critter_extra_stat(dude_obj, STAT_iq) + 2);
         debug_msg("Night person trait is active!");
      end
      else if not(night) and (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 2) then begin
         set_global_var(GVAR_NIGHT_PERSON_ACTIVE, 1);
         set_critter_extra_stat(dude_obj, STAT_pe, get_critter_extra_stat(dude_obj, STAT_pe) - 2);
         set_critter_extra_stat(dude_obj, STAT_iq, get_critter_extra_stat(dude_obj, STAT_iq) - 2);
         debug_msg("Night person trait is not active!");
      end
   end
   // If used Mutate!-perk to remove Night Person, reset everything to default:
   else if (not(dude_trait(TRAIT_night_person)) and (global_var(GVAR_NIGHT_PERSON_ACTIVE) > 0)) then begin
      set_global_var(GVAR_NIGHT_PERSON_ACTIVE, 0);
      mod_skill_points_per_level(0);
      if night then begin
         set_critter_extra_stat(dude_obj, STAT_pe, get_critter_extra_stat(dude_obj, STAT_pe) - 2);
         set_critter_extra_stat(dude_obj, STAT_iq, get_critter_extra_stat(dude_obj, STAT_iq) - 2);
      end
      debug_msg("Night person trait is disabled!");
   end
end

procedure item_description_handler begin
   variable
      item := obj_pid(get_sfall_arg),
      description;

   if (item == PID_FARM_PARTS) and get_quest_farm_active then begin
      //if (is_success(roll_vs_skill(dude_obj, SKILL_SCIENCE, 0))) then begin
         description := get_string_pointer(message_str(SCRIPT_FARMPART, 101));
         set_sfall_return(description);
      //end
   end
end

procedure afterhitroll_handler begin
   variable
      willHit := get_sfall_arg,
      attacker := get_sfall_arg,
      target := get_sfall_arg,
      slot, item;

   if (attacker) then begin
      if (attacker == dude_obj) then begin
         get_active_hand(slot);
         item := critter_inven_obj(attacker, slot);
      end else begin
         item := get_right_hand(attacker);
      end

      if (item > 0) then begin
         if (obj_pid(item) == PID_MOLOTOV_COCKTAIL) then begin
            if (obj_type(target) == OBJ_TYPE_SCENERY and willHit > 1) then begin
               set_proto_data(PID_MOLOTOV_COCKTAIL, PROTO_WP_DMG_TYPE, DMG_explosion);
            end else begin
               set_proto_data(PID_MOLOTOV_COCKTAIL, PROTO_WP_DMG_TYPE, DMG_fire);
               set_attack_explosion_radius(2); // grenade radius
               set_attack_is_explosion_fire;
            end
         end
      end
   end
end

procedure useanimobj_handler begin
   variable
      critter := get_sfall_arg,
      target := get_sfall_arg,
      use_anim := get_sfall_arg,
      sound,
      training_anim;

   /*if (obj_pid(target) == PID_GIZMO_REMAINS) then begin
      obj_close(target);
   end*/

   if (obj_pid(target) == PID_PUNCHING_BAG) then begin
      if (get_script(target) == SCRIPT_ZSPNCHBG) then begin
         training_anim := random(0, 1);
         if (training_anim == 0) then
            training_anim := ANIM_throw_punch;
         else
            training_anim := ANIM_kick_leg;
         sound := sfx_build_char_name(critter, training_anim, snd_contact);
         reg_anim_play_sfx(critter, sound, 0);
         set_sfall_return(training_anim);

         //set_sfall_return(-1); // TODO: The animation started in SCRIPT_ZSPNCHBG will only run once if this is set to -1.
      end
   end
end

procedure useskillon_handler begin
   variable
      critter := get_sfall_arg,
      target := get_sfall_arg,
      skill := get_sfall_arg;

   if (skill == SKILL_STEAL) then begin
      if (obj_type(target) == OBJ_TYPE_SCENERY and obj_pid(target) == PID_TROPHY) then begin
         set_cursor_mode(0);
         reg_anim_begin();
         if (tile_distance_objs(critter, target) < 5) or (dude_is_sneaking) then
            reg_anim_obj_move_to_obj(critter, target, 0);
         else
            reg_anim_obj_run_to_obj(critter, target, 0);
         reg_anim_animate(critter, ANIM_magic_hands_middle, 0);
         reg_anim_callback(steal_urn);
         reg_anim_end();
      end
   end
end

procedure steal_urn begin
   if (Neal_urn_ptr != 0) then begin
      add_timer_event(Neal_urn_ptr, game_ticks(0), 1);
   end
end

procedure useobjon_handler begin
   variable
      target := get_sfall_arg,
      source := get_sfall_arg,
      item := get_sfall_arg;

   // Give 1 bottle cap when drinking a Nuka Cola :>
   if (obj_pid(item) == PID_NUKA_COLA) then
      item_caps_adjust(source, 1);
end

procedure calcapcost_handler begin
   variable
      critter := get_sfall_arg,
      attack_type := get_sfall_arg,
      is_aimed := get_sfall_arg,
      ap_cost := get_sfall_arg;

   /*
      In Fo1 vanilla, unarmed punch attacks could never go below 2AP/attack.
      The Fast Shot perk just gave the -1AP a bit faster, but then later when picking
      the Bonus HTH attack perk, it didn't change shit, it was still 2ap/attack.
      In Sfall's fix, the player get's fast shot fix's -1AP/attack for HTH and *also*
      can get -1AP/attack for HTH from 'bonus HTH attacks' rendering total number of
      AP player has equal the number of attacks per turn.

      tl;dr - melee and unarmed attacks should never go < 2 AP. However, it makes
      little sense to apply this logic only to melee and unarmed builds, so now no
      weapon will go below 2 AP (not sure if that was even achievable in Fo1 or now
      in Fo2... but just in case).
   */

   if (critter == dude_obj) then begin
      if (ap_cost < 2) then begin
         set_sfall_return(2);
      end
   end
end
