/*

   Brotherhood - Supply room guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_SUPGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if (Only_Once == 1) then begin
      set_self_team(TEAM_BROTHERHOOD);
      set_self_ai(AI_BROTHERHOOD_PALADIN);
      Only_Once := 0;
   end
end

procedure destroy_p_proc
begin

end

procedure critter_p_proc
begin
   if (map_var(18) != 0) then begin
      if (map_var(18) > 2) then begin
         tmp_hostile := 1;
      end
   end
// if (obj_can_see_obj(self_obj, dude_obj)) then begin
//    tmp_hostile := 1;
// end
   if (global_var(GVAR_ENEMY_BROTHERHOOD)) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure talk_p_proc
begin
   variable Line := 0;
   variable Msg := 0;
   dude_look_at_self;
   Line := random(601, 607);
   if (dude_is_male) and (Line == 607) then begin
      Line := 606;
   end
   else begin
      if (dude_is_female) and (Line == 606) then begin
         Line := 607;
      end
   end
   Msg := gen_mstr(Line);
   float_msg(self_obj, Msg, FLOAT_MSG_NORMAL);
end
