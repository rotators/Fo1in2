/*

   Brotherhood of Steel - Paladin Farli - cut content

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_FARLI
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure farli0;
procedure farli1;
procedure farli2;
procedure farli3;
procedure farli4;

#define LVAR_Herebefore       (4)

variable Herebefore;

procedure start begin
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure talk_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) > 1) then begin
      call farli4;
   end
   else begin
      if (local_var(LVAR_Herebefore) == 1) then begin
         call farli3;
      end
      else begin
         start_dialog_at_node(farli0);
      end
      set_local_var(LVAR_Herebefore, local_var(LVAR_Herebefore) + 1);
   end
end

procedure farli0 begin
   Reply(101);
   NOption(102, farli1, 3);
end

procedure farli1 begin
   Reply(103);
   NOption(mstr(104) + dude_name + mstr(105), farli2, 3);
end

procedure farli2 begin
   variable Item := 0;
   NMessage(106);
   Item := create_object(PID_SHOTGUN_SHELLS, 0, 0);
   add_obj_to_inven(dude_obj, Item);
end

procedure farli3 begin
   float_msg(self_obj, mstr(107), FLOAT_MSG_NORMAL);
end

procedure farli4 begin
   float_msg(self_obj, mstr(108), FLOAT_MSG_NORMAL);
end

procedure destroy_p_proc begin
   inc_good_critter
end
