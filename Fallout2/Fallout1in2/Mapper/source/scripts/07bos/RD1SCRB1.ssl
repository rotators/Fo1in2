/*

   Brotherhood - Random Scribe

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_RD1SCRB1
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure say_something;

variable tmp_hostile;
variable Only_Once := 1;
variable message;

procedure start begin
   if Only_Once then begin
      set_self_team(TEAM_BROTHERHOOD);
      set_self_ai(AI_BROTHERHOOD_SCRIBE);
   end
end

procedure combat
begin
   tmp_hostile := 1;
end

procedure critter_p_proc
begin
   if REP_ENEMY_BOS then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (local_var(1) == 0) then begin
         set_local_var(1, 1);
         if (self_can_see_dude) then begin
            call say_something;
         end
      end
   end
end

procedure pickup_p_proc
begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc
begin
   dude_look_at_self;
   call say_something;
end

procedure destroy_p_proc begin
   set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
   inc_good_critter
end

procedure look_at_p_proc
begin
   script_overrides;
   display_msg(mstr(101));
end

procedure say_something
begin
   if (local_var(0) == 0) then begin
      set_local_var(0, 1);
      float_msg(self_obj, mstr(random(102, 113)), FLOAT_MSG_RED);
   end
   else begin
      float_msg(self_obj, mstr(111), FLOAT_MSG_RED);
   end
end
