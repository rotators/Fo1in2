/*

   Brotherhood - High Elder
   Gives Mariposa quest + invasion support soldiers

*/

/* Include Files */
#include "define.h"
#include "necropolis.h"

#define NAME                    SCRIPT_HIGHELD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Meeting01;
procedure Meeting02;
procedure Meeting02a;
procedure Meeting03;
procedure Meeting04;
procedure Meeting05;
procedure Meeting05a;
procedure Meeting06;
procedure Meeting07;
procedure Meeting08;
procedure Meeting09;
procedure Meeting10;
procedure Meeting11;
procedure Meeting12;
procedure Meeting13;
procedure Elder01;
procedure Elder02;
procedure Elder03;
procedure Elder04;
procedure Elder05;
procedure Elder06;
procedure Elder07;
procedure Elder07a;
procedure Elder08;
procedure Elder09;
procedure Elder10;
procedure Elder11;
procedure Elder12;
procedure ElderEnd;

variable tmp_hostile;
variable Only_Once := 1;
variable awardex;
variable temp;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_BROTHERHOOD);
      set_self_ai(AI_BOS_ELDERS);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (self_can_see_dude and (tile_distance_objs(self_obj, dude_obj) < 3)) then begin
      if ((map_var(19) == 1) and (local_var(5) == 0)) then begin
         dialogue_system_enter;
      end
   end
   if (global_var(GVAR_ENEMY_BROTHERHOOD)) then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_BOS);

   dude_look_at_self;
   get_reaction
   if military_base_destroyed then begin
      start_dialog_at_node(Elder10);
   end
   else if ((map_var(19) == 1) and (local_var(5) == 0)) then begin
      set_local_var(5, 1);
      start_dialog_at_node(Meeting01);
   end
   else if (local_var(1) == 1) then begin
      call Elder12;
   end
   else begin
      if (local_var(5) == 1) then begin
         float_msg(self_obj, mstr(165), FLOAT_MSG_RED);
      end
      else if (local_var(4) == 0) then begin
         set_local_var(4, 1);
         start_dialog_at_node(Elder01);
      end
      else begin
         start_dialog_at_node(Elder04);
      end
   end
   if (awardex) then begin
      if (local_var(6) == 0) then begin
         set_local_var(6, 1);
         give_xp_msg(EXP_MBASE_PALADIN_SUPPORT, mstr(167));
      end
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Meeting01 begin
   Reply(mstr(134) + dude_name + mstr(135) + dude_name + mstr(136));
   NOption(137, Meeting02, 4);
   BLowOption(138, Meeting13);
end

procedure Meeting02 begin
   Reply(139);
   NOption(140, Meeting02a, 6);
   NOption(141, Meeting04, 4);
end

procedure Meeting02a begin
   if (global_var(GVAR_VREEDISK_HOLODISK_USED) == 1) or (global_var(GVAR_VREE_DISK_TRACKER) == 2) or (global_var(GVAR_VREE_GIVEN_DUDE_AUTOPSY) > 0) then begin// USED_DISK, DISK_REBUILT, DISK_GIVEN
      call Meeting03;
   end
   else begin
      call Meeting04;
   end
end

procedure Meeting03 begin
   Reply(142);
   NOption(143, Meeting04, 6);
end

procedure Meeting04 begin
   Reply(144);
   NOption(145, Meeting05, 4);
   BLowOption(138, Meeting13);
end

procedure Meeting05 begin
   Reply(146);
   if necropolis_invaded then begin
      NOption(147, Meeting06, 4);
   end
   NOption(148, Meeting05a, 4);
end

procedure Meeting05a begin
   if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
      call Meeting06;
   end
   else begin
      call Meeting07;
   end
end

procedure Meeting06 begin
   Reply(149);
   NOption(150, Meeting08, 4);
end

procedure Meeting07 begin
   Reply(151);
   NOption(mstr(152) + mstr(153), Meeting08, 4);
end

procedure Meeting08 begin
   Reply(154);
   NOption(155, Meeting09, 4);
   if ((global_var(GVAR_WORLDMAPLIST_MASTERS_LAIR) == 2) and not(cathedral_destroyed)) then begin
      NOption(156, Meeting10, 4);
   end
end

procedure Meeting09 begin
   awardex := 1;
   set_global_var(GVAR_BROTHERHOOD_SEND_HELP, 2);
   NMessage(157);
end

procedure Meeting10 begin
   Reply(mstr(158) + mstr(159));
   NOption(160, Meeting09, 4);
   NOption(161, Meeting11, 4);
   BLowOption(138, Meeting13);
end

procedure Meeting11 begin
   awardex := 1;
   set_global_var(GVAR_BROTHERHOOD_SEND_HELP, 1);
   NMessage(162);
end

procedure Meeting12 begin
   set_global_var(GVAR_BROTHERHOOD_SEND_HELP, 2);
end

procedure Meeting13 begin
   BMessage(164);
end

procedure Elder01 begin
   Reply(101);
   NOption(102, Elder02, 4);
   NOption(103, Elder08, 4);
   BLowOption(104, Elder11);
end

procedure Elder02 begin
   Reply(mstr(105) + mstr(106) + mstr(107));
   NOption(108, Elder03, 4);
   NOption(109, Elder09, 4);
   if military_base_visit then begin
      NOption(110, Elder07, 4);
   end
end

procedure Elder03 begin
   Reply(111);
   NOption(112, ElderEnd, 4);
end

procedure Elder04 begin
   Reply(113);
   if military_base_visit then begin
      NOption(114, Elder05, 4);
   end
   else begin
      NOption(115, Elder06, 4);
   end
end

procedure Elder05 begin
   Reply(116);
   NOption(117, Elder07, 4);
end

procedure Elder06 begin
   Reply(118);
   NOption(119, ElderEnd, 4);
end

procedure Elder07 begin
   Reply(120);
   NOption(121, ElderEnd, 4);
end

procedure Elder07a begin
   set_local_var(1, 1);
end

procedure Elder08 begin
   Reply(122);
   NOption(123, Elder02, 4);
   BOption(124, Elder07a, 4);
   BOption(125, Elder07a, 4);
end

procedure Elder09 begin
   Reply(126);
   NOption(127, ElderEnd, 4);
end

procedure Elder10 begin
   NMessage(128);
end

procedure Elder11 begin
   BMessage(130);
end

procedure Elder12 begin
   float_msg(self_obj, mstr(random(131, 133)), FLOAT_MSG_RED);
end

procedure ElderEnd begin
end
