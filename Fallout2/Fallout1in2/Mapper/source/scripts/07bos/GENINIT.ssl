/*

   Brotherhood - Generic Initiate

*/

/* Include Files */
#include "define.h"
#include "area/brotherhood.h"

#define NAME                    SCRIPT_RD1INIT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable temp;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_BROTHERHOOD);
      if horrible_critter_reaction then begin
         temp := random(BAD, NEUTRAL);
         set_local_var(LVAR_reaction_level, temp);
      end
      set_self_ai(AI_BROTHERHOOD_INITIATE);
   end
end

procedure map_enter_p_proc begin
   check_bos_ca_enabled
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if REP_ENEMY_BOS then begin
      tmp_hostile := 1;
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
   if (tmp_hostile) then begin
      set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   float_msg(self_obj, mstr(random(126, 132)), FLOAT_MSG_RED);
end

procedure destroy_p_proc begin
   set_global_var(GVAR_ENEMY_BROTHERHOOD, 1);
    inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(0) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(102));
   end
end
