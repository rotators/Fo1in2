/*

	Generic rats

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\vaultbur.h"

#define NAME                    SCRIPT_WANRATS

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure timed_event_p_proc;
procedure use_skill_on_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

variable initial := 0;
variable tmp_hostile := 0;
variable tmp_attacked;

#define LVAR_V15_Spawn        (10)

procedure start begin
   // Used for respawning rats in V15:
   if CUR_MAP_VAULTBUR then begin
      if (local_var(LVAR_V15_Spawn) == 0 and self_elevation == 2) then begin
         set_local_var(LVAR_V15_Spawn, 1);
         inc_map_var(MVAR_RoamingCount);
      end
   end
end

procedure map_enter_p_proc begin
	if (map_first_run) then begin
		if (self_pid == PID_LESSER_MOLE_RAT) or (self_pid == PID_GREATER_MOLE_RAT)/* or (obj_pid (self_obj) == 16777614)*/ then begin// MOLE
			set_self_ai(AI_MOLE_RATS);
		end
		else if (self_pid == PID_RAT_CAVE) or (self_pid == PID_TOUGH_RAT) or (self_pid == PID_RADIATED_RAT) or (self_pid == PID_TOUGH_RADIATED_RAT) /*or (obj_pid (self_obj) == 16777615) or (obj_pid (self_obj) == 16777616)*/ then begin// RAT
			set_self_ai(AI_RATS);
		end
		else if (self_pid == PID_PIGRAT) or (self_pid == PID_MEAN_PIG_RAT) /*or (obj_pid (self_obj) == 16777613)*/ then begin// PIG
			set_self_ai(AI_PIG_RATS);
		end

		set_self_team(TEAM_RATS);

		debug_msg("WANRATS m" + cur_map_index + " e" + self_elevation + " " + self_tile);
	end
   add_timer_event(self_obj, random(1, 5), 0);
end

procedure critter_p_proc begin
	if CUR_AREA_VAULT_13 then begin
		if v13_invaded or map_var(2) then begin
			if (local_var(9) != 1) then begin
				set_local_var(9, 1);//		<-- only once
				if not(is_loading_game) then begin
					set_self_visible;
				end
				debug_msg("Vault 13 invasion, killing WANRATS.SSL with DeathType " + 59 + ".  ");
				kill_critter(self_obj, ANIM_exploded_to_nothing_sf);
			end
		end
	end

	if (tmp_hostile and (tmp_attacked == 0)) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (dude_animal_friend == 0) then begin
			if (self_can_see_dude or self_can_hear_dude) then begin
				tmp_hostile := 1;
				attack(dude_obj);
			end
		end
	end
end

procedure timed_event_p_proc begin
	self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
	add_timer_event(self_obj, game_ticks(random(1, 5)), 0);
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_STEAL) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_GENCHAT, 211));
		script_overrides;
	end
end

procedure damage_p_proc begin
	if obj_in_party(source_obj) then begin
		party_remove(self_obj);
		set_self_team(TEAM_RATS);
		tmp_hostile := 1;
	end
end

procedure destroy_p_proc begin
   // Used for respawning rats in V15:
   if (CUR_MAP_VAULTBUR and self_elevation == 2) then begin
      dec_map_var(MVAR_RoamingCount);
   end
end

procedure talk_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_GENCHAT, 800));
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      tmp_attacked := 1;
   end
end
