#include "..\headers\define.h"

#define NAME                    SCRIPT_CHILD

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure critter_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;
procedure map_enter_p_proc;

procedure child01;
procedure child02;
procedure child03;
procedure child04;
procedure child05;
procedure child06;
procedure childend;

variable Only_Once := 1;
variable tmp_hostile;
variable Start_Moving;

// Fallout Fixt:
procedure PickDeadBodyType;
variable DeathType := 56;

procedure push_p_proc begin
end

procedure start begin
	if shady_invaded then begin
		if CUR_AREA_SHADY_SANDS then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin


		if Only_Once then begin
			Only_Once := 0;
			set_self_team(TEAM_SHADY_SANDS);
			set_self_ai(AI_LITTLE_KIDS);
			if (local_var(3) == 0) then begin
				set_local_var(3, self_tile);
			end
			if self_item_count(PID_ROCK) < 8 then begin
				variable Rocks := 0;
				Rocks := create_object(PID_ROCK, 0, 0);
				add_mult_objs_to_inven(self_obj, Rocks, random(8, 14));
			end
		end
	end
end

procedure map_enter_p_proc begin
	// We make you look like a real girl!
   if (self_pid == PID_GIRL) then begin
      art_change_fid_num(self_obj, FID_NFCHLD);
   end
end


procedure child01 begin
	gsay_reply(SCRIPT_CHILD, 101);
	NOption(102, childend, 4);
	NOption(103, child02, 5);
	NOption(104, child03, -3);
end

procedure child02
begin
	gsay_message(SCRIPT_CHILD, 105, NEUTRAL_REACTION);
end

procedure child03
begin
	gsay_message(SCRIPT_CHILD, 106, NEUTRAL_REACTION);
end

procedure child04
begin
	gsay_reply(SCRIPT_CHILD, 107);
	NOption(108, child05, 4);
	NOption(109, child06, 4);
	NOption(110, child03, -3);
end

procedure child05
begin
	gsay_message(SCRIPT_CHILD, 111, NEUTRAL_REACTION);
end

procedure child06
begin
	gsay_message(SCRIPT_CHILD, 112, NEUTRAL_REACTION);
end

procedure childend
begin
end

procedure critter_p_proc begin
	if ((game_time_hour > 600) and (game_time_hour < 1900)) then begin
		set_local_var(0, 0);
		set_local_var(1, 1);
	end
	else begin
		set_local_var(1, 0);
		set_local_var(0, 1);
	end
	if (CUR_MAP_SHADYE) then begin //  SHADY SANDS - EAST
		if (local_var(0) == 1) then begin
			if (self_tile != 14950) then begin
				self_run_to_tile(14950);
			end
			else begin
				if not(is_loading_game) then begin    set_self_invisible;    end
			end
		end
		else begin
			if not(is_loading_game) then begin    set_self_visible;    end//  MAKE VISIBLE
			if (random(0, 9) == 1) then begin//(10 * global_var(GVAR_CHILD_RUNS_HOW_OFTEN))) == 1) then begin
				if (random(0, 9) == 1) then begin
					self_walk_to_tile(tile_num_in_direction(local_var(3), random(0, 5), random(2, 4)));
				end
				else begin
					self_run_to_tile(tile_num_in_direction(local_var(3), random(0, 5), random(3, 14)));// distance was (3, 7)
				end
			end
		end
	end
	else begin
		if (CUR_MAP_SHADYW) then begin //  SHADY SANDS - WEST
			if (local_var(0) == 1) then begin
				if (self_tile != 20437) then begin
					self_run_to_tile(22443);
				end
				else begin
					if not(is_loading_game) then begin    set_self_invisible;    end
				end
			end
			else begin
				if not(is_loading_game) then begin    set_self_visible;    end//  MAKE VISIBLE
				if (random(0, 9) == 1) then begin//(10 * global_var(GVAR_CHILD_RUNS_HOW_OFTEN))) == 1) then begin
					if (random(0, 9) == 1) then begin
						self_walk_to_tile(tile_num_in_direction(local_var(3), random(0, 5), random(2, 4)));
					end
					else begin
						self_run_to_tile(tile_num_in_direction(local_var(3), random(0, 5), random(3, 14)));// distance was (3, 7)
					end
				end
			end
		end
		else begin
			if ((random(0, 15) == 1) and (Start_Moving == 0)) then begin
				add_timer_event(self_obj, game_ticks(random(3, 6)), 1);
			end
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	start_gdialog(237, self_obj, 4, -1, -1);
	gsay_start;
	if (dude_is_armed) then begin
		call child04;
	end
	else begin
		call child01;
	end
	gsay_end;
	end_dialogue;
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	inc_childkiller
	rm_timer_event(self_obj);
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHILD, 100));
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHILD, 100));
end

procedure timed_event_p_proc begin
	Start_Moving := 0;
	if (obj_on_screen(self_obj)) then begin
		self_run_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(3, 7)));
	end
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
