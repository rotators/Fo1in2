#include "define.h"

#define NAME                    SCRIPT_CHILD

#include "command.h"
#include "modreact.h"

procedure start;
procedure critter_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;
procedure map_enter_p_proc;

procedure child01;
procedure child02;
procedure child03;
procedure child04;
procedure child05;
procedure child06;
procedure childend;

#define LVAR_Sleeping         (0)
#define LVAR_1                (1) // ??
#define LVAR_Razlo_Kid        (2)
#define LVAR_Home_Tile        (3)
#define LVAR_Home_Rotation    (4)

#define razlo_kid             (local_var(LVAR_Razlo_Kid) != 0)
#define razlo_kid_home        (tile_in_tile_rect(23735, 23721, 23721, 25321, self_tile)) //top left, top right, bottom left, bottom right

variable Only_Once := 1;
variable tmp_hostile;
variable Start_Moving;

procedure push_p_proc begin
end

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_SHADY_SANDS);
      set_self_ai(AI_LITTLE_KIDS);
      if (local_var(LVAR_Home_Tile) == 0) then begin
         set_local_var(LVAR_Home_Tile, self_tile);
      end
      if self_item_count(PID_ROCK) < 8 then begin
         variable Rocks := 0;
         Rocks := create_object(PID_ROCK, 0, 0);
         add_mult_objs_to_inven(self_obj, Rocks, random(8, 14));
      end
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_AREA_SHADY_SANDS then begin
      invasion_kill_critter;
   end

   if map_first_run then begin
      set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);

      if CUR_MAP_SHADYW and razlo_kid_home then
         set_local_var(LVAR_Razlo_Kid, 1);
   end

   // We make you look like a real girl!
   if (self_pid == PID_GIRL) then
      art_change_fid_num(self_obj, FID_NFCHLD);
end

procedure child01 begin
   Reply(101);
   NOption(102, childend, 4);
   NOption(103, child02, 5);
   NLowOption(104, child03);
end

procedure child02 begin
   if razlo_kid and razlo_kid_home then
      NMessage(mstr(300 + random(0, 1)));
   else
      NMessage(105);
end

procedure child03 begin
   NMessage(106);
end

procedure child04 begin
   Reply(107);
   NOption(108, child05, 4);
   NOption(109, child06, 4);
   NLowOption(110, child03);
end

procedure child05 begin
   NMessage(111);
end

procedure child06 begin
   NMessage(112);
end

procedure childend begin
end

procedure critter_p_proc begin
   if ((game_time_hour > 600) and (game_time_hour < 1900)) then begin
      set_local_var(LVAR_Sleeping, 0);
      set_local_var(1, 1);
   end
   else begin
      set_local_var(1, 0);
      set_local_var(LVAR_Sleeping, 1);
   end
   if (CUR_MAP_SHADYE) then begin
      if (local_var(LVAR_Sleeping) == 1) then begin
         if (self_tile != 14950) then begin
            self_run_to_tile(14950);
         end
         else if not(is_loading_game) then set_self_invisible;
      end
      else begin
         if not(is_loading_game) then set_self_visible;
         if (random(0, 9) == 1) then begin
            self_run_to_tile(tile_num_in_direction(local_var(LVAR_Home_Tile), random(0, 5), random(3, 7)));
         end
      end
   end
   else if (CUR_MAP_SHADYW) then begin
      if (local_var(LVAR_Sleeping) == 1) then begin
         if razlo_kid then begin
            if (self_tile != local_var(LVAR_Home_Tile)) then
               self_run_to_tile(local_var(LVAR_Home_Tile));
         end
         else begin
            if (self_tile != 22443) then begin // was 20437
               self_run_to_tile(22443);
            end
            else if not(is_loading_game) then set_self_invisible;
         end
      end
      else begin
         if not(is_loading_game) then set_self_visible;
         if (random(0, 9) == 1) then begin
            self_run_to_tile(tile_num_in_direction(local_var(LVAR_Home_Tile), random(0, 5), random(3, 7)));
         end
      end
   end
   else begin
      if ((random(0, 15) == 1) and (Start_Moving == 0)) then begin
         add_timer_event(self_obj, game_ticks(random(3, 6)), 1);
      end
   end

   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure talk_p_proc begin
   if CUR_AREA_SHADY_SANDS then begin
      set_tma_data(NAME, 1000, -1, -1, TMA_MSG_DONTKNOW_START, TMA_MSG_DONTKNOW_END);
   end
   else if CUR_AREA_HUB then begin
      set_tma_data(NAME, 2000, -1, -1, TMA_MSG_DONTKNOW_START, TMA_MSG_DONTKNOW_END);
   end

   dude_look_at_self;
   if (dude_is_armed) then
      start_dialog_at_node(child04);
   else
      start_dialog_at_node(child01);
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure destroy_p_proc begin
   inc_good_critter
   inc_childkiller
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure timed_event_p_proc begin
   Start_Moving := 0;
   if (obj_on_screen(self_obj)) then begin
      self_run_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(3, 7)));
   end
end
