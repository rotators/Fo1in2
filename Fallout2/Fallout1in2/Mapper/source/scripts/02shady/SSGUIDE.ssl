/*

	Shady Sands - Seth in Radscorpion cave?

	   UNUSED! - Cut content

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_SSGUIDE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

procedure DialogWeapon;
procedure DialogFirstTime;
procedure DialogSubsequent;
procedure DialogExit;
procedure DialogWeapon1;
procedure DialogWeapon2;
procedure DialogWeapon3;
procedure DialogWeapon4;
procedure DialogMain;
procedure DialogMain1;
procedure DialogMain2;
procedure DialogMain3;
procedure DialogMain4;
procedure DialogMain5;
procedure DialogMain6;
procedure DialogMain7;
procedure DialogMain8;
procedure DialogMain9;
procedure DialogMain10;
procedure DialogSpecial1;
procedure DialogSpecial2;
procedure DialogSpecial3;
procedure DialogSpecial4;

variable Only_Once := 1;
variable tmp_hostile;
variable DisplayMessage;
variable Hurt;
variable Equipment;
variable Barter;
variable Bartered;
variable Place;
variable World;
variable Yourself;
variable Vault;
variable Vaulted;

procedure PickDeadBodyType;
variable DeathType := 56;


procedure start
begin
	if (map_first_run) and (local_var(7) != 1) then begin
		variable LVar0;
		set_local_var(7, 1);
		if (self_item( PID_BROWN_BAG ) > 0) then begin
			LVar0 := self_item( PID_BROWN_BAG );
			rm_obj_from_inven(self_obj, LVar0);
			destroy_object(LVar0);
			LVar0 := 0;
		end
		if (self_item( PID_LEATHER_JACKET ) > 0) then begin
			LVar0 := self_item( PID_LEATHER_JACKET );
			rm_obj_from_inven(self_obj, LVar0);
			destroy_object(LVar0);
			LVar0 := 0;
		end
	end
	if shady_invaded then begin
		if CUR_AREA_SHADY_SANDS then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(8) != 1) then begin
				set_local_var(8, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin


		if (Only_Once == 1) then begin
			Only_Once := 0;
			set_self_team(TEAM_SHADY_SANDS );
			set_self_ai( AI_PEASANT );
		end
		if (script_action == talk_proc) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
			call talk_p_proc;
		end
		else begin
			if (script_action == look_at_proc) then begin
				call look_at_p_proc;
			end
			else begin
				if (script_action == description_proc) then begin
					call description_p_proc;
				end
				else begin
					if (script_action == pickup_proc) then begin//<---caught stealing! (pickup_p_proc)
						call pickup_p_proc;
					end
					else begin
						if (script_action == destroy_proc) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
							call destroy_p_proc;
						end
						else begin
							if (script_action == critter_proc) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
								call critter_p_proc;
							end
							else begin
								if (script_action == damage_proc) then begin
									call damage_p_proc;
								end
							end
						end
					end
				end
			end
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(NAME, 100));
end

procedure description_p_proc
begin
	script_overrides;
	display_msg(message_str(NAME, 100));
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if global_var( GVAR_ENEMY_SHADY_SANDS ) then begin//   Is Shady Sands hostile to player?
		float_msg( self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED );
	end
	else begin
		get_reaction
		start_gdialog(211, self_obj, -1, -1, -1);
		gsay_start;
		if dude_is_armed then begin
			call DialogWeapon;
		end
		else begin
			if (local_var(4) == 0) then begin
				set_local_var(4, 1);
				call DialogFirstTime;
			end
			else begin
				call DialogSubsequent;
			end
		end
		gsay_end;
		end_dialogue;
		Hurt := 0;
		Equipment := 0;
		Barter := 0;
		Bartered := 0;
		Place := 0;
		World := 0;
		Yourself := 0;
		Vault := 0;
		Vaulted := 0;
	end
end

procedure critter_p_proc
begin
	if (self_can_see_dude) then begin
		if global_var( GVAR_ENEMY_SHADY_SANDS ) then begin//   Is Shady Sands hostile to player?
			attack(dude_obj);
		end
	end
end

procedure damage_p_proc
begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
	end
	if source_obj == dude_obj then begin
		if (has_rep_berserker) then begin
			set_global_var( GVAR_BERSERKER_REPUTATION, 1 );
			set_global_var( GVAR_CHAMPION_REPUTATION, 0 );
		end
		if (has_rep_champion) then begin
			set_global_var( GVAR_CHAMPION_REPUTATION, 1 );
			set_global_var( GVAR_BERSERKER_REPUTATION, 0 );
		end
		set_global_var( GVAR_GOOD_MONSTER, global_var( GVAR_GOOD_MONSTER ) + 1 );// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var( GVAR_GOOD_MONSTER ) % 2) == 0) then begin
			set_global_var( GVAR_PLAYER_REPUTATION, (check_general_rep - 1) );
		end
	end
end

procedure pickup_p_proc
begin
	if (local_var(6) == 0) then begin
		float_msg( self_obj, message_str(NAME, 151), FLOAT_MSG_RED );
		set_local_var(6, 1);
	end
	else begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
	end
end

procedure DialogWeapon
begin
	gsay_reply( NAME, 101 );
	giq_option( -3, NAME, 102, DialogWeapon1, NEUTRAL_REACTION );
	giq_option( 4, NAME, 103, DialogWeapon2, NEUTRAL_REACTION );
	giq_option( 4, NAME, 104, DialogWeapon3, NEUTRAL_REACTION );
	giq_option( 4, NAME, 105, DialogWeapon4, NEUTRAL_REACTION );
end

procedure DialogFirstTime begin
	gsay_reply( NAME, 111 );
	DisplayMessage := 125;
	giq_option( -3, NAME, 112, DialogSpecial1, NEUTRAL_REACTION );
	giq_option( 4, NAME, 113, DialogMain5, NEUTRAL_REACTION );
	giq_option( 4, NAME, 114, DialogMain, NEUTRAL_REACTION );
	giq_option( 5, NAME, 115, DialogMain8, NEUTRAL_REACTION );
	if tandi_is_kidnapped then begin
		giq_option( 4, NAME, 116, DialogMain9, NEUTRAL_REACTION );
	end
	if radscorp_quest_active then begin
		giq_option( 4, NAME, 117, DialogMain10, NEUTRAL_REACTION );
	end
	giq_option( 4, NAME, 118, DialogExit, NEUTRAL_REACTION );
end

procedure DialogSubsequent
begin
	variable LVar0 := 0;
	LVar0 := game_time_hour;
	gsay_reply( NAME, 144 );
	DisplayMessage := 125;
	giq_option( -3, NAME, 112, DialogSpecial1, NEUTRAL_REACTION );
	if ((LVar0 > 1800) or (LVar0 < 800)) then begin
		giq_option( 4, NAME, 145, DialogSpecial4, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, NAME, 113, DialogMain5, NEUTRAL_REACTION );
	end
	giq_option( 4, NAME, 114, DialogMain, NEUTRAL_REACTION );
	giq_option( 5, NAME, 115, DialogMain8, NEUTRAL_REACTION );
	if tandi_is_kidnapped then begin
		giq_option( 4, NAME, 116, DialogMain9, NEUTRAL_REACTION );
	end
	if radscorp_quest_active then begin
		giq_option( 4, NAME, 117, DialogMain10, NEUTRAL_REACTION );
	end
	giq_option( 4, NAME, 118, DialogExit, NEUTRAL_REACTION );
end

procedure DialogExit
begin
end

procedure DialogWeapon1
begin
	gsay_message( NAME, 106, NEUTRAL_REACTION );
end

procedure DialogWeapon2
begin
	gsay_reply( NAME, 107 );
	giq_option( 4, NAME, 108, DialogExit, NEUTRAL_REACTION );
end

procedure DialogWeapon3
begin
	gsay_reply( NAME, 109 );
	giq_option( 4, NAME, 110, DialogExit, NEUTRAL_REACTION );
end

procedure DialogWeapon4
begin
	gsay_reply( NAME, 109 );
	giq_option( 4, NAME, 110, DialogExit, NEUTRAL_REACTION );
end

procedure DialogMain
begin
	gsay_reply( NAME, DisplayMessage );
	if (Hurt == 0) then begin
		giq_option( 4, NAME, 126, DialogMain1, NEUTRAL_REACTION );
	end
	if (Equipment == 0) then begin
		giq_option( 4, NAME, 127, DialogMain2, NEUTRAL_REACTION );
	end
	else begin
		if (Barter == 1) then begin
			giq_option( 4, NAME, 134, DialogMain6, NEUTRAL_REACTION );
		end
	end
	if (Place == 0) then begin
		giq_option( 4, NAME, 128, DialogMain3, NEUTRAL_REACTION );
	end
	if (World == 0) then begin
		giq_option( 4, NAME, 129, DialogMain4, NEUTRAL_REACTION );
	end
	if (Yourself == 0) then begin
		giq_option( 4, NAME, 130, DialogMain5, NEUTRAL_REACTION );
	end
	else begin
		if (Vault == 1) then begin
			giq_option( 4, NAME, 138, DialogMain7, NEUTRAL_REACTION );
		end
	end
	giq_option( 4, NAME, 131, DialogExit, NEUTRAL_REACTION );
	if ((local_var(5) == 0) and ((Hurt == 1) and (Equipment == 1) and (Place == 1) and (World == 1) and (Yourself == 1) and (Bartered == 1) and (Vaulted == 1))) then begin
		set_local_var(5, 1);
		give_exp_points(250);
		display_msg(message_str(NAME, 150));
	end
end

procedure DialogMain1
begin
	Hurt := 1;
	DisplayMessage := 132;
	call DialogMain;
end

procedure DialogMain2
begin
	Equipment := 1;
	DisplayMessage := 133;
	Barter := 1;
	call DialogMain;
end

procedure DialogMain3
begin
	Place := 1;
	DisplayMessage := 135;
	call DialogMain;
end

procedure DialogMain4
begin
	World := 1;
	DisplayMessage := 136;
	call DialogMain;
end

procedure DialogMain5
begin
	Yourself := 1;
	DisplayMessage := 137;
	Vault := 1;
	call DialogMain;
end

procedure DialogMain6
begin
	Barter := 0;
	DisplayMessage := 139;
	Bartered := 1;
	call DialogMain;
end

procedure DialogMain7
begin
	Vault := 0;
	DisplayMessage := 140;
	Vaulted := 1;
	call DialogMain;
end

procedure DialogMain8
begin
	DisplayMessage := 143;
	call DialogMain;
end

procedure DialogMain9
begin
	DisplayMessage := 141;
	call DialogMain;
end

procedure DialogMain10
begin
	DisplayMessage := 142;
	call DialogMain;
end

procedure DialogSpecial1
begin
	gsay_reply( NAME, 119 );
	giq_option( -3, NAME, 120, DialogSpecial2, NEUTRAL_REACTION );
end

procedure DialogSpecial2
begin
	gsay_reply( NAME, 121 );
	giq_option( -3, NAME, 122, DialogSpecial3, NEUTRAL_REACTION );
end

procedure DialogSpecial3
begin
	gsay_reply( NAME, 123 );
	giq_option( -3, NAME, 124, DialogExit, NEUTRAL_REACTION );
end

procedure DialogSpecial4
begin
	gsay_reply( NAME, 146 );
	giq_option( 4, NAME, 147, DialogExit, NEUTRAL_REACTION );
end

procedure PickDeadBodyType
begin
	pick_dead_body_type
end
