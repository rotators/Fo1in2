/*

   Shady Sands - generic niceguy in town

*/

#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_PLEASANT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "command.h"
#include "modreact.h"

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;

variable tmp_hostile;
variable initial := 0;
variable round_counter;
variable PsstTime;

procedure push_p_proc begin
end

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_SHADY_SANDS);
      set_self_ai(AI_PEASANT);
      add_timer_event(self_obj, game_ticks(random(15, 60)), 1);
      add_timer_event(self_obj, game_ticks(random(3, 5)), 1);
   end
end

procedure map_enter_p_proc begin
   if shady_invaded then begin
      invasion_kill_critter;
   end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      round_counter := round_counter + 1;
      if (round_counter > 3) then begin
         if not(REP_ENEMY_SHADY_SANDS) then begin
            set_global_var(GVAR_ENEMY_SHADY_SANDS, 1);
            dec_general_rep(5);
         end
      end
   end
end

procedure critter_p_proc begin
   variable LVar0 := 0;
   if (self_can_see_dude) then begin
      if REP_ENEMY_SHADY_SANDS then begin
         tmp_hostile := 1;
      end
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (local_var(0)) then begin
         if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
            call flee_dude;
         end
      end
   end
   LVar0 := global_var(GVAR_CITIZEN_LOSER_PLEASANT_0);
   if ((((game_time / 10) - LVar0) >= 10) and (tile_distance_objs(self_obj, dude_obj) <= 4) and not(REP_ENEMY_SHADY_SANDS) and ((CUR_MAP_SHADYW) or (CUR_MAP_SHADYE))) then begin
      float_msg(self_obj, message_str(SCRIPT_CITIZEN, random(110, 114)), FLOAT_MSG_NORMAL);
      LVar0 := game_time / 10;
      set_global_var(GVAR_CITIZEN_LOSER_PLEASANT_0, LVar0);
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_local_var(0, 1);
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_SHADY_SANDS, 1);
   end
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(0) or REP_ENEMY_SHADY_SANDS) then
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_NORMAL);
   else if radscorp_quest_completed then
      float_msg(self_obj, mstr(random(101, 104)), FLOAT_MSG_NORMAL);
   else
      float_msg(self_obj, mstr(random(101, 107)), FLOAT_MSG_NORMAL);
end

procedure timed_event_p_proc begin
   if (obj_on_screen(self_obj)) then begin
      self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 5)));
   end
   add_timer_event(self_obj, game_ticks(random(6, 10)), 1);
end
