/*

   Shady Sands - Razlo

*/

/* Include Files */
#include "define.h"
#include "maps/shadyw.h"
#include "shadysands.h"

#define NAME                    SCRIPT_RAZLO
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure look_at_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

procedure razlo00;
procedure razlo00a;
procedure razlo01;
procedure razlo02;
procedure razlo03;
procedure razlo04;
procedure razlo05;
procedure razlo06;
procedure razlo06na;
procedure razlo06nb;
procedure razlo07;
procedure razlo08;
procedure razlo09;
procedure razlo10;
procedure razlo11;
procedure razlo12;
procedure razlo14;
procedure razlo15;
procedure razlo17;
procedure razlo18;
procedure razlo19;
procedure razlo20;
procedure razlo21;
procedure razlo22;
procedure razlo23;
procedure razlo23a;
procedure razlo24;
procedure razlo25;
procedure razlo26;
procedure razlo27;
procedure razlo27a;
procedure razlo28;
procedure razlo29;
procedure razlo30;
procedure razlo30a;
procedure razlo30b;
procedure razlo31;
procedure razlo00n;
procedure razlo00n_a;
procedure razlo01n;
procedure razlo02n;
procedure razlo03n;
procedure razlo04n;
procedure razlo05n;
procedure razlo06n;
procedure razlo07n;
procedure razlo08n;
procedure razlo09n;
procedure razlo10n;
procedure razlo11n;
procedure razlo14n;
procedure razlo15n;
procedure razlo16n;
procedure razlowmpa;
procedure razlowmpb;
procedure razlofixa;
procedure razlofixb;
procedure razlofixc;
procedure razlox;
procedure razloend;

procedure givestuff;
procedure remove_items;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

variable STIM;
variable FRUIT;
variable damage;
variable tmp_hostile;
variable heal;
variable COST;
variable BONUS;
variable NIGHT;
variable round_counter;
variable dummyvar;
variable MONEY;
variable MAX_HITS;
variable HITS;
variable initial := 0;
variable tail;

variable how_many_tails;

#define LVAR_Here_Before         (4)
#define LVAR_Talk_TandiRescued   (5)
#define LVAR_Invasion            (6)
#define LVAR_Tail_EXP            (7)
#define LVAR_Sleeping            (9)

#define craft_antidote_time      (14400)

#define craft_antidote_adv_time  gfade_out(1);                                                           \
                                    game_time_advance(game_ticks(craft_antidote_time * how_many_tails)); \
                                 gfade_in(1);                                                            \
                                 set_local_var(8, 1)

procedure start begin
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
      round_counter := round_counter + 1;
   end
   if (round_counter > 3) then begin
      if not(global_var(GVAR_ENEMY_SHADY_SANDS)) then begin//Shady Sands is NOT tmp_hostile to player;  i.e. globalvar ENEMY_SHADY_SANDS is not enabled
         set_global_var(GVAR_ENEMY_SHADY_SANDS, 1);
         dec_general_rep(5);
      end
   end
end

procedure map_enter_p_proc begin
   if shady_invaded then begin
      invasion_kill_critter;
   end
end

procedure map_update_p_proc begin
   if not(initial) then begin
      set_self_team(TEAM_SHADY_SANDS);
      set_self_ai(AI_PEASANT);
      home_tile := 24529;
      sleep_tile := 23133;
      wake_time := 630;
      sleep_time := 2230;
      initial := 0;
   end
end

procedure talk_p_proc begin
   set_tma_data(NAME, 1000, -1, -1, TMA_MSG_DONTKNOW_START, TMA_MSG_DONTKNOW_END);

   dude_look_at_self;
   MAX_HITS := dude_max_hp;
   HITS := dude_cur_hp;

   get_reaction
   CHECK_REP_BERSERKER
   CHECK_REP_CHAMPION

   if (local_var(9) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_GENVAULT, 166), FLOAT_MSG_NORMAL);
   end
   else begin
      if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
         set_local_var(LVAR_Invasion, 1);
         start_dialog_at_node(razlo00n);
      end
      else if radscorp_quest_active then begin
         if (tandi_is_kidnapped and (local_var(LVAR_Here_Before) == 0) and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
            set_local_var(LVAR_Here_Before, 1);
            start_dialog_at_node(razlo01);
         end
         else begin
            start_dialog_at_node(razlo23);
         end
      end
      else if (dude_item_count(PID_SCORPION_TAIL)) then begin
         start_dialog_at_node(razlo23);
      end
      else if ((local_var(LVAR_Invasion) == 0) and (local_var(LVAR_reaction_level) >= NEUTRAL)) then begin
         set_local_var(LVAR_Invasion, 1);
         start_dialog_at_node(razlo10);
      end
      else if (local_var(LVAR_Invasion) == 0) then begin
         set_local_var(LVAR_Invasion, 1);
         start_dialog_at_node(razlo18);
      end
      else if not(is_tandi_alive) then begin
         call razlo09;
      end
      else if (tandi_is_rescued and (local_var(LVAR_Talk_TandiRescued) == 0)) then begin
         set_local_var(LVAR_Talk_TandiRescued, 1);
         call razlo08;
      end
      else if (tandi_is_kidnapped and (local_var(LVAR_Here_Before) == 0) and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
         set_local_var(LVAR_Here_Before, 1);
         start_dialog_at_node(razlo01);
      end
      else if (tandi_is_kidnapped and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1) and (local_var(LVAR_Here_Before) == 1)) then begin
         set_local_var(LVAR_Here_Before, 2);
         call razlo07;
      end
      else if (has_rep_berserker or has_rep_childkiller) then begin
         start_dialog_at_node(razlo00);
         display_msg(mstr(103));
      end
      else if (local_var(LVAR_reaction_level) >= NEUTRAL) then begin
         start_dialog_at_node(razlo19);
      end
      else begin
         start_dialog_at_node(razlo21);
      end
   end
   call remove_items;
end

procedure razlo00 begin
   NMessage(101);
end

procedure razlo00a begin
   display_msg(mstr(103));
end

procedure razlo01 begin
   Reply(104);
   if (tandi_is_kidnapped and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(105, razlo03, 4);
   end
   NOption(143, razlo22, 4);
   NOption(144, razlo23a, 4);
   NLowOption(106, razlo02);
end

procedure razlo02 begin
   NMessage(107);
   set_local_var(LVAR_Here_Before, 1);
end

procedure razlo03 begin
   set_local_var(LVAR_Here_Before, 1);
   Reply(108);
   NOption(110, razlo04, 5);
   NOption(109, razloend, 4);
   NOption(111, razloend, 5);
end

procedure razlo04 begin
   Reply(112);
   NOption(113, razlo05, 4);
   NOption(114, razlo06, 4);
end

procedure razlo05 begin
   NMessage(115);
end

procedure razlo06 begin
   NMessage(116);
   call givestuff;
end

procedure razlo06na begin
   NMessage(306);
   call razlo06nb;
end

procedure razlo06nb begin
   variable Item := 0;

   NMessage(307);

   Item := create_object(PID_ANTIDOTE, 0, 0);
   add_obj_to_inven(dude_obj, Item);

   Item := dude_item(PID_SCORPION_TAIL);
   rm_obj_from_inven(dude_obj, Item);
   destroy_object(Item);

   if (local_var(LVAR_Tail_EXP) == 0) then begin
      set_local_var(LVAR_Tail_EXP, 1);
      give_xp_msg(EXP_MAKE_ANTIDOTE, mstr(300));
   end

   gfade_out(1);
   game_time_advance(game_ticks(1200 * damage));
   gfade_in(1);

   critter_heal(dude_obj, damage);
end

procedure razlo07 begin
   float_msg(self_obj, mstr(117), FLOAT_MSG_RED);
end

procedure razlo08 begin
   float_msg(self_obj, mstr(118), FLOAT_MSG_LIGHT_RED);
end

procedure razlo09 begin
   float_msg(self_obj, mstr(119), FLOAT_MSG_LIGHT_GREY);
end

procedure razlo10 begin
   Reply(120);
   NOption(122, razlo14, 4);
   NOption(144, razlo23a, 4);
   NOption(121, razloend, 4);
   NLowOption(123, razlo11);
end

procedure razlo11 begin
   NMessage(124);
   damage := MAX_HITS - HITS;
   if (damage > (MAX_HITS / 4)) then begin
      call razlo14;
   end
   else begin
      call razlo12;
   end
end

procedure razlo12 begin
   NMessage(125);
end

procedure razlo14 begin
   NMessage(126);
   damage := MAX_HITS - HITS;
   if (damage < (MAX_HITS / 4)) then begin
      call razlowmpa;
   end
   else if (damage < (MAX_HITS / 2)) then begin
      call razlofixa;
   end
   else if (damage < (3 * MAX_HITS / 4)) then begin
      call razlofixb;
   end
   else begin
      call razlofixc;
   end
end

procedure razlo15 begin
   NMessage(127);
end

procedure razlo17 begin
   variable LVar0 := 0;

   if (damage < (MAX_HITS / 2)) then begin
      LVar0 := create_object(PID_BARTER_LIGHT_HEALING, 0, 0);
      add_obj_to_inven(self_obj, LVar0);
   end
   else if (damage < (3 * MAX_HITS / 4)) then begin
      LVar0 := create_object(PID_BARTER_MEDIUM_HEALING, 0, 0);
      add_obj_to_inven(self_obj, LVar0);
   end
   else begin
      LVar0 := create_object(PID_BARTER_HEAVY_HEALING, 0, 0);
      add_obj_to_inven(self_obj, LVar0);
   end

   gdialog_mod_barter(0);
   NMessage(126);
   call razlo08n;
end

procedure razlo18 begin
   Reply(128);
   NOption(130, razlo14, 4);
   NOption(144, razlo23a, 4);
   NOption(129, razloend, 5);
end

procedure razlo19 begin
   Reply(131);
   NOption(132, razlo20, 4);
   NOption(143, razlo22, 4);
   NOption(144, razlo23a, 4);
   NLowOption(134, razlo11);
end

procedure razlo20 begin
   NMessage(135);
end

procedure razlo21 begin
   Reply(136);
   NOption(138, razlo22, 4);
   NOption(144, razlo23a, 4);
   NLowOption(139, razlo11);
   NOption(137, razloend, 4);
end

procedure razlo22 begin
   NMessage(140);
   call razlo14;
end

procedure razlo23 begin
   Reply(141);
   NOption(143, razlo22, 4);
   NOption(144, razlo23a, 4);
   NOption(145, razlo27, 5);

   if (dude_item_count(PID_SCORPION_TAIL)) then begin
      NOption(146, razlo30, 4);
      NLowOption(303, razlo30);
   end

   NOption(142, razloend, 4);
   NLowOption(147, razlo11);
end

procedure razlo23a begin
   if (local_var(8) == 1) then begin
      call razlo25;
   end
   else begin
      call razlo24;
   end
end

procedure razlo24 begin
   NMessage(148);
end

procedure razlo25 begin
   NMessage(149);
   if (dude_poison_stat != 0) then begin
      NMessage(150);
      poison(dude_obj, -dude_poison_stat);
      call razlo26;
   end
   else begin
      NMessage(151);
   end
end

procedure razlo26 begin
   NMessage(152);
end

procedure razlo27 begin
   NMessage(153);
   Reply(154);
   NOption(156, razlo29, 5);
   NOption(157, razlo28, 5);
   GOption(155, razlo27a, 4);
end

procedure razlo27a begin
   UpReact
end

procedure razlo28 begin
   Reply(158);
   NOption(159, razloend, 5);
end

procedure razlo29 begin
   NMessage(160);
end

procedure razlo30 begin
variable count_tails := dude_item_count(PID_SCORPION_TAIL);

   if (count_tails == 1) then begin
      how_many_tails := 1;

      NMessage(161);
      craft_antidote_adv_time;
      call razlo31;
   end
   else if (count_tails > 1) then begin
      variable num_text := num_to_num_text(count_tails);

      Reply(161);
      NOption(2000, razlo30a, 1);
      NOption(mstr(2001) + num_text + mstr(2002), razlo30b, 1);
      NOption(2003, razloend, 4);
      NLowOption(g_grunt, razloend);
   end

   // Original Fo1:
   /*
   NMessage(161);
   gfade_out(1);
   game_time_advance(game_ticks(14400));
   gfade_in(1);
   set_local_var(8, 1);
   call razlo31;
   */
end

procedure razlo30a begin
   how_many_tails := 1;
   craft_antidote_adv_time;
   call razlo31;
end

procedure razlo30b begin
   how_many_tails := dude_item_count(PID_SCORPION_TAIL);
   craft_antidote_adv_time;
   call razlo31;
end

procedure razlo31 begin
   variable Item := 0;

   NMessage(162);

   while (how_many_tails > 0) do begin
      how_many_tails--;

      Item := create_object(PID_ANTIDOTE, 0, 0);
      add_obj_to_inven(dude_obj, Item);

      tail := dude_item(PID_SCORPION_TAIL);
      rm_obj_from_inven(dude_obj, tail);
      destroy_object(tail);
   end

   if (local_var(LVAR_Tail_EXP) == 0) then begin
      set_local_var(LVAR_Tail_EXP, 1);
      give_xp_msg(EXP_MAKE_ANTIDOTE, mstr(300));
   end
end

procedure razlo00n begin
   Reply(163);
   NOption(164, razlo00n_a, 4);
   if radscorp_quest_active then begin
      NOption(165, razlo09n, 5);
   end
   if (tandi_is_kidnapped and (global_var(GVAR_TANDI_TALKED_ABOUT) == 1)) then begin
      NOption(166, razlo15n, 5);
   end
   NLowOption(167, razlo01n);
end

procedure razlo01n begin
   NMessage(168);
   DownReact
end

procedure razlo02n begin
   Reply(169);
   NOption(170, razlo04n, 4);
   NOption(171, razlo03n, 4);
end

procedure razlo03n begin
   NMessage(172);
   DownReact
end

procedure razlo04n begin
   Reply(173);
   NOption(174, razlo05n, 4);
end

procedure razlo05n begin
   damage := MAX_HITS - HITS;
   if (damage < (MAX_HITS / 4)) then begin
      call razlowmpb;
   end
   else if (damage < (MAX_HITS / 2)) then begin
      call razlofixa;
   end
   else if (damage < (3 * MAX_HITS / 4)) then begin
      call razlofixb;
   end
   else begin
      call razlofixc;
   end
end

procedure razlo06n begin
   Reply(186);
   NOption(187, razlo17, 4);
   if (dude_item_count(PID_SCORPION_TAIL)) then begin
      NOption(305, razlo06na, 4);
      NLowOption(303, razlo06na);
   end
   NOption(188, razloend, 4);
   NLowOption(304, razlo17);
   NLowOption(302, razloend);
end

procedure razlo07n begin
   GMessage(189);
end

procedure razlo08n begin
   if (dude_item_count(PID_BARTER_LIGHT_HEALING) or dude_item_count(PID_BARTER_MEDIUM_HEALING) or dude_item_count(PID_BARTER_HEAVY_HEALING)) then begin
      call razlo14n;
   end
   else if (dude_caps >= COST) then begin
      dude_caps_adjust(-COST);
      call razlo14n;
   end
   else begin
      call razlo06n;
   end
end

procedure razlo09n begin
   Reply(190);
   NOption(191, razlo10n, 6);
end

procedure razlo10n begin
   NMessage(192);
end

procedure razlo11n begin
   Reply(108);
   NOption(194, razlo04, 5);
   NOption(195, razloend, 5);
end

procedure razlo14n begin
   NMessage(196);
   gfade_out(1);
   game_time_advance(game_ticks(1200 * damage));
   gfade_in(1);
   critter_heal(dude_obj, damage);
   critter_uninjure(dude_obj, (DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
end

procedure razlo15n begin
   if (local_var(11) == 1) then begin
      call razlo16n;
   end
   else begin
      set_local_var(11, 1);
      call razlo11n;
   end
end

procedure razlo16n begin
   NMessage(197);
end

procedure razlowmpa begin
   NMessage(175);
end

procedure razlowmpb begin
   NMessage(176);
end

procedure razlofixa begin
   COST := 25;
   Reply(177);
   NOption(178, razlo08n, 4);
   NOption(179, razlo06n, 4);
   NLowOption(301, razlo08n);
   NLowOption(302, razloend);
end

procedure razlofixb begin
   COST := 50;
   Reply(180);
   NOption(181, razlo08n, 4);
   NOption(182, razlo06n, 4);
   NLowOption(301, razlo08n);
   NLowOption(302, razloend);
end

procedure razlofixc begin
   COST := 100;
   Reply(183);
   NOption(184, razlo08n, 4);
   NOption(185, razlo06n, 4);
   NLowOption(301, razlo08n);
   NLowOption(302, razloend);
end

procedure razlox begin
end

procedure razloend begin
end

procedure givestuff begin
   variable Item := 0;

   Item := create_object(PID_STIMPAK, 0, 0);
   add_obj_to_inven(dude_obj, Item);

   Item := create_object(PID_STIMPAK, 0, 0);
   add_obj_to_inven(dude_obj, Item);

   Item := create_object(PID_MUTATED_FRUIT, 0, 0);
   add_obj_to_inven(dude_obj, Item);

   if (local_var(8) == 1) then begin
      Item := create_object(PID_ANTIDOTE, 0, 0);
      add_obj_to_inven(dude_obj, Item);
   end
end

procedure remove_items begin
   variable Item := 0;
   if (dude_item_count(PID_BARTER_LIGHT_HEALING)) then begin
      Item := dude_item(PID_BARTER_LIGHT_HEALING);
      rm_obj_from_inven(dude_obj, Item);
      destroy_object(Item);
   end
   if (self_item_count(PID_BARTER_LIGHT_HEALING)) then begin
      Item := self_item(PID_BARTER_LIGHT_HEALING);
      rm_obj_from_inven(self_obj, Item);
      destroy_object(Item);
   end
   if (dude_item_count(PID_BARTER_MEDIUM_HEALING)) then begin
      Item := dude_item(PID_BARTER_MEDIUM_HEALING);
      rm_obj_from_inven(dude_obj, Item);
      destroy_object(Item);
   end
   if (self_item_count(PID_BARTER_MEDIUM_HEALING)) then begin
      Item := self_item(PID_BARTER_MEDIUM_HEALING);
      rm_obj_from_inven(self_obj, Item);
      destroy_object(Item);
   end
   if (dude_item_count(PID_BARTER_HEAVY_HEALING)) then begin
      Item := dude_item(PID_BARTER_HEAVY_HEALING);
      rm_obj_from_inven(dude_obj, Item);
      destroy_object(Item);
   end
   if (self_item_count(PID_BARTER_HEAVY_HEALING)) then begin
      Item := self_item(PID_BARTER_HEAVY_HEALING);
      rm_obj_from_inven(self_obj, Item);
      destroy_object(Item);
   end
end

procedure critter_p_proc begin
   if (map_var(MVAR_Caught_Stealing) == 1) then begin
      set_local_var(10, local_var(10) + 1);
      set_map_var(MVAR_Caught_Stealing, 0);
      if (local_var(10) < 3) then begin
         float_msg(self_obj, mstr(308), FLOAT_MSG_RED);
      end
      else begin
         tmp_hostile := 1;
      end
   end
   if (self_can_see_dude) then begin
      if global_var(GVAR_ENEMY_SHADY_SANDS) then begin
         tmp_hostile := 1;
      end
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure destroy_p_proc begin
   inc_good_critter
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_SHADY_SANDS, 1);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure razlo00n_a begin
   if (has_rep_berserker or has_rep_childkiller) then begin
      call razlo02n;
   end
   else begin
      call razlo00;
   end
end
