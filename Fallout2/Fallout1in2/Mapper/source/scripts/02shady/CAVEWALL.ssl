#include "define.h"
#include "command.h"
#include "shadysands.h"

#define NAME                        SCRIPT_CAVEWALL

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure damage_p_proc;

variable EXP_EARNED;

#define cave_entrance_hex  (21155)

procedure start begin
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   if (is_success(do_check(dude_obj, STAT_pe, 0)) or  dude_perk(PERK_bonus_awareness)) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure damage_p_proc begin
variable LVar0 := 0;
variable LVar1 := 0;

   if (map_var(0) == 0) then begin
      set_map_var(0, 1);
      gfade_out(1);

      Create_Cave_In(22345, 0)

      EXP_EARNED := (global_var(GVAR_SHADYSANDS_RADSCORPS_LEFT) * 75) + 75;
      if (EXP_EARNED < 300) then begin
         EXP_EARNED := 300;
      end
      if (global_var(GVAR_SHADYSANDS_RADSCORPS_LEFT) > 0) then begin
         EXP_EARNED := EXP_EARNED + 500;
      end

      set_global_var(GVAR_RADSCORPIONS_KILLED, 2);

      // Only show the "sealed"-message if there are still alive scorpions
      if (global_var(GVAR_SHADYSANDS_RADSCORPS_LEFT) > 0) then
         display_msg(message_str(SCRIPT_SSRADSCO, 188));

      give_xp(EXP_EARNED);

      inc_general_rep(3);
      inc_shady_rep(REP_BONUS_RADSCORPIONS_GONE);

      if not(radscorp_quest_completed) then begin
         set_radscorp_quest_completed;
      end
      gfade_in(1);

      // Player is still inside the cave and will die now:
      if (not(tile_distance(dude_tile, cave_entrance_hex) < 15)) then begin
         signal_end_game;
      end
   end
end
