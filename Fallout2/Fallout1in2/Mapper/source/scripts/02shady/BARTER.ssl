/*

	Shady Sands - Merchant in East Side

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\shadye.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_BARTER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;

procedure barter00;
procedure barter01;
procedure barter02;
procedure barter03;
procedure barter04;
procedure do_barter;
procedure BarterGuy00;
procedure BarterGuy01;
procedure BarterGuy02;
procedure BarterGuy03;
procedure BarterGuy04;
procedure BarterGuy05;
procedure BarterGuy06;
procedure BarterGuy07;
procedure BarterGuy08;
procedure BarterGuyEnd;

variable item;
variable in_combat;
variable new_obj;
variable new_obj_picked;
variable no_deal;
variable Herebefore;
variable tmp_hostile;
variable Only_Once := 1;
variable Caught_Stealing;
variable Here_To_Shop;

procedure PickDeadBodyType;
variable DeathType := 56;

procedure start begin
	if shady_invaded then begin
		if CUR_AREA_SHADY_SANDS then begin
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin
		if Only_Once then begin
			Only_Once := 0;
		   set_self_team(TEAM_SHADY_SANDS );
		   set_self_ai( AI_MERCHANT );
			exit_line := message_str(SCRIPT_MODREACT, random(100, 105));
		end
   end
end

procedure description_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure talk_p_proc begin
	dude_look_at_self;

	get_reaction

	start_gdialog(3, self_obj, 4, -1, -1);
	gsay_start;
	if (Caught_Stealing) then begin
		call BarterGuy00;
	end
	else if dude_is_armed then begin
		call BarterGuy02;
	end
	else begin
		//call get_stuff_from_safe;
		call BarterGuy01;
	end
	gsay_end;
	end_dialogue;
	//call put_stuff_in_safe;
end

procedure BarterGuy08 begin
	gdialog_mod_barter(0);
	gsay_message( SCRIPT_KILLIAN, exit_line, NEUTRAL_REACTION );
end

procedure BarterGuy01 begin
	gsay_reply( SCRIPT_BARTER, 101 );
	giq_option( 4, SCRIPT_BARTER, 102, do_barter, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_BARTER, 103, BarterGuyEnd, NEUTRAL_REACTION );
end

procedure BarterGuy04 begin
	no_deal := 1;
	gsay_reply( SCRIPT_BARTER, 107 );
end

procedure BarterGuy05 begin
	gsay_reply( SCRIPT_BARTER, 108 );
end

procedure BarterGuy03 begin
	gsay_reply( SCRIPT_BARTER, 104 );
	giq_option( 4, SCRIPT_BARTER, 105, BarterGuy05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_BARTER, 106, BarterGuy04, NEUTRAL_REACTION );
end

procedure BarterGuy06 begin
	gsay_reply( SCRIPT_BARTER, 109 );
end

procedure BarterGuy07 begin
	gsay_reply( SCRIPT_BARTER, 110 );
end

procedure BarterGuy00 begin
	gsay_message( SCRIPT_BARTER, 110, NEUTRAL_REACTION );
end

procedure BarterGuy02 begin
	gsay_message( SCRIPT_BARTER, 113, NEUTRAL_REACTION );
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
   inc_good_critter
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if not(local_var(4)) then begin
		display_msg(mstr(112));
		set_local_var(4, 1);
	end
	else begin
		display_msg(mstr(100));
	end
end

procedure timed_event_p_proc begin
	if (fixed_param == 1) then begin
		Here_To_Shop := 1;
		dialogue_system_enter;
	end
	else if (fixed_param == 2) then begin
		Caught_Stealing := 1;
		dialogue_system_enter;
	end
end

procedure BarterGuyEnd begin
end

procedure do_barter begin
	gdialog_mod_barter(0);
	gsay_reply( SCRIPT_MODREACT, 100 );
	giq_option( 4, SCRIPT_DUC, 129, BarterGuyEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_DUC, 131, BarterGuyEnd, NEUTRAL_REACTION );
end

procedure barter00 begin
	float_msg( self_obj, mstr(110), FLOAT_MSG_NORMAL );
end

procedure barter01 begin
	variable Item := 0;

	gsay_message( SCRIPT_BARTER, 108, NEUTRAL_REACTION );
	gfade_out(1);
	game_time_advance(600);
	gfade_in(1);

	Item := create_object( PID_KNIFE, 0, 0 );
	add_obj_to_inven(dude_obj, Item);
	Item := create_object( PID_SPEAR, 0, 0 );
	add_obj_to_inven(dude_obj, Item);
	Item := create_object( PID_STIMPAK, 0, 0 );
	add_obj_to_inven(dude_obj, Item);
end

procedure barter02 begin
	set_global_var( GVAR_BARTER_WAS_GIVEN, 1 );
	gsay_reply( SCRIPT_BARTER, 104 );
	giq_option( 4, SCRIPT_BARTER, 105, barter01, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_BARTER, 106, barter04, NEUTRAL_REACTION );
end

procedure barter03 begin
	if (global_var( GVAR_RADSCORPION_SEED ) <= 1) then begin
		gsay_message( SCRIPT_BARTER, 109, NEUTRAL_REACTION );
	end
	if radscorp_quest_completed then begin
		gsay_message( SCRIPT_BARTER, 111, NEUTRAL_REACTION );
	end
end

procedure barter04 begin
	no_deal := 1;
	gsay_message( SCRIPT_BARTER, 107, NEUTRAL_REACTION );
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
