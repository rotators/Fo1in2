/*

   Shady Sands - Merchant in East Side

*/

/* Include Files */
#include "define.h"
#include "maps/shadye.h"
#include "shadysands.h"

#define NAME                    SCRIPT_BARTER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "command.h"
#include "modreact.h"
#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;

procedure barter00;
procedure barter01;
procedure barter02;
procedure barter03;
procedure barter04;
procedure do_barter;
procedure BarterGuy00;
procedure BarterGuy01;
procedure BarterGuy02;
procedure BarterGuy03;
procedure BarterGuy04;
procedure BarterGuy05;
procedure BarterGuy06;
procedure BarterGuy07;
procedure BarterGuyEnd;

procedure Get_Stuff;
procedure Put_Stuff;

import variable Barter_Box_Ptr;

variable item;
variable in_combat;
variable new_obj;
variable new_obj_picked;
variable no_deal;
variable Herebefore;
variable tmp_hostile;
variable Only_Once := 1;
variable Caught_Stealing;
variable Here_To_Shop;

#ifndef barter_mod_initial
   #define barter_mod_initial          (30) // vanilla : 0
#endif

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_SHADY_SANDS);
      set_self_ai(AI_MERCHANT);
   end
end

procedure map_enter_p_proc begin
   if shady_invaded and CUR_AREA_SHADY_SANDS then begin
      invasion_kill_critter;
   end

   if fo1in2_shady_merchant_enabled then begin
      set_self_visible;
   end
   else begin
      set_self_invisible;
   end
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_SHADYE);

   dude_look_at_self;

   get_reaction
   gdialog_set_barter_mod(barter_mod_initial);

   call Get_Stuff;
   if (Caught_Stealing) then begin
      start_dialog_at_node(BarterGuy00);
   end
   else if dude_is_armed then begin
      start_dialog_at_node(BarterGuy02);
   end
   else begin
      start_dialog_at_node(BarterGuy01);
   end
   call Put_Stuff;
end

procedure BarterGuy01 begin
   Reply(101);
   NOption(102, do_barter, 4);
   NOption(103, BarterGuyEnd, 4);
end

procedure BarterGuy04 begin
   no_deal := 1;
   Reply(107);
end

procedure BarterGuy05 begin
   Reply(108);
end

procedure BarterGuy03 begin
   Reply(104);
   NOption(105, BarterGuy05, 4);
   NOption(106, BarterGuy04, 4);
end

procedure BarterGuy06 begin
   Reply(109);
end

procedure BarterGuy07 begin
   Reply(110);
end

procedure BarterGuy00 begin
   NMessage(110);
end

procedure BarterGuy02 begin
   NMessage(113);
end

procedure destroy_p_proc begin
   inc_good_critter
   move_obj_inven_to_obj(Barter_Box_Ptr, self_obj);
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if not(local_var(4)) then begin
      display_msg(mstr(112));
      set_local_var(4, 1);
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      Here_To_Shop := 1;
      dialogue_system_enter;
   end
   else if (fixed_param == 2) then begin
      Caught_Stealing := 1;
      dialogue_system_enter;
   end
end

procedure BarterGuyEnd begin
end

procedure do_barter begin
   gdialog_mod_barter(barter_mod_initial);
   gsay_reply(SCRIPT_MODREACT, 100);
   giq_option(4, SCRIPT_DUC, 129, BarterGuyEnd, NEUTRAL_REACTION);
   giq_option(-3, SCRIPT_DUC, 131, BarterGuyEnd, NEUTRAL_REACTION);
end

procedure barter00 begin
   float_msg(self_obj, mstr(110), FLOAT_MSG_NORMAL);
end

procedure barter01 begin
   variable Item := 0;

   NMessage(108);
   gfade_out(1);
   game_time_advance(600);
   gfade_in(1);

   Item := create_object(PID_KNIFE, 0, 0);
   add_obj_to_inven(dude_obj, Item);
   Item := create_object(PID_SPEAR, 0, 0);
   add_obj_to_inven(dude_obj, Item);
   Item := create_object(PID_STIMPAK, 0, 0);
   add_obj_to_inven(dude_obj, Item);
end

procedure barter02 begin
   set_global_var(GVAR_BARTER_WAS_GIVEN, 1);
   Reply(104);
   NOption(105, barter01, 4);
   NOption(106, barter04, 4);
end

procedure barter03 begin
   if (global_var(GVAR_RADSCORPION_SEED) <= 1) then begin
      NMessage(109);
   end
   if radscorp_quest_completed then begin
      NMessage(111);
   end
end

procedure barter04 begin
   no_deal := 1;
   NMessage(107);
end

procedure Get_Stuff begin
   get_barter_inven(Barter_Box_Ptr);
end

procedure Put_Stuff begin
   put_barter_inven(Barter_Box_Ptr);
end
