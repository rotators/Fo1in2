/*

   Necropolis - Sewer King / albino mole rat

*/

/* Include Files */
#include "define.h"
#include "area/necropolis.h"

#define NAME                    SCRIPT_NKINGRAT

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure timed_event_p_proc;
procedure use_skill_on_p_proc;
procedure destroy_p_proc;

#define LVAR_Home_Tile     (4)

variable tmp_hostile := 0;

procedure start begin
   if (local_var(LVAR_Home_Tile) == 0) then begin
      set_local_var(LVAR_Home_Tile, self_tile);
   end
   add_timer_event(self_obj, random(1, 5), 0);
end

procedure map_enter_p_proc begin
   if (map_first_run) then begin
      if (self_pid == PID_SEWER_KING) then
         set_self_ai(AI_MOLE_RATS);
      else
         set_self_ai(AI_RATS);
      set_self_team(TEAM_RATS);
   end
   add_timer_event(self_obj, random(1, 5), 0);
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else if (self_can_see_dude) then begin
      tmp_hostile := 1;
      attack(dude_obj);
   end
end

procedure timed_event_p_proc begin
   if (self_pid == PID_SEWER_KING) then begin
      if not(combat_is_initialized) then
         self_walk_to_tile(tile_num_in_direction(local_var(LVAR_Home_Tile), random(0, 5), random(1, 3)));
      add_timer_event(self_obj, game_ticks(random(10, 15)), 0);
   end
   else begin
      if not(combat_is_initialized) then
         self_walk_to_tile(tile_num_in_direction(local_var(LVAR_Home_Tile), random(0, 5), random(1, 3)));
      add_timer_event(self_obj, game_ticks(random(1, 5)), 0);
   end
end

procedure use_skill_on_p_proc begin
end

procedure damage_p_proc begin
   if obj_in_party(source_obj) then begin
      tmp_hostile := 1;
   end
end

procedure destroy_p_proc begin
   inc_evil_critter
end

procedure combat_p_proc begin
end
