/*

   Necropolis - Garret

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GUARD2
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure damage_p_proc;

variable Only_Once := 1;
variable tmp_hostile;
variable oktoyell := 1;
variable active;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_GANGER);
      set_self_ai(AI_GHOUL_NASTY);
      if (global_var(GVAR_HUB_FILLER_30) == 1) then begin
         active := 1;
         if not(is_loading_game) then set_self_visible;
      end
      else begin
         if not(is_loading_game) then set_self_invisible;
      end
   end
end

procedure talk_p_proc begin
   if (necropolis_wpump_fixed) then begin
      float_msg(self_obj, mstr(102), FLOAT_MSG_YELLOW);
   end
   else begin
      float_msg(self_obj, mstr(101), FLOAT_MSG_YELLOW);
   end
end

procedure critter_p_proc begin
   if (active) then begin
      if (global_var(GVAR_ENEMY_NECROPOLIS) and self_can_see_dude) then begin
         tmp_hostile := 1;
      end
      if (tmp_hostile) then begin
         tmp_hostile := 0;
         set_global_var(GVAR_HUB_FILLER_30, 0);
         set_global_var(GVAR_ENEMY_NECROPOLIS, 1);
         attack(dude_obj);
      end
      else if not(necropolis_wpump_fixed) then begin
         if (tile_distance_objs(self_obj, dude_obj) > 7) then begin
            tmp_hostile := 1;
         end
         else if ((tile_distance_objs(self_obj, dude_obj) > 4) and oktoyell) then begin
            oktoyell := 0;
            float_msg(self_obj, mstr(103), FLOAT_MSG_YELLOW);
         end
         else if (tile_distance_objs(self_obj, dude_obj) < 5) then begin
            oktoyell := 1;
         end
      end
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc begin
tmp_hostile := 1;
end

procedure destroy_p_proc begin
   debug("test");
   set_global_var(GVAR_GARRET_STATUS, 3);
   set_global_var(GVAR_HUB_FILLER_30, 0);
   set_global_var(GVAR_ENEMY_NECROPOLIS, 1);
   inc_inv_evil_crit
end
