/*******************************************************************************
        Name : Old bed
        Location : Necropolis
        Description : Griffith is hiding the fuel cell controller for the motorcycle here.

            Created : by sFall Script Editor
            Updated:

*******************************************************************************/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\sfall\main.h"
#include "..\headers\necropolis.h"
#include "..\headers\maps\watrshd.h"

#define NAME                    SCRIPT_GRIFFBED

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure spatial_p_proc;
procedure description_p_proc;
procedure look_at_p_proc;
procedure use_p_proc;

/* Defines */
#define LVAR_Only_Once     (0)

#define usable_chair       if (self_pid == PID_RED_ARMCHAIR and map_var(MVAR_FUEL_CELL_STASH) == STATE_SPOTTED) then    \
                              set_proto_data(self_pid, PROTO_IT_FLAGS, ITEM_ACTION_USE);                                  \
                           else                                                                                         \
                              set_proto_data(self_pid, PROTO_IT_FLAGS, RMOBJ_USE_OBJ)

variable Perception_Check;
variable Item;

procedure start begin
   usable_chair;
end

procedure spatial_p_proc begin
   if (map_var(MVAR_FUEL_CELL_STASH) == STATE_HIDDEN) and not(fo1in2_motorcycle_disabled) then begin
      if source_obj == dude_obj then begin
         if (is_success(do_check(dude_obj, STAT_pe, -5))) and (is_success(do_check(dude_obj, STAT_lu, -2))) then begin
            display_msg(mstr(200));
            set_map_var(MVAR_FUEL_CELL_STASH, STATE_SPOTTED);

            set_proto_data(PID_RED_ARMCHAIR, PROTO_IT_FLAGS, ITEM_ACTION_USE);
            destroy_object(self_obj);
         end
      end
   end
end

procedure look_at_p_proc begin
   usable_chair;
end

procedure description_p_proc begin
   if not(fo1in2_motorcycle_disabled) then begin
      script_overrides;

      if (map_var(MVAR_FUEL_CELL_STASH) == STATE_HIDDEN) and (self_distance_from_dude < 5) then begin
         if (dude_perception > 5 or is_success(roll_vs_skill(dude_obj, SKILL_OUTDOORSMAN, -40)) or is_success(roll_vs_skill(dude_obj, SKILL_STEAL, -20)))  then begin
            display_msg(mstr(200));
            set_map_var(MVAR_FUEL_CELL_STASH, STATE_SPOTTED);
         end
      end
      else if (map_var(MVAR_FUEL_CELL_STASH) == STATE_SPOTTED) then begin
         display_msg(mstr(200));
      end
      else begin
         display_msg(mstr(random(100, 102)));
      end
      usable_chair;

   end
end

procedure use_p_proc begin
   if (map_var(MVAR_FUEL_CELL_STASH) == STATE_SPOTTED) then begin
      script_overrides;

      display_msg(mstr(120));

      Item := create_object(PID_MOTO_KEY, 0, 0);
      add_mult_objs_to_inven(dude_obj, Item, 1);

      set_map_var(MVAR_FUEL_CELL_STASH, STATE_TAKEN_DUDE);
      usable_chair;
   end
end
