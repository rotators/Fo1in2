/*

   Necropolis - Watershed super mutant

*/

/* Include Files */
#include "define.h"
#include "necropolis.h"

#define NAME                    SCRIPT_SALLY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure critter_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

variable tmp_hostile;
variable init_teams := 0;
variable noevent;
variable loopcount;

#define float(x)                    float_msg(self_obj, mstr(x), FLOAT_MSG_GREEN)

procedure start begin
   if (not(init_teams)) then begin
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_NECRO);
      set_local_var(1, 0);
      init_teams := 1;
   end
end

procedure map_enter_p_proc begin
   if (global_var(GVAR_WATERSHED_MUTANTS_LEAVE) > 0) then begin
      if not(is_loading_game) then set_self_invisible;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if ((global_var(GVAR_SUPER_MUTANTS_KILLED) > 0) and self_can_see_dude and (global_var(GVAR_WATERSHED_MUTANTS_LEAVE) < 1)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile > 0) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      sm_leave_watershed;
   end
end

procedure talk_p_proc begin
   if (global_var(GVAR_SUPER_MUTANTS_KILLED) < 1) then begin
      if (global_var(GVAR_WATERSHED_MUTANTS_LEAVE) == 0) then begin
         dude_look_at_self;
         float(101);
      end
      else begin
         float(102);
      end
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_SUPER_MUTANTS_KILLED, global_var(GVAR_SUPER_MUTANTS_KILLED) + 1);

   check_watershed_sm_killed;
   inc_evil_critter
end
