/*

   Necropolis - Hotel Manager

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_MANAGER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "command.h"
//#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure combat_p_proc;

procedure ghoul00;
procedure ghoul01;
procedure ghoul02;
procedure ghoul03;
procedure ghoul04;
procedure ghoul05;
procedure ghoul05a;
procedure ghoul06;
procedure ghoul07;
procedure ghoul08;
procedure ghoul09;
procedure ghoul10;
procedure ghoul11;
procedure ghoulend;
procedure ghoulcbt;

variable tmp_hostile;
variable initial := 0;
variable noevent;
variable loopcount;

#define LVAR_Herebefore   (4)
#define LVAR_Examined     (5)

procedure start begin
   if (not(initial)) then begin
      initial := 1;
      set_self_team(TEAM_32);//  TEAM -- was 32 in Manager.ssl, 30 in Ghoul.ssl
      set_self_ai(AI_GHOUL_NASTY);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(200));
   set_local_var(LVAR_Examined, 1);
end

procedure look_at_p_proc begin
   script_overrides;
   if (not(local_var(LVAR_Examined)) and not(local_var(LVAR_Herebefore))) then
      display_msg(mstr(100));
   else
      display_msg(mstr(201));
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if ((game_time_hour >= 1900) or (game_time_hour < 600) and (self_tile != 22531)) then begin
         self_walk_to_tile(22531);
      end
      if (day and (self_tile != 24730)) then begin
         self_walk_to_tile(24730);
      end

      loopcount := loopcount + 1;
      if (loopcount > 40) then begin
         loopcount := 0;
         if (tile_distance_objs(self_obj, dude_obj) < 7) then begin
            if (self_can_see_dude) then begin
               if (noevent == 0) then begin
                  noevent := 1;
                  add_timer_event(self_obj, game_ticks(5), 0);
               end
            end
         end
      end
   end
end

procedure destroy_p_proc begin
   inc_neutral_critter
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      tmp_hostile := 1;
   end
   else if (local_var(LVAR_Herebefore) == 0) then begin
      dialogue_system_enter;
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_HOTEL);

   dude_look_at_self;
   if (local_var(LVAR_Herebefore)) then begin
      call ghoul08;
   end
   else begin
      set_local_var(LVAR_Herebefore, 1);
      start_dialog_at_node(ghoul00);
   end
end

procedure ghoulend begin
end

procedure ghoulcbt begin
   add_timer_event(self_obj, game_ticks(2), 1);
end

procedure ghoul00 begin
   Reply(101);
   NOption(102, ghoul01, 6);
   NOption(103, ghoul04, 4);
   NOption(118, ghoul09, 4);
   NOption(104, ghoul05, 4);
   NLowOption(105, ghoulend);
end

procedure ghoul01 begin
   Reply(106);
   NOption(107, ghoul02, 6);
   NOption(108, ghoul03, 6);
end

procedure ghoul02 begin
   NMessage(109);
end

procedure ghoul03 begin
   NMessage(110);
end

procedure ghoul04 begin
   NMessage(111);
end

procedure ghoul05 begin
   Reply(112);
   NOption(113, ghoul07, 4);
   NOption(114, ghoul05a, 4);
end

procedure ghoul05a begin
   if (is_success(do_check(dude_obj, STAT_heal_rate, 0))) then begin
      call ghoul06;
   end
   else begin
      call ghoul07;
   end
end

procedure ghoul06 begin
   NMessage(115);
end

procedure ghoul07 begin
   NMessage(116);
   call ghoulcbt;
end

procedure ghoul08 begin
   float_msg(self_obj, mstr(117), FLOAT_MSG_LIGHT_RED);
   call ghoulend;
end

procedure ghoul09 begin
   Reply(119);
   NOption(120, ghoul10, 4);
   NOption(121, ghoul11, 4);
end

procedure ghoul10 begin
   NMessage(122);
end

procedure ghoul11 begin
   NMessage(123);
end

procedure combat_p_proc begin
end
