/*

   Necropolis - Old computer terminal in the Motel.

*/

#include "define.h"

#define NAME                    SCRIPT_DEADCOMP

#include "command.h"

procedure start;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure use_p_proc;
procedure use_skill_on_p_proc;

procedure PlayDemo;

#define EVENT_FLOAT     (1)

variable Damage;
variable DisplayLine := 0;

#define LVAR_Comp_Fixed       (4)
#define LVAR_EXP_Earned       (5)

procedure start begin
end

procedure map_enter_p_proc begin
   if (local_var(LVAR_Comp_Fixed) == 1) and not(fo1_demo_completed) then begin
      add_timer_event(self_obj, game_ticks(6), EVENT_FLOAT);
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_FLOAT and fo1in2_scrapheap_enabled) then begin
      if not(fo1_demo_completed) then begin
         floater(1000 + DisplayLine);
         DisplayLine := DisplayLine + 1;
         if (DisplayLine > 3) then DisplayLine := 0;
         flush_add_timer_event(self_obj, game_ticks(10), EVENT_FLOAT);
      end
   end
end

procedure use_p_proc begin
   script_overrides;
   if (fo1in2_scrapheap_enabled) then begin
      if fo1_demo_completed then begin
         display_msg(mstr(100));
      end
      else begin
         if (local_var(LVAR_Comp_Fixed) == 0) then begin
            display_msg(message_str(SCRIPT_ROBCTRL,196));
         end
         else begin
            call PlayDemo;
         end
      end
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure use_skill_on_p_proc begin
   if (fo1in2_scrapheap_enabled and local_var(LVAR_Comp_Fixed) == 0) then begin
      if (action_being_used == SKILL_SCIENCE) then begin
         if (local_var(LVAR_Comp_Fixed) == 0) then begin
            display_msg(mstr(102));
         end
         else begin
            call PlayDemo;
         end
      end
      else if (action_being_used == SKILL_REPAIR) then begin
         if (dude_skill_success(SKILL_REPAIR, 0)) then begin
            set_local_var(LVAR_Comp_Fixed, 1);
            display_msg(mstr(101));

            add_timer_event(self_obj, game_ticks(0), EVENT_FLOAT);

            if (local_var(LVAR_EXP_Earned) == 0) then begin
               set_local_var(LVAR_EXP_Earned, 1);
               give_xp(EXP_REPAIR_NEG_40);
            end
         end
         else begin
            display_msg(mstr(130));
         end
      end
      script_overrides;
   end
end

procedure PlayDemo begin
   if fo1_demo_completed then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(126));
      display_msg(mstr(117));
      load_map(MAP_SCRAPHEAP, 0);
   end
end
