/*

   Necropolis - Hall of the Dead door guard

*/

/* Include Files */
#include "define.h"
//#include "necropolis.h"

#define NAME                    SCRIPT_GHDORGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;

import variable GhDorGrd_Ptr;

procedure ghoul00a;
procedure ghoul00b;
procedure ghoul01;
procedure ghoul02;
procedure ghoul03;
procedure ghoul03a;
procedure ghoulend;
procedure ghoulendFade;
procedure timed_event_p_proc;
procedure combat_rads_inc;

variable tmp_hostile;
variable init := 0;
variable maxleash := 9;
variable noevent;
variable loopcount;
variable new_tile;
variable Warned;

procedure start begin
   if (init == 0) then begin
      init := 1;
      if (local_var(7) == 0) then begin
         set_local_var(7, self_tile);
         set_local_var(5, 0);
      end
      set_self_team(TEAM_NECROPOLIS_GANGER);
      set_self_ai(AI_GHOUL_COWARD);
      GhDorGrd_Ptr := self_obj;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_HALLDED);

   get_reaction
   if (map_var(12) == 1) then begin
      display_msg(mstr(120));
   end
   else begin
      start_gdialog(70, self_obj, 4, -1, -1);
      gsay_start;
      if (local_var(4)) then begin
         set_map_var(11, 1);
         call ghoul02;// "I told you to leave. Your kind is not wanted here."
      end
      else begin
         if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
            if (local_var(1) >= 3) then begin
               set_map_var(11, 0);
               call ghoul00b;
            end
            else begin
               set_local_var(4, 1);
               set_map_var(11, 1);
               call ghoul03;
               end
         end
         else begin
            if (local_var(1) >= 3) then begin
               if (local_var(8) == 0) then begin
                  set_local_var(8, 1);
                  set_map_var(11, 1);
                  call ghoul01;
               end
               else begin
                  set_map_var(11, 1);
                  call ghoul00a;
               end
            end
            else begin
               set_local_var(4, 1);
               set_map_var(11, 1);
               call ghoul03;
            end
         end
      end
      gsay_end;
      end_dialogue;
   end
end

procedure ghoulend begin
end

procedure ghoul00a begin
   Reply(111);
   NOption(102, ghoulend, 4);
   NLowOption(103, ghoulend);
end

procedure ghoul00b begin
   NMessage(104);
   call ghoulend;
end

procedure ghoul01 begin
   Reply(105);
   NOption(106, ghoulend, 4);
   NLowOption(107, ghoulend);
end

procedure ghoul02 begin
   Reply(108);
   NOption(109, ghoulend, 4);
   NLowOption(110, ghoulend);
end

procedure ghoul03 begin
   Reply(101);
   if (map_var(12) == 0) then begin
      NOption(112, ghoul03a, 5);
   end
   NOption(113, ghoulend, 4);
   NLowOption(114, ghoulend);
end

procedure ghoul03a begin
   if is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0)) then begin
      set_map_var(11, 0);
      set_map_var(12, 1);
      Reply(115);
      NOption(118, ghoulendFade, 5);
   end
   else begin
      set_map_var(11, 1);
      set_map_var(12, 2);
      Reply(116);
      NOption(117, ghoulend, 5);
      set_map_var(11, 1);
      set_map_var(12, 2);
   end
end

procedure ghoulendFade begin
   gfade_out(1);
   gfade_in(1);
   set_map_var(11, 0);
   set_map_var(12, 1);
end

procedure critter_p_proc begin
   if self_can_see_dude and global_var(GVAR_ENEMY_NECROPOLIS) then begin
      tmp_hostile := 1;
      attack(dude_obj);
   end
   else begin
      if (global_var(GVAR_SET_DOOR_GUARD_WARNING) != 0) then begin
         float_msg(self_obj, mstr(119), FLOAT_MSG_RED);
         set_global_var(GVAR_SET_DOOR_GUARD_WARNING, 0);
         set_global_var(GVAR_ENEMY_NECROPOLIS, 1);
         tmp_hostile := 1;
         attack(dude_obj);
      end
      else if (tmp_hostile > 0) then begin
         tmp_hostile := 0;
         attack(dude_obj);
      end
      else if (local_var(5)) then begin
         loopcount := loopcount + 1;
         if (loopcount > 40) then begin
            loopcount := 0;
            if (noevent == 0) then begin
               noevent := 1;
               add_timer_event(self_obj, game_ticks(random(5, 13)), 0);
            end
         end
      end
   end
end

procedure timed_event_p_proc begin
   noevent := 0;
   set_local_var(6, self_tile);
   new_tile := tile_num_in_direction(local_var(6), random(0, 5), 2);
   if (tile_distance(local_var(7), new_tile) < maxleash) then begin
      self_walk_to_tile(new_tile);
   end
end

procedure destroy_p_proc begin
   GhDorGrd_Ptr := 0;
   inc_inv_evil_crit
end

procedure combat_p_proc begin
end
