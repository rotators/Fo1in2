/*

   Show current / max carry weight when looting objects.

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;

procedure GameModeChange_handler;
procedure create_windows;
procedure delete_windows;
procedure PrintText;
procedure GetText(variable obj);

#define weight(obj)                          who_carry_amount(obj)
#define maxWeight(obj)                       who_max_carry(obj)

variable Scr_Width;
variable Scr_Height;
variable cur_obj_weight;
variable max_obj_weight;

procedure start begin
   if (game_loaded) then begin
      set_global_script_type(1);
      register_hook_proc(HOOK_GAMEMODECHANGE, GameModeChange_handler);
   end
   call PrintText;
end

procedure GameModeChange_handler begin
   if ((get_sfall_global_int("show_wgt") == 0) and get_game_mode bwand (INTFACELOOT)) then
      call create_windows;
   else if get_sfall_global_int("show_wgt") and (get_game_mode bwand (INTFACELOOT) == false) then
      call delete_windows;
end

procedure create_windows begin
   set_sfall_global("show_wgt", 1);
   set_global_script_repeat(1);

   Scr_Width := (get_screen_width / 2);
   Scr_Height := (get_screen_height / 2);

   create_win_flag("win_loot_left", (Scr_Width - 95), (Scr_Height + 88), 80, 15, WIN_FLAG_MOVEONTOP + WIN_FLAG_TRANSPARENT);
   create_win_flag("win_loot_right", (Scr_Width + 22), (Scr_Height + 88), 80, 15, WIN_FLAG_MOVEONTOP + WIN_FLAG_TRANSPARENT);
   call PrintText;
end

procedure delete_windows begin
   set_sfall_global("show_wgt", 0);
   set_global_script_repeat(0);
   DeleteWin("win_loot_left");
   DeleteWin("win_loot_right");
end

procedure PrintText begin
   if get_sfall_global_int("show_wgt") then begin
      SelectWin("win_loot_left");
      call GetText(dude_obj);

      SelectWin("win_loot_right");
      call GetText(loot_obj);
   end
end

procedure GetText(variable obj) begin
   // Container don't have weight, they use size.
   if (obj_item_subtype(obj) == item_type_container) then begin
      cur_obj_weight := get_current_inven_size(obj);
      max_obj_weight := get_proto_data(obj_pid(obj), PROTO_CN_MAX_SIZE);
   end
   else begin
      cur_obj_weight := weight(obj);
      max_obj_weight := maxWeight(obj);
   end

   SetFont(101);
   if (cur_obj_weight > max_obj_weight) then
      SetTextColor(1.0, 0.1, 0.0);
   else
      SetTextColor(0.0, 1.0, 0.0);

   sfall_func0("win_fill_color");
   Format((cur_obj_weight + "/" + max_obj_weight), 0, 0, 80, 15, justifycenter);
   ShowWin;
end
