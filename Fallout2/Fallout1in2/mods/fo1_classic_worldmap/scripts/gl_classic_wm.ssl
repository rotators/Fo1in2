/*

   Classic Worldmap Mod - Add town titles to the green city circles

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "sfall/sfall.rotators.h"
#include "voodoo.h"

procedure start;
procedure mark_locations;

#define mstr_unknown       mstr_worldmap(1004)

variable i;
variable town_list;
variable special_list;

procedure start begin
   if (game_loaded) then begin
      set_global_script_type(0);
      register_hook_proc(HOOK_GAMEMODECHANGE, mark_locations);

      // Removed the location names under green circles
      VOODOO_remove_circle_name;

      // Fallout 1 style location reveal, no additional tiles around the location are shown.
      // Only works with rotators ddraw.dll for now.
      if sfall_rotators then begin
         VOODOO_location_discover_radius;
      end

      town_list := [0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 60, 61, 62, 63];
      fix_array(town_list);
   end
end

procedure mark_locations begin
   if (get_game_mode == WORLDMAP) then begin
      set_town_title(AREA_CAR_OUTTA_GAS, mstr_map(1521)); // Car location needs to be named at all times

      foreach (i in town_list) begin
         if (town_known(i) >= MARK_STATE_KNOWN) then
            set_town_title(i, mstr_map(1500 + i));
         else
            set_town_title(i, mstr_unknown);
      end
   end
end
