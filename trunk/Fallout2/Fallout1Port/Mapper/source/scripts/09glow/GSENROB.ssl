/*

	Glow - security robots

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\glow.h"

#define NAME                    SCRIPT_GSENROB

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure use_skill_on_p_proc;
procedure combat_p_proc;

#define LVAR_RobotExp               (0)
#define LVAR_AnimStatus       		(1)

procedure start begin
end

procedure map_update_p_proc begin
   if glow_power_online and glow_traps_active then begin
      if (local_var(LVAR_AnimStatus) == 0) then begin
         set_local_var(LVAR_AnimStatus,1);

         reg_anim_clear( self_obj );
         reg_anim_begin();
         reg_anim_animate( self_obj,ANIM_back_to_standing,-1);
         reg_anim_end();
      end
   end
end

procedure critter_p_proc begin
	if glow_power_online and glow_traps_active then begin
		if self_can_see_dude then begin
			attack(dude_obj);
		end
	end
	else begin
	   if (local_var(LVAR_AnimStatus) == 0) then begin
		   anim( self_obj, ANIM_fall_back_sf, 0 );
		end
		else if (local_var(LVAR_AnimStatus) == 1) then begin
			reg_anim_clear( self_obj );
			reg_anim_begin();
			reg_anim_animate( self_obj,ANIM_fall_back,-1);
			reg_anim_end();
			set_local_var(LVAR_AnimStatus,0);
	   end
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_SCIENCE) then begin
		script_overrides;
		if (glow_power_online and is_success(roll_vs_skill(dude_obj, SKILL_SCIENCE, -10))) then begin
			display_msg(message_str(SCRIPT_GENCHAT, 173));
			if (local_var(LVAR_RobotExp) == 0) then begin
				display_msg(message_str(SCRIPT_GENCHAT, 103) + 100 + message_str(SCRIPT_GENCHAT, 104));
				give_exp_points(100);
				set_local_var(LVAR_RobotExp, 1);
			end
		end
		else begin
			display_msg(message_str(SCRIPT_GENCHAT, 174));
		end
	end
end

procedure combat_p_proc begin
	if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
		if (not(glow_power_online) or not(glow_traps_active)) then begin
			script_overrides;
			anim( self_obj, ANIM_fall_back_sf, 0 );
		end
	end
end
