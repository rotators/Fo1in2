/*

	Shady Sands - old fortune teller or something

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_AGATHA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;

procedure Agatha00;
procedure Agatha01;
procedure Agatha02;
procedure Agatha03;
procedure Agatha04;
procedure Agatha05;
procedure Agatha06;
procedure Agatha07;
procedure Agatha08;
procedure Agatha09;
procedure Agatha10;
procedure Agatha11;
procedure Agatha12;
procedure Agatha13;
procedure Agatha14;
procedure Agatha15;
procedure Agatha16;
procedure Agatha17;
procedure Agatha18;
procedure Agatha19;
procedure Agatha20;
procedure AgathaEnd;

procedure PickDeadBodyType;
variable DeathType := 56;
variable initial :=  0;
variable KillBox_ptr := 0;

procedure start begin
	if global_var(FIXT_BUGFIXES_ONLY) then begin
		destroy_object(self_obj);
	end
	else begin
		if global_var(AGATHA_IS_ENABLED) == 0 then begin
			destroy_object(self_obj);
		end
	end
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin
		if (cur_map_index == MAP_SHADYE) or (cur_map_index == MAP_SHADYW) then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(4) != 1) then begin
				set_local_var(4, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if not(initial) then begin
		initial :=  1;
		set_self_team(TEAM_SHADY_SANDS );
	end
end

procedure description_p_proc begin
	if (is_success(do_check(dude_obj, STAT_pe, dude_awareness))) then begin
		script_overrides;
		display_msg(mstr(102));
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;
	if (global_var(AGATHA_4_ONCE)) then begin
		display_msg(mstr(101));
	end
	else begin
		display_msg(mstr(100));
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	start_gdialog(663, self_obj, 4, -1, -1);
	gsay_start;
	if (global_var(AGATHA_4_ONCE)) then begin
		if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
			call Agatha14;
		end
		else begin
			call Agatha16;
		end
	end
	else begin
		set_global_var(AGATHA_4_ONCE, 1);
		call Agatha08;
	end
	gsay_end;
	end_dialogue;
end

procedure Agatha00 begin
	float_msg(self_obj, message_str(SCRIPT_AGATHA, 103), 0);
end

procedure Agatha01 begin
	gsay_reply(663, 104);
	GOption( 105, Agatha02, -3 );
	NOption( 106, AgathaEnd, 4 );
	NOption( 107, Agatha03, 5 );
	NOption( 108, Agatha04, 4 );
end

procedure Agatha02 begin
	gsay_message(663, 109, 49);
end

procedure Agatha03 begin
	gsay_reply(663, 110);
	GOption( 111, AgathaEnd, 5 );
	NOption( 112, Agatha04, 6 );
	NOption( 113, AgathaEnd, 5 );
end

procedure Agatha04 begin
	gsay_reply(663, 114);
	GOption( 115, AgathaEnd, 5 );
end

procedure Agatha05 begin
	gsay_message(663, 116, 50);
end

procedure Agatha06 begin
	UpReact
	gsay_message(663, 117, 49);
end

procedure Agatha07 begin
	gsay_message(663, 118, 50);
end

procedure Agatha08 begin
	gsay_reply(663, 119);
	NOption( 120, Agatha09, -3 );
	NOption( 121, Agatha10, 4 );
	if (dude_is_female) then begin
		NOption( 122, Agatha13, 5 );
	end
	else begin
		NOption( 123, Agatha13, 7 );
	end
	BOption( 124, Agatha09, 4 );
end

procedure Agatha09 begin
	gsay_message(663, 125, 50);
end

procedure Agatha10 begin
	gsay_reply(663, 126);
	NOption( 127, Agatha11, 4 );
	NOption( 128, AgathaEnd, 4 );
	GOption( 129, Agatha12, 6 );
end

procedure Agatha11 begin
	gsay_message(663, 130, 50);
end

procedure Agatha12 begin
	UpReact
	gsay_message(663, 131, 50);
end

procedure Agatha13 begin
	gsay_message(663, 132, 50);
end

procedure Agatha14 begin
	gsay_reply(663, 133);
	NOption( 134, AgathaEnd, -3 );
	NOption( 135, AgathaEnd, 4 );
	NOption( 136, Agatha15, 6 );
end

procedure Agatha15 begin
	gsay_message(663, 137, 50);
end

procedure Agatha16 begin
	gsay_reply(663, 138);
	NOption( 139, Agatha17, -3 );
	NOption( 140, Agatha20, 4 );
	NOption( 141, Agatha18, 6 );
	NOption( 142, AgathaEnd, 4 );
end

procedure Agatha17 begin
	gsay_message(663, 143, 50);
end

procedure Agatha18 begin
	gsay_reply(663, 144);
	NOption( 145, Agatha20, 5 );
	BOption( 146, Agatha19, 5 );
end

procedure Agatha19 begin
	DownReactLevel
	DownReactLevel
	gsay_message(663, 147, 51);
end

procedure Agatha20 begin
	gsay_message(663, 128, 50);
	gfade_out(1);
	game_time_advance(600);
	gfade_in(1);
end

procedure AgathaEnd begin
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end



