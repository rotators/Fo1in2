#include "..\headers\define.h"
#include "..\headers\command.h"

#define NAME 								SCRIPT_CAVEWALL

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure damage_p_proc;

variable EXP_EARNED;

procedure start begin
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure description_p_proc begin
	script_overrides;
	if (is_success(do_check(dude_obj, STAT_pe, 0)) or has_trait( TRAIT_PERK, dude_obj, PERK_bonus_awareness )) then begin
		display_msg(mstr(101));
	end
	else begin
		display_msg(mstr(100));
	end
end

procedure damage_p_proc begin
variable LVar0 := 0;
variable LVar1 := 0;

	if (map_var(0) == 0) then begin
		set_map_var(0, 1);
		gfade_out(1);

		Create_Cave_In(22345,0)
		create_object( PID_BLOCKING_HEX, 22147, 0 );
		create_object( PID_BLOCKING_HEX, 22347, 0 );
		create_object( PID_BLOCKING_HEX, 21946, 0 );
		create_object( PID_BLOCKING_HEX, 22547, 0 );
		create_object( PID_BLOCKING_HEX, 21745, 0 );

		set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) + 3);

		if (global_var(SHADYSANDS_RADSCORPS_LEFT) > 0) then begin
			//previously had been changed to * 55 + 250
			EXP_EARNED := (global_var(SHADYSANDS_RADSCORPS_LEFT) * 75) + 75;
			if (EXP_EARNED < 300) then begin
				EXP_EARNED := 300;
			end
			if (global_var(SHADYSANDS_RADSCORPS_LEFT) > 0) then begin
				EXP_EARNED := EXP_EARNED + 500;
			end
			if (global_var(RADSCORPIONS_KILLED) != 1) then begin
				set_global_var(RADSCORPIONS_KILLED, 2);
				display_msg(message_str(SCRIPT_SSRADSCO, 188));
				display_msg(message_str(SCRIPT_GENCHAT, 103) + EXP_EARNED + message_str(SCRIPT_GENCHAT, 104));
				give_exp_points(EXP_EARNED);
				set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) + 3);
			end
		end

		if (global_var(RADSCORPION_SEED) != 2) then begin
			set_global_var(RADSCORPION_SEED, 2);
		end
		gfade_in(1);

		// Player is still inside the cave and will die now:
		if (not(tile_distance(tile_num(dude_obj), 21155) < 15)) then begin
			signal_end_game;
		end
	end
end


