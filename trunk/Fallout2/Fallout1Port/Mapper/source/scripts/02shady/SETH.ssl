/*

	Shady Sands - Seth, the entrance guard

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_SETH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure Seth01;
procedure Seth02;
procedure Seth03;
procedure Seth04;
procedure Seth05;
procedure Seth06;
procedure Seth07;
procedure Seth08;
procedure Seth08a;
procedure Seth09;
procedure Seth10;
procedure Seth11;
procedure Seth12;
procedure Seth13;
procedure Seth14;
procedure Sethend;
procedure TanSeth00;
procedure TanSeth01;
procedure TanSeth02;
procedure TanSeth03;
procedure TanSeth04;
procedure TanSeth05;
procedure TanSeth06;
procedure TanSeth07;

procedure travel;
procedure no_travel;
procedure pick_start;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

variable tmp_hostile;
variable initial :=  0;
variable round_counter;

procedure PickDeadBodyType;
variable DeathType := 56;

#define LVAR_Here_Before                  (4)
#define LVAR_Sleeping 							(5)
#define LVAR_Welcome                      (6)
#define LVAR_Invasion                     (10)

procedure start begin
	if shady_invaded then begin
		if CUR_AREA_SHADY_SANDS then begin
			if (local_var(LVAR_Invasion) != 1) then begin
				set_local_var(LVAR_Invasion, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
end

procedure description_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure critter_p_proc begin
	if (local_var(LVAR_Welcome) == 0) then begin
		set_local_var(LVAR_Welcome, 1);
		if dude_is_armed then begin
			float_msg(self_obj, mstr(204), 8);
		end
		else begin
			float_msg(self_obj, mstr(205), 8);
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure combat_p_proc begin
	if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
		round_counter := round_counter + 1;
	end
	if (round_counter > 3) then begin
		if not(global_var( GVAR_ENEMY_SHADY_SANDS )) then begin//Shady Sands is NOT tmp_hostile to player;  i.e. globalvar ENEMY_SHADY_SANDS is not enabled
			set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );//Set ENEMY_SHADY_SANDS to True
			set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep - 5 );
		end
	end
end

procedure map_update_p_proc begin
	if not(initial) then begin
		set_self_team(TEAM_SHADY_SANDS );
		set_self_ai( AI_GUARD );
		home_tile := 14108;
		sleep_tile := 15925;
		wake_time := 600;
		sleep_time := 2100;
		initial :=  1;
	end
end

procedure talk_p_proc begin
	dude_look_at_self;

	get_reaction

	if (local_var(9) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_SETH, 320), 2);
		tmp_hostile := 1;
	end
	else if (tandi_is_kidnapped and (dude_iq > 3)) then begin
		set_global_var( GVAR_TANDI_TALKED_ABOUT, 1 );
		if (local_var(8) == 0) then begin
			set_local_var(8, 1);
			start_dialog_at_node(TanSeth00);
		end
		else begin
			start_dialog_at_node(TanSeth01);
		end
	end
	else if (local_var(5) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_GENVAULT, 166), 0);
	end
	else if global_var( GVAR_ENEMY_SHADY_SANDS ) then begin//   Is Shady Sands tmp_hostile to player?
		float_msg(self_obj, mstr(101), 2);
		tmp_hostile := 1;
	end
	else begin
		start_dialog_at_node(pick_start);
	end
end

procedure destroy_p_proc begin
	if obj_in_party(source_obj) then begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
		inc_good_critter
	end
	set_global_var( GVAR_SETH_STATUS, 3 );
end

procedure travel begin
	game_time_advance(game_ticks(60 * 30));
	if (radscorp_quest_inactive and not(radscorp_quest_completed)) then begin
		set_radscorp_quest_active;
	end
	load_map("caves.map", 1);
end

procedure no_travel begin
	if radscorp_quest_completed then begin
		gsay_message(183, 203, 50);
	end
	else begin
		gsay_message(183, 128, 50);
	end
end

procedure pick_start begin
	if (global_var( GVAR_SETH_STATUS ) == 0) then begin
		if (local_var(1) < 2) then begin
			call Seth04;
		end
		else begin
			call Seth05;
		end
	end
	else begin
		if radscorp_quest_completed then begin
			BigUpReact
			gsay_message(183, 200, 49);
			if (dude_iq > 3) then begin
				call Seth07;
			end
		end
		else begin
			if (local_var(1) < 2) then begin
				call Seth09;
			end
			else begin
				call Seth07;
			end
		end
	end
end

procedure Seth01 begin
	gsay_message(183, 101, 50);
	tmp_hostile := 1;
end

procedure Seth02 begin
	gsay_message(183, 102, 50);
	tmp_hostile := 1;
end

procedure Seth03 begin
	gsay_reply(183, 103);
	set_local_var(LVAR_Here_Before, 1);
	gsay_option(183, 104, Seth13, 50);
	gsay_option(183, 105, Seth08a, 50);
end

procedure Seth04 begin
	gsay_reply(183, 106);
	set_global_var( GVAR_SETH_STATUS, 1 );
	giq_option( 4, SCRIPT_SETH, 107, Seth02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 108, Seth06, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_SETH, 109, Seth03, NEUTRAL_REACTION );
end

procedure Seth05 begin
	gsay_reply(183, 110);
	set_global_var( GVAR_SETH_STATUS, 1 );
	giq_option( 4, SCRIPT_SETH, 111, Seth06, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_SETH, 112, Seth03, NEUTRAL_REACTION );
end

procedure Seth06 begin
	gsay_reply(183, 113);
	call Seth08;
end

procedure Seth07 begin
	gsay_reply(183, 114);
	call Seth08;
end

procedure Seth08 begin
	if radscorp_quest_active then begin
		giq_option( 4, SCRIPT_SETH, 115, Seth10, NEUTRAL_REACTION );
	end
	if tandi_is_kidnapped then begin
		giq_option( 4, SCRIPT_SETH, 116, Seth11, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 117, Seth12, NEUTRAL_REACTION );
	if ((local_var(LVAR_Here_Before) == 1) and not(radscorp_quest_completed)) then begin
		giq_option( 4, SCRIPT_SETH, 118, Seth13, NEUTRAL_REACTION );
		giq_option( -3, SCRIPT_SETH, 119, Seth13, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 120, Seth08a, NEUTRAL_REACTION );
end

procedure Seth08a begin
	if (local_var(7) == 0) then begin
		set_local_var(7, 1);
		call Seth14;
	end
	else begin
		call Sethend;
	end
end

procedure Seth09 begin
	gsay_reply(183, 121);
	if radscorp_quest_active then begin
		giq_option( 4, SCRIPT_SETH, 115, Seth10, NEUTRAL_REACTION );
	end
	if tandi_is_kidnapped then begin
		giq_option( 4, SCRIPT_SETH, 116, Seth11, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 117, Seth12, NEUTRAL_REACTION );
	if (local_var(LVAR_Here_Before) == 1) then begin
		giq_option( 4, SCRIPT_SETH, 118, Seth13, NEUTRAL_REACTION );
		giq_option( -3, SCRIPT_SETH, 119, Seth13, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 120, Seth08a, NEUTRAL_REACTION );
end

procedure Seth10 begin
	if radscorp_quest_completed then begin
		gsay_reply(183, 201);
	end
	else begin
		gsay_reply(183, 122);
	end
	set_local_var(LVAR_Here_Before, 1);
	if tandi_is_kidnapped then begin
		giq_option( 4, SCRIPT_SETH, 116, Seth11, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 117, Seth12, NEUTRAL_REACTION );
	if (local_var(LVAR_Here_Before) == 1) then begin
		giq_option( 4, SCRIPT_SETH, 118, Seth13, NEUTRAL_REACTION );
		giq_option( -3, SCRIPT_SETH, 119, Seth13, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 120, Seth08a, NEUTRAL_REACTION );
end

procedure Seth11 begin
	set_global_var( GVAR_WORLDMAPLIST_RAIDERS, 1 );
	mark_on_map(AREA_RAIDERS)

	gsay_reply(183, 123);
	if radscorp_quest_active then begin
		giq_option( 4, SCRIPT_SETH, 115, Seth10, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 117, Seth12, NEUTRAL_REACTION );
	if (local_var(LVAR_Here_Before) == 1) then begin
		giq_option( 4, SCRIPT_SETH, 118, Seth13, NEUTRAL_REACTION );
		giq_option( -3, SCRIPT_SETH, 119, Seth13, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 120, Seth08a, NEUTRAL_REACTION );
end

procedure Seth12 begin
	gsay_reply(183, 124);
	giq_option( 4, SCRIPT_SETH, 115, Seth10, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 116, Seth11, NEUTRAL_REACTION );
	if (local_var(LVAR_Here_Before) == 1) then begin
		giq_option( 4, SCRIPT_SETH, 118, Seth13, NEUTRAL_REACTION );
		giq_option( -3, SCRIPT_SETH, 119, Seth13, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_SETH, 120, Seth08a, NEUTRAL_REACTION );
end

procedure Seth13 begin
	if radscorp_quest_completed then begin
		gsay_reply(183, 202);
	end
	else begin
		gsay_reply(183, 125);
	end
	giq_option( 4, SCRIPT_SETH, 126, travel, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 127, no_travel, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_SETH, 130, travel, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_SETH, 131, no_travel, NEUTRAL_REACTION );
end

procedure Seth14 begin
	gsay_message(183, 206, 50);
end

procedure Sethend begin
end

procedure TanSeth00 begin
	gsay_reply(183, 300);
	giq_option( 4, SCRIPT_SETH, 301, TanSeth03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 302, TanSeth02, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 303, TanSeth02, NEUTRAL_REACTION );
	if radscorp_quest_active then begin
		giq_option( 4, SCRIPT_SETH, 304, TanSeth07, NEUTRAL_REACTION );
	end
end

procedure TanSeth01 begin
	gsay_reply(183, 305);
	giq_option( 4, SCRIPT_SETH, 306, TanSeth06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 307, TanSeth05, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 308, TanSeth06, NEUTRAL_REACTION );
	if radscorp_quest_active then begin
		giq_option( 4, SCRIPT_SETH, 309, TanSeth07, NEUTRAL_REACTION );
	end
end

procedure TanSeth02 begin
	gsay_message(183, 310, 50);
end

procedure TanSeth03 begin
	set_global_var( GVAR_WORLDMAPLIST_RAIDERS, 1 );
	mark_on_map(AREA_RAIDERS)

	gsay_reply(183, 311);
	giq_option( 4, SCRIPT_SETH, 312, Sethend, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 313, TanSeth02, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 314, TanSeth04, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 315, TanSeth02, BAD_REACTION );
end

procedure TanSeth04 begin
	gsay_reply(183, 316);
	giq_option( 4, SCRIPT_SETH, 317, TanSeth05, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 318, Sethend, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 319, TanSeth02, BAD_REACTION );
end

procedure TanSeth05 begin
	set_local_var(9, 1);
	tmp_hostile := 1;
	gsay_message(183, 320, 51);
end

procedure TanSeth06 begin
	gsay_message(183, 321, 50);
end

procedure TanSeth07 begin
	gsay_reply(183, 322);
	giq_option( 4, SCRIPT_SETH, 323, travel, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SETH, 324, TanSeth02, BAD_REACTION );
	giq_option( 4, SCRIPT_SETH, 325, Sethend, NEUTRAL_REACTION );
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
