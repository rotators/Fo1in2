/*

	Shady Sands - generic wife?

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_WIFE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;
procedure damage_p_proc;

procedure Wife01;
procedure Wife02;
procedure Wife03;
procedure Wife00n;
procedure WifeEnd;

variable tmp_hostile;
variable Only_Once := 1;

procedure PickDeadBodyType;
variable DeathType := 56;

procedure start begin
	if shady_invaded then begin
		if (CUR_MAP_SHADYE) or (CUR_MAP_SHADYW) then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(4) != 1) then begin
				set_local_var(4, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin
		if Only_Once then begin
			set_self_team(TEAM_SHADY_SANDS );
			set_self_ai( AI_PEASANT );
			Only_Once := 0;
		end
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		call Wife00n;
	end
	else begin
		start_gdialog(119, self_obj, 4, -1, -1);
		gsay_start;
		call Wife01;
		gsay_end;
		end_dialogue;
	end
end

procedure Wife01
begin
	gsay_reply(119, 100);
	giq_option( 4, SCRIPT_WIFE, 101, Wife02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_WIFE, 107, WifeEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_WIFE, 102, Wife03, NEUTRAL_REACTION );
end

procedure Wife02
begin
	gsay_message(119, 103, 50);
end

procedure Wife03
begin
	gsay_message(119, 104, 50);
end

procedure Wife00n
begin
	float_msg(self_obj, message_str(SCRIPT_WIFE, 105), 8);
end

procedure WifeEnd
begin
end

procedure critter_p_proc begin
	if (map_var(2) == 1) then begin
		set_local_var(0, local_var(0) + 1);
		set_map_var(2, 0);
		if (local_var(0) < 3) then begin
			float_msg(self_obj, message_str(SCRIPT_RAZLO, 308), 2);
		end
		else begin
			tmp_hostile := 1;
		end
	end
	if (self_can_see_dude) then begin
		if global_var( GVAR_ENEMY_SHADY_SANDS ) then begin//   Is Shady Sands tmp_hostile to player?
			tmp_hostile := 1;
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure destroy_p_proc begin
	inc_good_critter
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );//Set ENEMY_SHADY_SANDS to True
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );//Set ENEMY_SHADY_SANDS to True
	end
end

procedure PickDeadBodyType
begin
	pick_dead_body_type
end
