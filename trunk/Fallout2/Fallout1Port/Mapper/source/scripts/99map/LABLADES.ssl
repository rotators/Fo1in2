#include "..\headers\define.h"
#include "..\headers\updatmap.h"

#include "..\headers\MAPLABLADES.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure map_exit_p_proc;

procedure Invasion;

export variable InBladePtr1;
export variable InBladePtr2;
export variable InBladePtr3;
export variable InBladePtr4;
export variable InBladePtr5;
export variable RazorPtr;

variable invaderPtr;
variable kidPtr;
variable KidCount;

#define MaxKids 				(5)
#define KidsSpawnHex 		(31533)

procedure start begin
end

procedure map_enter_p_proc begin
	set_global_var(MARK_LA_2, 1);
	Lighting;

	if ((map_first_run) and (global_var(GVAR_FOLLOWERS_INVADED) != 1)) then begin
		display_msg(message_str(SCRIPT_SHADYWST, 113));
	end

	call Invasion;

	if get_car_from_worldmap then begin
		override_map_start_hex(28517,0,5);

		// This will spawn yelling kids around the motorcycle
		if (map_var(MVAR_Car_Kids_created) == 0) and not(boneyard_invaded) then begin
			set_map_var(MVAR_Car_Kids_created,1);
			debug("spawning kids!");
			while (KidCount < MaxKids) do begin
				KidCount := KidCount + 1;
				kidPtr := create_object_sid(PID_BOY, 0, 0, SCRIPT_LAKIDS);
				critter_attempt_placement(kidPtr, KidsSpawnHex, 0);
				set_ai(kidPtr, AI_LITTLE_KIDS);
				set_team(kidPtr, TEAM_LA_BLADES);
			end
		end
	end
end

procedure map_update_p_proc begin
	Lighting;

	if (global_var(BLADES_STATUS) == 9104) then begin
		if not(is_loading_game) then begin	set_obj_visibility(RazorPtr, 1);	end
		move_to(RazorPtr, 0, 0);
		kill_critter(RazorPtr, ANIM_stand);
		gfade_in(1);
	end

end

procedure map_exit_p_proc begin
	if ((global_var(BLADES_STATUS) == 9103) or (global_var(BLADES_STATUS) == 9102)) then begin
		if not(is_loading_game) then begin	set_obj_visibility(RazorPtr, 1);	end
		move_to(RazorPtr, 0, 0);
		kill_critter(RazorPtr, ANIM_stand);
	end
end

procedure Invasion begin
	if (boneyard_invaded and not(is_loading_game) and (map_var(1) == 0)) then begin
		set_map_var(1, 1);
		kill_critter_type(PID_LOSER, 1);
		kill_critter_type(PID_PEASANT_BLACK, 1);
		kill_critter_type(PID_LEATHER_JACKET_WOMAN, 1);
		kill_critter_type(PID_PEASANT_GREEN, 1);
		kill_critter_type(PID_PEASANT_FEMALE_CUTE, 1);
		kill_critter_type(PID_PEASANT_YELLOW_FEMALE, 1);
		kill_critter_type(PID_PEASANT_2_FEMALE, 1);
		kill_critter_type(PID_BOY, 1);
		kill_critter_type(PID_RUTGER, 1);
		kill_critter_type(PID_DUGAN, 1);
		kill_critter_type(PID_HAMMER, 1);

		invaderPtr := create_object_sid(PID_TOUGH_NIGHTKIN, 0, 0, SCRIPT_SCSUPMUT);
		critter_add_trait( invaderPtr, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_SUPER_MUTANT_AGGRESSIVE );
		critter_attempt_placement(invaderPtr, 20306, 0);

		invaderPtr := create_object_sid(PID_TOUGH_NIGHTKIN, 0, 0, SCRIPT_SCSUPMUT);
		critter_add_trait( invaderPtr, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_SUPER_MUTANT_AGGRESSIVE );
		critter_attempt_placement(invaderPtr, 15904, 0);

		invaderPtr := create_object_sid(PID_TOUGH_NIGHTKIN, 0, 0, SCRIPT_SCSUPMUT);
		critter_add_trait( invaderPtr, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_SUPER_MUTANT_AGGRESSIVE );
		critter_attempt_placement(invaderPtr, 22480, 0);

		invaderPtr := create_object_sid(PID_TOUGH_NIGHTKIN, 0, 0, SCRIPT_SCSUPMUT);
		critter_add_trait( invaderPtr, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_SUPER_MUTANT_AGGRESSIVE );
		critter_attempt_placement(invaderPtr, 21538, 0);
	end
end

