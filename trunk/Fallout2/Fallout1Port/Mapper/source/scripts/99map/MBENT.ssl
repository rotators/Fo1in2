#include "..\headers\define.h"
#include "..\headers\COMMAND.h"
#include "..\headers\updatmap.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure map_exit_p_proc;

export variable Team9_Count := 4;
export variable radio_trick;
export variable ignoring_dude;
export variable removal_ptr;
export variable know_door_code;
export variable comptroller_status;

variable party_elevation;
variable dude_start_hex;

procedure radio_kludge;

procedure start begin
end

procedure map_enter_p_proc begin
	// Failsafe in case the wrong map loads after cutscene:
	if military_base_destoryed then
		load_map(MAP_MBDEAD, 0);

	Lighting;
	set_global_var(WORLDMAPLIST_MILITARYBASE, 2);
	if (map_first_run) then begin
		display_msg(message_str(SCRIPT_SHADYWST, 109));
	end
	radio_trick := map_var(0);
	know_door_code := map_var(1);
	if (global_var( GVAR_LOAD_MAP_INDEX ) == 0) then begin
		override_map_start(133, 111, 0, 5);
	end
	else if (global_var( GVAR_LOAD_MAP_INDEX ) == 1) then begin
		override_map_start(73, 107, 0, 2);
	end
	else begin
		override_map_start(133, 111, 0, 5);
	end

	if get_car_from_worldmap then begin
		override_map_start_hex(18932,0,2);
	end
end

procedure map_update_p_proc begin
	// Failsafe in case the wrong map loads after cutscene:
	if military_base_destoryed then
		load_map(MAP_MBDEAD, 0);

	Lighting;
	if (removal_ptr != 0) then begin
		destroy_object(removal_ptr);
		removal_ptr := 0;
	end
end

procedure map_exit_p_proc begin
	// The Vats have been destroyed
	if (global_var(VATS_BLOWN) == 1) and not(military_base_destoryed) then begin
		set_global_var(VATS_BLOWN_CUTSCENE_DONE, 1);
		set_end_Vats;

		set_world_map_pos(175,105); // MBase

		inc_mbase_rep(REP_BLOWN_UP_VATS);

		give_exp_points(EXP_VATS_DESTROYED);
		display_msg(message_str(SCRIPT_GENCHAT, 701));

		mark_map_entrance_state(MAP_MBENT,0);
		mark_map_entrance_state(MAP_MBSTRG12,0);
		mark_map_entrance_state(MAP_MBVATS12,0);
		mark_map_entrance_state(MAP_MBDEAD,1);

		play_gmovie(VATSEXPLODE_MOVIE);

		if get_car_in_cur_map then begin
			set_car_worldmap;
			car_give_to_party;
		end

		if (global_var(MASTER_BLOWN)) and (waterchip_returned /*or dude_item_count(PID_WATER_CHIP)*/) then begin
			force_encounter(MAP_V13ENT);
		end
	end

	set_map_var(0, radio_trick);
end

procedure radio_kludge begin
	variable LVar0 := 0;
	variable LVar1 := 0;
	LVar0 := dude_item_count( PID_RADIO );
	if (LVar0 > 0) then begin
		LVar1 := dude_item( PID_RADIO );
		LVar1 := destroy_mult_objs(LVar1, LVar0);
		LVar1 := create_object_sid(PID_RADIO, 0, 0, SCRIPT_RADIO);
		add_mult_objs_to_inven(dude_obj, LVar1, LVar0);
	end
end



