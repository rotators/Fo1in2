/*

	Raiders - Woman prisoners

*/

/* Include Files */

#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_WOMEN
#define TOWN_REP_VAR            GVAR_TOWN_REP_RAIDERS

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

import variable women_killed;
import variable signal_women;

variable tmp_hostile;
variable Only_Once := 1;
variable message;
variable temp;

procedure PickDeadBodyType;
variable DeathType := 56;


procedure start begin
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin
		if (cur_map_index == MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(4) != 1) then begin
				set_local_var(4, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_BROTHERHOOD );
		set_self_ai( AI_LOSER );
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if ((global_var(GARL_DEAD) == 1) and (global_var(TOTAL_RAIDERS) <= 12) or (global_var(TOTAL_RAIDERS) <= 6)) then begin
		if (local_var(0) == 0) then begin
			set_local_var(0, 1);
			temp := 200;
			display_msg(message_str(SCRIPT_RAIDMAP, 100) + temp + message_str(SCRIPT_RAIDMAP, 103));
			set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) + 1);
			give_exp_points(temp);
		end
	end
	else begin
		if (signal_women == 2) then begin
			signal_women := signal_women - 1;
			float_msg(self_obj, message_str(SCRIPT_WOMEN, 101), 2);
		end
		else begin
			if (signal_women == 1) then begin
				signal_women := signal_women - 1;
				float_msg(self_obj, message_str(SCRIPT_WOMEN, 102), 2);
			end
		end
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (local_var(0) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_WOMEN, 104), 2);
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_WOMEN, random(102, 103)), 2);
	end
end

procedure destroy_p_proc begin
	women_killed := women_killed + 1;
	if (women_killed > 1) then begin
		set_global_var(PLAYER_REPUTATION_GENERAL, (global_var(PLAYER_REPUTATION_GENERAL) - 1));
	end

	inc_inv_good_crit
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_WOMEN, 100));
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end

