#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure destroy_p_proc;//    script_action == 18
procedure talk_p_proc;//    script_action == 11
procedure Critter_Action;//    script_action == 12
procedure damage_p_proc;//    script_action == 14
procedure timed_event_p_proc;//    script_action == 22
procedure description_p_proc;//    script_action == 3

import variable killing_women;
procedure guard00;
procedure guard01;
procedure guard02;
procedure guard03;
procedure guard04;
procedure guard05;
procedure guard06;
procedure guard07;
procedure guard07a;
procedure guard07b;
procedure guard08;
procedure guard09;
procedure guard10;
procedure guard11;
procedure guard12;
procedure guard13;
procedure guard14;
procedure guard15;
procedure combat;
procedure PickDeadBodyType;

procedure flee_dude;

variable tmp_hostile;
variable initial :=  0;
variable rndz;
variable Weapons;

variable DeathType := 56;
variable LastTime;

procedure start
begin
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin
		if (cur_map_index == MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin
		

		if not(initial) then begin
			initial :=  1;
			item_caps_adjust(self_obj, random(1, get_critter_stat(dude_obj, STAT_lu) * 2));
			set_self_team(TEAM_RAIDERS );
			set_self_ai( AI_GENERIC_RAIDER );
			debug_msg("GENRAIDR init complete.");
		end
		if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
			script_overrides;
			display_msg(message_str(SCRIPT_GENRAIDR, 101));
		end
		else begin
			if (script_action == 22) then begin//<-- timed_event_p_proc -- called by ADD_TIMER_EVENT commands. "fixed_param==#" in this procedure is the number of the event in question (i.e. Add_Timer_Event dude,5,1 would be fixed_param 1) -- can also be "timeforwhat"
				call timed_event_p_proc;
			end
			else begin
				if (script_action == 12) then begin//<-- Critter_Action - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					call Critter_Action;
				end
				else begin
					if (script_action == 3) then begin//ON-CLICK DESCRIPTION (Binoculars icon)
						call description_p_proc;
					end
					else begin
						if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
							tmp_hostile := 1;
						end
					end
				end
			end
		end
	end
end

procedure description_p_proc
begin
	script_overrides;
	if (self_is_male) then begin// MALE raider
		display_msg(message_str(SCRIPT_GENRAIDR, random(200, 202)));
	end
	else begin
		display_msg(message_str(SCRIPT_GENRAIDR, random(203, 205)));// FEMALE raider
	end
end

procedure talk_p_proc
begin
	if (local_var(5) == 1) then begin
		display_msg(message_str(SCRIPT_GENRAIDR, 100));
	end
	else begin
		if (global_var(MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
			call guard00;
		end
		else begin
			if ((Weapons == 1) and (killing_women == 0) and (global_var(GARLS_FRIEND) != 1)) then begin
				call guard02;
			end
			else begin
				set_local_var(3, 1);
				get_reaction
				start_gdialog(136, self_obj, 4, -1, -1);
				gsay_start;
				if (local_var(4) == 0) then begin
					set_local_var(4, 1);
					call guard04;
				end
				else begin
					call guard15;
				end
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure guard00
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
end

procedure guard01
begin
	gsay_message(136, random(107, 111), 50);
end

procedure guard02
begin
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(112, 115)), 8);
	add_timer_event(self_obj, game_ticks(9), 1);
end

procedure guard03
begin
	gsay_message(136, random(116, 118), 50);
	add_timer_event(self_obj, game_ticks(10), 2);
end

procedure guard04
begin
	gsay_reply(136, 119);
	if ((global_var(TANDI_RESCUE) == 1) and (global_var(TANDI_TALKED_ABOUT) == 1)) then begin
		giq_option( 4, SCRIPT_GENRAIDR, 120, guard07, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_GENRAIDR, 121, guard06, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_GENRAIDR, 122, guard05, NEUTRAL_REACTION );
end

procedure guard05
begin
	gsay_message(136, 123, 50);
end

procedure guard06
begin
	gsay_message(136, 124, 50);
	DownReact
end

procedure guard07
begin
	gsay_reply(136, 125);
	giq_option( 4, SCRIPT_GENRAIDR, 126, guard08, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_GENRAIDR, 127, guard07a, NEUTRAL_REACTION );
	if (is_success(do_check(dude_obj, STAT_pe, 0))) then begin
		giq_option( 4, SCRIPT_GENRAIDR, 128, guard07b, NEUTRAL_REACTION );
	end
end

procedure guard07a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call guard09;
	end
	else begin
		call guard11;
	end
end

procedure guard07b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20))) then begin
		call guard12;
	end
	else begin
		call guard13;
	end
end

procedure guard08
begin
	gsay_message(136, 129, 50);
end

procedure guard09
begin
	gsay_reply(136, 130);
	giq_option( 6, SCRIPT_GENRAIDR, 131, guard10, NEUTRAL_REACTION );
end

procedure guard10
begin
	gsay_message(136, 132, 50);
end

procedure guard11
begin
	gsay_message(136, 133, 50);
end

procedure guard12
begin
	gsay_message(136, 134, 50);
	set_local_var(5, 1);
end

procedure guard13
begin
	gsay_message(136, 135, 50);
	call combat;
end

procedure guard14
begin
	gsay_message(136, 136, 50);
	add_timer_event(self_obj, game_ticks(12), 3);
end

procedure guard15
begin
	gsay_message(136, 137, 50);
end

procedure combat
begin
	if (global_var(MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
		set_self_team(87);
	end
	tmp_hostile := 1;
end

procedure flee_dude
begin
	variable Msg := 102;
	variable Run := 0;
	variable Distance := 0;
	variable Direction := 0;
	variable DistanceBetween := 0;
	Msg := random(102, 106);
	float_msg(self_obj, message_str(SCRIPT_GENRAIDR, Msg), 8);
	if (Msg == 105) or (Msg == 106) then begin
		Run := 1;
	end
	while(Direction < 5) do begin
		if (tile_distance(tile_num(dude_obj), tile_num_in_direction(tile_num(self_obj), Direction, (3 + Run))) > DistanceBetween) then begin
			Distance := tile_num_in_direction(tile_num(self_obj), Direction, (3 + Run));
			DistanceBetween := tile_distance(tile_num(dude_obj), Distance);
		end
		Direction := (Direction + 1);
	end
	animate_move_obj_to_tile(self_obj, Distance, Run);
end

procedure Critter_Action
begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		attack(dude_obj);
	end
	else begin
		if (global_var(TANDI_HIRELING_STATUS) == 2) or global_var(TANDI_ESCAPE) or (map_var(2) == 1) or global_var(RAID_LOOTING) then begin
			set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		end
	end
	if global_var(MISTAKEN_ID) then begin//Raiders think you are Garl's father
		set_global_var(ENEMY_RAIDERS, 0);
		if (tile_distance_objs(self_obj, dude_obj) < 5) then begin
			if (local_var(7) == 0) then begin
				set_local_var(7, 1);
				LastTime := game_time;
				call flee_dude;
			end
			else begin
				if (anim_busy(self_obj) == 0) and (game_time - LastTime > 100) then begin
					rndz := random(0, 20);
					if (rndz == 1) then begin
						LastTime := game_time;
						call flee_dude;
					end
				end
			end
		end
	end
	else begin
		if obj_can_see_obj(self_obj, dude_obj) then begin
			if global_var(ENEMY_RAIDERS) then begin
				tmp_hostile := 1;
			end
			else begin
				if (global_var(GARLS_FRIEND) != 1) then begin
					if (dude_is_armed) and ((Weapons == 0) and (killing_women == 0)) then begin
						Weapons := 1;
						dialogue_system_enter;
					end
				end
			end
		end
	end
	rndz := random(0, (30 + (global_var(MISTAKEN_ID) * 10)));
	if (rndz == 1) then begin
		if (anim_busy(self_obj) == 0) then begin
			animate_move_obj_to_tile(self_obj, tile_num_in_direction(tile_num(self_obj), random(0, 5), random(1, 2)), 0);
		end
	end
end

procedure damage_p_proc
begin
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		if (global_var(MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		end
	end
end

procedure timed_event_p_proc
begin
	if (fixed_param == 1) then begin
		if (dude_is_armed) then begin
			call combat;
		end
	end
	else begin
		if (fixed_param == 2) then begin
			if (tile_distance_objs(self_obj, dude_obj) < 6) then begin
				call combat;
			end
		end
		else begin
			if (fixed_param == 3) then begin
				if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
					call combat;
				end
			end
		end
	end
end

procedure destroy_p_proc
begin
	set_global_var(TOTAL_RAIDERS, (global_var(TOTAL_RAIDERS) - 1));// Number of raiders left
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		set_global_var(GARLS_FRIEND, 0);//GARLS_FRIEND
	end
	rm_timer_event(self_obj);

	inc_evil_critter
//
// "ARMOR DESTROY ON DEATH" MOD:
//
	fixt_destroy_armor
	rm_timer_event(self_obj);
end

procedure PickDeadBodyType
begin
	pick_dead_body_type
end


