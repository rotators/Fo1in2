/*

	Raiders - Alya

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_ALYA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;

procedure alya00;
procedure alya01;
procedure alya02;
procedure alya03;
procedure alya04;
procedure alya05;
procedure alya06;
procedure alya07;
procedure alya08;
procedure alya09;
procedure alya10;
procedure alyaend;

procedure combat;

variable tmp_hostile;
variable initial :=  0;
variable rndz;

procedure PickDeadBodyType;
variable DeathType := 56;

procedure start begin
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin
		if (cur_map_index == MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if not(initial) then begin
		initial :=  1;
		set_self_team(TEAM_RAIDERS );
		set_self_ai( AI_RAIDER_GUARD );
		debug_msg("ALYA init.");
	end
end

procedure timed_event_p_proc begin
	if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
		call combat;
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure alya00
begin
	gsay_reply(143, 101);
	if ((global_var(TANDI_RESCUE) == 1) and (global_var(TANDI_TALKED_ABOUT) == 1)) then begin
		giq_option( 4, SCRIPT_ALYA, 102, alya01, NEUTRAL_REACTION );
	end
	if (dude_is_male) then begin
		giq_option( 4, SCRIPT_ALYA, 103, alya02, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_ALYA, 104, alya03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 105, alyaend, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_ALYA, 106, alya05, NEUTRAL_REACTION );
end

procedure alya01
begin
	gsay_message(143, 107, 50);
end

procedure alya02
begin
	gsay_message(143, 108, 50);
	call combat;
end

procedure alya03
begin
	gsay_reply(143, 109);
	giq_option( 4, SCRIPT_ALYA, 110, alya04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 111, alya09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 112, alyaend, NEUTRAL_REACTION );
end

procedure alya04
begin
	gsay_reply(143, 113);
	giq_option( 4, SCRIPT_ALYA, 114, alya06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 115, alya08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 116, alyaend, NEUTRAL_REACTION );
end

procedure alya05
begin
	gsay_message(143, 117, 50);
end

procedure alya06
begin
	gsay_reply(143, 118);
	giq_option( 4, SCRIPT_ALYA, 119, alya07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 120, combat, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ALYA, 121, alyaend, NEUTRAL_REACTION );
end

procedure alya07
begin
	gsay_message(143, 122, 50);
	call combat;
end

procedure alya08
begin
	gsay_message(143, 123, 50);
	add_timer_event(self_obj, game_ticks(15), 1);
end

procedure alya09
begin
	gsay_message(143, 124, 50);
end

procedure alya10
begin
	gsay_message(143, 125, 50);
end

procedure alyaend
begin
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var(MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
		float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
	end
	else begin
		set_local_var(7, 1);
		set_local_var(3, 1);
		get_reaction
		start_gdialog(143, self_obj, 4, -1, -1);
		gsay_start;
		if (local_var(4) == 5) then begin
			call alya10;
		end
		else begin
			set_local_var(4, local_var(4) + 1);
			call alya00;
		end
		gsay_end;
		end_dialogue;
	end
end

procedure combat begin
	if (global_var(MISTAKEN_ID) == 1) then begin//Raiders think you are Garl's father
		set_self_team(87);
	end
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
//	variable LVar0 := 0;
//	rndz := random(0, 500);
//	set_local_var(10, game_time);
//	set_local_var(11, game_time + 5);

	if (global_var(TANDI_HIRELING_STATUS) == 2) then begin
		set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
	end
	else begin
		if (global_var(MISTAKEN_ID)) then begin//Raiders think you are Garl's father
			variable LVar0 := 0;
			rndz := random(0, 500);
			set_global_var(ENEMY_RAIDERS, 0);
			if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
				LVar0 := tile_num_in_direction(tile_num(self_obj), random(0, 5), 3);
				if ((tile_distance(tile_num(self_obj), LVar0) > 2) and (rndz == 0)) then begin
					if (rndz == 0) then begin
						float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
					end
					animate_move_obj_to_tile(self_obj, LVar0, 0);
				end
			end
		end
		else begin
			if global_var(TANDI_ESCAPE) then begin
				set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
			end
			if (obj_can_see_obj(self_obj, dude_obj)) then begin
				if (global_var(RAID_LOOTING)) then begin//  "RAID_LOOTING" (are you stealing from them)
					set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
				end
			end
			if (map_var(2) == 1) then begin
				set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
			end
		end
	end
	if (global_var(ENEMY_RAIDERS) and obj_can_see_obj(self_obj, dude_obj)) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		attack(dude_obj);
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		if (global_var(MISTAKEN_ID) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		end
	end
end



procedure destroy_p_proc begin
	set_global_var(TOTAL_RAIDERS, (global_var(TOTAL_RAIDERS) - 1));// Number of raiders left
	if (source_obj == dude_obj) then begin
		set_global_var(ENEMY_RAIDERS, 1);//ENEMY_RAIDERS
		set_global_var(GARLS_FRIEND, 0);//GARLS_FRIEND

		inc_raiders_rep(REP_KILLED_RAIDER);
	end
	rm_timer_event(self_obj);

	inc_inv_evil_crit
	fixt_destroy_armor
	rm_timer_event(self_obj);
end

procedure description_p_proc
begin
	if not(local_var(7)) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_ALYA, 200));
	end
	set_local_var(7, 1);
end

procedure look_at_p_proc
begin
	script_overrides;
	if not(local_var(7)) then begin
		display_msg(message_str(SCRIPT_ALYA, 100));
	end
	else begin
		display_msg(message_str(SCRIPT_ALYA, 200));
	end
end

procedure PickDeadBodyType
begin
	pick_dead_body_type
end


