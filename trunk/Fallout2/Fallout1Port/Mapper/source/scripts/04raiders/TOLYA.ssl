/*

	Raiders - Tolya

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_TOLYA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure tolya00;
procedure tolya01;
procedure tolya02;
procedure tolya03;
procedure tolya04;
procedure tolya05;
procedure tolya06;
procedure tolya07;
procedure tolya08;
procedure tolya09;
procedure tolya10;
procedure tolyaend;

procedure combat;

variable rndz;
variable tmp_hostile;
variable Only_Once := 1;

procedure PickDeadBodyType;
variable DeathType := 56;


procedure start begin
	if shady_invaded then begin
		if (CUR_MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(7) != 1) then begin
				set_local_var(7, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_RAIDERS );
		set_self_ai( AI_GENERIC_RAIDER );
	end
end

procedure look_at_p_proc begin
	display_msg(message_str(SCRIPT_TOLYA, 100));
	script_overrides;
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure tolya00 begin
	gsay_reply(140, 101);
	giq_option( 4, SCRIPT_TOLYA, message_str(SCRIPT_TOLYA, 102) + dude_name + message_str(SCRIPT_TOLYA, 103), tolya01, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_TOLYA, 104, tolya02, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_TOLYA, 105, tolya03, NEUTRAL_REACTION );
end

procedure tolya01
begin
	gsay_reply(140, 106);
	giq_option( 4, SCRIPT_TOLYA, 107, tolya04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TOLYA, 108, tolyaend, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TOLYA, 109, tolya05, NEUTRAL_REACTION );
end

procedure tolya02
begin
	gsay_reply(140, 110);
	giq_option( 4, SCRIPT_TOLYA, message_str(SCRIPT_TOLYA, 111) + dude_name + message_str(SCRIPT_TOLYA, 112), tolya07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TOLYA, 113, combat, NEUTRAL_REACTION );
end

procedure tolya03
begin
	gsay_message(140, 114, 50);
end

procedure tolya04
begin
	gsay_reply(140, 115);
	giq_option( 4, SCRIPT_TOLYA, 116, tolya06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TOLYA, 117, tolyaend, NEUTRAL_REACTION );
end

procedure tolya05
begin
	gsay_reply(140, 118);
	giq_option( 4, SCRIPT_TOLYA, 119, tolya08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TOLYA, 120, tolya09, NEUTRAL_REACTION );
end

procedure tolya06
begin
	gsay_message(140, 121, 50);
	call combat;
end

procedure tolya07
begin
	gsay_message(140, 122, 50);
end

procedure tolya08
begin
	set_local_var(6, 1);
	if (local_var(5) == 0) then begin
		gsay_reply(140, 123);
		giq_option( 4, SCRIPT_TOLYA, 124, tolya09, NEUTRAL_REACTION );
	end
	else begin
		gsay_message(140, 123, 50);
		call tolyaend;
	end
end

procedure tolya09
begin
	set_local_var(5, 1);
	if (local_var(6) == 0) then begin
		gsay_reply(140, 125);
		giq_option( 4, SCRIPT_TOLYA, 126, tolya08, NEUTRAL_REACTION );
	end
	else begin
		gsay_message(140, 125, 50);
		call tolyaend;
	end
end

procedure tolya10
begin
	gsay_message(140, 127, 50);
end

procedure tolyaend
begin
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
	end
	else begin
		set_local_var(3, 1);
		get_reaction
		start_gdialog(140, self_obj, 4, -1, -1);
		gsay_start;
		if (local_var(4) == 5) then begin
			call tolya10;
		end
		else begin
			set_local_var(4, local_var(4) + 1);
			call tolya00;
		end
		gsay_end;
		end_dialogue;
	end
end

procedure combat begin
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		set_self_team( TEAM_87 );
	end
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	variable LVar0 := 0;
	rndz := random(0, 500);
//	set_local_var(10, game_time);
//	set_local_var(11, game_time + 5);
	if (global_var( GVAR_TANDI_HIRELING_STATUS ) == 2) then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
	end
	else begin
		if (global_var( GVAR_MISTAKEN_ID )) then begin//Raiders think you are Garl's father
			set_global_var( GVAR_ENEMY_RAIDERS, 0 );
			if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
				LVar0 := tile_num_in_direction(self_tile, random(0, 5), 3);
				if ((tile_distance(self_tile, LVar0) > 2) and (rndz == 0)) then begin
					if (rndz == 0) then begin
						float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
					end
					animate_move_obj_to_tile(self_obj, LVar0, 0);
				end
			end
		end
		else begin
			if global_var( GVAR_TANDI_ESCAPE ) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
			end
			if (self_can_see_dude) then begin
				if (global_var( GVAR_RAID_LOOTING )) then begin//  "RAID_LOOTING" (are you stealing from them)
					set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
				end
			end
			if (map_var(2) == 1) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
			end
		end
	end
	if (global_var( GVAR_ENEMY_RAIDERS ) and self_can_see_dude) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		attack(dude_obj);
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		if (global_var( GVAR_MISTAKEN_ID ) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		end
	end
end

procedure destroy_p_proc begin
	set_global_var( GVAR_TOTAL_RAIDERS, (get_raiders_alive - 1) );// Number of raiders left
	//if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		set_global_var( GVAR_GARLS_FRIEND, 0 );//GARLS_FRIEND
		inc_raiders_rep(REP_BONUS_KILLED_RAIDER);
	//end
	inc_inv_evil_crit
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
