/*

	Raiders - Petrox

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_PETROX
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure petrox00;
procedure petrox01;
procedure petrox02;
procedure petrox03;
procedure petrox04;
procedure petrox05;
procedure petrox06;
procedure petrox07;
procedure petrox08;
procedure petrox09;
procedure petrox10;
procedure petroxend;
procedure combat;

variable tmp_hostile;
variable Only_Once := 1;

variable rndz;

procedure PickDeadBodyType;
variable DeathType := 56;


procedure start begin
	if shady_invaded then begin
		if (cur_map_index == MAP_RAIDERS) then begin //  RAIDERS - KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_RAIDERS );
		set_self_ai( AI_GENERIC_RAIDER );
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_PETROX, 100));
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure petrox00
begin
	gsay_reply(139, 101);
	giq_option( 4, SCRIPT_PETROX, message_str(SCRIPT_PETROX, 102) + dude_name + message_str(SCRIPT_PETROX, 103), petrox01, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_PETROX, 104, petrox02, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_PETROX, 105, petrox03, NEUTRAL_REACTION );
end

procedure petrox01
begin
	gsay_reply(139, 106);
	giq_option( 4, SCRIPT_PETROX, 107, petrox04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_PETROX, 108, petroxend, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_PETROX, 109, petrox05, NEUTRAL_REACTION );
end

procedure petrox02
begin
	gsay_reply(139, 110);
	giq_option( 4, SCRIPT_PETROX, 111, petrox07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_PETROX, 112, petrox08, NEUTRAL_REACTION );
end

procedure petrox03
begin
	gsay_message(139, 113, 50);
end

procedure petrox04
begin
	gsay_reply(139, 114);
	giq_option( 4, SCRIPT_PETROX, 115, petrox06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_PETROX, 116, petroxend, NEUTRAL_REACTION );
end

procedure petrox05
begin
	gsay_reply(139, 117);
	giq_option( 4, SCRIPT_PETROX, 118, petrox07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_PETROX, 119, petrox08, NEUTRAL_REACTION );
end

procedure petrox06
begin
	gsay_message(139, 120, 50);
	call combat;
end

procedure petrox07
begin
	gsay_reply(139, 121);
	giq_option( 4, SCRIPT_PETROX, 122, petrox08, NEUTRAL_REACTION );
end

procedure petrox08
begin
	if (dude_is_female) then begin
		gsay_reply(139, 124);
		giq_option( 4, SCRIPT_PETROX, 125, petrox09, NEUTRAL_REACTION );
		giq_option( 4, SCRIPT_PETROX, 127, petrox09, NEUTRAL_REACTION );
	end
	else begin
		gsay_message(139, 123, 50);
	end
end

procedure petrox09
begin
	gsay_message(139, 126, 50);
end

procedure petrox10
begin
	gsay_message(139, 128, 50);
end

procedure petroxend
begin
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
	end
	else begin
		set_local_var(3, 1);
		get_reaction
		start_gdialog(139, self_obj, 4, -1, -1);
		gsay_start;
		if (local_var(4) == 5) then begin
			call petrox10;
		end
		else begin
			set_local_var(4, local_var(4) + 1);
			call petrox00;
		end
		gsay_end;
		end_dialogue;
	end
end

procedure combat begin
	if (global_var( GVAR_MISTAKEN_ID ) == 1) then begin//Raiders think you are Garl's father
		set_self_team( TEAM_87 );
	end
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	variable LVar0 := 0;
	rndz := random(0, 500);
//	set_local_var(10, game_time);
//	set_local_var(11, game_time + 5);
	if (global_var( GVAR_TANDI_HIRELING_STATUS ) == 2) then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
	end
	else begin
		if (global_var( GVAR_MISTAKEN_ID )) then begin//Raiders think you are Garl's father
			set_global_var( GVAR_ENEMY_RAIDERS, 0 );
			if ((tile_distance_objs(self_obj, dude_obj) < 5) and (rndz == 0)) then begin
				LVar0 := tile_num_in_direction(tile_num(self_obj), random(0, 5), 3);
				if ((tile_distance(tile_num(self_obj), LVar0) > 2) and (rndz == 0)) then begin
					if (rndz == 0) then begin
						float_msg(self_obj, message_str(SCRIPT_GENRAIDR, random(102, 106)), 8);
					end
					animate_move_obj_to_tile(self_obj, LVar0, 0);
				end
			end
		end
		else begin
			if global_var( GVAR_TANDI_ESCAPE ) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
			end
			if (obj_can_see_obj(self_obj, dude_obj)) then begin
				if (global_var( GVAR_RAID_LOOTING )) then begin//  "RAID_LOOTING" (are you stealing from them)
					set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
				end
			end
			if (map_var(2) == 1) then begin
				set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
			end
		end
	end
	if (global_var( GVAR_ENEMY_RAIDERS ) and obj_can_see_obj(self_obj, dude_obj)) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		attack(dude_obj);
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		if (global_var( GVAR_MISTAKEN_ID ) == 0) then begin//PC was (is?) not mistaken for Garl's father
			set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		end
	end
end

procedure destroy_p_proc begin
	set_global_var( GVAR_TOTAL_RAIDERS, (get_raiders_alive - 1) );// Number of raiders left
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_RAIDERS, 1 );//ENEMY_RAIDERS
		set_global_var( GVAR_GARLS_FRIEND, 0 );//GARLS_FRIEND
		inc_raiders_rep(REP_KILLED_RAIDER);
	end

	inc_inv_evil_crit
	//fixt_destroy_armor
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end






