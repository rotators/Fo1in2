/*

	Brotherhood - Generic Initiate

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_GENINIT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable temp;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_BROTHERHOOD );
		if (local_var(0) < 1) then begin
			temp := random(1, 2);
			set_local_var(0, temp);
		end
		set_self_ai( AI_BROTHERHOOD_INITIATE );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (global_var( GVAR_ENEMY_BROTHERHOOD )) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	float_msg(self_obj, message_str(SCRIPT_RD1INIT, random(126, 132)), 2);
end

procedure destroy_p_proc begin

	set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
	rm_timer_event(self_obj);
	inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(0) == 1) then begin
		display_msg(message_str(SCRIPT_RD1INIT, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_RD1INIT, 102));
	end
end


