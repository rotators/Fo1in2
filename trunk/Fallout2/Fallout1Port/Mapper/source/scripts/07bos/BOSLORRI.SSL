/*

	Brotherhood - Lorri the scribe

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_BOSLORRI
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure map_enter_p_proc;//    script_action == 15
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21

procedure BoS_Lorri00;
procedure BoS_Lorri01;
procedure BoS_Lorri02;
procedure BoS_Lorri03;
procedure BoS_Lorri04;
procedure BoS_Lorri05;
procedure BoS_Lorri06;
procedure BoS_Lorri07;
procedure BoS_Lorri08;
procedure BoS_Lorri09;
procedure BoS_Lorri10;
procedure BoS_Lorri11;
procedure BoS_Lorri12;
procedure BoS_Lorri13;
procedure BoS_Lorri14;
procedure BoS_Lorri15;
procedure BoS_Lorri16;
procedure BoS_Lorri17;
procedure BoS_Lorri18;
procedure BoS_Lorri19;
procedure BoS_Lorri20;
procedure BoS_Lorri21;
procedure BoS_Lorri22;
procedure BoS_Lorri23;
procedure BoS_Lorri24;
procedure BoS_Lorri25;
procedure BoS_Lorri26;
procedure BoS_Lorriend;

procedure Dumb02;
procedure Dumb03;
procedure Dumb04;
procedure Dumb05;
procedure Dumb06;
procedure Dumb06a;
procedure raisiq;
procedure sorry;

variable tmp_hostile;
variable Only_Once := 1;
variable Heal_amount;
variable cost;
variable money;
variable temp;
variable rad_level;
variable bag_ptr := 0;

procedure Sex01;
procedure Sex02;
procedure Sex03kis;
procedure Sex04kis;
procedure Sex03ask;
procedure Sex04ask;
procedure Sex05ask;
procedure SexEnd01;
procedure SexAgain;
procedure SexInterim;

variable INTcost;
//variable Sex_SoundByte := 0;

//	local_var(4) == been told about operations
//	local_var(5) == exchanged names
//	local_var(6) == # of times have talked to Lori
//	local_var(7) == TimesOpSTR
//	local_var(8) == TimesOpPER
//	local_var(9) == TimesOpENDUR
//	local_var(10) == TimesOpCHA
//	local_var(11) == TimesOpINT
//		12 is reserved, do not use
//	local_var(13) == OpDumb
//	local_var(14) == BackLine
//	local_var(15) == had sex once already
//  local_var(16) == TimesOpAGL


procedure start begin
end

procedure map_enter_p_proc begin
	if Only_Once then begin
		set_self_team(TEAM_BROTHERHOOD );
		set_self_ai( AI_BROTHERHOOD_SCRIBE );
		Only_Once := 0;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if not(local_var(5)) then begin
		display_msg(message_str(SCRIPT_BOSLORI, 100));
	end
	else begin
		display_msg(message_str(SCRIPT_BOSLORI, 101));
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (global_var(ENEMY_BROTHERHOOD)) then begin
		tmp_hostile := 1;
	end
	else begin
		if (tmp_hostile) then begin
			set_global_var(ENEMY_BROTHERHOOD, 1);
			tmp_hostile := 0;
			attack(dude_obj);
		end
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	Heal_amount := get_critter_stat(dude_obj, STAT_max_hp) - get_critter_stat(dude_obj, STAT_current_hp);
	get_reaction
	start_gdialog(675, self_obj, 4, -1, -1);
	gsay_start;
	call BoS_Lorri00;
	gsay_end;
	end_dialogue;
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);

	if  (source_obj == dude_obj) then begin
		set_global_var(ENEMY_BROTHERHOOD, 1);
	end
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure BoS_Lorri00 begin
	if (local_var(6) < 5) then begin
		set_local_var(6, (local_var(6) + 1));
	end
	if local_var(5) then begin
		gsay_reply(675, 158);
	end
	else begin
		if (local_var(6) >= 2) then begin
			gsay_reply(675, 158);
			NOption( 124, BoS_Lorri23, 4 );
		end
		else begin
			gsay_reply(675, 102);
			NOption( 124, BoS_Lorri23, 4 );
		end
	end
	if (local_var(15) and not(global_var(FIXT_BUGFIXES_ONLY)) and global_var(BOS_LORRI_SEX_ENABLED) and local_var(5)) then begin
		GOption( 314, SexAgain, 4 );
	end
	if (Heal_amount > 0) then begin
		NOption( 103, BoS_Lorri04, 4 );//	hurt HP
	end
	if (get_critter_stat(dude_obj, STAT_current_rad) != 0) then begin
		NOption( 104, BoS_Lorri05, 4 );//	irradiated
	end
	if (get_critter_stat(dude_obj, STAT_current_poison) != 0) then begin//  (get_poison(dude_obj) != 0)  gives an error
		NOption( 140, BoS_Lorri21, 4 );//	poisoned
	end
	if (local_var(4) == 0) then begin
		NOption( 105, BoS_Lorri07, 4 );//	operations
	end
	else begin
		NOption( 106, BoS_Lorri09, 4 );//	operations again
	end
	if (local_var(13) == 1) or (local_var(11) != 0) then begin
		NOption( 107, BoS_Lorri01, -3 );//	"you fix?"
	end
	else begin
		NOption( 107, Dumb02, -3 );//	"you fix?"
	end
end

procedure BoS_Lorri23 begin
	gsay_reply(675, 125);
	set_local_var(5, 1);
	NOption( message_str(SCRIPT_BOSLORI, 151) + dude_name + message_str(SCRIPT_BOSLORI, 152), BoS_Lorri24, 4 );
end

procedure BoS_Lorri24 begin
	gsay_reply(675, message_str(SCRIPT_BOSLORI, 138) + dude_name + message_str(SCRIPT_BOSLORI, 152) + message_str(SCRIPT_BOSLORI, 142));
	if (local_var(15) and not(global_var(FIXT_BUGFIXES_ONLY)) and global_var(BOS_LORRI_SEX_ENABLED) and local_var(5)) then begin
		GOption( 314, SexAgain, 4 );
	end
	if (Heal_amount > 0) then begin
		NOption( 103, BoS_Lorri04, 4 );//	hurt HP
	end
	if (get_critter_stat(dude_obj, STAT_current_rad) != 0) then begin
		NOption( 104, BoS_Lorri05, 4 );//	irradiated
	end
	if (get_critter_stat(dude_obj, STAT_current_poison) != 0) then begin//  (get_poison(dude_obj) != 0)  gives an error
		NOption( 140, BoS_Lorri21, 4 );//	poisoned
	end
	if (local_var(4) == 0) then begin
		NOption( 105, BoS_Lorri07, 4 );//	operations
	end
	else begin
		NOption( 106, BoS_Lorri09, 4 );//	operations again
	end
	if (local_var(13) == 1) or (local_var(11) != 0) then begin
		NOption( 107, BoS_Lorri01, -3 );//	"you fix?"
	end
	else begin
		NOption( 107, Dumb02, -3 );//	"you fix?"
	end
end

procedure BoS_Lorri01 begin
	gsay_message(675, 108, 50);
	if (Heal_amount > 0) then begin
		call BoS_Lorri02;
	end
	else begin
		call BoS_Lorri03;
	end
end

procedure BoS_Lorri02 begin
	gfade_out(1);
	critter_heal(dude_obj, Heal_amount);
	gfade_in(1);
	gsay_message(675, 109, 50);
end

procedure BoS_Lorri03 begin
	gsay_message(675, 110, 50);
end

procedure BoS_Lorri04 begin
	gfade_out(1);
	critter_heal(dude_obj, Heal_amount);
	gfade_in(1);
	gsay_message(675, random(112, 116), 50);
end

procedure BoS_Lorri05 begin
	gsay_message(675, 117, 50);
	gfade_out(1);
	rad_level := get_critter_stat(dude_obj, STAT_current_rad);
	radiation_dec(dude_obj, rad_level);
//set_critter_stat(dude_obj, 37, 0);
	game_time_advance(game_ticks(86400));
	gfade_in(1);
	gsay_message(675, 146, 50);
end

procedure BoS_Lorri06 begin
	gsay_message(675, 118, 50);
end

procedure BoS_Lorri07 begin
	gsay_reply(675, 119);
	NOption( 120, BoS_Lorri08, 4 );
	NOption( 121, BoS_Lorriend, 4 );
end

procedure BoS_Lorri08 begin
	set_local_var(4, 1);
	if global_var(FIXT_BUGFIXES_ONLY) or not(global_var(BOS_LORRI_CHA_ENABLED)) then begin
		gsay_reply(675, 222);
	end
	else begin
		gsay_reply(675, 122);
	end
	call BoS_Lorri10;
end

procedure BoS_Lorri09 begin
	gsay_reply(675, 126);
	call BoS_Lorri10;
end

procedure BoS_Lorri17 begin
	gsay_reply(675, 153);
	call BoS_Lorri10;
end

procedure BoS_Lorri10 begin
	variable TotalOpsAllowed;
	TotalOpsAllowed	:= global_var(BOS_LORRI_NUM_OPS_ALLOWED);

	if (local_var(7) < TotalOpsAllowed) then begin
		NOption( 127, BoS_Lorri11, 4 );//  STRENGTH
	end
	if (local_var(8) < TotalOpsAllowed) then begin
		NOption( 128, BoS_Lorri12, 4 );//  PERCEPTION
	end
	if (local_var(9) < TotalOpsAllowed) then begin
		NOption( 129, BoS_Lorri13, 4 );//  ENDURANCE
	end

	if (not(global_var(FIXT_BUGFIXES_ONLY)) and global_var(BOS_LORRI_CHA_ENABLED)) then begin
		if (local_var(10) < TotalOpsAllowed) then begin
			NOption( 123, BoS_Lorri25, 4 );//  CHARISMA
		end
	end

	if (local_var(11) < TotalOpsAllowed) then begin
		NOption( 130, BoS_Lorri14, 4 );//  INTELLIGENCE
	end
	if (local_var(16) < TotalOpsAllowed) then begin
		NOption( 131, BoS_Lorri15, 4 );//  AGILITY
	end
	NOption( 132, BoS_Lorriend, 4 );
end

//	STRENGTH
procedure BoS_Lorri11 begin
	variable STRcheck := 0;
	STRcheck := get_critter_stat(dude_obj, STAT_st);
	if (STRcheck > 9) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorCheck := 0;
			ArmorCheck := critter_inven_obj(dude_obj, 0);//  if (ArmorCheck == 139099868) or (ArmorCheck == 43082196) then begin
			if (obj_pid(ArmorCheck) == PID_POWERED_ARMOR) or (obj_pid(ArmorCheck) == PID_HARDENED_POWER_ARMOR) then begin
				gsay_message(675, 150, 50);
			end
			else begin
				call sorry;//  Stat already at 10
			end
		end
		else begin
			call sorry;//  Stat already at 10
		end
	end
	else begin
		cost := 2000 + (2000 * local_var(7));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 133) + cost + message_str(SCRIPT_BOSLORI, 233));
		NOption( 135, BoS_Lorri18, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	STRENGTH
procedure BoS_Lorri18 begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
//
//  TeamX code:
//
//		if ArmorMove > 0 then begin
//			bag_ptr := create_object_sid(16777262, 0, 0, 860);
//			move_to(bag_ptr, 0, 0);
//			move_obj_inven_to_obj(dude_obj, bag_ptr);
//			move_obj_inven_to_obj(bag_ptr, dude_obj);
//			destroy_object(bag_ptr);
//		end
//
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(7, (local_var(7) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 154, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(3 * 604800));//	3 weeks
		temp := get_critter_stat(dude_obj, STAT_st);//	STRENGTH
		set_critter_stat( dude_obj, STAT_st, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 161));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 156);
		GOption( 182, BoS_Lorriend, 4 );
	end
	else begin
		call BoS_Lorri16;
	end
end

//	PERCEPTION
procedure BoS_Lorri12 begin
	temp := get_critter_stat(dude_obj, STAT_pe);
	if (temp > 9) then begin
		call sorry;//  Stat already at 10
	end
	else begin
		cost := 4000 + (4000 * local_var(8));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 137) + cost + message_str(SCRIPT_BOSLORI, 237));
		NOption( 135, BoS_Lorri19, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	PERCEPTION
procedure BoS_Lorri19 begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(8, (local_var(8) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 159, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(604800));//	1 week
		temp := get_critter_stat(dude_obj, STAT_pe);//	PERCEPTION
		set_critter_stat( dude_obj, STAT_pe, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 162));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 161);
		GOption( 182, BoS_Lorriend, 4 );
	end
	else begin
		call BoS_Lorri16;
	end
end

//	ENDURANCE
procedure BoS_Lorri13 begin
	temp := get_critter_stat(dude_obj, STAT_en);
	if (temp > 9) then begin
		call sorry;//  Stat already at 10
	end
	else begin
		cost := 3000 + (3000 * local_var(9));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 139) + cost + message_str(SCRIPT_BOSLORI, 239));
		NOption( 135, BoS_Lorri20, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	ENDURANCE
procedure BoS_Lorri20 begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(9, (local_var(9) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 164, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(604800));//	1 week
		temp := get_critter_stat(dude_obj, STAT_en);//	ENDURANCE
		set_critter_stat( dude_obj, STAT_en, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 163));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 166);
		GOption( 182, BoS_Lorriend, 4 );
	end
	else begin
		call BoS_Lorri16;
	end
end

//	CHARISMA
procedure BoS_Lorri25 begin
	temp := get_critter_stat(dude_obj, STAT_ch);
	if (temp > 9) then begin
		call sorry;//  Stat already at 10
	end
	else begin
		cost := 3500 + (3500 * local_var(10));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 145) + cost + message_str(SCRIPT_BOSLORI, 245));
		NOption( 135, BoS_Lorri26, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	CHARISMA
procedure BoS_Lorri26 begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(10, (local_var(10) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 180, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(3 * 604800));//	3 weeks
		temp := get_critter_stat(dude_obj, STAT_ch);//	CHARISMA
		set_critter_stat( dude_obj, STAT_ch, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 164));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 181);
		GOption( 182, BoS_Lorriend, 4 );
		if not(global_var(FIXT_BUGFIXES_ONLY)) and global_var(BOS_LORRI_SEX_ENABLED) and not(local_var(15)) then begin
			GOption( 185, Sex01, 4 );
		end
	end
	else begin
		call BoS_Lorri16;
	end
end

//	INTELLIGENCE
procedure BoS_Lorri14 begin
	temp := get_critter_stat(dude_obj, STAT_iq);
	if (temp > 9) then begin
		call sorry;//  Stat already at 10
	end
	else begin
		cost := 6000 + (6000 * local_var(11));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 141) + cost + message_str(SCRIPT_BOSLORI, 241));
		NOption( 135, raisiq, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	INTELLIGENCE
procedure raisiq begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(11, (local_var(11) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 168, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(3 * 604800));//	3 weeks
		temp := get_critter_stat(dude_obj, STAT_iq);//	INTELLIGENCE
		set_critter_stat( dude_obj, STAT_iq, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 165));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 170);
		GOption( 182, BoS_Lorriend, 4 );
	end
	else begin
		call BoS_Lorri16;
	end
end

//	AGILITY
procedure BoS_Lorri15 begin
	temp := get_critter_stat(dude_obj, STAT_ag);
	if (temp > 9) then begin
		call sorry;//  Stat already at 10
	end
	else begin
		cost := 5000 + (5000 * local_var(16));
		gsay_reply(675, message_str(SCRIPT_BOSLORI, 143) + cost + message_str(SCRIPT_BOSLORI, 243));
		NOption( 135, BoS_Lorri22, 4 );
		NOption( 136, BoS_Lorri17, 4 );
	end
end

//	AGILITY
procedure BoS_Lorri22 begin
	if (item_caps_total(dude_obj) >= cost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		if critter_inven_obj(dude_obj, 0) > 0 then begin
			variable ArmorMove := 0;
			ArmorMove := critter_inven_obj(dude_obj, 0);
			rm_obj_from_inven(dude_obj, ArmorMove);
			add_obj_to_inven(dude_obj, ArmorMove);
		end
		set_local_var(16, (local_var(16) + 1));
		if (item_caps_total(dude_obj) >= cost) then begin
			item_caps_adjust(dude_obj, -cost);
		end
		gsay_message(675, 174, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(3 * 604800));//	3 weeks
		temp := get_critter_stat(dude_obj, STAT_ag);//	AGILITY
		set_critter_stat( dude_obj, STAT_ag, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 166));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 176);
		GOption( 182, BoS_Lorriend, 5 );
	end
	else begin
		call BoS_Lorri16;
	end
end

//	NOT ENOUGH MONEY
procedure BoS_Lorri16 begin
	gsay_message(675, random(147, 149), 50);
end

// Poison detox
procedure BoS_Lorri21 begin
	gsay_message(675, 504, 50);
	gfade_out(1);
	poison(dude_obj, -get_critter_stat(dude_obj, STAT_current_poison));
	gfade_out(1);
	game_time_advance(game_ticks(3600));
	gfade_in(1);
	gsay_message(675, random(112, 116), 50);
end

procedure Dumb02 begin
	gsay_reply(675, 400);
	if (local_var(11) == 0) then begin
		NOption( 401, Dumb04, -3 );
	end
	NOption( 402, Dumb03, -3 );
end

procedure Dumb03 begin
	gsay_reply(675, 403);
	if (Heal_amount != 0) then begin
		NOption( 404, Dumb06a, -3 );
	end
	if get_critter_stat(dude_obj, STAT_current_rad) then begin
		NOption( 405, BoS_Lorri05, -3 );
	end
	if get_critter_stat(dude_obj, STAT_current_poison) then begin
		NOption( 155, BoS_Lorri21, -3 );
	end
	NOption( 406, BoS_Lorriend, -3 );
end

procedure Dumb04 begin
	INTcost := (6000 + (6000 * local_var(11))) / 2;
	if (item_caps_total(dude_obj) >= INTcost) or ((game_time / (10 * 60 * 60 * 24 * 365)) >= 10) then begin
		set_local_var(13, 1);
		set_local_var(11, 1);
		if (item_caps_total(dude_obj) >= INTcost) then begin
			item_caps_adjust(dude_obj, -INTcost);
		end
		gsay_message(675, 168, 50);
		gfade_out(1);
		gsay_end;
		end_dialogue;
		game_time_advance(game_ticks(604800));//	1 week
		temp := get_critter_stat(dude_obj, STAT_iq);//	INTELLIGENCE
		set_critter_stat( dude_obj, STAT_iq, 1 );//		 + 1
		display_msg(message_str(SCRIPT_GENCHAT, 165));
		start_gdialog(675, self_obj, 4, -1, -1);
		gsay_start;
		gfade_in(1);
		gsay_reply(675, 407);
		NOption( 409, Dumb06, 4 );
		NOption( 408, Dumb05, -3 );
	end
	else begin
		call BoS_Lorri16;
	end
end

procedure Dumb05 begin
	gsay_message(675, 410, 50);
end

procedure Dumb06 begin
	gsay_reply(675, 411);
	NOption( 412, BoS_Lorriend, 4 );
end

procedure Dumb06a begin
	gfade_out(1);
	critter_heal(dude_obj, Heal_amount);
	gfade_in(1);
	gsay_message(675, 413, 50);
end

procedure BoS_Lorriend begin
end

procedure sorry begin
	gsay_message(675, 500, 50);
end

procedure Sex01 begin
	if (get_critter_stat(dude_obj, STAT_ch) < 5) then begin
		gsay_message(675, 301, 50);
	end
	else begin
		if (get_critter_stat(dude_obj, STAT_ch) < 8) then begin
			gsay_message(675, 300, 50);
		end
		else begin
			if (get_critter_stat(dude_obj, STAT_ch) >= 8) or (has_skill(dude_obj, SKILL_SPEECH) >= 80) then begin
				gsay_reply(675, 302);
				GOption( 303, Sex02, 6 );
				NOption( 304, BoS_Lorriend, 4 );
			end
		end
	end
end

procedure Sex02 begin
	gsay_reply(675, 305);
	NOption( 306, SexEnd01, 5 );
	if (global_var(BOS_LORRI_SEX_ENABLED) == 1) then begin
		NOption( 308, Sex03kis, 4 );
		NOption( 307, Sex03ask, 6 );
	end
	else begin
		NOption( 307, SexEnd01, 6 );
	end
end

procedure Sex03kis begin
	gsay_reply(675, 316);
	NOption( 144, Sex04kis, 4 );
end

procedure Sex04kis begin
	call SexInterim;
	gsay_message(675, 312, 49);
	set_local_var(15, 1);
end

procedure SexInterim begin
	variable lvar0;
	gfade_out(1);
/*
	lvar0 := random(1, 3);
	while(lvar0 == Sex_SoundByte) do
	begin
		lvar0 := random(1, 3);
	end
	Sex_SoundByte := lvar0;
	if (Sex_SoundByte == 1) then begin
		play_sfx("moan1");
	end
	else begin
		if (Sex_SoundByte == 2) then begin
			play_sfx("moan2");
		end
		else begin
			play_sfx("moan3");
		end
	end
*/
	gfade_in(1);
end

procedure Sex03ask begin
	gsay_reply(675, 310);
	NOption( 144, Sex04ask, 4 );
end

procedure Sex04ask begin
	gfade_out(1);
	gfade_in(1);
	gsay_reply(675, 311);
	NOption( 144, Sex05ask, 4 );
end

procedure Sex05ask begin
	call SexInterim;
	if (get_critter_stat(dude_obj, STAT_st) > 6) or (get_critter_stat(dude_obj, STAT_en) > 6) then begin// STR or END
		gsay_reply(675, 313);
		GOption( 315, BoS_Lorriend, 4 );
	end
	else begin
		gsay_message(675, 312, 49);
	end
	set_local_var(15, 1);
end

procedure SexEnd01 begin
	gsay_message(675, 309, 50);
end

procedure SexAgain begin
	variable LVar0;
	LVar0 := random(317, 319);
	while(LVar0 == local_var(14)) do
	begin
		LVar0 := random(317, 319);
	end
	set_local_var(14, LVar0);
	gsay_message(675, message_str(SCRIPT_BOSLORI, local_var(14)), 49);
	call SexInterim;
	gsay_message(675, 312, 49);
end




