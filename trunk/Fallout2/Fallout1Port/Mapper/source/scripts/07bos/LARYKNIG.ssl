/*

	Brotherhood - Knight Larry

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_LARYKNIG
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure goto00;
procedure goto00b;
procedure goto01;
procedure goto02;
procedure goto03;
procedure goto04;
procedure goto05;
procedure goto06;

variable tmp_hostile;
variable Only_Once := 1;
variable rndx;


procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_BROTHERHOOD );
		set_self_ai( AI_BROTHERHOOD_KNIGHT );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (global_var( GVAR_ENEMY_BROTHERHOOD )) then begin
		tmp_hostile := 1;
	end
	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (local_var(0)) then begin
		call goto05;
	end
	else begin
		if (local_var(1) == 0) then begin
			start_gdialog(770, self_obj, 4, -1, -1);
			gsay_start;
			call goto00;
			gsay_end;
			end_dialogue;
		end
		else begin
			call goto06;
		end
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_LARYKNIG, 100));
end

procedure goto00
begin
	gsay_reply(770, 101);
	giq_option( 4, SCRIPT_LARYKNIG, 102, goto02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LARYKNIG, 103, goto00b, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_LARYKNIG, 104, goto01, NEUTRAL_REACTION );
end

procedure goto00b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call goto03;
	end
	else begin
		call goto04;
	end
end

procedure goto01
begin
	gsay_message(770, 105, 50);
end

procedure goto02
begin
	set_local_var(1, 1);
	critter_mod_skill(dude_obj, SKILL_ENERGY_WEAPONS, 5);
	game_time_advance(game_ticks(1800));
	gsay_message(770, 106, 50);
	display_msg(message_str(SCRIPT_GENCHAT, 116) + 5 + message_str(SCRIPT_GENCHAT, 119));
end

procedure goto03
begin
	set_local_var(1, 1);
	critter_mod_skill(dude_obj, SKILL_ENERGY_WEAPONS, 5);
	game_time_advance(game_ticks(1800));
	gsay_message(770, 107, 50);
	display_msg(message_str(SCRIPT_GENCHAT, 116) + 5 + message_str(SCRIPT_GENCHAT, 119));
end

procedure goto04
begin
	set_local_var(0, 1);
	gsay_message(770, 108, 50);
end

procedure goto05
begin
	float_msg(self_obj, message_str(SCRIPT_LARYKNIG, 109), 2);
end

procedure goto06
begin
	float_msg(self_obj, message_str(SCRIPT_LARYKNIG, random(216, 223)), 2);
end



