/*

	Hub - Captor (related to bos quest?)

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_HUBCAPTR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable GunLine;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_UNDERGROUND );
		set_self_ai( AI_HUB_UNDERGROUND );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	if (self_can_see_dude) then begin
		tmp_hostile := 1;
		GunLine := random(105, 108);
		if (GunLine == 108) then begin
			if (dude_is_female) then begin
				GunLine := 109;
			end
		end
		float_msg(self_obj, message_str(SCRIPT_HUBCAPTR, GunLine), 8);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	set_local_var(4, local_var(4) + 1);
	if (local_var(4) < 4) then begin
		float_msg(self_obj, message_str(SCRIPT_HUBCAPTR, 100 + local_var(4)), 8);
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_HUBCAPTR, 104), 8);
		call combat;
	end
end

procedure destroy_p_proc
begin

	set_map_var(0, map_var(0) + 1);
	inc_evil_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HUBCAPTR, 100));
end
