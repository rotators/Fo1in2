/*

	Farm Raider, Irwine's Farm

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_IRWIN
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;

variable Only_Once := 0;
variable PsstTime;

#define LVAR_Herebefore 		(4)

procedure Start begin
	if (Only_Once == 0) then begin
		item_caps_adjust(self_obj, random(1, 10));
		set_self_team(TEAM_RAIDERS );
		set_self_ai( AI_GUARD_HUMOROUS );
		Only_Once := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	float_msg(self_obj, mstr(random(300, 303)), 0);
end

procedure critter_p_proc begin
	if not(combat_is_initialized) then begin

		if not(local_var(LVAR_Herebefore)) then begin
			if (obj_can_see_obj(self_obj, dude_obj)) then begin
				float_msg(self_obj, mstr(random(300, 303)), 0);
				attack(dude_obj);
				set_local_var(LVAR_Herebefore, 1);
			end
		end
		else begin
			if ((((game_time / 10) - PsstTime) >= 30) and (tile_distance_objs(self_obj, dude_obj) <= 4)) then begin
				float_msg(self_obj, message_str( SCRIPT_IRWIN, random(300, 303) ), 0);
				PsstTime := game_time / 10;
			end
		end

	end
end

procedure destroy_p_proc begin
	set_global_var( GVAR_RAIDERS_LEFT_IRWIN, (global_var( GVAR_RAIDERS_LEFT_IRWIN ) - 1) );
	if (global_var( GVAR_RAIDERS_LEFT_IRWIN ) <= 0) then begin
		set_global_var( GVAR_IRWIN_FARM_STATUS, 2 );
	end
	inc_evil_critter
end

procedure map_enter_p_proc begin
	if global_var( GVAR_IRWIN_FARM_STATUS ) == 2 then begin
		if not(is_loading_game) then begin
			set_self_invisible;
		end
		move_to(self_obj, 29050, 0);
	end
end
