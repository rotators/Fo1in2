#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure combat;
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure damage_p_proc;//    script_action == 14
procedure Rick01;
procedure Rick02;
procedure Rick03;
procedure Rick04;
procedure Rick05;
procedure Rick06;
procedure Rick07;
procedure Rick08;
procedure Rick09;
procedure Rick10;
procedure RickEnd;
procedure RickCombat;

variable tmp_hostile;
variable Only_Once := 1;
variable nightWarned;
variable visible := 1;

procedure start
begin
	

	if Only_Once then begin
		Only_Once := 0;
		if (global_var(KILL_MERCHANT) == 5) then begin
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
			visible := 0;
		end
		set_self_team(42);
		set_self_ai( AI_GUARD_SADISTIC );
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		call look_at_p_proc;
	end
	else begin
		if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
			call pickup_p_proc;
		end
		else begin
			if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
				call talk_p_proc;
			end
			else begin
				if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					call critter_p_proc;
				end
				else begin
					if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
						call destroy_p_proc;
					end
					else begin
						if (script_action == 14) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
							call damage_p_proc;
						end
					end
				end
			end
		end
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
				call combat;
			end
		end
		else begin
			if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
				if ((tile_distance_objs(self_obj, dude_obj) <= 7) and (nightWarned == 0)) then begin
					nightWarned := 1;
					dialogue_system_enter;
				end
				if (tile_num(self_obj) != 19713) then begin
					animate_move_obj_to_tile(self_obj, 19713, 0);
				end
			end
			else begin
				if (tile_num(self_obj) != 17892) then begin
					animate_move_obj_to_tile(self_obj, 17892, 0);
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		start_gdialog(871, self_obj, 4, -1, -1);
		gsay_start;
		call Rick09;
		gsay_end;
		end_dialogue;
	end
	else begin
		start_gdialog(871, self_obj, 4, -1, -1);
		gsay_start;
		call Rick01;
		gsay_end;
		end_dialogue;
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		set_global_var(NUM_GOOD_MONSTERS_KILLED, global_var(NUM_GOOD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var(NUM_GOOD_MONSTERS_KILLED) % 2) == 0) then begin
			set_global_var(PLAYER_REPUTATION_GENERAL, (global_var(PLAYER_REPUTATION_GENERAL) - 1));
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HTWRRICK, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
end

procedure Rick01
begin
	gsay_reply(871, 101);
	giq_option( 4, SCRIPT_HTWRRICK, 103, Rick03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 104, Rick04, NEUTRAL_REACTION );
	if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
		giq_option( 4, SCRIPT_HTWRRICK, 105, Rick08, BAD_REACTION );
	end
	giq_option( -3, SCRIPT_HTWRRICK, 102, Rick02, NEUTRAL_REACTION );
end

procedure Rick02
begin
	gsay_reply(871, 106);
	giq_option( -3, SCRIPT_HTWRRICK, 107, Rick10, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HTWRRICK, 108, RickEnd, NEUTRAL_REACTION );
end

procedure Rick03
begin
	gsay_reply(871, 109);
	giq_option( 4, SCRIPT_HTWRRICK, 110, Rick05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 111, Rick06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 112, RickEnd, NEUTRAL_REACTION );
end

procedure Rick04
begin
	gsay_reply(871, 113);
	giq_option( 4, SCRIPT_HTWRRICK, 114, RickCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 115, Rick08, NEUTRAL_REACTION );
end

procedure Rick05
begin
	gsay_reply(871, 116);
	giq_option( 4, SCRIPT_HTWRRICK, 117, Rick06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 118, Rick07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 119, RickEnd, NEUTRAL_REACTION );
end

procedure Rick06
begin
	gsay_reply(871, 120);
	giq_option( 4, SCRIPT_HTWRRICK, 121, RickCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 122, Rick05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 123, RickEnd, NEUTRAL_REACTION );
end

procedure Rick07
begin
	gsay_message(871, 124, 51);
end

procedure Rick08
begin
	gsay_reply(871, 125);
	giq_option( 4, SCRIPT_HTWRRICK, 126, RickEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 127, RickCombat, BAD_REACTION );
end

procedure Rick09
begin
	gsay_reply(871, 128);
	giq_option( 4, SCRIPT_HTWRRICK, 131, RickCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRRICK, 132, RickEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HTWRRICK, 129, RickEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HTWRRICK, 130, RickCombat, BAD_REACTION );
end

procedure Rick10
begin
	gsay_message(871, 133, 51);
end

procedure RickEnd
begin
end

procedure RickCombat
begin
	call combat;
end




