#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure combat;
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure damage_p_proc;//    script_action == 14
procedure George01;
procedure George01a;
procedure George01b;
procedure George02;
procedure George02a;
procedure George03;
procedure George04;
procedure George05;
procedure George06;
procedure George07;
procedure George08;
procedure GeorgeEnd;

variable tmp_hostile;
variable Only_Once := 1;
variable lastTraverse;
variable currentLocation;
variable hereBefore;
variable visible := 1;

procedure start
begin
	fixt_critter_healing

	if Only_Once then begin
		Only_Once := 0;
		set_map_var(3, 0);
		if (global_var(KILL_MERCHANT) == 5) then begin
			if (metarule(22, 0) == 0) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
			visible := 0;
			set_map_var(3, 1);
		end
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, 42);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET, 16);
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		call look_at_p_proc;
	end
	else begin
		if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
			call pickup_p_proc;
		end
		else begin
			if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
				call talk_p_proc;
			end
			else begin
				if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					call critter_p_proc;
				end
				else begin
					if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
						call destroy_p_proc;
					end
					else begin
						if (script_action == 14) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
							call damage_p_proc;
						end
					end
				end
			end
		end
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
				call combat;
			end
		end
		else begin
			if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
				if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
					dialogue_system_enter;
				end
				else begin
					if ((global_var(KILL_MERCHANT) == 1) or (global_var(STEAL_NECKLACE) == 1)) then begin
						set_map_var(3, 1);
						if (((game_time / 10) - lastTraverse) >= 20) then begin
							lastTraverse := game_time / 10;
							if (currentLocation == 0) then begin
								currentLocation := 1;
								animate_move_obj_to_tile(self_obj, 19516, 0);
							end
							else begin
								currentLocation := 0;
								animate_move_obj_to_tile(self_obj, 22529, 0);
							end
						end
					end
					else begin
						if (tile_num(self_obj) != 22529) then begin
							animate_move_obj_to_tile(self_obj, 22529, 0);
						end
					end
				end
			end
			else begin
				if (tile_num(self_obj) != 22529) then begin
					animate_move_obj_to_tile(self_obj, 22529, 0);
				end
				if (map_var(2) == 1) then begin
					dialogue_system_enter;
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	anim(dude_obj, 1000, rotation_to_tile(tile_num(dude_obj), tile_num(self_obj)));
	get_reaction
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		float_msg(self_obj, message_str(SCRIPT_HTWRGRGE, 118), 2);
		call combat;
	end
	else begin
		if (global_var(ENEMY_HUB) == 1) then begin
			float_msg(self_obj, message_str(SCRIPT_HTWRGRGE, 120), 2);
			call combat;
		end
		else begin
			if (map_var(2) == 1) then begin
				set_map_var(2, 0);
				call George08;
			end
			else begin
				if (hereBefore == 0) then begin
					hereBefore := 1;
					start_gdialog(870, self_obj, 4, -1, -1);
					gsay_start;
					call George01;
					gsay_end;
					end_dialogue;
				end
				else begin
					call George07;
				end
			end
		end
	end
end

procedure destroy_p_proc
begin

	set_map_var(4, 1);
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		set_global_var(NUM_GOOD_MONSTERS_KILLED, global_var(NUM_GOOD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var(NUM_GOOD_MONSTERS_KILLED) % 2) == 0) then begin
			set_global_var(PLAYER_REPUTATION_GENERAL, (global_var(PLAYER_REPUTATION_GENERAL) - 1));
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HTWRGRGE, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
end

procedure George01
begin
	gsay_reply(870, 101);
	giq_option(4, 870, 103, George01a, 50);
	giq_option(4, 870, 104, George01b, 50);
	giq_option(4, 870, 105, George06, 50);
	giq_option(-3, 870, 102, George06, 50);
end

procedure George01a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -10))) then begin
		call George03;
	end
	else begin
		call George02;
	end
end

procedure George01b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 10))) then begin
		call George05;
	end
	else begin
		call George02;
	end
end

procedure George02
begin
	gsay_reply(870, 106);
	giq_option(4, 870, 107, George02a, 50);
	giq_option(4, 870, 108, George06, 50);
	giq_option(4, 870, 109, George03, 50);
	giq_option(4, 870, 110, GeorgeEnd, 50);
end

procedure George02a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call George03;
	end
	else begin
		call George06;
	end
end

procedure George03
begin
	set_map_var(3, 1);
	gsay_message(870, 111, 50);
end

procedure George04
begin
	gsay_message(870, 112, 51);
end

procedure George05
begin
	set_map_var(3, 1);
	gsay_message(870, 113, 50);
end

procedure George06
begin
	gsay_message(870, 114, 51);
end

procedure George07
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRGRGE, random(114, 117)), 2);
end

procedure George08
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRGRGE, 119), 2);
end

procedure GeorgeEnd
begin
end


