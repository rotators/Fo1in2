#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure combat;
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure damage_p_proc;//    script_action == 14
procedure Leon01;
procedure Leon01a;
procedure Leon02;
procedure Leon03;
procedure Leon04;
procedure Leon04a;
procedure Leon05;
procedure Leon06;
procedure Leon07;
procedure Leon08;
procedure Leon08a;
procedure Leon09;
procedure LeonEnd;
procedure LeonKickOut;
procedure LeonPerformDump;
procedure LeonCombat;

variable tmp_hostile;
variable Only_Once := 1;
variable visible := 1;

import variable Front_Door_Ptr;

procedure start
begin
	

	if Only_Once then begin
		Only_Once := 0;
		if (global_var(KILL_MERCHANT) == 5) then begin
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
			visible := 0;
		end
		set_self_team(42);
		set_self_ai( AI_GUARD_SADISTIC );
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		call look_at_p_proc;
	end
	else begin
		if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
			call pickup_p_proc;
		end
		else begin
			if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
				call talk_p_proc;
			end
			else begin
				if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					call critter_p_proc;
				end
				else begin
					if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
						call destroy_p_proc;
					end
					else begin
						if (script_action == 14) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
							call damage_p_proc;
						end
					end
				end
			end
		end
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
				call combat;
			end
		end
		else begin
			if (map_var(1) == 1) then begin
				call LeonPerformDump;
			end
		end
		if ((game_time_hour >= 600) and (game_time_hour < 700) or ((game_time_hour >= 700) and (game_time_hour < 1800))) then begin
			if (tile_num(self_obj) != 22120) then begin
				animate_move_obj_to_tile(self_obj, 22120, 0);
			end
		end
		else begin
			if (tile_num(self_obj) != 16519) then begin
				animate_move_obj_to_tile(self_obj, 16519, 0);
			end
		end
		if (tile_distance_objs(self_obj, dude_obj) < 6) then begin
			if ((local_var(5) == 0) and (tile_num(self_obj) == 22120)) then begin
				set_local_var(5, 1);
				dialogue_system_enter;
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		float_msg(self_obj, message_str(SCRIPT_HTWRLEON, 141), 2);
	end
	else begin
		if (global_var(ENEMY_HUB) == 1) then begin
			float_msg(self_obj, message_str(SCRIPT_HTWRLEON, 142), 2);
			call combat;
		end
		else begin
			if (local_var(4) == 0) then begin
				set_local_var(4, 1);
				start_gdialog(866, self_obj, 4, -1, -1);
				gsay_start;
				call Leon01;
				gsay_end;
				end_dialogue;
			end
			else begin
				start_gdialog(866, self_obj, 4, -1, -1);
				gsay_start;
				call Leon08;
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		set_global_var(NUM_GOOD_MONSTERS_KILLED, global_var(NUM_GOOD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var(NUM_GOOD_MONSTERS_KILLED) % 2) == 0) then begin
			set_global_var(PLAYER_REPUTATION_GENERAL, (global_var(PLAYER_REPUTATION_GENERAL) - 1));
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HTWRLEON, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
end

procedure Leon01
begin
	gsay_reply(866, 101);
	giq_option( 4, SCRIPT_HTWRLEON, 102, Leon01a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 103, LeonCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 104, Leon03, NEUTRAL_REACTION );
	if (global_var(KILL_MERCHANT) == 1) then begin
		giq_option( 4, SCRIPT_HTWRLEON, 140, Leon04, NEUTRAL_REACTION );
	end
	giq_option( -3, SCRIPT_HTWRLEON, 138, Leon09, NEUTRAL_REACTION );
end

procedure Leon01a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Leon02;
	end
	else begin
		call Leon04;
	end
end

procedure Leon02
begin
	gsay_reply(866, 105);
	giq_option( 4, SCRIPT_HTWRLEON, 106, LeonEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 107, Leon03, NEUTRAL_REACTION );
end

procedure Leon03
begin
	gsay_reply(866, 108);
	giq_option( 4, SCRIPT_HTWRLEON, 109, LeonCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 110, LeonKickOut, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 111, LeonCombat, BAD_REACTION );
end

procedure Leon04
begin
	gsay_reply(866, 112);
	giq_option( 4, SCRIPT_HTWRLEON, 113, Leon03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 114, Leon04a, NEUTRAL_REACTION );
	if (global_var(KILL_MERCHANT) == 1) then begin
		giq_option( 4, SCRIPT_HTWRLEON, 115, Leon06, NEUTRAL_REACTION );
	end
end

procedure Leon04a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Leon02;
	end
	else begin
		call Leon05;
	end
end

procedure Leon05
begin
	gsay_reply(866, 116);
	giq_option( 4, SCRIPT_HTWRLEON, 117, Leon03, NEUTRAL_REACTION );
	if (global_var(KILL_MERCHANT) == 1) then begin
		giq_option( 4, SCRIPT_HTWRLEON, 118, Leon06, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_HTWRLEON, 119, Leon03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 120, LeonKickOut, NEUTRAL_REACTION );
end

procedure Leon06
begin
	gsay_message(866, 121, 50);
	gsay_message(866, 122, 50);
	gsay_reply(866, 123);
	giq_option( 4, SCRIPT_HTWRLEON, 124, Leon07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 125, LeonCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 126, Leon03, NEUTRAL_REACTION );
end

procedure Leon07
begin
	gsay_reply(866, 127);
	giq_option( 4, SCRIPT_HTWRLEON, 128, Leon03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 129, LeonKickOut, NEUTRAL_REACTION );
end

procedure Leon08
begin
	gsay_reply(866, 130);
	giq_option( 4, SCRIPT_HTWRLEON, 131, LeonCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 132, LeonKickOut, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 133, Leon08a, NEUTRAL_REACTION );
end

procedure Leon08a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20))) then begin
		call Leon02;
	end
	else begin
		call Leon04;
	end
end

procedure Leon09
begin
	gsay_reply(866, 134);
	giq_option( 4, SCRIPT_HTWRLEON, 135, LeonCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 136, LeonKickOut, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HTWRLEON, 137, LeonKickOut, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HTWRLEON, 139, LeonKickOut, NEUTRAL_REACTION );
end

procedure LeonEnd
begin
end

procedure LeonKickOut
begin
	set_map_var(1, 1);
end

procedure LeonPerformDump begin
	set_map_var(1, 0);
	set_map_var(3, 0);
	gfade_out(1);
	obj_close(Front_Door_Ptr);
	set_local_var(5, 0);
	move_to(dude_obj, 21931, 0);
	gfade_in(1);
end

procedure LeonCombat
begin
	set_local_var(5, 1);
	call combat;
end




