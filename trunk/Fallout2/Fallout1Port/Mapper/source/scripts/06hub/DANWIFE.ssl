/*

	Hub - Dan's wife

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_DANWIFE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure damage_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable Sleeping;
variable SetDayNight;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_DAN_THE_BRAHMIN_MAN );
		set_self_ai( AI_HUB_PEASANTS );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	if (morning) then begin
		if (SetDayNight == 0) then begin
			add_timer_event(self_obj, game_ticks(15), 1);
			SetDayNight := 1;
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if (global_var( GVAR_DAN_HUB_STATUS ) == 1) then begin
		if (dude_is_male) then begin
			float_msg(self_obj, message_str(SCRIPT_DANWIFE, 103), 2);
		end
		else begin
			float_msg(self_obj, message_str(SCRIPT_DANWIFE, 104), 2);
		end
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_DANWIFE, random(101, 102)), 2);
	end
end

procedure destroy_p_proc
begin
	rm_timer_event(self_obj);

	inc_good_critter
	rm_timer_event(self_obj);
end

procedure look_at_p_proc
begin
	script_overrides;
	if (global_var( GVAR_DAN_HUB_STATUS ) == 1) then begin
		display_msg(message_str(SCRIPT_DANWIFE, 105));
	end
	else begin
		display_msg(message_str(SCRIPT_DANWIFE, 100));
	end
end

procedure timed_event_p_proc
begin
	if (morning or (day)) then begin
		Sleeping := 0;
		if (fixed_param == 1) then begin
			reg_anim_func(2, self_obj);
			reg_anim_func(1, 1);
			reg_anim_obj_move_to_tile(self_obj, 15330, -1);
			reg_anim_func(3, 0);
			add_timer_event(self_obj, game_ticks(30), 2);
		end
		else begin
			if (fixed_param == 2) then begin
				reg_anim_func(2, self_obj);
				reg_anim_func(1, 1);
				reg_anim_obj_move_to_tile(self_obj, 15122, -1);
				reg_anim_func(3, 0);
				add_timer_event(self_obj, game_ticks(60 * 1), 3);
			end
			else begin
				if (fixed_param == 3) then begin
					reg_anim_func(2, self_obj);
					reg_anim_func(1, 1);
					reg_anim_obj_move_to_tile(self_obj, 14930, -1);
					reg_anim_func(3, 0);
				end
			end
		end
	end
	else begin
		if (Sleeping == 0) then begin
			reg_anim_func(2, self_obj);
			reg_anim_func(1, 1);
			reg_anim_obj_move_to_tile(self_obj, 14930, -1);
			reg_anim_func(3, 0);
			Sleeping := 1;
			SetDayNight := 0;
		end
	end
end

procedure damage_p_proc
begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_HUB, 1 );
	end
end




