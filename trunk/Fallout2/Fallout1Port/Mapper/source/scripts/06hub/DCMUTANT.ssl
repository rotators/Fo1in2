/*

	Hub - Dying Mutant

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_DCMUTANT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */

procedure start;
procedure critter_p_proc;
procedure combat_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;

procedure Mutant00;
procedure Mutant01;
procedure Mutant02;
procedure Mutant03;
variable Only_Once := 1;
variable lastBabble;
variable kill_me;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_NECROPOLIS_MUTANT );
		set_self_ai( AI_SUPER_MUTANT_NORMAL );
		if obj_is_carrying_obj_pid(self_obj, PID_MUTANT_TRANSMISSION_HOLODISK) > 0 then begin
			variable HasDisk := 0;
			variable NewDisk := 0;
			HasDisk := obj_carrying_pid_obj(self_obj, PID_MUTANT_TRANSMISSION_HOLODISK);
			rm_obj_from_inven(self_obj, HasDisk);
			destroy_object(HasDisk);
			NewDisk := create_object_sid(PID_HOLODISK, 0, 0, SCRIPT_MUTDISK);
			add_obj_to_inven(self_obj, NewDisk);
			set_global_var(DCMUTANT_DISK_POINTER, NewDisk);
		end
		anim(self_obj, 48, 0);
	end
end

procedure critter_p_proc
begin
	if not(local_var(5)) and (((game_time / 10) - lastBabble) >= 10) and (tile_distance_objs(self_obj, dude_obj) <= 15) then begin
		lastBabble := game_time / 10;
		float_msg(self_obj, message_str(SCRIPT_DCMUTANT, random(111, 113)), 2);
	end
end

procedure combat_p_proc
begin
	script_overrides;
end


procedure talk_p_proc
begin
	dude_look_at_critter;
	if (local_var(5) == 1) then begin
		display_msg(message_str(SCRIPT_DCMUTANT, 103));
	end
	else begin
		start_gdialog(848, self_obj, 4, -1, -1);
		gsay_start;
		call Mutant00;
		gsay_end;
		end_dialogue;
	end
	if (kill_me == 1) then begin
		kill_me := 0;
		kill_critter(self_obj, ANIM_fall_back_sf);
	end
end

procedure destroy_p_proc begin
	inc_neutral_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(5) == 0) then begin
		display_msg(message_str(SCRIPT_DCMUTANT, 100));
	end
	else begin
		display_msg(message_str(SCRIPT_DCMUTANT, 102));
	end
end

procedure use_skill_on_p_proc
begin
	script_overrides;
	dialogue_system_enter;
end

procedure use_obj_on_p_proc
begin
	script_overrides;
	dialogue_system_enter;
end

procedure Mutant00
begin
	gsay_reply(848, 104);
	giq_option( 4, SCRIPT_DCMUTANT, 105, Mutant01, NEUTRAL_REACTION );
end

procedure Mutant01
begin
	set_global_var(MISSING_CARAVAN, 2);
	gsay_reply(848, 106);
	if obj_is_carrying_obj_pid(self_obj, PID_HOLODISK) > 0 then begin
		set_global_var(DCMUTANT_GAVE_DISK, 1);
		rm_obj_from_inven(self_obj, global_var(DCMUTANT_DISK_POINTER));
		add_obj_to_inven(dude_obj, global_var(DCMUTANT_DISK_POINTER));
	end
	giq_option( 4, SCRIPT_DCMUTANT, 107, Mutant02, NEUTRAL_REACTION );
end

procedure Mutant02
begin
	gsay_reply(848, 108);
	set_global_var(DCMUTANT_TOLD_NORTHWEST, 1);
	giq_option( 4, SCRIPT_DCMUTANT, 109, Mutant03, NEUTRAL_REACTION );
end

procedure Mutant03
begin
	set_local_var(5, 1);
	kill_me := 1;
	gsay_message(848, 110, 50);
end


