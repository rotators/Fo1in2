#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure combat;
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure description_p_proc;//    script_action == 3

procedure Vance00;
procedure Vance01;
procedure Vance02;
procedure Vance02a;
procedure Vance03;
procedure Vance04;
procedure Vance05;
procedure Vance06;
procedure Vance07;
procedure Vance08;
procedure Vance09;
procedure Vance10;
procedure Vance11;
procedure Vance11a;
procedure Vance12;
procedure Vance13;
procedure Vance14;
procedure Vance15;
procedure Vance16;
procedure VanceEnd;
procedure VanceKnowsPC;
procedure Get_Stuff;
procedure Put_Stuff;
procedure Barter;

import variable Vance_Box_Ptr;


variable tmp_hostile;
variable Only_Once := 1;
variable kickOut;
variable initiateDialogue := 1;

// local_var(5) == BeenIntroduced
// local_var(6) == BarterMod


procedure start
begin
	

	if Only_Once then begin
		set_local_var(6, -20);
		set_self_team(TEAM_HUB_VANCE );
		set_self_ai( AI_GUARD );
		call Put_Stuff;
		Only_Once := 0;
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		call look_at_p_proc;
	end
	else begin
		if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
			call pickup_p_proc;
		end
		else begin
			if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
				call talk_p_proc;
			end
			else begin
				if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					call critter_p_proc;
				end
				else begin
					if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
						call destroy_p_proc;
					end
					else begin
						if (script_action == 3) then begin//DETAILED ON-CLICK DESCRIPTION (Binoculars icon) - description_p_proc
							call description_p_proc;
						end
					end
				end
			end
		end
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if ((initiateDialogue == 1) and (tile_distance_objs(self_obj, dude_obj) <= 4)) then begin
			initiateDialogue := 0;
			dialogue_system_enter;
		end
		else begin
			if (kickOut == 1) then begin
				kickOut := 0;
				initiateDialogue := 1;
				gfade_out(1);
				move_to(dude_obj, 16265, 0);
				gfade_in(1);
			end
			if (tile_distance_objs(self_obj, dude_obj) >= 5) then begin
				initiateDialogue := 1;
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure look_at_p_proc
begin
	if (global_var(VANCE_KNOWS_PLAYER) == 1) or (local_var(5) == 1) then begin
	end
	else begin
		script_overrides;
		display_msg(message_str(SCRIPT_VANCE, 100));
	end
end

procedure description_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_VANCE, 119));
end

procedure talk_p_proc
begin
//	dude_look_at_critter;
//
	get_reaction
	call Get_Stuff;
	if (global_var(VANCE_KNOWS_PLAYER) == 1) then begin// Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
		set_local_var(6, 20);
	end
	else begin
		if (local_var(5) == 1) then begin
			set_local_var(6, 0);
		end
		else begin
			set_local_var(6, -20);
		end
	end
	gdialog_set_barter_mod(local_var(6));
	if (global_var(ENEMY_HUB) == 1) or (local_var(1) == 1) or (local_var(0) <= 25) then begin// gvar248 = ENEMY_HUB
		start_gdialog(822, self_obj, 4, -1, -1);
		gsay_start;
		call Vance12;
		gsay_end;
		end_dialogue;
	end
	else begin
		if (global_var(VANCE_KNOWS_PLAYER) == 1) then begin// Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
			if (local_var(5) != 1) then begin
				start_gdialog(822, self_obj, 4, -1, -1);
				gsay_start;
				call VanceKnowsPC;
				gsay_end;
				end_dialogue;
				set_local_var(5, 1);
			end
			else begin
				start_gdialog(822, self_obj, 4, -1, -1);
				gsay_start;
				call Vance06;
				gsay_end;
				end_dialogue;
			end
		end
		else begin
			if (local_var(1) == 1) then begin
				start_gdialog(822, self_obj, 4, -1, -1);
				gsay_start;
				call Vance11;
				gsay_end;
				end_dialogue;
				set_local_var(5, 1);
			end
			else begin
				if ((local_var(4) == 0) or (global_var(VANCE_KNOWS_PLAYER) == 0)) then begin// Has the player been introduced to Vance by Lemmy or Jacob? ("HUBJAKE")
					set_local_var(4, 1);
					start_gdialog(822, self_obj, 4, -1, -1);
					gsay_start;
					call Vance01;
					gsay_end;
					end_dialogue;
					set_local_var(5, 1);
				end
				else begin
					start_gdialog(822, self_obj, 4, -1, -1);
					gsay_start;
					call Vance06;
					gsay_end;
					end_dialogue;
					set_local_var(5, 1);
				end
			end
		end
	end
	call Put_Stuff;
end

procedure VanceKnowsPC
begin
	gsay_reply(822, 101);//{101}{}{What can I do for you?}
	giq_option( 4, SCRIPT_VANCE, 105, Barter, BAD_REACTION );//{105}{}{Vance, right? I heard you've got what I need. Can I score some?}
	giq_option( 4, SCRIPT_VANCE, 104, Vance03, NEUTRAL_REACTION );//{104}{}{What goes on in this building?}
	giq_option( 4, SCRIPT_VANCE, 106, Vance04, BAD_REACTION );//{106}{}{Nothing. Bye.}
	giq_option( -3, SCRIPT_VANCE, 161, Vance05, NEUTRAL_REACTION );//{161}{}{me Want some.}
	giq_option( -3, SCRIPT_VANCE, 162, VanceEnd, BAD_REACTION );//{162}{}{Nuuhhh... buh-bye.}
end

procedure Vance00
begin
	gsay_reply(822, message_str(SCRIPT_VANCE, 157) + message_str(SCRIPT_VANCE, 107));
	giq_option( 4, SCRIPT_VANCE, 152, Vance16, NEUTRAL_REACTION );//maybe change to 51 react
	giq_option( 4, SCRIPT_VANCE, 110, Vance02a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 111, Vance04, NEUTRAL_REACTION );

end

procedure Vance01
begin
	gsay_reply(822, 153);// was 101
	giq_option( 4, SCRIPT_VANCE, 103, Vance02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 104, Vance03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 106, Vance04, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_VANCE, 102, Vance16, BAD_REACTION );
	giq_option( -3, SCRIPT_VANCE, message_str(SCRIPT_VANCE, 154) + dude_name + message_str(SCRIPT_VANCE, 155), Vance00, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_VANCE, message_str(SCRIPT_VANCE, 154) + dude_name + message_str(SCRIPT_VANCE, 156), Vance00, BAD_REACTION );

end

procedure Vance02
begin
	gsay_reply(822, 107);
	giq_option( 4, SCRIPT_VANCE, 108, Vance03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 109, Vance02a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 111, Vance04, NEUTRAL_REACTION );
end

procedure Vance02a
begin
	if ((is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -30)) == 1) or is_success(do_check(dude_obj, STAT_ch, -3))) then begin
		call Vance05;
	end
	else begin
		call Vance03;
	end
end

procedure Vance03
begin
	gsay_reply(822, 112);
	giq_option( 4, SCRIPT_VANCE, 113, Vance13, BAD_REACTION );
	giq_option( 4, SCRIPT_VANCE, 114, Vance14, NEUTRAL_REACTION );
end

procedure Vance04
begin
	gsay_reply(822, 115);// {115}{}{'Nothing' is right! Don't waste my time. Now get out of here!}
	giq_option( 4, SCRIPT_VANCE, 116, Vance13, BAD_REACTION );// {116}{}{Make me... if you can, freak!}
	giq_option( 4, SCRIPT_VANCE, 117, Vance14, NEUTRAL_REACTION );// {117}{}{Okay okay... I'm leavin'.}
end

procedure Vance05
begin
	call Barter;
end

procedure Vance06
begin
	gsay_reply(822, 120);
	giq_option( -3, SCRIPT_VANCE, 121, Barter, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 122, Barter, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 123, Vance07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 124, Vance08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 125, Vance09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 126, Vance15, NEUTRAL_REACTION );
end

procedure Vance07
begin
	gsay_reply(822, 127);
	giq_option( 4, SCRIPT_VANCE, 128, Vance13, BAD_REACTION );
	giq_option( 4, SCRIPT_VANCE, 129, Vance14, NEUTRAL_REACTION );
end

procedure Vance08
begin
	gsay_reply(822, 130);
	giq_option( 4, SCRIPT_VANCE, 131, Vance13, BAD_REACTION );
	giq_option( 4, SCRIPT_VANCE, 132, Vance10, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 133, Vance15, NEUTRAL_REACTION );
end

procedure Vance09
begin
	gsay_reply(822, 134);
	giq_option( 4, SCRIPT_VANCE, 135, Barter, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 136, Vance07, BAD_REACTION );
	giq_option( 4, SCRIPT_VANCE, 137, Vance15, NEUTRAL_REACTION );
end

procedure Vance10
begin
	gsay_reply(822, 138);
	giq_option( 4, SCRIPT_VANCE, 139, Barter, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 140, Vance15, NEUTRAL_REACTION );
end

procedure Vance11
begin
	gsay_reply(822, 141);
	giq_option( 4, SCRIPT_VANCE, 142, Vance13, BAD_REACTION );
	giq_option( 4, SCRIPT_VANCE, 143, Vance14, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_VANCE, 144, Vance11a, GOOD_REACTION );
end

procedure Vance11a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20)) == 1) then begin
		call Vance02;
	end
	else begin
		call Vance13;
	end
end

procedure Vance12
begin
	gsay_reply(822, 145);//{145}{}{I don't want any trouble, so I think it would be best if you leave before I do something you'll regret.}
	giq_option( 4, SCRIPT_VANCE, 148, Vance13, BAD_REACTION );//{148}{}{I don't care what you think.}
	giq_option( 4, SCRIPT_VANCE, 149, Vance14, NEUTRAL_REACTION );//{149}{}{Alright, alright, I'm leaving.}
	giq_option( -3, SCRIPT_VANCE, 146, Vance13, BAD_REACTION );//{146}{}{No, me stay.}
	giq_option( -3, SCRIPT_VANCE, 147, Vance14, NEUTRAL_REACTION );//{147}{}{OhhKaay. I go.}
end

procedure Vance13
begin
	BottomReact
	call combat;
	float_msg(self_obj, message_str(SCRIPT_VANCE, 160), 2);// {160}{}{Justin, Chad, let's show this asshole how we deal with their kind...}
end

procedure Vance14
begin
	kickOut := 1;
end

procedure Vance15
begin
end

procedure Vance16
begin
	gsay_reply(822, 158);//{158}{}{Great, another burnt-out braindead junkie. Get the fuck out of here.}
	giq_option( -3, SCRIPT_VANCE, 159, VanceEnd, NEUTRAL_REACTION );//{159}{}{Ungh!}
end

procedure VanceEnd
begin
end

procedure Barter
begin
	gsay_message(822, 118, 49);//{118}{}{Yeah, you seem okay. Here's what I've got.}
	gdialog_mod_barter(local_var(6));
	gsay_reply(822, 150);//{150}{}{Don't use it all in one place.}
	giq_option( 4, SCRIPT_VANCE, 151, VanceEnd, NEUTRAL_REACTION );//{151}{}{Thanks, take it easy.}
	giq_option( -3, SCRIPT_VANCE, 152, VanceEnd, NEUTRAL_REACTION );//{152}{}{Ug?}
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		set_global_var(NUM_GOOD_MONSTERS_KILLED, global_var(NUM_GOOD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var(NUM_GOOD_MONSTERS_KILLED) % 2) == 0) then begin
			set_global_var(PLAYER_REPUTATION_GENERAL, (global_var(PLAYER_REPUTATION_GENERAL) - 1));
		end
	end
	call Get_Stuff;
end

procedure Get_Stuff
begin
	move_obj_inven_to_obj(Vance_Box_Ptr, self_obj);
end

procedure Put_Stuff
begin
	move_obj_inven_to_obj(self_obj, Vance_Box_Ptr);
end


