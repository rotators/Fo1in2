/*

	Hub - Irwin the farmer who's farm is occupied by raiders.

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_IRWIN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

procedure IrwinTalks;
procedure IrwinAccept;
procedure IrwinEnd;
procedure Irwin00;
procedure Irwin01;
procedure Irwin02;
procedure Irwin03;
procedure Irwin04;
procedure Irwin05;
procedure Irwin06;
procedure Irwin07;

variable tmp_hostile;
variable Only_Once := 1;

// Fallout Fixt:
procedure GoBack;
procedure MapLoad;


procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_SKAG );
		set_self_ai( AI_HUB_PEASANTS );
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	if (global_var(IRWIN_FARM_STATUS) == 501) then begin
		if (tile_num(self_obj) != 27969) then begin
			animate_move_obj_to_tile(self_obj, 27969, 0);
		end
		else if (tile_num(self_obj) == 27969) then begin
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
		end
	end
end

procedure pickup_p_proc begin
	if source_obj == dude_obj then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	get_reaction
	call IrwinTalks;
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_IRWIN, 100));
end

procedure damage_p_proc begin
	if source_obj == dude_obj then begin
		set_global_var(ENEMY_HUB, 1);
	end
end

procedure IrwinTalks begin
	if (global_var(IRWIN_FARM_STATUS) == 0) then begin
		start_dialog_at_node(Irwin00);
	end
	else if ((global_var(IRWIN_FARM_STATUS) == 1) or (global_var(IRWIN_FARM_STATUS) == 2)) then begin
		start_dialog_at_node(Irwin05);
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_IRWIN, 117), 8);
	end
end

procedure IrwinAccept begin
	gsay_message(935, 109, 50);
	set_global_var(IRWIN_FARM_STATUS, 1);
	call MapLoad;
end

procedure IrwinEnd begin
end

procedure Irwin00 begin
	Reply(101);
	NOption( 102, Irwin01, 4 );
	NOption( 103, Irwin03, 4 );
	NOption( 104, IrwinEnd, 4 );
	NOption( 118, Irwin07, -3 );
end

procedure Irwin01 begin
	if (get_pc_stat( PCSTAT_level ) < 5) then 
		gsay_message(935, 105, 50);
	else 
		call Irwin02;
end

procedure Irwin02 begin
	Reply( 106);
	NOption( 107, IrwinAccept, 4 );
	NOption( 108, Irwin04, 4 );
end

procedure Irwin03 begin
	gsay_message(935, 110, 50);
end

procedure Irwin04 begin
	gsay_message(935, 111, 51);
end

procedure Irwin05 begin
	Reply( 112);
	if (global_var(IRWIN_FARM_STATUS) == 1) then begin
		NOption( 400, GoBack, 4 );
		NOption( 123, GoBack, -3 );
	end
	NOption( 113, Irwin06, 4 );
	NOption( 114, Irwin04, 4 );
	NOption( 120, Irwin06, -3 );
	NOption( 121, Irwin07, -3 );
end

procedure Irwin06 begin
	variable LVar0 := 0;
	if (global_var(IRWIN_FARM_STATUS) == 2) then begin
		gsay_message(935, 115, 50);
		LVar0 := create_object( PID_223_PISTOL, 0, 0 );
		add_obj_to_inven(dude_obj, LVar0);
		give_exp_points(500);
		display_msg(message_str(SCRIPT_GENCHAT, 103) + 500 + message_str(SCRIPT_GENCHAT, 104));
		set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) + 2);
		set_global_var(IRWIN_FARM_STATUS, 501);
	end
	else begin
		gsay_message(935, 116, 51);
	end
end

procedure Irwin07 begin
	gsay_message(935, 119, 51);
end

procedure GoBack begin
	Reply( 401);
	NOption( 402, MapLoad, 4 );
	NOption( 403, MapLoad, -3 );
end

procedure MapLoad begin
	set_map_var(58, 1);
	load_map(MAP_HUBMIS0, 1);
end






