/*

	Hub - Hooker A

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_HHOOKERA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure map_enter_p_proc;

procedure hookerend;
procedure hooker00;
procedure hooker01;
procedure hooker02;
procedure hooker03;
procedure hooker04;
procedure hooker05;
procedure hooker06;
procedure hooker07;
procedure hooker08;
procedure hooker09;
procedure hookendPoor;
procedure hooker10;
procedure HookGROUP;
procedure hookdone;
procedure hookdoneGROUP;
procedure enddialog;
procedure enddialogGROUP;
procedure hookdoneStupid;

variable tmp_hostile;
variable Only_Once := 1;

variable FloatWhenDone := 1;
variable FloatMsg;
variable AskedHowMuch;
//
// local_var(4) == LineToDisplay
// local_var(5) == SexCost
// local_var(6) == SexOnceBefore
// local_var(7) == DumbAndPoor


procedure start
begin
	if not(global_var( GVAR_ENABLE_HOOKERS )) or global_var( GVAR_FIXT_BUGFIXES_ONLY ) then begin
		variable KillBox_ptr := 0;
		KillBox_ptr := create_object( PID_DEAD_TRAVELER, 0, 0 );// <-- Dead Traveler (Ghoul)  Was:  create_object( PID_IGUANA_STAND, 0, 0 );// <-- Bob's Iguana Stand. MAX_SIZE = 1000, biggest of all containers. -1 = NO SCRIPT ATTACHED.
		move_to(KillBox_ptr, 0, 0);
		//inven_unwield;
		move_obj_inven_to_obj(self_obj, KillBox_ptr);
		destroy_object(KillBox_ptr);
		move_to(self_obj, 0, 0);
		destroy_object(self_obj);
	end


	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_UNDERGROUND );
		set_self_ai( AI_BOUNCER );
		set_local_var(4, 200);
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if (local_var(7) and (item_caps_total(dude_obj) < 150)) then begin
		script_overrides;
		float_msg(self_obj, message_str(SCRIPT_HHOOKER, 263), 2);
	end
	else begin
		start_gdialog(708, self_obj, 4, -1, -1);
		gsay_start;
		set_local_var(4, (random(200, 209) + (get_critter_stat(dude_obj, STAT_gender) * 10)));// add 10 if player is female
		call hooker00;
		gsay_end;
		end_dialogue;
		if FloatWhenDone then begin
			FloatMsg := (random(206, 209) + (get_critter_stat(dude_obj, STAT_gender) * 10));
			while(FloatMsg == local_var(4)) do
			begin
				FloatMsg := (random(206, 209) + (get_critter_stat(dude_obj, STAT_gender) * 10));
			end
			float_msg(self_obj, message_str(SCRIPT_HHOOKER, FloatMsg), 2);
		end
		if (dude_is_stupid) then begin
			float_msg(self_obj, message_str(SCRIPT_HHOOKER, 262), 2);
		end
	end
//
// OLD CODE:
//		if ((local_var(4) < 200) or (local_var(4) > 208)) then begin
//			set_local_var(4, 200);
//		end
//		else begin
//			set_local_var(4, (local_var(4) + 1));
//		end
end

procedure hooker00
begin
	AskedHowMuch := 0;
	gsay_reply(708, local_var(4));
	giq_option( 4, SCRIPT_HHOOKER, 220, hooker02, NEUTRAL_REACTION );
	if ((global_var( GVAR_IAN_HIRELING_STATUS ) == 2) or (global_var( GVAR_TYCHO_HIRELING_STATUS ) == 2) or (global_var( GVAR_KATJA_HIRELING_STATUS ) == 2)) then begin
		giq_option( 4, SCRIPT_HHOOKER, 225, hooker03, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_HHOOKER, 222, hooker01, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HHOOKER, 223, hookerend, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HHOOKER, 232, hooker05, BAD_REACTION );
end

procedure hooker05// stupid characters
begin
	gsay_reply(708, 233);
	giq_option( -3, SCRIPT_HHOOKER, 235, hooker07, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_HHOOKER, 238, hooker06, NEUTRAL_REACTION );
end

procedure hooker06// stupid characters
begin
	gsay_reply(708, 239);
	giq_option( -3, SCRIPT_HHOOKER, 240, enddialog, NEUTRAL_REACTION );
end

procedure hooker07// stupid characters
begin
	gsay_reply(708, 405);
	giq_option( -3, SCRIPT_HHOOKER, 243, hooker08, NEUTRAL_REACTION );
end

procedure hooker08// stupid characters
begin
	gsay_reply(708, 244);
	if (item_caps_total(dude_obj) >= 150) then begin
		giq_option( -3, SCRIPT_HHOOKER, 257, hooker09, NEUTRAL_REACTION );
	end
	else begin
		giq_option( -3, SCRIPT_HHOOKER, 258, hookendPoor, BAD_REACTION );
		set_local_var(7, 1);
	end
	giq_option( -3, SCRIPT_HHOOKER, 259, enddialog, NEUTRAL_REACTION );
end

procedure hooker09// stupid characters
begin
	gsay_reply(708, 260);
	giq_option( -3, SCRIPT_HHOOKER, 264, hookdoneStupid, NEUTRAL_REACTION );
	item_caps_adjust(dude_obj, -150);
end

procedure hookdoneStupid// stupid characters
begin
	gfade_out(1);
	game_time_advance(random(1, ((10 * 60 * 2 * dude_strength) + (10 * 60 * 7 * dude_endurance))));// random between 1 minute and (2*STR)+(7*END)
	gfade_in(1);
	gsay_reply(708, 261);
	giq_option( -3, SCRIPT_HHOOKER, 265, enddialog, BAD_REACTION );
	FloatWhenDone := 0;
end

procedure hooker01
begin
	gsay_reply(708, 227);
	if AskedHowMuch then begin
		giq_option( 4, SCRIPT_HHOOKER, 246, hooker10, NEUTRAL_REACTION );// "Just sex"
		giq_option( 4, SCRIPT_HHOOKER, 237, hooker10, NEUTRAL_REACTION );// "Anything and everything! Surprise me..."
		giq_option( 4, SCRIPT_HHOOKER, 245, hooker10, NEUTRAL_REACTION );// "I don't care, let's just get this over with."
	end
	else begin
		giq_option( 4, SCRIPT_HHOOKER, 246, hooker04, NEUTRAL_REACTION );// "Just sex"
		giq_option( 4, SCRIPT_HHOOKER, 237, hooker04, NEUTRAL_REACTION );// "Anything and everything! Surprise me..."
		giq_option( 4, SCRIPT_HHOOKER, 245, hooker04, NEUTRAL_REACTION );// "I don't care, let's just get this over with."
	end
//	giq_option(4, 708, 234, hooker10, 50);
//	giq_option(4, 708, 230, hooker10, 50);
//	giq_option(4, 708, 231, hooker10, 50);
	giq_option( 4, SCRIPT_HHOOKER, 250, hookerend, NEUTRAL_REACTION );
end

procedure hooker02// just player
begin
//Charisma
	AskedHowMuch := 1;
	if (dude_charisma < 4) then begin
		set_local_var(5, 30);
		gsay_reply(708, message_str(SCRIPT_HHOOKER, 247) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
	end
	else begin
		if (dude_charisma > 7) then begin
			set_local_var(5, 10);
			gsay_reply(708, message_str(SCRIPT_HHOOKER, 248) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
		end
		else begin
			set_local_var(5, 20);
			gsay_reply(708, 221);
		end
	end
	if (item_caps_total(dude_obj) >= local_var(5)) then begin
		giq_option( 4, SCRIPT_HHOOKER, 249, hooker01, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_HHOOKER, 252, hookendPoor, BAD_REACTION );
	end
	giq_option( 4, SCRIPT_HHOOKER, 223, hookerend, NEUTRAL_REACTION );
end

procedure hooker03// group action
begin
//Charisma
	if (dude_charisma < 4) then begin
		set_local_var(5, 125);
		gsay_reply(708, message_str(SCRIPT_HHOOKER, 247) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
	end
	else begin
		if (dude_charisma > 7) then begin
			set_local_var(5, 75);
			gsay_reply(708, message_str(SCRIPT_HHOOKER, 248) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
		end
		else begin
			set_local_var(5, 100);
			gsay_reply(708, 226);
		end
	end
	if (item_caps_total(dude_obj) >= local_var(5)) then begin
		giq_option( 4, SCRIPT_HHOOKER, 249, HookGROUP, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_HHOOKER, 252, hookendPoor, BAD_REACTION );
	end
	giq_option( 4, SCRIPT_HHOOKER, 223, hookerend, NEUTRAL_REACTION );
end

procedure hooker04// just player, didn't start by asking "How much?"
begin
//Charisma
	if (dude_charisma < 4) then begin
		set_local_var(5, 30);
		gsay_reply(708, message_str(SCRIPT_HHOOKER, 247) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
	end
	else begin
		if (dude_charisma > 7) then begin
			set_local_var(5, 10);
			gsay_reply(708, message_str(SCRIPT_HHOOKER, 248) + local_var(5) + message_str(SCRIPT_HHOOKER, 300));
		end
		else begin
			set_local_var(5, 20);
			gsay_reply(708, 221);
		end
	end
	if (item_caps_total(dude_obj) >= local_var(5)) then begin
		giq_option( 4, SCRIPT_HHOOKER, 249, hooker10, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_HHOOKER, 252, hookendPoor, BAD_REACTION );
	end
	giq_option( 4, SCRIPT_HHOOKER, 223, hookerend, NEUTRAL_REACTION );
end

procedure hooker10
begin
	item_caps_adjust(dude_obj, -local_var(5));
	gsay_reply(708, 228);
	giq_option( 4, SCRIPT_HHOOKER, 254, hookdone, NEUTRAL_REACTION );
end

procedure HookGROUP
begin
	item_caps_adjust(dude_obj, -local_var(5));
	gsay_reply(708, 242);
	giq_option( 4, SCRIPT_HHOOKER, 254, hookdoneGROUP, NEUTRAL_REACTION );
end

procedure hookendPoor
begin
	FloatWhenDone := 0;
	gsay_message(708, 253, 51);// "get out!"
end

procedure hookerend
begin
	FloatWhenDone := 1;
	gsay_message(708, 251, 50);// "suit yourself"
end

procedure hookdone
begin
	gfade_out(1);
	game_time_advance(random(1, ((10 * 60 * 2 * dude_strength) + (10 * 60 * 7 * dude_endurance))));// random between 1 minute and (2*STR)+(7*END)
	gfade_in(1);
	if local_var(6) then begin// does she know player
		gsay_reply(708, 256);
	end
	else begin
		gsay_reply(708, 229);
		set_local_var(6, 1);
	end
	giq_option( 4, SCRIPT_HHOOKER, 224, enddialog, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HHOOKER, 255, enddialog, NEUTRAL_REACTION );
end

procedure hookdoneGROUP begin
	gfade_out(1);
	game_time_advance(random(1, (((10 * 60 * 2 * dude_strength) + (10 * 60 * 7 * dude_endurance)) * 2)));// random between 1 minute and (2*STR)+(7*END), times 2 for being a group
	gfade_in(1);
	if local_var(6) then begin// does she know player
		gsay_reply(708, 256);
	end
	else begin
		gsay_reply(708, 229);
		set_local_var(6, 1);
	end
	giq_option( 4, SCRIPT_HHOOKER, 224, enddialogGROUP, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_HHOOKER, 255, enddialogGROUP, NEUTRAL_REACTION );
end

procedure enddialog
begin
	FloatWhenDone := 0;
end

procedure enddialogGROUP
begin
	FloatWhenDone := 0;
	if (global_var( GVAR_IAN_HIRELING_STATUS ) == 2) and (Ian_ptr != 0) then begin
		float_msg(Ian_ptr, message_str(SCRIPT_HHOOKER, 400), 0);
	end
	if (global_var( GVAR_DOGMEAT_HIRELING_STATUS )) and (Dog_ptr != 0) then begin
		float_msg(Dog_ptr, message_str(SCRIPT_ALLDOGS, 117), 3);
	end
	if (global_var( GVAR_TYCHO_HIRELING_STATUS ) == 2) and (Tycho_ptr != 0) then begin
		float_msg(Tycho_ptr, message_str(SCRIPT_HHOOKER, 402), 12);
	end
	if (global_var( GVAR_KATJA_HIRELING_STATUS ) == 2) and (Katja_ptr != 0) then begin
		float_msg(Katja_ptr, message_str(SCRIPT_HHOOKER, 403), 5);
	end
	if (global_var( GVAR_TANDI_HIRELING_STATUS ) == 5) and (Tandi_ptr != 0) then begin
		float_msg(Tandi_ptr, message_str(SCRIPT_HHOOKER, 404), -1);
	end
end

procedure destroy_p_proc begin
	inc_neutral_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HHOOKER, 102));
end

procedure description_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HHOOKER, 302));
end

procedure map_enter_p_proc
begin
	variable LVar0 := 0;
	LVar0 := self_item( PID_BOTTLE_CAPS );
	if (LVar0 <= 50) then begin
		variable LVar1 := 0;
		LVar1 := create_object( PID_BOTTLE_CAPS, 0, 0 );
		add_mult_objs_to_inven(self_obj, LVar1, random(10, 40) + random(10, 40) + random(10, 40));
	end
end








