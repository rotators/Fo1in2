/*

	Hub - Deadguy

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_DEADGUY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Deadguy00;
procedure Deadguy01;
procedure Deadguy02;
procedure Deadguy03;
procedure Deadguy04;
procedure Deadguy05;
procedure Deadguy06;
procedure Deadguy07;
procedure Deadguy08;
procedure DeadguyEnd;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_SKAG );
		set_self_ai( AI_HUB_PEASANTS );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if (local_var(4) == 1) then begin
		call Deadguy02;
	end
	else begin
		set_local_var(4, 1);
		start_gdialog(564, self_obj, 4, -1, -1);
		gsay_start;
		call Deadguy00;
		gsay_end;
		end_dialogue;
	end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_DEADGUY, 100));
end

procedure Deadguy00
begin
	gsay_reply(564, 101);
	giq_option( 4, SCRIPT_DEADGUY, 102, Deadguy01, BAD_REACTION );
	if (local_var(5) == 0) then begin
		giq_option( 4, SCRIPT_DEADGUY, 103, Deadguy03, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_DEADGUY, 104, Deadguy07, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_DEADGUY, 105, Deadguy06, NEUTRAL_REACTION );
end

procedure Deadguy01
begin
	gsay_message(564, 106, 50);
end

procedure Deadguy02
begin
	float_msg(self_obj, message_str(SCRIPT_DEADGUY, 107), 2);
end

procedure Deadguy03
begin
	set_local_var(5, 1);
	dude_caps_adjust( 20 );
	gsay_reply(564, 108);
	giq_option( 4, SCRIPT_DEADGUY, 109, DeadguyEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_DEADGUY, 110, Deadguy04, NEUTRAL_REACTION );
end

procedure Deadguy04
begin
	gsay_reply(564, 111);
	giq_option( 4, SCRIPT_DEADGUY, 112, Deadguy05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_DEADGUY, 113, DeadguyEnd, NEUTRAL_REACTION );
end

procedure Deadguy05
begin
	if (dude_is_male) then begin
		gsay_message(564, 114, 50);
	end
	else begin
		gsay_message(564, 115, 50);
	end
end

procedure Deadguy06
begin
	if (dude_is_male) then begin
		gsay_message(564, 118, 50);
	end
	else begin
		gsay_message(564, 119, 50);
	end
end

procedure Deadguy07
begin
	if (dude_is_male) then begin
		gsay_reply(564, 122);
	end
	else begin
		gsay_reply(564, 123);
	end
	giq_option( 4, SCRIPT_DEADGUY, 126, Deadguy08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_DEADGUY, 127, combat, NEUTRAL_REACTION );
end

procedure Deadguy08
begin
	if (dude_is_male) then begin
		gsay_message(564, 128, 50);
	end
	else begin
		gsay_message(564, 129, 50);
	end
end

procedure DeadguyEnd
begin
end
