/*

	Hub - Hightower's wife

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_HTWRWIFE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;
procedure combat_p_proc;

procedure Wife01;
procedure Wife02;
procedure Wife03;
procedure Wife04;

variable tmp_hostile;
variable Only_Once := 1;
variable visible := 1;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		if (global_var( GVAR_KILL_MERCHANT ) == 5) then begin
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
			visible := 0;
		end
		set_self_team( TEAM_42 );
		set_self_ai( AI_LOSER );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
				call combat;
			end
		end
		else begin
			if ((game_time_hour >= 600) and (game_time_hour < 700) or ((game_time_hour >= 700) and (game_time_hour < 1800))) then begin
				if (tile_num(self_obj) != 22114) then begin
					animate_move_obj_to_tile(self_obj, 22114, 0);
				end
			end
			else begin
				if (tile_num(self_obj) != 25108) then begin
					animate_move_obj_to_tile(self_obj, 25108, 0);
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
		call Wife03;
	end
	else begin
		if (global_var( GVAR_ENEMY_HUB ) == 1) then begin
			call Wife04;
		end
		else begin
			if (local_var(4) == 0) then begin
				set_local_var(4, 1);
				call Wife01;
			end
			else begin
				call Wife02;
			end
		end
	end
end

procedure destroy_p_proc begin
	inc_good_critter
	set_map_var(0, map_var(0) + 1);
	if (map_var(0) > 1) then begin
		set_global_var( GVAR_KILL_MERCHANT, 2 );
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HTWRWIFE, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
	set_global_var( GVAR_KILL_MERCHANT, 5 );
end

procedure combat_p_proc
begin
	critter_set_flee_state(self_obj, 1);
end

procedure Wife01
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRWIFE, 101), 2);
end

procedure Wife02
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRWIFE, 102), 2);
end

procedure Wife03
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRWIFE, random(103, 105)), 2);
end

procedure Wife04
begin
	float_msg(self_obj, message_str(SCRIPT_HTWRWIFE, 106), 2);
	call combat;
end





