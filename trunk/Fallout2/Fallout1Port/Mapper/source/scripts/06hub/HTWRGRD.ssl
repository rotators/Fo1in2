/*

	Hub - Water Merchants guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_HTWRGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;//    script_action == 12
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure damage_p_proc;//    script_action == 14

variable tmp_hostile;
variable Only_Once := 1;
variable guardNumber;
variable threatened;
variable visible := 1;

procedure start
begin


	if Only_Once then begin
		Only_Once := 0;
		if (tile_num(self_obj) == 19511) then begin
			guardNumber := 1;
		end
		if (tile_num(self_obj) == 19935) then begin
			guardNumber := 2;
		end
		if (global_var(KILL_MERCHANT) == 5) then begin
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
			visible := 0;
		end
		set_self_team(42);
		set_self_ai( AI_GUARD_SADISTIC );
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (visible == 0) then begin
		script_overrides;
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		if (map_var(5) == 1) then begin
			if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1)) then begin
				call combat;
			end
		end
		else begin
			if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
				if ((obj_can_hear_obj(self_obj, dude_obj) == 1) or (obj_can_see_obj(self_obj, dude_obj) == 1) and (threatened == 0)) then begin
					dialogue_system_enter;
				end
				if (guardNumber == 1) then begin
					if (tile_num(self_obj) != 17119) then begin
						animate_move_obj_to_tile(self_obj, 17119, 0);
					end
				end
				else begin
					if (guardNumber == 2) then begin
						if (tile_num(self_obj) != 16930) then begin
							animate_move_obj_to_tile(self_obj, 16930, 0);
						end
					end
				end
			end
			else begin
				if (guardNumber == 1) then begin
					if (tile_num(self_obj) != 19511) then begin
						animate_move_obj_to_tile(self_obj, 19511, 0);
					end
				end
				else begin
					if (guardNumber == 2) then begin
						if (tile_num(self_obj) != 19935) then begin
							animate_move_obj_to_tile(self_obj, 19935, 0);
						end
					end
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	if ((game_time_hour > 700) and (game_time_hour < 1900)) then begin
		float_msg(self_obj, message_str(SCRIPT_HTWRGRD, random(101, 106)), 2);
	end
	else begin
		threatened := 1;
		float_msg(self_obj, message_str(SCRIPT_HTWRGRD, random(107, 109)), 2);
		call combat;
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HTWRGRD, 100));
end

procedure damage_p_proc
begin
	set_map_var(5, 1);
end



