/*

	Hub - gambling stuff

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_HROULET
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Dealer00;
procedure Dealer01;
procedure Dealer02;
procedure Dealer03;
procedure Dealer04;
procedure Dealer05;
procedure Dealer06;
procedure CheckMoney00;
procedure CheckMoney01;
procedure CheckMoney02;
procedure CheckMoney03;
procedure DealerEnd;
procedure GetOdds;

variable tmp_hostile;
variable Bet;
variable Only_Once := 1;


procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_UNDERGROUND );
		set_self_ai( AI_MERCHANT );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	get_reaction
	start_gdialog(697, self_obj, 4, -1, -1);
	gsay_start;
	call Dealer00;
	gsay_end;
	end_dialogue;
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_FALCON1, 167));
end

procedure Dealer00
begin
	gsay_reply(697, 173);
	giq_option( 4, SCRIPT_FALCON1, 174, CheckMoney00, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_FALCON1, 175, CheckMoney01, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_FALCON1, 176, CheckMoney02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_FALCON1, 177, CheckMoney03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_FALCON1, 178, DealerEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_FALCON1, 179, Dealer01, NEUTRAL_REACTION );
end

procedure Dealer01
begin
	gsay_message(697, message_str(SCRIPT_FALCON1, 180) + message_str(SCRIPT_FALCON1, 181), 50);
end

procedure Dealer02
begin
	gsay_message(697, 182, 50);
end

procedure Dealer03
begin
	gsay_message(697, 183, 50);
	dude_caps_adjust( Bet );
end

procedure Dealer04
begin
	gsay_message(697, 184, 50);
	dude_caps_adjust( Bet * 2 );
end

procedure Dealer05
begin
	gsay_message(697, 185, 50);
	dude_caps_adjust( Bet * 3 );
end

procedure Dealer06
begin
	if (dude_is_male) then begin
		gsay_message(697, 186, 50);
	end
	else begin
		gsay_message(697, 187, 50);
	end
end

procedure CheckMoney00
begin
	Bet := 5;
	if (dude_caps < Bet) then begin
		call Dealer06;
	end
	else begin
		call GetOdds;
	end
end

procedure CheckMoney01
begin
	Bet := 15;
	if (dude_caps < Bet) then begin
		call Dealer06;
	end
	else begin
		call GetOdds;
	end
end

procedure CheckMoney02
begin
	Bet := 25;
	if (dude_caps < Bet) then begin
		call Dealer06;
	end
	else begin
		call GetOdds;
	end
end

procedure CheckMoney03
begin
	Bet := 50;
	if (dude_caps < Bet) then begin
		call Dealer06;
	end
	else begin
		call GetOdds;
	end
end

procedure DealerEnd
begin
end

procedure GetOdds
begin
	variable LVar0 := 0;
	variable LVar1 := 0;
	LVar0 := random(1, 36) - 10;
	LVar1 := roll_vs_skill(dude_obj, SKILL_GAMBLING, -15);
	if (is_critical(LVar1)) then begin
		if (is_success(LVar1)) then begin
			LVar0 := LVar0 + 10;
		end
		else begin
			LVar0 := LVar0 - 5;
		end
	end
	else begin
		if (is_success(LVar1)) then begin
			LVar0 := LVar0 + 5;
		end
	end
	//debug_msg("dice == " + LVar0);
	if (LVar0 < 10) then begin
		call Dealer02;
	end
	else begin
		if (LVar0 < 20) then begin
			call Dealer03;
		end
		else begin
			if (LVar0 < 30) then begin
				call Dealer04;
			end
			else begin
				call Dealer05;
			end
		end
	end
end
