#include "..\headers\define.h"
#include "..\headers\command.h"

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure timed_event_p_proc;
procedure use_skill_on_p_proc;
procedure description_p_proc;
procedure damage_p_proc;

variable critter_tile;
variable tmp_hostile := 0;
variable initial :=  0;
variable rndz;

procedure start begin
// script ID == 928
//	////display_msg("INIT_RADSCOR2.SSL");// - please tell Sduibek when and where you saw this message. Thank you!");
// MOUNTAIN ENCOUNTER:  Vault1  ("You see a lone radscorpion, skittering about."
//
	if not(initial) then begin
		variable LVar0 := 0;
		variable LVar1 := 0;
		initial :=  1;
		LVar0 := self_item( PID_SCORPION_TAIL );
		if not(LVar0) and (obj_pid(self_obj) != (16777216 + 394)) then begin//Check if already has one
			LVar1 := create_object( PID_SCORPION_TAIL, 0, 0 );//Scorpion Tail
			add_obj_to_inven(self_obj, LVar1);//Scorpion Tail
		end
		set_self_ai( AI_RADSCORPION );
		set_self_team(TEAM_RADSCORPIONS );
		add_timer_event(self_obj, game_ticks(random(1, 5)), 1);
	end
end

procedure combat_p_proc
begin
	if (target_obj == dude_obj) then begin
		if (fixed_param == 2) then begin
			if not(global_var( GVAR_FIXT_BUGFIXES_ONLY )) then begin
				variable LuckRoll := 0;
				variable EnduranceRoll := 0;
				LuckRoll := do_check(dude_obj, STAT_lu, 0);
				EnduranceRoll := do_check(dude_obj, STAT_en, 0);
				if (not(is_success(LuckRoll)) and not(is_success(EnduranceRoll))) then begin
					rndz := (random(5, 12) + (combat_difficulty * 3));// EASY 5-to-12, NORMAL 8-to-15, HARD 11-to-18
					poison(target_obj, rndz);
				end
				else begin
					if not(is_success(LuckRoll)) or not(is_success(EnduranceRoll)) then begin
						rndz := (random(3, 9) + (combat_difficulty * 2));// EASY 3-to-9, NORMAL 5-to-11, HARD 7-to-13
						poison(target_obj, rndz);
					end
					else begin// EASY: never, NORMAL: 50% chance, HARD: 66% chance
						if (random(0, combat_difficulty) >= 1) then begin
							rndz := (random(1, 6) + combat_difficulty);// EASY 1-to-6, NORMAL 2-to-7, HARD 3-to-8
							poison(target_obj, rndz);
						end
					end
				end
			end
			else begin
				if (not(is_success(do_check(dude_obj, STAT_lu, -1)))) then begin
					rndz := random(1, 6);
					poison(target_obj, rndz);
				end
			end
		end
	end
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else if not(dude_is_animal_friend) then begin
		if (self_can_see_dude or self_can_hear_dude) then begin
			tmp_hostile := 1;
			attack(dude_obj);
		end
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_evil_critter
	rm_timer_event(self_obj);
end

procedure timed_event_p_proc begin
	if (not(combat_is_initialized)) then begin
		critter_tile := tile_num_in_direction(self_tile, random(0, 5), 3);
		reg_anim_func(2, self_obj);
		reg_anim_func(1, 1);
		reg_anim_obj_move_to_tile(self_obj, critter_tile, -1);
		reg_anim_func(3, 0);
	end
	add_timer_event(self_obj, game_ticks(random(3, 5)), 1);
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_STEAL) then begin//-- STEAL
		script_overrides;
		display_msg(message_str(SCRIPT_GENCHAT, 211));
		script_overrides;
	end
end

procedure description_p_proc begin
	if (dude_awareness) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_SSRADSCO, 102));
		script_overrides;
	end
end

procedure damage_p_proc begin
	if obj_in_party(source_obj) then begin
		tmp_hostile := 1;
	end
end
