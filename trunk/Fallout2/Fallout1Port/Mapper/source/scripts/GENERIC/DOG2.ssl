#include "..\headers\define.h"

#define NAME                    SCRIPT_DOG2

#include "..\headers\command.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure talk_p_proc;
procedure use_skill_on_p_proc;
procedure push_p_proc;

variable rndx;
variable rndy;
variable critter_tile;
variable Only_Once := 1;
variable tmp_hostile := 0;

procedure PickDeadBodyType;
variable DeathType := 56;

procedure push_p_proc begin
end

procedure start begin
	if shady_invaded then begin
		if (CUR_MAP_SHADYE) or (CUR_MAP_SHADYW)  or (CUR_MAP_RAIDERS) then begin //  SHADY SANDS EAST OR SHADY SANDS WEST OR RAIDERS KHANS
			if (local_var(6) != 1) then begin
				set_local_var(6, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if Only_Once then begin
		Only_Once := 0;
		set_self_ai( AI_DOG );
		if (CUR_MAP_SHADYE) or (CUR_MAP_SHADYW) then begin
			set_self_team(TEAM_SHADY_SANDS );
		end
		else begin
			set_self_team( TEAM_DOGS );
		end
		if global_var( GVAR_DEBUG_MODE_MESSAGES_ON ) then begin
			display_msg("DOG2 m" + cur_map_index + " e" + self_elevation + " " + tile_num(self_obj));
		end
	end
end

procedure talk_p_proc begin
	/*dude_look_at_critter;
	if random(0, 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ALLDOGS, 102), -1);
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_ALLDOGS, 117), -1);
	end*/
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		rndy := random(1, 20);
		if (rndy == 1) then begin
			rndx := random(0, 5);
			critter_tile := tile_num_in_direction(tile_num(self_obj), rndx, 1);
			animate_move_obj_to_tile(self_obj, critter_tile, 0);
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		party_remove(self_obj);
		if (CUR_MAP_SHADYE) or (CUR_MAP_SHADYW) then begin
			set_self_team(TEAM_SHADY_SANDS );
		end
		else begin
			set_self_team( TEAM_DOGS );
		end
		tmp_hostile := 1;
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_STEAL) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_GENCHAT, 211));
		script_overrides;
	end
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
