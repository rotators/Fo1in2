/*

	Caravan guy

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_CARCUST
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure destroy_p_proc;
procedure timed_event_p_proc;

variable Only_Once;
variable Home_Tile;
variable critter_tile;

procedure start begin
	call map_enter_p_proc;
end

procedure map_enter_p_proc begin
	if map_first_run then begin
		Home_Tile := self_tile;

		if (CUR_MAP_JUNKENT) then begin //  JUNKTOWN - ENTRANCE, MORBID, LARS
			set_self_team(TEAM_JUNKTOWN_GUARD );
			set_self_ai( AI_PEASANT );
		end
		else if (CUR_MAP_BROHDENT) then begin //  BROTHERHOOD - ENTRANCE
			set_self_team(TEAM_BROTHERHOOD );
			set_self_ai( AI_BROTHERHOOD_KNIGHT );
		end
		else if (CUR_MAP_HOTEL) then begin //  NECROPOLIS - HOTEL
			set_self_team( TEAM_87 );
			set_self_ai( AI_GHOUL_MINDLESS );
		end
		else if (CUR_MAP_LAADYTUM) then begin //  L.A. BONEYARD - ADYTUM (ENTRANCE)
			set_self_team(TEAM_LA_ADYTOWNER );
			set_self_ai( AI_PEASANT );
		end
	end

	add_timer_event(self_obj, game_ticks(random(3, 5)), 1);
	if not(map_is_caravan_escort) then begin
		if (local_var(0) == 0) then begin
			set_local_var(0, GAME_TIME_IN_HOURS);
		end
		if (((GAME_TIME_IN_HOURS) - local_var(0)) >= 2) then begin
			destroy_object(self_obj);
		end
	end
end

procedure talk_p_proc begin
	if (CUR_MAP_JUNKENT) then begin //  JUNKTOWN - ENTRANCE, MORBID, LARS
		float_msg(self_obj, message_str(SCRIPT_CARCUST, random(100, 104)), 0);
	end
	else if (CUR_MAP_BROHDENT) then begin //  BROTHERHOOD - ENTRANCE
		float_msg(self_obj, message_str(SCRIPT_CARCUST, random(105, 109)), 0);
	end
	else if (CUR_MAP_HOTEL) then begin //  NECROPOLIS - HOTEL
		float_msg(self_obj, message_str(SCRIPT_CARCUST, random(110, 114)), 0);
	end
	else if (CUR_MAP_LAADYTUM) then begin //  L.A. BONEYARD - ADYTUM (ENTRANCE)
		float_msg(self_obj, message_str(SCRIPT_CARCUST, random(115, 119)), 0);
	end
end

procedure critter_p_proc begin
	if ((((GAME_TIME_IN_HOURS) - local_var(0)) >= 2) and (local_var(0) != 0)) then begin
		destroy_object(self_obj);
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure timed_event_p_proc begin
	if (((critter_state(self_obj) bwand 2) == 0) and not(combat_is_initialized)) then begin
		critter_tile := tile_num_in_direction(Home_Tile, random(0, 5), 3);
		reg_anim_func(2, self_obj);
		reg_anim_func(1, 1);
		reg_anim_obj_move_to_tile(self_obj, critter_tile, -1);
		reg_anim_func(3, 0);
	end
	add_timer_event(self_obj, game_ticks(random(3, 5)), 1);
end
