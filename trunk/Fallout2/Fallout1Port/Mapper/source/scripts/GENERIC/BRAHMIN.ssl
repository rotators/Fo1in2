#include "..\headers\define.h"

#define NAME                    SCRIPT_BRAHMIN

#include "..\headers\command.h"

procedure start;
procedure description_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure push_p_proc;

procedure PickDeadBodyType;

variable critter_tile;
variable initial :=  0;
variable temp_hostile := 0;
variable DeathType := 56;
variable SrcObj := 0;
variable SrcIsParty := 0;

#define TIMER_STAND_UP 4

////////////////////////////////////////
//	COW SCRIPTS
////////////////////////////////////////

//	cow		5
//	BRAHMIN		34	<-- USE THIS ONE FOR DIALOG
//	carcow		966

////////////////////////////////////////

procedure start begin
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin
		if (cur_map_index == MAP_SHADYE) or (cur_map_index == MAP_SHADYW) then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(4) != 1) then begin
				set_local_var(4, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin
		if not(initial) then begin
			initial :=  1;
			if global_var(DEBUG_MODE_MESSAGES_ON) then begin
				//display_msg("INIT BRAHMIN.SSL at map number " + cur_map_index + " and elevation " + elevation(self_obj));
			end
			set_self_ai( AI_COW );
			set_self_team(4);
			if (cur_map_index != MAP_DESCRVN1) and (cur_map_index != MAP_DESCRVN2) and (cur_map_index != MAP_MNTCRVN1) and (cur_map_index != MAP_MNTCRVN2) and (cur_map_index != MAP_MNTCRVN3) and (cur_map_index != MAP_DESCRVN4) and (cur_map_index != MAP_MNTCRVN4) and (cur_map_index != MAP_DESERT1) then begin
				add_timer_event(self_obj, game_ticks(random(2, 10)), 1);
			end
		end
	end
end

procedure push_p_proc begin
   if (obj_in_party(source_obj)) then begin
      if (stat_success(source_obj, STAT_st, 0)) then begin
         if ((critter_state(self_obj) bwand DAM_KNOCKED_DOWN) == FALSE) then begin
            reg_anim_clear(self_obj);
            reg_anim_begin();
               reg_anim_animate(self_obj,ANIM_hit_from_front,-1);
               reg_anim_animate(self_obj,ANIM_fall_back,5);
               reg_anim_animate(self_obj,ANIM_fall_back_sf,-1);
            reg_anim_end();
            critter_injure(self_obj,DAM_KNOCKED_DOWN);
            add_timer_event(self_obj, game_ticks(Random(10, 30)), TIMER_STAND_UP);
            debug_msg("ZCBRAHMN: ahh Cow TIPPED you get +10 Hick Experience Points.");
         end
      end
   end
end

procedure critter_p_proc begin
	if (temp_hostile) then begin
		temp_hostile := 0;
		attack(dude_obj);
	end
end

procedure description_p_proc begin
	display_msg(message_str(SCRIPT_BRAHMIN, 100));
end

procedure talk_p_proc begin
	dude_look_at_critter;
	display_msg(message_str(SCRIPT_BRAHMIN, random(102, 105)));
end

procedure timed_event_p_proc begin
	if map_cow_can_push then begin
	 	if (((critter_state(self_obj) bwand 2) == 0) and not(combat_is_initialized) and (anim_busy(self_obj) == 0)) then begin
			critter_tile := tile_num_in_direction(tile_num(self_obj), random(0, 5), 3);
			reg_anim_func(2, self_obj);
			reg_anim_func(1, 1);
			reg_anim_obj_move_to_tile(self_obj, critter_tile, -1);
			reg_anim_func(3, 0);
      end
      else if (fixed_param == TIMER_STAND_UP) then begin
         reg_anim_clear(self_obj);
         reg_anim_begin();
         reg_anim_animate_reverse(self_obj, ANIM_fall_back, -1);
         reg_anim_end();
         critter_uninjure(self_obj, DAM_KNOCKED_DOWN);
      end
		add_timer_event(self_obj, game_ticks(random(6, 10)), 1);
	end
end

procedure use_obj_on_p_proc begin
	variable LVar0 := 0;
	if ((obj_pid(obj_being_used_with) == PID_BEER) or (obj_pid(obj_being_used_with) == PID_BOOZE)) then begin
		script_overrides;
		LVar0 := obj_being_used_with;
		rm_obj_from_inven(source_obj, obj_being_used_with);
		destroy_object(LVar0);
		play_sfx("MABROMAQ");
		reg_anim_func(2, self_obj);
		reg_anim_func(1, 1);
		reg_anim_animate(self_obj, 14, -1);
		reg_anim_animate(self_obj, 20, 5);
		reg_anim_animate(self_obj, 48, -1);
		reg_anim_func(3, 0);
		critter_injure(self_obj, 2);//    DAM_KNOCKED_DOWN       2		<-- DOES NOT WORK!!!
		critter_injure(dude_obj, 4);//    DAM_CRIP_LEG_LEFT      4
		critter_injure(dude_obj, 8);//    DAM_CRIP_LEG_RIGHT     8
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_SCIENCE) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_BRAHMIN, 101));
	end
	else if (action_being_used == SKILL_STEAL) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_GENCHAT, 211));
		script_overrides;
	end
end

procedure damage_p_proc begin
	if obj_in_party(source_obj) then begin
		if (cur_map_index == MAP_HUBENT) or (cur_map_index == MAP_HUBDWNTN) or (cur_map_index == MAP_HUBHEIGT) or (cur_map_index == MAP_HUBOLDTN) or (cur_map_index == MAP_HUBWATER) or (cur_map_index == MAP_HUBMIS0) then begin //  THE HUB - ANY MAPS
			set_global_var(ENEMY_HUB, 1);
		end
		else if (cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_JUNKCSNO) or (cur_map_index == MAP_JUNKKILL) then begin //  JUNKTOWN - ANY MAPS //  JUNKTOWN MAPS
			set_global_var(ENEMY_JUNKTOWN, 1);
		end
		else if (cur_map_index == MAP_SHADYE) or (cur_map_index == MAP_SHADYW) then begin //  SHADY SANDS - EAST OR WEST
			set_global_var(ENEMY_SHADY_SANDS, 1);//Set ENEMY_SHADY_SANDS to True
		end
		party_remove(self_obj);
		set_self_team(4 );/* TEAM_NUM */
		temp_hostile := 1;
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		if (cur_map_index == MAP_HUBENT) or (cur_map_index == MAP_HUBDWNTN) or (cur_map_index == MAP_HUBHEIGT) or (cur_map_index == MAP_HUBOLDTN) or (cur_map_index == MAP_HUBWATER) or (cur_map_index == MAP_HUBMIS0) then begin //  THE HUB - ANY MAPS
			set_global_var(ENEMY_HUB, 1);
		end
		else begin
			if (cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_JUNKCSNO) or (cur_map_index == MAP_JUNKKILL) then begin //  JUNKTOWN - ANY MAPS //  JUNKTOWN MAPS
				set_global_var(ENEMY_JUNKTOWN, 1);
			end
			else begin
				if (cur_map_index == MAP_SHADYE) or (cur_map_index == MAP_SHADYW) then begin //  SHADY SANDS - EAST OR WEST
					set_global_var(ENEMY_SHADY_SANDS, 1);//Set ENEMY_SHADY_SANDS to True
				end
			end
		end
	end
	rm_timer_event(self_obj);
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end

