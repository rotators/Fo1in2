/*

	Junktown - Andrew the jail guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_ANDREW
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure map_enter_p_proc;

procedure Andrew01;
procedure Andrew02;
procedure Andrew03;
procedure Andrew04;
procedure Andrew05;
procedure Andrew06;

import variable jail_door_ptr;

variable tmp_hostile;
variable round_counter;
variable moving_flag := 1;


procedure start begin
end

procedure combat_p_proc begin
	if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
		round_counter := round_counter + 1;
	end
	if (round_counter > 3) then begin
		if (global_var( GVAR_ENEMY_JUNKTOWN ) == 0) then begin
			set_global_var( GVAR_ENEMY_JUNKTOWN, 1 );
			set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep - 5 );
		end
	end
end

procedure critter_p_proc begin
	if (global_var( GVAR_ENEMY_JUNKTOWN ) == 1) then begin
		if (self_can_see_dude) then begin
			attack(dude_obj);
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if ((map_var(1) == 1) and ((GAME_TIME_IN_DAYS) > map_var(5))) then begin
			if (jail_door_ptr != 0) then begin
				obj_unlock(jail_door_ptr);
				use_obj(jail_door_ptr);
				moving_flag := 0;
				rm_timer_event(self_obj);
				add_timer_event(self_obj, game_ticks(5), 1);
			end
			set_map_var(1, 0);
			float_msg(self_obj, message_str(SCRIPT_ANDREW, 101), 3);
		end
		else begin
			if (map_var(3) == 1) then begin
				set_map_var(3, map_var(3) + 1);
				float_msg(self_obj, message_str(SCRIPT_ANDREW, 116), 2);
			end
			else begin
				if (map_var(3) == 3) then begin
					if (jail_door_ptr != 0) then begin
						obj_unlock(jail_door_ptr);
					end
					tmp_hostile := 1;
				end
				else begin
					if (moving_flag == 1) then begin
						if (self_tile != local_var(1)) then begin
							animate_move_obj_to_tile(self_obj, local_var(1), 0);
						end
					end
					if ((global_var( GVAR_ENEMY_JUNKTOWN ) == 1) and self_can_see_dude) then begin
						dialogue_system_enter;
					end
				end
			end
		end
	end
end

procedure look_at_p_proc
begin
	if (local_var(0) == 1) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_ANDREW, 100));
	end
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (global_var( GVAR_ENEMY_JUNKTOWN ) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ANDREW, 102), 2);
		tmp_hostile := 1;
	end
	else begin
		start_gdialog(172, self_obj, 4, -1, -1);
		gsay_start;
		call Andrew01;
		gsay_end;
		end_dialogue;
	end
end

procedure timed_event_p_proc
begin
	if (fixed_param == 1) then begin
		obj_close(jail_door_ptr);
		rm_timer_event(self_obj);
		add_timer_event(self_obj, game_ticks(10), 2);
	end
	else begin
		if (fixed_param == 2) then begin
			moving_flag := 1;
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj)  then begin
		set_global_var( GVAR_ENEMY_JUNKTOWN, 1 );
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_JUNKTOWN, 1 );
	end
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure Andrew01
begin
	gsay_reply(172, 103);
	giq_option( 4, SCRIPT_ANDREW, 104, Andrew02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ANDREW, 105, Andrew03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ANDREW, 106, Andrew04, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_ANDREW, 113, Andrew06, NEUTRAL_REACTION );
end

procedure Andrew02
begin
	set_local_var(0, 1);
	gsay_reply(172, 107);
	giq_option( 4, SCRIPT_ANDREW, 105, Andrew03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ANDREW, 106, Andrew04, NEUTRAL_REACTION );
end

procedure Andrew03
begin
	gsay_reply(172, 108);
	giq_option( 6, SCRIPT_ANDREW, 109, Andrew05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_ANDREW, 110, Andrew04, NEUTRAL_REACTION );
end

procedure Andrew04
begin
	gsay_message(172, 111, 50);
end

procedure Andrew05
begin
	gsay_reply(172, 112);
	giq_option( 4, SCRIPT_ANDREW, 110, Andrew04, NEUTRAL_REACTION );
end

procedure Andrew06
begin
	if (dude_is_male) then begin
		gsay_message(172, 114, 50);
	end
	else begin
		gsay_message(172, 115, 50);
	end
end

procedure map_enter_p_proc
begin
	if junktown_invaded then begin
		kill_critter(self_obj, ANIM_fall_front_sf);
	end
	set_self_team(TEAM_JUNKTOWN_GUARD );
	set_self_ai( AI_GUARD_HUMOROUS );
	if (local_var(1) == 0) then begin
		set_local_var(1, self_tile);
	end
end
