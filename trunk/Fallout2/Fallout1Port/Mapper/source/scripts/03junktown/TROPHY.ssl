/*

	Junktown - Neil's Urn

*/
#include "..\headers\define.h"

#define NAME                    SCRIPT_TROPHY

#include "..\headers\command.h"
#include "..\headers\Sfall\main.h"

procedure start;
procedure description_p_proc;
procedure use_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;

variable item;

procedure start begin
end

procedure map_enter_p_proc begin
	//set_proto_data(obj_pid(self_obj),PROTO_IT_FLAGS,ITEM_ACTION_USE);
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_TROPHY, 100));
end

procedure use_p_proc begin
	script_overrides;
	if (global_var( GVAR_DESTROY_VATS_14 ) == 0) then begin
		if ((game_time_hour >= 410) and (game_time_hour <= 1200)) then begin
			script_overrides;
			set_global_var( GVAR_DESTROY_VATS_14, 1 );
			item := create_object( PID_URN, 0, 0 );
			add_obj_to_inven(source_obj, item);
			display_msg(mstr(101));
			animate_stand_obj(self_obj);
		end
		else begin
			display_msg(mstr(103));
		end
	end
end

procedure use_obj_on_p_proc begin
	if (obj_pid(obj_being_used_with) == PID_URN) then begin
		script_overrides;
		set_global_var( GVAR_DESTROY_VATS_14, 0 );
		set_map_var(4, 0);
		animate_stand_obj(self_obj);
		rm_obj_from_inven(source_obj, obj_being_used_with);
		destroy_object(obj_being_used_with);
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_STEAL) then begin
		if (global_var( GVAR_DESTROY_VATS_14 ) == 0) then begin
			script_overrides;
			set_global_var( GVAR_DESTROY_VATS_14, 1 );
			item := create_object( PID_URN, 0, 0 );
			add_obj_to_inven(source_obj, item);
			if (is_success(roll_vs_skill(source_obj, SKILL_STEAL, 0))) then begin
				display_msg(mstr(101));
			end
			else begin
				display_msg(mstr(102));
				set_map_var(4, 1);
			end
			reg_anim_func(2, self_obj);
			animate_stand_obj(self_obj);
		end
	end
end


