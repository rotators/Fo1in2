/*

	Junktown - Izo, Gizmo's guard

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_IZO
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure description_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure map_update_p_proc;
procedure map_enter_p_proc;

procedure ShowToDoor_talk;
procedure PayPlayer_talk;
procedure IzoEnd;

import variable Gizmo_ptr;
import variable Gizmo_is_angry;
import variable show_to_door;
import variable payment;

variable initial :=  0;
variable tmp_hostile;
variable line103flag;

procedure start begin
	if (not(initial)) then begin
		initial :=  1;
		set_self_team(TEAM_JUNKTOWN_GIZMO);
		if (global_var(CAPTURE_GIZMO) == 2) then begin
			kill_critter(self_obj, ANIM_fall_front_blood_sf);
		end
	end
end

procedure critter_p_proc
begin
	if (global_var(ENEMY_GIZMOS_GANG) == 1) then begin// - ENEMY_GIZMOS_GANG
		if (obj_can_see_obj(self_obj, dude_obj)) then begin
			tmp_hostile := 1;
			attack(dude_obj);
		end
	end
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (Gizmo_is_angry) then begin
			Gizmo_is_angry := 0;
			attack(dude_obj);//	tmp_hostile := 1;
		end
		else begin
			// FIXT:
			/*if (show_to_door) then begin
				gfade_out(1);
				move_to(self_obj, 27719, elevation(self_obj));// was 22916
				move_to(dude_obj, 28119, elevation(dude_obj));// was 23516
				gfade_in(1);
				start_gdialog(622, self_obj, 4, -1, -1);
				gsay_start;
				call ShowToDoor_talk;
				gsay_end;
				end_dialogue;
				animate_move_obj_to_tile(self_obj, 16720, 1);
//				gfade_out(1);
//				move_to(self_obj, 16921, elevation(self_obj));
//				gfade_in(1);
				show_to_door := 0;
			end*/

		// Fo1 original:
			if (show_to_door) then begin
				if (tile_distance_objs(dude_obj, Gizmo_ptr) > 8) then begin
					if (tile_num(self_obj) != 16520) then begin
						animate_move_obj_to_tile(self_obj, 16520, 0);
					end
					else begin
						anim(self_obj, 1000, 2);
						line103flag := 0;
						show_to_door := 0;
					end
				end
				else begin
					if (tile_num(self_obj) != 18720) then begin
						animate_move_obj_to_tile(self_obj, 18720, 0);
					end
					else begin
						if (not(line103flag)) then begin
							line103flag := 1;
							display_msg(message_str(SCRIPT_IZO, 103));
						end
					end
				end
			end
		// Original end

		end
	end
end

procedure map_enter_p_proc begin
	set_local_var(4, 0);
	if (tile_num(self_obj) == 27719) then begin
		animate_move_obj_to_tile(self_obj, 16720, 1);
	end
end

procedure map_update_p_proc begin
	if ((tile_num(self_obj) == 16720) and (local_var(4) == 0)) then begin
		set_local_var(4, 1);
		anim(self_obj, 1000, rotation_to_tile(tile_num(self_obj), 16920));
	end
end

procedure destroy_p_proc begin
	if source_obj == dude_obj then begin
		set_global_var(ENEMY_GIZMOS_GANG, 1);
	end
	inc_evil_critter
end

procedure description_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	dude_look_at_critter;
	display_msg(message_str(SCRIPT_IZO, 101));
	if ((payment > 0) and (local_var(3) == 0)) then begin
//
//	   use_obj(dude_obj);//What the fuck is this? --Sduibek
//
		item_caps_adjust(dude_obj, payment);
		start_dialog_at_node(PayPlayer_talk);

		payment := 0;
		set_local_var(3, 1);
	end
end

procedure ShowToDoor_talk begin
	if not(global_var(GIZMO_NICE_SHOWDOOR)) then begin
		gsay_message(622, 103, 51);
	end
	else begin
		set_global_var(GIZMO_NICE_SHOWDOOR, 0);
		gsay_message(622, 106, 50);
	end
end

procedure PayPlayer_talk begin
	gsay_reply(622, 102);
	NOption( 104, IzoEnd, 4 );
	BOption( 105, IzoEnd, -3 );
end

procedure IzoEnd begin
end

