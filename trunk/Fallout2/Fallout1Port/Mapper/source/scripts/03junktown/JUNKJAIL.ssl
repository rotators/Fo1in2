/*

	Junktown - Jail door

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_JUNKJAIL

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure description_p_proc;
procedure map_enter_p_proc;
procedure spatial_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure lockpicking;

variable skill_test;
variable triggered;

import variable jail_door_ptr;

procedure use_p_proc;

procedure start begin
end

procedure damage_p_proc begin
	if (weapon_dmg_type(target_obj) == DMG_explosion) then begin
		set_global_var(ENEMY_JUNKTOWN, 1);
		destroy_object(self_obj);
	end
   else begin
       display_msg(message_str(SCRIPT_DOOR,193));
   end
end

procedure description_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_JUNKJAIL, 101));
end

procedure map_enter_p_proc
begin
	if (tile_num(self_obj) == 18666) then begin
		obj_close(self_obj);
		obj_lock(self_obj);
		jail_door_ptr := self_obj;
	end
end

procedure spatial_p_proc
begin
	if ((source_obj == dude_obj) and (triggered == 0) and (map_var(1) == 1)) then begin
		display_msg(message_str(SCRIPT_JUNKJAIL, 100));
		set_map_var(1, 0);
		set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) - 3);
		display_msg(message_str(SCRIPT_JUNKJAIL, 107));
		give_exp_points(250);
		if (global_var(ENEMY_JUNKTOWN) == 0) then begin
			set_global_var(ENEMY_JUNKTOWN, 1);
			set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) - 5);
		end
		triggered := 1;
		destroy_object(self_obj);
	end
end

procedure use_obj_on_p_proc
begin
	if (obj_pid(obj_being_used_with) == PID_LOCKPICKS) then begin
		skill_test := roll_vs_skill(dude_obj, SKILL_LOCKPICK, -20);
		call lockpicking;
	end
end

procedure use_skill_on_p_proc
begin
	if (action_being_used == SKILL_LOCKPICK) then begin//-- LOCKPICK
		skill_test := roll_vs_skill(dude_obj, SKILL_LOCKPICK, 0);
		call lockpicking;
	end
end

procedure lockpicking
begin
	script_overrides;
	set_map_var(3, map_var(3) + 1);
	if (is_success(skill_test)) then begin
		if (obj_is_locked(self_obj)) then begin
			obj_unlock(self_obj);
			display_msg(message_str(SCRIPT_JUNKJAIL, 103));
		end
		else begin
			obj_lock(self_obj);
			display_msg(message_str(SCRIPT_JUNKJAIL, 104));
		end
	end
	else begin
		if (obj_is_locked(self_obj)) then begin
			display_msg(message_str(SCRIPT_JUNKJAIL, 105));
		end
		else begin
			display_msg(message_str(SCRIPT_JUNKJAIL, 106));
		end
	end
end

procedure use_p_proc
begin
	if (obj_is_locked(self_obj)) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_JUNKJAIL, 102));
	end
end
