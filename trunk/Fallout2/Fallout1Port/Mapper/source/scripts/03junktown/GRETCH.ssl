/*

	Junktown - Gretch

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_VINNIE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;

procedure Gretch00;
procedure Gretch00a;
procedure Gretch00b;
procedure Gretch01;
procedure Gretch01a;
procedure Gretch02;
procedure Gretch02a;
procedure Gretch03;
procedure Gretch03a;
procedure Gretch04;
procedure Gretch05;
procedure Gretch05a;
procedure Gretch06;
procedure Gretch07;
procedure Gretch08;
procedure Gretch09;
procedure Gretch10;
procedure GretchEnd;
procedure GretchCombat;

variable tmp_hostile;
variable initial :=  0;
variable warned_about_messing;

import variable Gretch_call;
import variable messing_with_Morbid_stuff;

procedure damage_p_proc;

procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_JUNKTOWN_MORBID );
		initial :=  1;
	end
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (messing_with_Morbid_stuff) then begin
			if ((tile_distance_objs(self_obj, dude_obj) < 12) and (self_elevation == dude_elevation)) then begin
				if (warned_about_messing) then begin
					tmp_hostile := 1;
				end
				else begin
					warned_about_messing := 1;
					float_msg(self_obj, message_str(SCRIPT_GRETCH, 141), 2);
				end
			end
			messing_with_Morbid_stuff := 0;
		end
		if (Gretch_call) then begin
			Gretch_call := 0;
			tmp_hostile := 1;
		end
	end
	if (global_var( GVAR_IS_MORBID_TEAM_HOSTILE ) == 1) then begin
		if (obj_can_see_obj(self_obj, dude_obj)) then begin
			attack(dude_obj);
		end
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(0) == 0) then begin
		display_msg(message_str(SCRIPT_GRETCH, 100));
	end
	else begin
		display_msg(message_str(SCRIPT_GRETCH, 101));
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	start_gdialog(103, self_obj, 4, -1, -1);
	gsay_start;
	if (local_var(0) == 1) then begin
		call Gretch01;
	end
	else begin
		call Gretch00;
	end
	gsay_end;
	end_dialogue;
end

procedure timed_event_p_proc
begin
end

procedure Gretch00
begin
	gsay_reply(103, 102);
	giq_option( -3, SCRIPT_GRETCH, 103, Gretch07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 104, Gretch00a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 105, Gretch00b, BAD_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 106, Gretch02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 107, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch00a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch04;
	end
	else begin
		call Gretch02;
	end
end

procedure Gretch00b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch02;
	end
	else begin
		call Gretch08;
	end
end

procedure Gretch01
begin
	gsay_reply(103, 108);
	giq_option( 4, SCRIPT_GRETCH, 109, Gretch02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 110, Gretch01a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 111, GretchEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_GRETCH, 103, Gretch07, NEUTRAL_REACTION );
end

procedure Gretch01a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch05;
	end
	else begin
		call Gretch02;
	end
end

procedure Gretch02
begin
	gsay_reply(103, 112);
	giq_option( 4, SCRIPT_GRETCH, 113, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 114, Gretch03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 115, Gretch02a, NEUTRAL_REACTION );
end

procedure Gretch02a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch03;
	end
	else begin
		call Gretch07;
	end
end

procedure Gretch03
begin
	gsay_reply(103, 116);
	giq_option( 4, SCRIPT_GRETCH, 117, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 118, Gretch03a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 119, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch03a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch05;
	end
	else begin
		call Gretch04;
	end
end

procedure Gretch04
begin
	gsay_reply(103, 120);
	giq_option( 4, SCRIPT_GRETCH, 121, Gretch06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 122, Gretch05, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 123, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch05
begin
	set_global_var( GVAR_GRETCH_PRIMECUTS, 1 );
	gsay_reply(103, 124);
	giq_option( 4, SCRIPT_GRETCH, 125, Gretch05a, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 126, GretchCombat, BAD_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 127, GretchEnd, NEUTRAL_REACTION );
end

procedure Gretch05a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Gretch09;
	end
	else begin
		call Gretch06;
	end
end

procedure Gretch06
begin
	set_global_var( GVAR_GRETCH_PRIMECUTS, 1 );
	gsay_reply(103, 128);
	giq_option( 4, SCRIPT_GRETCH, 129, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 130, Gretch09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 131, Gretch07, NEUTRAL_REACTION );
end

procedure Gretch07
begin
	gsay_reply(103, 132);
	giq_option( 4, SCRIPT_GRETCH, 133, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 134, Gretch04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 135, Gretch08, BAD_REACTION );
	giq_option( -3, SCRIPT_GRETCH, 136, Gretch10, NEUTRAL_REACTION );
end

procedure Gretch08
begin
	gsay_message(103, 137, 51);
end

procedure Gretch09
begin
	gsay_reply(103, 138);
	giq_option( 4, SCRIPT_GRETCH, 139, GretchEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GRETCH, 126, GretchCombat, BAD_REACTION );
end

procedure Gretch10
begin
	gsay_message(103, 140, 50);
end

procedure GretchEnd
begin
end

procedure GretchCombat
begin
	tmp_hostile := 1;
end



procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_IS_MORBID_TEAM_HOSTILE, 1 );
	end
end
