/*

	Necropolis - Leader

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_LEADER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure combat_p_proc;
procedure combat_rads_inc;

procedure leader00;
procedure leader02;
procedure leader03;
procedure leader03a;
procedure leader04;
procedure leader04a;
procedure leader04b;
procedure leader05;
procedure leader05a;
procedure leader06;
procedure leader06a;
procedure leader06b;
procedure leader07;
procedure leader08;
procedure leader09;
procedure leader10;
procedure leader10a;
procedure leader11;
procedure leader12;
procedure leader13;
procedure leader14;
procedure leader15;
procedure leader15a;
procedure leader15b;
procedure leader16;
procedure leader17;
procedure leader18;
procedure leader19;
procedure leader19a;
procedure leader19b;
procedure leader20;
procedure leader21;
procedure leader22;
procedure leader23;
procedure leader24;
procedure leader25;
procedure leader26;
procedure leader27;
procedure leader28;
procedure leader29;
procedure leader30;
procedure leader31;
procedure leadercbt;
procedure leaderend;

variable tmp_hostile;
variable init_teams := 0;
variable has_parts;
variable repair_skill;
variable stuff;

variable rndRads;


procedure start
begin


	if (not(init_teams)) then begin
		set_self_team(TEAM_NECROPOLIS_UNDERGROUND );
		set_self_ai( AI_GHOUL_NORMAL );
		init_teams := 1;
	end
	if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
		call talk_p_proc;
	end
	else begin
		if (script_action == 13) then begin//<-- combat_p_proc , basically does combat_is_initialized == 1
			call combat_p_proc;
		end
		else begin
			if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
				script_overrides;
				display_msg(message_str(SCRIPT_LEADER, 100));
			end
			else begin
				if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
					tmp_hostile := 1;
				end
				else begin
					if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
						call critter_p_proc;
					end
					else begin
						if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
							call destroy_p_proc;
						end
						else begin
							if (script_action == 3) then begin//DETAILED ON-CLICK DESCRIPTION (Binoculars icon) - description_p_proc
								display_msg(message_str(SCRIPT_LEADER, 300));
							end
						end
					end
				end
			end
		end
	end
end

procedure destroy_p_proc begin
	inc_inv_good_crit
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (obj_can_see_obj(self_obj, dude_obj)) then begin
			if (local_var(7) == 0) then begin
				set_local_var(7, 1);
				if (dude_is_armed) then begin
					float_msg(self_obj, message_str(SCRIPT_LEADER, 193), 8);
				end
				else begin
					float_msg(self_obj, message_str(SCRIPT_LEADER, 194), 8);
				end
			end
		end
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	get_reaction
	has_parts := obj_carrying_pid_obj(dude_obj, PID_PUMP_PARTS);
	start_gdialog(81, self_obj, 4, -1, -1);
	gsay_start;
	if (local_var(4)) then begin
		if (local_var(1) < 2) then begin
			call leader30;
		end
		else begin
			if (global_var(NECROP_WATER_PUMP_FIXED) == 2) then begin
				call leader26;
			end
			else begin
				if (local_var(6) == 1) then begin
					call leader20;
				end
				else begin
					if (global_var(NECROP_WATER_CHIP_TAKEN) == 1) then begin
						call leader29;
					end
					else begin
						if (local_var(5) == 1) then begin
							if (has_parts) then begin
								call leader18;
							end
							else begin
								call leader16;
							end
						end
						else begin
							call leader00;
						end
					end
				end
			end
		end
	end
	else begin
		set_local_var(4, 1);
		call leader00;
	end
	gsay_end;
	end_dialogue;
end

procedure leadercbt
begin
	BigDownReact
	tmp_hostile := 1;
end

procedure leaderend
begin
end

procedure leader00
begin
	gsay_reply(81, 101);
	giq_option( 4, SCRIPT_LEADER, 102, leader04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 103, leader03, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_LEADER, 104, leader02, NEUTRAL_REACTION );
end

procedure leader02
begin
	gsay_reply(81, 105);
	giq_option( -3, SCRIPT_LEADER, 106, leaderend, NEUTRAL_REACTION );
end

procedure leader03
begin
	gsay_reply(81, 107);
	giq_option( 4, SCRIPT_LEADER, 108, leader04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 109, leader03a, BAD_REACTION );
	giq_option( 4, SCRIPT_LEADER, 110, leaderend, NEUTRAL_REACTION );
end

procedure leader03a
begin
	DownReact
	call leader30;
end

procedure leader04
begin
	gsay_reply(81, 111);
	if (global_var(SET_DEAD) != 1) then begin
		giq_option( 4, SCRIPT_LEADER, 112, leader05, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_LEADER, 113, leader23, NEUTRAL_REACTION );
	end
	giq_option( 5, SCRIPT_LEADER, 114, leader04a, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_LEADER, 115, leader04b, BAD_REACTION );
end

procedure leader04a
begin
	if (global_var(SET_DEAD) != 1) then begin
		call leader06;
	end
	else begin
		call leader07;
	end
end

procedure leader04b
begin
	DownReact
	call leader05;
end

procedure leader05
begin
	gsay_reply(81, 116);
	giq_option( 4, SCRIPT_LEADER, 117, leader05a, BAD_REACTION );
	giq_option( 4, SCRIPT_LEADER, 118, leader06, NEUTRAL_REACTION );
end

procedure leader05a
begin
	DownReact
	call leader06;
end

procedure leader06
begin
	gsay_reply(81, 119);
	giq_option( 4, SCRIPT_LEADER, 120, leader07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 121, leader06a, BAD_REACTION );
	giq_option( 4, SCRIPT_LEADER, 122, leader08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 123, leader06b, BAD_REACTION );
end

procedure leader06a
begin
	DownReact
	call leader08;
end

procedure leader06b
begin
	DownReact
	call leader07;
end

procedure leader07
begin
	gsay_reply(81, 124);
	giq_option( 6, SCRIPT_LEADER, 125, leader25, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 126, leader09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 127, leader24, NEUTRAL_REACTION );
end

procedure leader08
begin
	gsay_reply(81, 128);
	giq_option( 4, SCRIPT_LEADER, 129, leaderend, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 130, leader09, NEUTRAL_REACTION );
end

procedure leader09
begin
	gsay_reply(81, 131);
	giq_option( 4, SCRIPT_LEADER, 129, leader10, NEUTRAL_REACTION );
end

procedure leader10
begin
	gsay_reply(81, 132);
	giq_option( 4, SCRIPT_LEADER, 133, leader10a, BAD_REACTION );
	giq_option( 4, SCRIPT_LEADER, 134, leader11, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 135, leader11, NEUTRAL_REACTION );
end

procedure leader10a
begin
	DownReact
	call leader13;
end

procedure leader11
begin
	gsay_reply(81, 136);
	giq_option( 4, SCRIPT_LEADER, 137, leader12, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 138, leader14, NEUTRAL_REACTION );
end

procedure leader12
begin
	gsay_message(81, 139, 50);
	call leadercbt;
end

procedure leader13
begin
	gsay_reply(81, 140);
	giq_option( 4, SCRIPT_LEADER, 141, leader11, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 142, leadercbt, BAD_REACTION );
end

procedure leader14
begin
	gsay_reply(81, 143);
	giq_option( 4, SCRIPT_LEADER, 144, leader15, NEUTRAL_REACTION );
	if (has_parts) then begin
		giq_option( 4, SCRIPT_LEADER, 145, leader21, NEUTRAL_REACTION );
	end
end

procedure leader15
begin
	gsay_reply(81, 146);
	giq_option( 4, SCRIPT_LEADER, 147, leader12, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 148, leader15b, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 149, leader15a, BAD_REACTION );
end

procedure leader15a
begin
	DownReact
	call leader15b;
end

procedure leader15b
begin
	if (global_var(NECROP_WATER_PUMP_FIXED) != 2) then begin
		set_global_var(NECROP_WATER_PUMP_FIXED, 1);
	end
	set_local_var(5, 1);
	gsay_reply(81, 150);
	giq_option( 4, SCRIPT_LEADER, 151, leaderend, NEUTRAL_REACTION );
end

procedure leader16
begin
	gsay_reply(81, 152);
	giq_option( 4, SCRIPT_LEADER, 153, leader17, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 154, leader12, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 155, leader22, NEUTRAL_REACTION );
end

procedure leader17
begin
	gsay_message(81, 156, 50);
end

procedure leader18
begin
	gsay_reply(81, 157);
	giq_option( 4, SCRIPT_LEADER, 158, leader19, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 159, leader12, NEUTRAL_REACTION );
end

procedure leader19
begin
	set_local_var(6, 1);
	repair_skill := has_skill(dude_obj, SKILL_REPAIR);
	if (repair_skill < 60) then begin
		call leader19a;
	end
	else begin
		call leader19b;
	end
end

procedure leader19a
begin
	stuff := create_object( PID_DEANS_ELECTRONICS, 0, 0 );
	add_obj_to_inven(dude_obj, stuff);
	stuff := create_object( PID_DEANS_ELECTRONICS, 0, 0 );
	add_obj_to_inven(dude_obj, stuff);
	stuff := create_object( PID_DEANS_ELECTRONICS, 0, 0 );
	add_obj_to_inven(dude_obj, stuff);
	gsay_message(81, 160, 50);
end

procedure leader19b
begin
	gsay_message(81, 161, 50);
end

procedure leader20
begin
	gsay_reply(81, 162);
	giq_option( 4, SCRIPT_LEADER, 163, leader17, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 164, leader12, NEUTRAL_REACTION );
end

procedure leader21
begin
	gsay_reply(81, 165);
	giq_option( 4, SCRIPT_LEADER, 166, leader19, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 167, leader12, NEUTRAL_REACTION );
end

procedure leader22
begin
	gsay_reply(81, 168);
	giq_option( 4, SCRIPT_LEADER, 169, leaderend, NEUTRAL_REACTION );
end

procedure leader23
begin
	gsay_reply(81, 170);
	giq_option( 4, SCRIPT_LEADER, 171, leader07, NEUTRAL_REACTION );
end

procedure leader24
begin
	gsay_reply(81, 172);
	giq_option( 4, SCRIPT_LEADER, 173, leader09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 129, leader10, NEUTRAL_REACTION );
end

procedure leader25
begin
	gsay_reply(81, 174);
	giq_option( 4, SCRIPT_LEADER, 175, leader09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 176, leader24, NEUTRAL_REACTION );
end

procedure leader26
begin
	gsay_reply(81, 177);
	giq_option( 4, SCRIPT_LEADER, 178, leaderend, NEUTRAL_REACTION );
end

procedure leader27
begin
	gsay_reply(81, 179);
	giq_option( 4, SCRIPT_LEADER, 180, leader28, NEUTRAL_REACTION );
end

procedure leader28
begin
	gsay_reply(81, 181);
	giq_option( 4, SCRIPT_LEADER, 129, leaderend, NEUTRAL_REACTION );
end

procedure leader29
begin
	gsay_reply(81, 182);
	if (local_var(5) == 1) then begin
		giq_option( 4, SCRIPT_LEADER, 183, leader22, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_LEADER, 184, leader12, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_LEADER, 185, leader31, NEUTRAL_REACTION );
end

procedure leader30
begin
	gsay_message(81, 186, 50);
end

procedure leader31
begin
	gsay_reply(81, 187);
	giq_option( 4, SCRIPT_LEADER, 189, leader15, NEUTRAL_REACTION );
	if (has_parts) then begin
		giq_option( 4, SCRIPT_LEADER, 145, leader21, NEUTRAL_REACTION );
	end
end

procedure combat_p_proc
begin
	if (fixed_param == 2) then begin
		if (target_obj == dude_obj) then begin
			if global_var(DEBUG_MODE_MESSAGES_ON) then begin
				debug_msg("Combat Rads From Irradiated Critters BEGINS");
			end
			if not(global_var(FIXT_BUGFIXES_ONLY)) then begin
				variable cont := 0;// checked to see if continue to run the code after each stage
				variable CritterTYPE := 0;// 1 = Ghoul, 2 = Floater, 3 = Centaur, 4 = RadRat, 5 = GlowingOne
				if global_var(HIT_GIVES_RADS_GHOULS) > 0 then begin// HIT_GIVES_RADS_GHOULS
					if (obj_pid(self_obj) == (16777216 + 14)) or (obj_pid(self_obj) == (16777216 + 104)) or (obj_pid(self_obj) == (16777216 + 105)) or (obj_pid(self_obj) == (16777216 + 106)) or (obj_pid(self_obj) == (16777216 + 109)) or (obj_pid(self_obj) == (16777216 + 110)) or (obj_pid(self_obj) == (16777216 + 131)) or (obj_pid(self_obj) == (16777216 + 157)) or (obj_pid(self_obj) == (16777216 + 184)) then begin// 14 = Zombie Guard, 104 = Set, 105 = Garret, 106 = Zombie, 109 = Ghoul Guard, 110 = Undergrounder, 131 = Talius, 157 = Harold, 184 = Zombie
						CritterTYPE := 1;
						cont := 1;
					end
				end
				if not(continue) then begin
					if global_var(HIT_GIVES_MORE_RADS_GLOW) > 0 then begin// HIT_GIVES_MORE_RADS_GLOW
						if (obj_pid(self_obj) == (16777216 + 16)) or (obj_pid(self_obj) == (16777216 + 182)) or (obj_pid(self_obj) == (16777216 + 183)) then begin// 16 = Glowing One, 182 = Glower, 183 = Mad Glowing One
							CritterTYPE := 5;
							cont := 1;
						end
					end
				end
				if not(continue) then begin
					if global_var(HIT_GIVES_MORE_RADS_RATS) > 0 then begin// HIT_GIVES_MORE_RADS_RATS
						if (obj_pid(self_obj) == (16777216 + 160)) or (obj_pid(self_obj) == (16777216 + 161)) then begin//  160, 161 = Rad Rat
							CritterTYPE := 4;
							cont := 1;
						end
					end
				end
				if not(continue) then begin
					if global_var(HIT_GIVES_RADS_FLOATERS) > 0 then begin// HIT_GIVES_RADS_FLOATERS
						if (obj_pid(self_obj) == (16777216 + 45)) or (obj_pid(self_obj) == (16777216 + 167)) then begin//  45 = Floater, 167 = Bloody Floater
							CritterTYPE := 2;
							cont := 1;
						end
					end
				end
				if not(continue) then begin
					if global_var(HIT_GIVES_RADS_CENTAURS) > 0 then begin// HIT_GIVES_RADS_CENTAURS
						if (obj_pid(self_obj) == (16777216 + 43)) or (obj_pid(self_obj) == (16777216 + 168)) then begin//  43 = Lesser Centaur, 168 = Greater Centaur
							CritterTYPE := 3;
							cont := 1;
						end
					end
				end
				if continue then begin
					variable BonusRads := 0;
					if (CritterTYPE == 5) or (CritterTYPE == 4) then begin
						BonusRads := 1;
					end
					if (random(0, 3) + combat_difficulty + BonusRads) >= 3 then begin// CHANCE TO RUN = Easy: 25%, 50% if glowing/radrat - Normal: 50%, 75% if glowing - Brutal: 75%, 100% if glowing/radrat
						variable giveRads := 1;
						if critter_inven_obj(self_obj, 1) then begin
							if (obj_pid(critter_inven_obj(self_obj, 1)) != PID_BRASS_KNUCKLES) and (obj_pid(critter_inven_obj(self_obj, 1)) != PID_SPIKED_KNUCKLES) and (obj_pid(critter_inven_obj(self_obj, 1)) != PID_POWER_FIST) then begin// Brass Knuckles, Spiked Knuckles, Power Fist
								giveRads := 0;
							end
						end
						if critter_inven_obj(self_obj, 2) then begin
							if (obj_pid(critter_inven_obj(self_obj, 2)) != PID_BRASS_KNUCKLES) and (obj_pid(critter_inven_obj(self_obj, 2)) != PID_SPIKED_KNUCKLES) and (obj_pid(critter_inven_obj(self_obj, 2)) != PID_POWER_FIST) then begin// Brass Knuckles, Spiked Knuckles, Power Fist
								giveRads := 0;
							end
						end
						if giveRads == 1 then begin
							variable LuckRoll := 0;
							variable EnduranceRoll := 0;
							LuckRoll := do_check(dude_obj, STAT_lu, 0);
							EnduranceRoll := do_check(dude_obj, STAT_en, 0);
							rndRads := 0;
							if not(is_success(LuckRoll)) and not(is_success(EnduranceRoll)) then begin
								rndRads := random((2 + combat_difficulty + BonusRads), (3 + combat_difficulty + BonusRads));// EASY: 2-3 / 3-4 if glowing/radrat, NORMAL: 3-4 / 4-5 if glowing/radrat, BRUTAL: 4-5 / 5-6 if glowing/radrat
							end
							else begin
								if not(is_success(LuckRoll)) or not(is_success(EnduranceRoll)) then begin
									rndRads := random((1 + combat_difficulty + BonusRads), (2 + combat_difficulty + BonusRads));// EASY: 1-2 / 2-3 if glowing/radrat, NORMAL: 2-3 / 3-4 if glowing/radrat, BRUTAL: 3-4 / 4-5 if glowing/radrat
								end
								else begin
									if (random(0, 5) + BonusRads) >= 5 then begin// Chance to run if both stat checks pass: 16.67%, 33% if glowing/radrat
										rndRads := random((0 + combat_difficulty + BonusRads), (1 + combat_difficulty + BonusRads));// EASY: 0-1 / 1-2 if glowing/radrat, NORMAL: 1-2 / 2-3 if glowing/radrat, BRUTAL: 2-3 / 3-4 if glowing/radrat
									end
								end
							end
							if rndRads > 0 then begin
								call combat_rads_inc;
							end
						end
					end
				end
			end
			else begin// if (BUG_FIXES_ONLY == 1)
				if (obj_pid(self_obj) == PID_GLOWING_ONE) or (obj_pid(self_obj) == PID_RADIATED_RAT) or (obj_pid(self_obj) == PID_TOUGH_RADIATED_RAT) or (obj_pid(self_obj) == PID_GLOWER) or (obj_pid(self_obj) == PID_MAD_GLOWING_ONE) then begin
					variable rndx := 0;
					rndx := (random(1, 4) - 3);// was 1d6 - 5, is now 1d4 - 3
					if (rndx < 0) then begin
						rndx := 0;
					end
					if (rndx > 0) then begin
						radiation_inc(dude_obj, rndx);
						display_msg(message_str(SCRIPT_GENGHOUL, 1004));
					end
				end
			end
			if global_var(DEBUG_MODE_MESSAGES_ON) then begin
				debug_msg("Combat Rads From Irradiated Critters ENDS");
			end
		end
	end
	else begin
		if (fixed_param == 4) then begin
			if ((global_var(NECROP_WATER_CHIP_TAKEN) != 0) and (global_var(NECROP_WATER_PUMP_FIXED) != 2)) then begin
				variable LVar1 := 0;
				LVar1 := random(0,25);
				if LVar1 == 1 then begin
					variable LVar0 := 0;
					LVar0 := random(2,5);
					if LVar0 == 2 then begin
						float_msg(self_obj, message_str(SCRIPT_VALTGLO, 102), 2);
					end
					else begin
						if LVar0 == 3 then begin
							float_msg(self_obj, message_str(SCRIPT_VALTGLO, 103), 2);
						end
						else begin
							if LVar0 == 4 then begin
								float_msg(self_obj, message_str(SCRIPT_VALTGLO, 104), 2);
							end
							else begin
								float_msg(self_obj, message_str(SCRIPT_VALTGLO, 105), 2);
							end
						end
					end
				end
			end
		end
	end
end

procedure combat_rads_inc
begin
	radiation_inc(dude_obj, rndRads);
	if ((rndRads == 1) or (rndRads == 2)) then begin
		display_msg(message_str(SCRIPT_GENGHOUL, 1004));
	end
	else begin
		if ((rndRads > 2) and (rndRads <= 5)) then begin
			display_msg(message_str(SCRIPT_GENGHOUL, 1005));
		end
		else begin
			if ((rndRads > 5) and (rndRads < 10)) then begin
				display_msg(message_str(SCRIPT_GENGHOUL, 1006));
			end
		end
	end
end



