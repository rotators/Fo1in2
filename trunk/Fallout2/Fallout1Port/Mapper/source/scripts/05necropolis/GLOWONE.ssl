/*

	Necropolis - Glowing ghoul

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_GLOWONE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure combat_p_proc;
procedure destroy_p_proc;

procedure combat_rads_inc;

variable tmp_hostile := 0;
variable init_teams := 0;
variable rndx;
variable AlreadyLooked := 0;


procedure start begin
	if (not(init_teams)) then begin
		init_teams := 1;
		set_self_team(TEAM_NECROPOLIS_UNDERGROUND );
		set_self_ai( AI_GHOUL_MINDLESS );
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		script_overrides;
		if not(AlreadyLooked) then begin
			display_msg(message_str(SCRIPT_GLOWONE, 100));
		end
		else begin
			display_msg(message_str(SCRIPT_GLOWONE, 105));
		end
		script_overrides;
	end
	else begin
		if (script_action == 3) then begin//DETAILED ON-CLICK DESCRIPTION (Binoculars icon) - description_p_proc
			script_overrides;
			display_msg(message_str(SCRIPT_GENGHOUL, 204));
			script_overrides;
		end
		else begin
			if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
				script_overrides;
				call talk_p_proc;
				script_overrides;
			end
			else begin
				if (script_action == 14) then begin//damage
					if (source_obj == dude_obj) then begin
						tmp_hostile := 1;
					end
				end
			end
		end
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure talk_p_proc begin
	dude_look_at_critter;
	rndx := random(1, 4);
	if (rndx == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_GLOWONE, 101), 2);
	end
	else if (rndx == 2) then begin
		float_msg(self_obj, message_str(SCRIPT_GLOWONE, 102), 2);
	end
	else if (rndx == 3) then begin
		display_msg(message_str(SCRIPT_GLOWONE, 103));
		end
	else if (rndx == 4) then begin
		display_msg(message_str(SCRIPT_GLOWONE, 104));
	end
end

procedure combat_p_proc begin
   if (fixed_param == 2) then begin
      rndx := random(1, 6) - 5;
      if (rndx < 0) then begin
         rndx := 0;
      end
      if (rndx > 0) then begin
         radiation_inc(target_obj, rndx);
      end
   end
end
