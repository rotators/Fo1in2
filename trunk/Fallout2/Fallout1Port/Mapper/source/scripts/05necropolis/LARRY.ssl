/*

	Necropolis - Watershed super mutant

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_LARRY
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure critter_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure mutant00;
procedure mutant01;
procedure mutant02;
procedure Node998;

variable tmp_hostile;
variable init_teams := 0;
variable noevent;
variable loopcount;

#define float(x)    						float_msg(self_obj, message_str(NAME, x), FLOAT_MSG_GREEN)

procedure start begin
	if (not(init_teams)) then begin
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_NECROPOLIS_MUTANT);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET, AI_SUPER_MUTANT_NECRO);
		set_local_var(1, 0);
		init_teams := 1;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(101));
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	if (global_var(SUPER_MUTANTS_KILLED) < 1) then begin
		if (global_var(WATERSHED_MUTANTS_LEAVE) == 0) then begin
			anim(dude_obj, 1000, rotation_to_tile(tile_num(dude_obj), tile_num(self_obj)));
			call mutant00;
		end
		else begin
			display_msg(mstr(108));
		end
	end
end

procedure mutant00 begin
	float(102);
end

procedure mutant01 begin
	float(103);
	call Node998;
end

procedure mutant02 begin
	float(107);
	call Node998;
end

procedure Node998 begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if ((global_var(SUPER_MUTANTS_KILLED) > 0) and obj_can_see_obj(self_obj, dude_obj) and (global_var(WATERSHED_MUTANTS_LEAVE) < 1)) then begin
		tmp_hostile := 1;
	end
	if (tmp_hostile > 0) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin

		if ((global_var(WATERSHED_MUTANTS_LEAVE) == 1) and (tile_num(self_obj) != 15507)) then begin
			animate_move_obj_to_tile(self_obj, 15507, 0);
		end
		else if ((global_var(WATERSHED_MUTANTS_LEAVE) == 1) and (tile_num(self_obj) == 15507)) then begin
			set_global_var(WATERSHED_MUTANTS_LEAVE, 2);
		end
		else if ((global_var(WATERSHED_MUTANTS_LEAVE) == 2) and (tile_num(self_obj) != 12536)) then begin
			animate_move_obj_to_tile(self_obj, 12536, 0);
		end
		else if ((tile_num(self_obj) == 12536) and (global_var(WATERSHED_MUTANTS_LEAVE) == 2)) then begin
			if (metarule(22, 0) == 0) then begin
				set_obj_visibility(self_obj, 1); //  MAKE INVISIBLE
			end
		end

	end
end

procedure destroy_p_proc begin
	set_global_var(SUPER_MUTANTS_KILLED, global_var(SUPER_MUTANTS_KILLED) + 1);

	check_watershed_sm_killed
	inc_evil_critter
end
