/*

	Necropolis - Glowing Ghoul

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_TREAD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure combat_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable rndx;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_NECROPOLIS_GANGER );
		set_self_ai( AI_GHOUL_NASTY );
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (self_can_see_dude and (tile_distance_objs(self_obj, dude_obj) < 6)) then begin
		float_msg(self_obj, message_str(SCRIPT_TREAD, 101), 2);
		tmp_hostile := 1;
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	float_msg(self_obj, message_str(SCRIPT_TREAD, 101), 2);
end

procedure destroy_p_proc begin
	inc_inv_evil_crit
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_TREAD, 100));
end

procedure combat_p_proc begin
	if (fixed_param == COMBAT_SUBTYPE_HIT_SUCCEEDED) then begin
	   rndx := random(1, 6) - 5;
	   if (rndx < 0) then begin
	      rndx := 0;
	   end
	   if (rndx > 0) then begin
	      radiation_inc(target_obj, rndx);
	   end
	end
end
