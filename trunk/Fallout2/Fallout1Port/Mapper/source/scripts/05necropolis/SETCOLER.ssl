//
// ---TRAP SCRIPT---  Sduibek
//
// I think this is unused.
//
#include "..\headers\define.h"

procedure start;
procedure trap_stuff;
procedure see_stuff;
procedure find_trap;
procedure find_iq;
procedure disarm_trap;
procedure disarm_mech;
procedure failure;
procedure explode;
procedure close_cooler;
procedure open_cooler;

variable damage;
variable test;


procedure start
begin
	if (map_first_run) then begin
		display_msg("INIT SETCOLER.SSL at map number " + cur_map_index + " and elevation " + elevation(self_obj) + ". Please take a screenshot now and send to Sduibek. Thanks!");
	end
	if ((script_action == 21) or (script_action == 3)) then begin// 21 is look_at, 3 is description (Binoculars)   //NEED TO FIX THIS, DUPLICATE DESCRIPTIONS IS BULLSHIT. --Sduibek
		call see_stuff;
	end
	else begin
		if (script_action == 8) then begin//<-- use_skill_on_p_proc
			call trap_stuff;
		end
		else begin
			if (script_action == 6) then begin//use_p_proc - Use, Activate, or Manipulate the Object or Item
				if (local_var(1) == 0) then begin
					call find_iq;
				end
				else begin
					if (local_var(2)) then begin
						call close_cooler;
					end
					else begin
						call open_cooler;
					end
				end
			end
			else begin
				if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
					if (local_var(2) == 0) then begin
						script_overrides;
					end
				end
			end
		end
	end
end

procedure trap_stuff
begin
	script_overrides;
	if (local_var(0)) then begin
		if (local_var(1) == 0) then begin
			if (action_being_used == 11) then begin//-- TRAPS
				call disarm_trap;
			end
			else begin
				if (action_being_used == 13) then begin//-- REPAIR
					call disarm_mech;
				end
				else begin
					display_msg(message_str(SCRIPT_SETCOLER, 100));
				end
			end
		end
	end
	else begin
		call find_trap;
	end
end

procedure see_stuff
begin
	script_overrides;
	display_msg(message_str(SCRIPT_SETCOLER, 109));
end

procedure find_trap
begin
	if (has_skill(dude_obj, SKILL_TRAPS)) then begin
		test := roll_vs_skill(dude_obj, 11, -10);
		if (is_success(test)) then begin
			display_msg(message_str(SCRIPT_SETCOLER, 101));
			set_local_var(0, 1);
		end
		else begin
			display_msg(message_str(SCRIPT_SETCOLER, 102));
			call explode;
		end
	end
end

procedure find_iq
begin
	script_overrides;
	if (local_var(0)) then begin
		call failure;
	end
	else begin
		test := do_check(dude_obj, 4, 0);
		if (is_success(test)) then begin
			display_msg(message_str(SCRIPT_SETCOLER, 101));
			set_local_var(0, 1);
		end
		else begin
			display_msg(message_str(SCRIPT_SETCOLER, 102));
			call explode;
		end
	end
end

procedure disarm_trap
begin
	script_overrides;
	test := roll_vs_skill(dude_obj, 11, 10);
	if (is_success(test)) then begin
		display_msg(message_str(SCRIPT_SETCOLER, 103));
		set_local_var(1, 1);
	end
	else begin
		if (is_critical(test)) then begin
			display_msg(message_str(SCRIPT_SETCOLER, 105));
			call explode;
		end
		else begin
			display_msg(message_str(SCRIPT_SETCOLER, 104));
		end
	end
end

procedure disarm_mech
begin
	script_overrides;
	test := roll_vs_skill(dude_obj, 13, 30);
	if (is_success(test)) then begin
		display_msg(message_str(SCRIPT_SETCOLER, 103));
		set_local_var(1, 1);
	end
	else begin
		if (is_critical(test)) then begin
			display_msg(message_str(SCRIPT_SETCOLER, 105));
			call explode;
		end
		else begin
			display_msg(message_str(SCRIPT_SETCOLER, 104));
		end
	end
end

procedure failure
begin
	script_overrides;
	display_msg(message_str(SCRIPT_SETCOLER, 106));
	call explode;
end

procedure explode
begin
	explosion(tile_num(self_obj), elevation(self_obj), 0);
	damage := random(1, 6) + random(1, 6) + random(1, 6);
	critter_dmg(dude_obj, damage, 0);
	set_local_var(1, 1);
	set_local_var(0, 1);
end

procedure close_cooler
begin
	script_overrides;
	animate_stand_reverse_obj(self_obj);
	set_local_var(2, 0);
end

procedure open_cooler
begin
	script_overrides;
	animate_stand_obj(self_obj);
	set_local_var(2, 1);
end

