/*

	Necropolis - Ganger

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GANGER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure combat_p_proc;
procedure talk_p_proc;

procedure ghoulend;
procedure ghoulcbt;
procedure ghoul00;
procedure ghoul01;
procedure ghoul02;
procedure ghoul03;
procedure ghoul04;
procedure ghoul04a;
procedure ghoul05;
procedure ghoul06;
procedure ghoul07;
procedure ghoul08;
procedure ghoul09;
procedure ghoul10;
procedure ghoul11;
procedure ghoul12;
procedure ghoul13;

variable tmp_hostile;
variable init_teams := 0;

variable rndRads;
variable rndx;

procedure start begin
	if (not(init_teams)) then begin
		set_self_team( TEAM_NECROPOLIS_GANGER );
		set_self_ai( AI_GHOUL_BRAVE );
		init_teams := 1;
	end
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		script_overrides;
		display_msg(message_str(SCRIPT_GANGER, 100));
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure combat_p_proc begin
end

procedure talk_p_proc begin
	dude_look_at_self;
	start_gdialog(64, self_obj, 4, -1, -1);
	gsay_start;
	if (local_var(0)) then begin
		call ghoul08;
	end
	else begin
		set_local_var(0, 1);
		call ghoul00;
	end
	gsay_end;
	end_dialogue;
end

procedure ghoulend begin
end

procedure ghoulcbt begin
	tmp_hostile := 1;
end

procedure ghoul00 begin
	gsay_reply(64, 101);
	giq_option( 6, SCRIPT_GANGER, 102, ghoul01, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GANGER, 103, ghoul08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GANGER, 104, ghoul11, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_GANGER, 105, ghoul12, NEUTRAL_REACTION );
end

procedure ghoul01 begin
	gsay_reply(64, 106);
	giq_option( 6, SCRIPT_GANGER, 107, ghoulend, NEUTRAL_REACTION );
	giq_option( 6, SCRIPT_GANGER, 108, ghoul02, NEUTRAL_REACTION );
	giq_option( 6, SCRIPT_GANGER, 109, ghoulcbt, BAD_REACTION );
end

procedure ghoul02 begin
	gsay_reply(64, 110);
	giq_option( 5, SCRIPT_GANGER, 111, ghoul04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GANGER, 112, ghoul05, NEUTRAL_REACTION );
end

procedure ghoul03 begin
	gsay_reply(64, 113);
	giq_option( 8, SCRIPT_GANGER, 114, ghoul05, NEUTRAL_REACTION );
end

procedure ghoul04 begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call ghoul03;
	end
	else begin
		call ghoul04a;
	end
end

procedure ghoul04a begin
	gsay_message(64, 115, 50);
	call ghoulend;
end

procedure ghoul05 begin
	gsay_reply(64, 116);
	giq_option( 5, SCRIPT_GANGER, 117, ghoul06, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_GANGER, 118, ghoulend, NEUTRAL_REACTION );
end

procedure ghoul06 begin
	gsay_reply(64, 119);
	giq_option( 5, SCRIPT_GANGER, 120, ghoul07, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_GANGER, 121, ghoulend, NEUTRAL_REACTION );
end

procedure ghoul07 begin
	gsay_message(64, 122, 50);
	call ghoulend;
end

procedure ghoul08 begin
	gsay_reply(64, 123);
	giq_option( 4, SCRIPT_GANGER, 124, ghoul09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GANGER, 125, ghoulend, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_GANGER, 105, ghoul12, NEUTRAL_REACTION );
end

procedure ghoul09 begin
	gsay_reply(64, 126);
	giq_option( 4, SCRIPT_GANGER, 127, ghoul10, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GANGER, 128, ghoulend, NEUTRAL_REACTION );
end

procedure ghoul10 begin
	gsay_reply(64, 129);
	call ghoulcbt;
end

procedure ghoul11 begin
	gsay_message(64, 130, 50);
	call ghoulend;
end

procedure ghoul12 begin
	gsay_message(64, 131, 50);
	call ghoulcbt;
end

procedure ghoul13 begin
	gsay_message(64, 132, 50);
	call ghoulend;
end

procedure destroy_p_proc begin
	inc_evil_critter
end
