/*

	Tycho - Party Member, Junktown

*/

#include "..\headers\define.h"

#define NAME                    SCRIPT_TYCHO

#include "..\headers\command.h"
#include "..\headers\modreact.h"

#define set_default_party_follow             set_follow_close

procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure talk_p_proc;
procedure use_obj_on_p_proc;
procedure combat_p_proc;
procedure push_p_proc;

procedure Tycho01;
procedure Tycho02;
procedure Tycho03;
procedure Tycho04;
procedure Tycho05;
procedure Tycho06;
procedure Tycho07;
procedure Tycho08;
procedure Tycho09;
procedure Tycho10;
procedure Tycho11;
procedure Tycho12;
procedure Tycho13;
procedure Tycho14;
procedure Tycho15;
procedure Tycho16;
procedure Tycho17;
procedure Tycho18;
procedure Tycho19;
procedure Tycho20;
procedure Tycho21;
procedure Tycho22;
procedure Tycho23;
procedure Tycho24;
procedure Tycho25;
procedure Tycho26;
procedure Tycho27;
procedure Tycho28;
procedure Tycho30;
procedure Tycho31;
procedure Tycho32;

procedure Node998;
procedure Node999;

procedure TychoJoins;
procedure TychoReJoins;
procedure TychoTactics;
procedure TychoClose;
procedure TychoModerate;
procedure TychoFar;

procedure NodeWait;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;
variable tmp_hostile;
variable item;
variable cola2_ptr;


procedure TychoCombatTactics;
procedure TychoSniper;
procedure TychoNearCombat;
procedure TychoFreeCombat;

procedure TychoHolsterWeapon;
procedure TychoRemoveArmor;

procedure PlayerTraitor;

#define LVAR_Sleeping 							(4)
#define LVAR_WAITING                      (10)
#define LVAR_FOLLOW_DISTANCE              (11)
#define LVAR_TEAM                         (12)

#define float_tycho(x)    						float_msg(self_obj, message_str(SCRIPT_GENCHAT, x), FLOAT_MSG_NORMAL)
#define tycho_joins_party               	party_add_self;                                                   \
                                          add_timer_event(self_obj,game_ticks(1),2);                        \
                                          set_self_team(TEAM_PLAYER)
#define dude_has_free_slot_for_tycho  		((dude_at_max_party_size == false) and (Tycho_In_Party == false) or unlimited_party_members)
#define EVENT_FLOAT_JOIN 						(2)
#define is_junktown_enemy 						((global_var(DUDE_ENEMY_JUNK_CITIZEN) > 0) or (global_var(DUDE_ENEMY_JUNK_BOXER) > 0) or (global_var(ENEMY_JUNKTOWN) > 0))
#define is_in_junktown 							((cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_JUNKCSNO) or (cur_map_index == MAP_JUNKKILL))

#define sleep_time 		(2200)
#define wake_time 		(1600)
#define home_tile 		(19690)
#define sleep_tile 		(7000)

procedure Node998 begin
end
procedure Node999 begin
end

procedure start begin
end

procedure push_p_proc begin
   if (Tycho_In_Party == false) then begin
      script_overrides;
   end
   else begin
      float_tycho(random(300,302));
   end
end

procedure timed_event_p_proc begin
	if (fixed_param == EVENT_FLOAT_JOIN) then begin
		debug("tycho is joining!");
	end
end

// Fallout Fixt to prevent Tycho from slaughtering his homies
procedure combat_p_proc begin
	if not(global_var(FIXT_BUGFIXES_ONLY)) then begin
		if (global_var(DUDE_ENEMY_JUNK_CITIZEN) > 0) or (global_var(DUDE_ENEMY_JUNK_BOXER) > 0) or (global_var(ENEMY_JUNKTOWN) > 0) then begin
			party_remove(self_obj);
			if (fixed_param == 4) then begin
				attack(dude_obj);
				attack_setup(self_obj, dude_obj);
				party_remove(self_obj);
				call PlayerTraitor;
			end
			if (fixed_param == 2) then begin
				attack(dude_obj);
				attack_setup(self_obj, dude_obj);
				party_remove(self_obj);
			end
			attack_setup(self_obj, dude_obj);
			attack(dude_obj);
		end
	end
end

// Messages when attacking player (for player killing Junktown citizens)
procedure PlayerTraitor begin
	float_msg(self_obj, message_str(SCRIPT_TYCHO, 175 + random(0,5)), 2);
end

procedure critter_p_proc begin
	if (global_var(TYCHO_HIRELING_STATUS) < 2) then begin
		sleeping
	end
	else if (Tycho_In_Party) then begin
      party_member_follow_dude
   end
   else begin
   	set_self_abandon_party;
   end

	if (tmp_hostile or is_junktown_enemy) then begin
		if (obj_can_see_obj(self_obj, dude_obj) or obj_can_hear_obj(self_obj, dude_obj)) then begin
			tmp_hostile := 0;
			set_self_abandon_party;
			attack(dude_obj);
		end
	end
	else begin
		//call map_commentary;
	end
end

procedure destroy_p_proc begin
	set_global_var(TYCHO_HIRELING_STATUS, 3);

	if (source_obj == dude_obj and is_in_junktown) then begin
		set_global_var(DUDE_ENEMY_JUNK_CITIZEN, 1);
		set_global_var(ENEMY_JUNKTOWN, 1);
		set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) - 5);
	end

	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;

	// If not in party and haven't had introductions, display "dusty trenchcoat and gasmask" description
	if (global_var(TYCHO_HIRELING_STATUS) == 0) then begin
		display_msg(message_str(SCRIPT_TYCHO, 100));
	end
	// if have had introductions or was in party but was asked to leave, display "Tycho"
	else if ((global_var(TYCHO_HIRELING_STATUS) == 1) or (global_var(TYCHO_HIRELING_STATUS) == 3)) then begin
		display_msg(message_str(SCRIPT_TYCHO, 101));
	end
end

procedure map_enter_p_proc begin
	// TODO: Delete??
	if (obj_is_carrying_obj_pid(self_obj, PID_NUKA_COLA) >= 2) then begin
		cola2_ptr := obj_carrying_pid_obj(self_obj, PID_NUKA_COLA);
	end

	/*if Tycho_In_Party then begin
		set_self_team(TEAM_PLAYER);
	end
	else begin
		set_self_team(TEAM_JUNKTOWNER);
	end*/

	// This code sets ai behavior based on set combat tactics:
	if ((has_trait( TRAIT_OBJECT, self_obj, OBJECT_AI_PACKET ) != 90) and (has_trait( TRAIT_OBJECT, self_obj, OBJECT_AI_PACKET ) != 95) and (has_trait( TRAIT_OBJECT, self_obj, OBJECT_AI_PACKET ) != 96)) then begin
		set_self_ai(AI_TYCHO);
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;

	get_reaction

	if is_junktown_enemy then begin
		float_msg(self_obj, message_str(SCRIPT_TYCHO, 180), 2);
		tmp_hostile := 1;
	end
	else begin

		start_gdialog(389, self_obj, 4, -1, -1);
		if (global_var(TYCHO_HIRELING_STATUS) == 2) or (global_var(TYCHO_HIRELING_STATUS) == 3) then begin
			gdialog_set_barter_mod(255);
		end

		gsay_start;

		if (global_var(TYCHO_HIRELING_STATUS) == 3) then begin
			call Tycho28;
		end
		else if (global_var(TYCHO_HIRELING_STATUS) == 2) then begin
			call Tycho22;
		end
		else if (global_var(TYCHO_HIRELING_STATUS) == 0) then begin
			call Tycho01;
		end
		else if ((global_var(HIRED_BY_GIZMO) == 1) and (global_var(HIRED_BY_KILLIAN) == 0)) then begin
			call Tycho19;
		end
		else if (local_var(1) < 2) then begin
			call Tycho14;
		end
		else begin
			call Tycho15;
		end

		gsay_end;
		end_dialogue;

		if Tycho_In_Party then begin
			if (global_var(DESTROY_MASTER_10) == 0) then begin
				display_msg(message_str(SCRIPT_TYCHO, 173));
				give_exp_points(EXP_HIRED_TYCHO);
				set_global_var(DESTROY_MASTER_10, 1);
			end
		end

	end
end

procedure use_obj_on_p_proc begin

	// TODO: Investigate this
	if Tycho_In_Party then begin
		item := obj_pid(obj_being_used_with);
		if ((item != 40) and (item != 71) and (item != 81) and (item != 103) and (item != 144)) then begin
			script_overrides;
			if (obj_item_subtype(obj_being_used_with) != 3) then begin
				rm_obj_from_inven(dude_obj, obj_being_used_with);
				add_obj_to_inven(self_obj, obj_being_used_with);
			end
			else begin
				if ((item == 8) or (item == 18) or (item == 143) or (item == 10) or (item == 94) or (item == 7) or (item == 21) or (item == 234) or (item == 235) or (item == 24) or (item == 16) or (item == 120) or (item == 242)) then begin
					rm_obj_from_inven(dude_obj, obj_being_used_with);
					add_obj_to_inven(self_obj, obj_being_used_with);
					float_msg(self_obj, message_str(SCRIPT_GENCHAT, 215), 12);
				end
				else begin
					float_msg(self_obj, message_str(SCRIPT_MODREACT, 109), 12);
				end
			end
		end
	end

end

procedure Tycho01 begin
	gsay_reply(389, 102);
	giq_option( 4, SCRIPT_TYCHO, 103, Node999, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 104, Tycho02, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_TYCHO, 105, Tycho03, NEUTRAL_REACTION );
end

procedure Tycho02 begin
	if (obj_is_carrying_obj_pid(self_obj, obj_pid(cola2_ptr))) then begin
		rm_obj_from_inven(self_obj, cola2_ptr);
		add_obj_to_inven(dude_obj, cola2_ptr);
	end
	gsay_reply(389, 106);
	giq_option( 4, SCRIPT_TYCHO, 107, Tycho04, NEUTRAL_REACTION );
end

procedure Tycho03 begin
	gsay_message(389, 108, 50);
end

procedure Tycho04 begin
	set_global_var(TYCHO_HIRELING_STATUS, 1);
	gsay_reply(389, 109);
	giq_option( 4, SCRIPT_TYCHO, 110, Tycho05, NEUTRAL_REACTION );
	giq_option( 6, SCRIPT_TYCHO, 111, Tycho06, NEUTRAL_REACTION );
end

procedure Tycho05 begin
	gsay_reply(389, 112);
	giq_option( 4, SCRIPT_TYCHO, 113, Tycho07, NEUTRAL_REACTION );
	giq_option( 6, SCRIPT_TYCHO, 114, Tycho08, NEUTRAL_REACTION );
end

procedure Tycho06 begin
	gsay_reply(389, 115);
	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, 116, Tycho11, NEUTRAL_REACTION );
	end
	giq_option( 6, SCRIPT_TYCHO, 117, Tycho09, NEUTRAL_REACTION );
end

procedure Tycho07 begin
	gsay_reply(389, 118);
	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, 119, Tycho11, NEUTRAL_REACTION );
	end
	else begin
		Goodbyes;
		giq_option( 4, SCRIPT_TYCHO, exit_line, Node999, NEUTRAL_REACTION );
	end
end

procedure Tycho08 begin
	gsay_reply(389, 120);
	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, 121, Tycho11, NEUTRAL_REACTION );
	end
	else begin
		Goodbyes;
		giq_option( 4, SCRIPT_TYCHO, exit_line, Node999, NEUTRAL_REACTION );
	end
end

procedure Tycho09 begin
	gsay_reply(389, 122);
	giq_option( 4, SCRIPT_TYCHO, 123, Tycho10, NEUTRAL_REACTION );
	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, 116, Tycho11, NEUTRAL_REACTION );
	end
end

procedure Tycho10 begin
	gsay_reply(389, 124);
	giq_option( 4, SCRIPT_TYCHO, 125, Tycho17, NEUTRAL_REACTION );
	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, 126, Tycho11, NEUTRAL_REACTION );
	end
end

procedure Tycho11 begin
	set_local_var(5, 1);
	gsay_reply(389, 127);
	giq_option( 4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 128) + dude_name + message_str(SCRIPT_TYCHO, 129), Tycho12, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 130, Tycho13, NEUTRAL_REACTION );
end

procedure Tycho12 begin
	UpReact
	gsay_message(389, 131, 50);
end

procedure Tycho13 begin
	DownReact
	gsay_reply(389, 132);
	giq_option( 4, SCRIPT_TYCHO, 133, Node998, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 134, Node999, NEUTRAL_REACTION );
end

procedure Tycho14 begin
	gsay_reply(389, 135);
	giq_option( 4, SCRIPT_TYCHO, 136, Tycho13, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 137, Tycho08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 138, Tycho16, NEUTRAL_REACTION );
end

procedure Tycho15 begin
	gsay_reply(389, 139);
	giq_option( 4, SCRIPT_TYCHO, 137, Tycho08, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 138, Tycho16, NEUTRAL_REACTION );
	if (global_var(HIRED_BY_KILLIAN)) then begin
		giq_option( 4, SCRIPT_TYCHO, 140, Tycho18, NEUTRAL_REACTION );
	end
end

procedure Tycho16 begin
	gsay_reply(389, 141);
	Goodbyes;
	giq_option( 4, SCRIPT_TYCHO, exit_line, Node999, NEUTRAL_REACTION );
end

procedure Tycho17 begin
	gfade_out(1);

	critter_mod_skill(dude_obj, SKILL_OUTDOORSMAN, 5);
	game_time_advance(game_ticks(120));
	gsay_reply(389, 142);
	gfade_in(1);

	Goodbyes;

	if (local_var(5) != 1) then begin
		giq_option( 4, SCRIPT_TYCHO, exit_line, Tycho11, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_TYCHO, exit_line, Node999, NEUTRAL_REACTION );
	end

	display_msg(message_str(SCRIPT_GENCHAT, 116) + 5 + message_str(SCRIPT_GENCHAT, 134));
end

procedure Tycho18 begin
	if (dude_is_male) then begin
		gsay_message(389, 168, 49);
	end
	else begin
		gsay_message(389, 169, 49);
	end
	call TychoJoins;
end

procedure Tycho19 begin
	gsay_reply(389, 144);
	giq_option( 4, SCRIPT_TYCHO, 145, Tycho20, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 146, Tycho21, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_TYCHO, 105, Node999, NEUTRAL_REACTION );
end

procedure Tycho20 begin
	gsay_message(389, 147, 50);
end

procedure Tycho21 begin
	DownReact
	gsay_message(389, 148, 50);
end

// Primary node
procedure Tycho22 begin
	gsay_reply(389, 149);
	if (cur_map_index == MAP_FOOT) then begin
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho23, GOOD_REACTION );
	end
	if (cur_map_index == MAP_GLOWENT) then begin //  THE GLOW - ENTRANCE (CRATER)
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho25, GOOD_REACTION );
	end
	if (cur_map_index == MAP_DETHCLAW) then begin//  THE HUB - DEATHCLAW CAVE
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho26, GOOD_REACTION );
	end
	if (cur_map_index == MAP_HALLDED) then begin //  NECROPOLIS - HALLS OF THE DEAD
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho30, GOOD_REACTION );
	end
	if (cur_map_index == MAP_MBSTRG12) then begin //  MILITARY BASE - STRONGHOLD (LEVELS 1 AND 2)
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho31, GOOD_REACTION );
	end
	if (cur_map_index == MAP_MBVATS12) then begin //  MILITARY BASE -  VATS  (LEVELS 3 AND 4)
		giq_option( 4, SCRIPT_TYCHO, 150, Tycho32, GOOD_REACTION );
	end
	giq_option( 4, SCRIPT_TYCHO, 151, Tycho24, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 164, Tycho27, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 159, TychoTactics, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 2000, TychoCombatTactics, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 166, Node999, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_CARVLEAD, 139, Node999, NEUTRAL_REACTION );
end

procedure Tycho23 begin
	gsay_message(389, 152, 49);
	call Tycho22;
end

// Wait here node
procedure Tycho24 begin
	gsay_message(389, 153, 49);
	NOption(2511,NodeWait,004);
end

procedure NodeWait begin
	set_global_var(TYCHO_HIRELING_STATUS, 3);
	set_party_waiting;
end

procedure Tycho25 begin
	gsay_message(389, 154, 49);
	call Tycho22;
end

procedure Tycho26 begin
	gsay_message(389, 155, 49);
	call Tycho22;
end

procedure Tycho27 begin
	gsay_message(389, 165, 49);
	call Tycho22;
end

procedure Tycho28 begin
	gsay_reply(389, 170);
	giq_option( 4, SCRIPT_TYCHO, 171, TychoReJoins, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 172, Node999, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_TYCHO, 174, TychoReJoins, GOOD_REACTION );
	giq_option( -3, SCRIPT_TYCHO, 105, Node999, NEUTRAL_REACTION );
end

procedure Tycho30 begin
	gsay_message(389, 158, 49);
	call Tycho22;
end

procedure Tycho31 begin
	gsay_message(389, 157, 49);
	call Tycho22;
end

procedure Tycho32 begin
	gsay_message(389, 156, 49);
	call Tycho22;
end

procedure TychoJoins begin
	if dude_has_free_slot_for_tycho then begin
		set_global_var(TYCHO_HIRELING_STATUS, 2);
		set_global_var(TYCHO_DISTANCE_VAL, 4);
		set_global_var(TYCHO_DUDE_LAST_LEVEL, get_pc_stat( PCSTAT_level ));
		//critter_add_trait(self_obj, 1, 6, 0);
		//party_add(self_obj);

		tycho_joins_party;
	end
	else begin
		Reply(2500);
		NOption(2510,Node999,004);
	end
end

procedure TychoReJoins begin
	if dude_has_free_slot_for_tycho then begin
		set_global_var(TYCHO_HIRELING_STATUS, 2);
		set_global_var(TYCHO_DISTANCE_VAL, 4);
		set_global_var(TYCHO_DUDE_LAST_LEVEL, get_pc_stat( PCSTAT_level ));
		end_party_waiting;
	end
	else begin
		Reply(2500);
		NOption(2510,Node999,004);
	end
end

procedure TychoTactics begin
	gsay_reply(389, 160);

	if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_CLOSE) then
		giq_option( 4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 161), TychoClose, NEUTRAL_REACTION );

	if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_MEDIUM) then
		giq_option( 4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 162), TychoModerate, NEUTRAL_REACTION );

	if (local_var(LVAR_FOLLOW_DISTANCE) != FOLLOW_DISTANCE_FAR) then
		giq_option( 4, SCRIPT_TYCHO, message_str(SCRIPT_TYCHO, 163), TychoFar, NEUTRAL_REACTION );
end

procedure TychoClose begin
	set_follow_close;

	/*set_local_var(6, 0);
	set_local_var(7, 0);
	set_local_var(8, 0);
	set_global_var(TYCHO_DISTANCE_VAL, 2);*/
	gsay_message(235, 201, 50);
	call Tycho22;
end

procedure TychoModerate begin
	set_follow_medium;

	/*set_local_var(6, 0);
	set_local_var(7, 0);
	set_local_var(8, 0);
	set_global_var(TYCHO_DISTANCE_VAL, 4);*/
	gsay_message(235, 201, 50);
	call Tycho22;
end

procedure TychoFar begin
	set_follow_far;

/*	set_local_var(6, 0);
	set_local_var(7, 0);
	set_local_var(8, 0);
	set_global_var(TYCHO_DISTANCE_VAL, 6);*/
	gsay_message(235, 201, 50);
	call Tycho22;
end

procedure TychoCombatTactics begin
	Reply(2001);

	if (self_is_armed) then
		giq_option( 4, SCRIPT_TYCHO, 2010, TychoHolsterWeapon, GOOD_REACTION );

	if (self_wearing_armor) then
		giq_option( 4, SCRIPT_TYCHO, 2011, TychoRemoveArmor, GOOD_REACTION );

	giq_option( 4, SCRIPT_TYCHO, 2012, TychoSniper, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 2013, TychoNearCombat, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 2014, TychoFreeCombat, GOOD_REACTION );
	giq_option( 4, SCRIPT_TYCHO, 2015, Tycho22, GOOD_REACTION );
end

procedure TychoHolsterWeapon begin
	inven_unwield(self_obj);
	gsay_message(389, 2100, 49);
	call TychoCombatTactics;
end

procedure TychoRemoveArmor begin
	remove_armor(self_obj)
	gsay_message(389, 2100, 49);
	call TychoCombatTactics;
end

procedure TychoSniper begin
	gsay_message(389, 2100, 49);
	set_self_ai( AI_PARTY_AN_AGGRESSIVE );
	call TychoCombatTactics;
end

procedure TychoNearCombat begin
	gsay_message(389, 2100, 49);
	set_self_ai( AI_PARTY_AN_BERSERK );
	call TychoCombatTactics;
end

procedure TychoFreeCombat begin
	gsay_message(389, 2100, 49);
	set_self_ai( AI_TYCHO );
	call TychoCombatTactics;
end




