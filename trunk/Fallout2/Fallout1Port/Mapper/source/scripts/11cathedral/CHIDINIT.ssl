/*

	Cathedral - Initiate

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\mapnecro.h"

#define NAME                    SCRIPT_CHIDINIT
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;

variable initial :=  0;
variable in_timed_event;
variable tmp_hostile;

procedure start begin
	if not(initial) then begin
		initial :=  1;
		set_self_team(TEAM_CATHEDRAL );
		set_self_ai( AI_BRAINWASHED_CHILDREN );
		if (local_var(0) == 0) then begin
			set_local_var(0, random(100, 109));
		end
		anim(self_obj, 1000, random(0, 5));
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);

	inc_inv_evil_crit
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_FOLL_INVADERS, 1 );
	end
	rm_timer_event(self_obj);
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHIDINIT, local_var(0)));
end

procedure talk_p_proc
begin
	script_overrides;
	dude_look_at_critter;
	float_msg(self_obj, message_str(SCRIPT_CHIDINIT, random(110, 116)), 0);
end

procedure timed_event_p_proc
begin
	if (fixed_param == 1) then begin
		anim(self_obj, 1000, random(0, 5));
	end
	else begin
		animate_move_obj_to_tile(self_obj, tile_num_in_direction(self_tile, random(0, 5), random(1, 3)), 0);
	end
	in_timed_event := 0;
end

procedure critter_p_proc
begin
	if (global_var( GVAR_ENEMY_FOLL_INVADERS )) then begin
		tmp_hostile := 1;
	end
	if (tmp_hostile and self_can_see_dude) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (not(in_timed_event)) then begin
			in_timed_event := 1;
			add_timer_event(self_obj, game_ticks(random(6, 10)), random(1, 3));
		end
	end
end
