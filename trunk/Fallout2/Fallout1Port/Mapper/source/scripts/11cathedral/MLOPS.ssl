/*

	Cathedral Masters Lair - Technician

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_MLOPS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure dialog_end;

variable tmp_hostile;
variable Only_Once := 1;

#define float(x)    						float_msg(self_obj, message_str(NAME, x), FLOAT_MSG_RED)

procedure gotonull;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		critter_add_trait( self_obj, TRAIT_OBJECT, OBJECT_TEAM_NUM, TEAM_NECROPOLIS_MUTANT );
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end

	if (not(dude_wearing_coc_robe) and (self_can_see_dude or self_can_hear_dude)) then begin
		float(103);
		tmp_hostile := 1;
	end
end

procedure pickup_p_proc begin
	if obj_in_party(source_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;

	if dude_wearing_coc_robe then begin
		if (local_var(0) == 1) then begin
			float(102);
		end
		else begin
			float(101);
		end
		set_local_var(0, 1);
	end
	else begin
		float(103);
		call combat;
	end

end

procedure destroy_p_proc begin
	inc_evil_critter
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(100));
end

procedure dialog_end begin
end

procedure gotonull begin
end

