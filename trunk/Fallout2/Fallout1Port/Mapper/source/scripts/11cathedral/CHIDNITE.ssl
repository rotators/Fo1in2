/*

	Cathedral - Nightkin guards

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h

#define NAME                    SCRIPT_CHIDNITE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure Nightkin01;
procedure Nightkin01a;
procedure Nightkin02;
procedure Nightkin03;
procedure Nightkin04;
procedure Nightkin05;
procedure Nightkin05a;
procedure Nightkin06;
procedure Nightkin07;
procedure Nightkinend;
procedure Combat;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
	if Only_Once then begin
		critter_add_trait( self_obj, TRAIT_OBJECT, OBJECT_TEAM_NUM, TEAM_CATHEDRAL );
		critter_add_trait( self_obj, TRAIT_OBJECT, OBJECT_AI_PACKET, AI_NIGHTKIN );
		Only_Once := 0;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHIDNITE, 100));
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHIDNITE, 100));
end

procedure critter_p_proc begin
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if (not(obj_is_carrying_obj_pid(dude_obj, PID_RED_COC_BADGE))) then begin
			if (not(obj_is_carrying_obj_pid(dude_obj, PID_BLACK_COC_BADGE))) then begin
				if (obj_pid(critter_inven_obj(dude_obj, 0)) != PID_PURPLE_ROBE) then begin
					tmp_hostile := 1;
				end
			end
		end
	end
	if ((party_size > 1) and obj_can_see_obj(self_obj, dude_obj)) then begin//  PARTY_COUNT > 1.    Is player travelling with homies?
		tmp_hostile := 1;
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	script_overrides;
	if (global_var(CATHEDRAL_ENEMY) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_CHIDNITE, 101), 0);
		tmp_hostile := 1;
	end
	else begin
		start_dialog_at_node(Nightkin01);
	end
end

procedure Nightkin01 begin
	gsay_reply(394, 102);
	giq_option(7, 394, 103, Nightkin01a, 50);
	giq_option(4, 394, 104, Nightkin04, 50);
	giq_option(4, 394, 105, Nightkin05, 50);
	if (global_var(IS_ALCOHOL_ADDICT) == 1) then begin
		giq_option(4, 394, 106, Nightkin05, 50);
	end
	if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
		giq_option(4, 394, 107, Nightkin04, 50);
	end
	giq_option(-3, 394, 108, Nightkinend, 50);
end

procedure Nightkin01a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Nightkin02;
	end
	else begin
		call Nightkin03;
	end
end

procedure Nightkin02 begin
	gsay_reply(394, 109);
	gsay_option(394, 125, Nightkinend, 50);
end

procedure Nightkin03 begin
	gsay_reply(394, 110);
	call Combat;
end

procedure Nightkin04 begin
	gsay_reply(394, 111);
	call Combat;
end

procedure Nightkin05 begin
	gsay_reply(394, 112);
	giq_option(7, 394, 113, Nightkin05a, 50);
	giq_option(4, 394, 114, Nightkin04, 50);
	giq_option(4, 394, 115, Nightkin07, 50);
	giq_option(4, 394, 116, Nightkin07, 50);
	giq_option(4, 394, 117, Nightkinend, 50);
	if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
		giq_option(4, 394, 118, Nightkin04, 50);
	end
end

procedure Nightkin05a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Nightkin06;
	end
	else begin
		call Nightkin03;
	end
end

procedure Nightkin06 begin
	gsay_reply(394, 119);
	gsay_option(394, 125, Nightkinend, 50);
end

procedure Nightkin07 begin
	gsay_reply(394, 120);
	giq_option(4, 394, 121, Nightkin04, 50);
	giq_option(4, 394, 122, Nightkin04, 50);
	giq_option(4, 394, 123, Nightkinend, 50);
	if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
		giq_option(4, 394, 124, Combat, 50);
	end
end

procedure Nightkinend begin
end

procedure Combat begin
	set_global_var(CATHEDRAL_ENEMY, 1);
	tmp_hostile := 1;
end


procedure damage_p_proc begin
	set_global_var(MUTANTS_ENEMY_UNSURE, 1);
end

procedure destroy_p_proc begin
	inc_evil_critter
end


