#include "..\headers\define.h"
#include "..\headers\command.h"

procedure start;
procedure look_at_p_proc;//    script_action == 21
procedure skills;

variable XP;

// "From this point on, you'll be attacked if you're not wearing Robes
// or if you have any follower. If you talk your way past the two guards
// by the vault door you gain 750 xp. You earn 1250 xp if you use Science
// on the computer just inside and gain the location of the Military Base
// (even if you have it). You can also get this from the computers on level 2."

procedure start
begin
	if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
		call look_at_p_proc;
	end
	else begin
		if (script_action == 8) then begin//<-- use_skill_on_p_proc
			script_overrides;
			call skills;
		end
		else begin
			if (script_action == 6) then begin//use_p_proc - Use, Activate, or Manipulate the Object or Item
				display_msg(message_str(SCRIPT_TERM, 201));
			end
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_TERM, 200));
end

procedure skills begin
	if (action_being_used == SKILL_SCIENCE) then begin//-- SCIENCE
		if is_success(roll_vs_skill(dude_obj, 12, -50)) then begin
			display_msg(message_str(SCRIPT_TERM, 203));
			if global_var(WORLDMAPLIST_MILITARYBASE) == 0 then begin
				XP := EXP_FOUND_MILITARY_BASE;
				set_global_var(WORLDMAPLIST_MILITARYBASE, 1);
				if (global_var(VATS_BLOWN_CUTSCENE_DONE) > 0) then begin
					mark_on_map(AREA_MILITARY_BASE_DEAD)
				end
				else begin
					mark_on_map(AREA_MILITARY_BASE)
				end
			end
			else begin
				if (global_var(WORLDMAPLIST_MILITARYBASE) == 1) then begin
					XP := EXP_KNEW_ABOUT_MILITARY_BASE;
					set_global_var(WORLDMAPLIST_MILITARYBASE, 2);
				end
				else begin
					XP := EXP_WENT_TO_MILITARY_BASE;
				end
			end
			display_msg(message_str(SCRIPT_TERM, 204) + XP + message_str(SCRIPT_TERM, 205));
			give_exp_points(XP);
		end
		else begin
			display_msg(message_str(SCRIPT_TERM, 201));
		end
	end
	else begin
		if (action_being_used == SKILL_REPAIR) then begin//-- REPAIR
			display_msg(message_str(SCRIPT_TERM, 202));
		end
	end
end

