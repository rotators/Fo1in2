/*

	Masters Vault, computer terminals

*/

/* Include Files */
#include "..\headers\define.h"

#define NAME                    SCRIPT_TERM

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure use_skill_on_p_proc;
procedure use_p_proc;
procedure look_at_p_proc;

procedure skills;

// "From this point on, you'll be attacked if you're not wearing Robes
// or if you have any follower. If you talk your way past the two guards
// by the vault door you gain 750 xp. You earn 1250 xp if you use Science
// on the computer just inside and gain the location of the Military Base
// (even if you have it). You can also get this from the computers on level 2."

variable XP;

#define LVAR_ReceivedXP 							(0)

procedure start begin
end

procedure use_skill_on_p_proc begin
	script_overrides;
	call skills;
end

procedure use_p_proc begin
   display_msg(mstr(201));
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(mstr(200));
end

procedure skills begin
	if (action_being_used == SKILL_SCIENCE) then begin
	   if (local_var(LVAR_ReceivedXP) == 0) then begin

   		if is_success(roll_vs_skill(dude_obj, SKILL_SCIENCE, -50)) then begin
   			display_msg(mstr(203));

   			if global_var( GVAR_WORLDMAPLIST_MILITARYBASE ) == 0 then begin
   				XP := EXP_FOUND_MILITARY_BASE;
   				set_global_var( GVAR_WORLDMAPLIST_MILITARYBASE, 1 );
   				mark_on_map(AREA_MILITARY_BASE)
   			end
   			else begin
   				if (global_var( GVAR_WORLDMAPLIST_MILITARYBASE ) == 1) then begin
   					XP := EXP_KNEW_ABOUT_MILITARY_BASE;
   					set_global_var( GVAR_WORLDMAPLIST_MILITARYBASE, 1 ); // Was 2 before!! Causes weird dialog, because the player wasn't actually there yet.
   				end
   				else begin
   					XP := EXP_WENT_TO_MILITARY_BASE;
   				end
   			end

   			display_msg(mstr(204) + XP + mstr(205));
   			give_exp_points(XP);
            set_local_var(LVAR_ReceivedXP, 1);
   		end
   		else begin
   			display_msg(mstr(201));
   		end

      end
	end
	else if (action_being_used == SKILL_REPAIR) then begin
		display_msg(mstr(202));
	end
end

