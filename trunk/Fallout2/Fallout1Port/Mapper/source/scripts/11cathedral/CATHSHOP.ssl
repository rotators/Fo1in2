/*

	Cathedral - Shopkeeper

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_CATHSHOP
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;

procedure combat;

procedure CathShop00;
procedure CathShop00a;
procedure CathShop01;
procedure CathShop02;
procedure CathShop02a;
procedure CathShop03;
procedure CathShop04;
procedure CathShop05;
procedure CathShop06;
procedure CathShop07;
procedure CathShop08;
procedure CathShop09;
procedure CathShop10;
procedure CathShop11;
procedure CathShop12;
procedure CathShop13;
procedure CathShop14;
procedure CathShopEnd;

import variable Shop_Ptr;
import variable Shopkeeper_Ptr;

variable tmp_hostile;
variable Only_Once := 1;
variable Caught_Stealing;
variable Here_To_Shop;
variable Go_Balistic;

procedure start begin
	if Only_Once then begin
		set_self_team(TEAM_CATHEDRAL );
		set_self_ai( AI_CHILDREN_TRUE );
		Shopkeeper_Ptr := self_obj;
		Only_Once := 0;
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	variable Minigun := 0;
	variable PurpleRobes := 0;
	dude_look_at_critter;
	get_reaction

	PurpleRobes := self_item( PID_PURPLE_ROBE );
	rm_obj_from_inven(self_obj, PurpleRobes);
	move_obj_inven_to_obj(Shop_Ptr, self_obj);

	start_gdialog(843, self_obj, 4, -1, -1);
	gsay_start;
	if (Caught_Stealing) then begin
		call CathShop00;
	end
	else 	if (dude_is_armed) then begin
		call CathShop02;
	end
	else if dude_wearing_coc_robe then begin
		call CathShop07;
	end
	else begin
		call CathShop10;
	end
	gsay_end;
	end_dialogue;

	move_obj_inven_to_obj(self_obj, Shop_Ptr);
	add_obj_to_inven(self_obj, PurpleRobes);
	wield_obj_critter(self_obj, PurpleRobes);

	if (Go_Balistic) then begin
		Minigun := create_object( PID_MINIGUN, 0, 0 );
		add_obj_to_inven(self_obj, Minigun);
		wield_obj_critter(self_obj, Minigun);
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_good_critter
	rm_timer_event(self_obj);
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_CATHSHOP, 100));
end

procedure timed_event_p_proc begin
	if (fixed_param == 1) then begin
		Here_To_Shop := 1;
		dialogue_system_enter;
	end
	else if (fixed_param == 2) then begin
		Caught_Stealing := 1;
		dialogue_system_enter;
	end
end

procedure CathShop00 begin
	gsay_reply(843, 101);
	NOption( 102, CathShop01, -3 );
	NOption( 103, CathShop01, 4 );
	NOption( 104, CathShop00a, 5 );
end

procedure CathShop00a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call combat;
	end
	else begin
		call CathShop01;
	end
end

procedure CathShop01 begin
	gsay_message(843, 105, 51);
	call combat;
end

procedure CathShop02 begin
	gsay_reply(843, 106);
	NOption( 107, CathShopEnd, -3 );
	NOption( 108, CathShop03, 4 );
	NOption( 109, CathShop02a, 6 );
end

procedure CathShop02a begin
	if (global_var(CATHEDRAL_ENEMY) == 1) then begin
		call CathShop04;
	end
	else begin
		call CathShop05;
	end
end

procedure CathShop03 begin
	gsay_message(843, 110, 50);
end

procedure CathShop04 begin
	gsay_reply(843, 111);
	NOption( 112, CathShop01, 6 );
	NOption( 113, CathShopEnd, 6 );
end

procedure CathShop05 begin
	gsay_reply(843, 114);
	NOption( 115, CathShop00a, 6 );
	NOption( 116, CathShop06, 6 );
end

procedure CathShop06 begin
	gsay_message(843, 117, 50);
end

procedure CathShop07 begin
	gsay_reply(843, 118);
	NOption( 119, CathShop08, -3 );
	NOption( 120, CathShop12, 4 );
	NOption( 121, CathShop09, 4 );
end

procedure CathShop08 begin
	gsay_reply(843, 122);
	NOption( 123, CathShopEnd, -3 );
end

procedure CathShop09 begin
	gsay_message(843, 124, 50);
end

procedure CathShop10 begin
	gsay_reply(843, 125);
	NOption( 126, CathShop08, -3 );
	NOption( 127, CathShop13, 4 );
	NOption( 128, CathShop11, 5 );
end

procedure CathShop11 begin
	gsay_reply(843, 129);
	NOption( 130, CathShopEnd, 4 );
	NOption( 131, CathShop01, 4 );
	NOption( 132, CathShop13, 4 );
end

procedure CathShop12 begin
	gsay_reply(843, 133);
	//giq_option(4, 843, 134, CathShopEnd, 50);
	NOption( 136, CathShop14, 4 );
end

procedure CathShop13 begin
	gsay_reply(843, 135);
	NOption( 136, CathShop14, 4 );
end

procedure CathShop14 begin
	gdialog_mod_barter(-10);
	gsay_message(843, 137, 50);
end

procedure CathShopEnd begin
end






