/*

	Caravan job encounter - Caravan Leader

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_CRVNTEAM
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"
#include "..\headers\caravan.h"
#include "..\headers\mapenc.h"

/* Standard Script Procedures */
procedure start;
procedure timed_event_p_proc;
procedure map_update_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;

procedure initialize_p_proc;

variable Only_Once;

#define LVAR_Called_Clear  (10)

#define TIMER_ALL_CLEAR 3

procedure start begin
	if (Only_Once == 0) then begin
		call initialize_p_proc;
	end
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_ALL_CLEAR) then begin
   	if (not(combat_is_initialized)) then
      	float_msg(self_obj,g_mstr(450),FLOAT_COLOR_NORMAL);
      add_timer_event( self_obj, game_ticks(10), TIMER_ALL_CLEAR );
   end
end

procedure map_update_p_proc begin
	debug("MVAR_Hostile_Total: " + map_var(MVAR_Hostile_Total));

   if ((not(is_loading_game)) and (not(combat_is_initialized)) ) then begin
      if map_is_caravan_escort and not(obj_pid(self_obj) == PID_MERCHENARY) then begin
         if ((local_var(LVAR_Called_Clear) == 0) and (map_var(MVAR_Hostile_Total) <= 0)) then begin
           set_local_var(LVAR_Called_Clear,1);
           add_timer_event( self_obj, game_ticks(2), TIMER_ALL_CLEAR );
         end
      end
   end
end

procedure talk_p_proc begin
	if (cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_BROHDENT) or (cur_map_index == MAP_HOTEL) or (cur_map_index == MAP_LAADYTUM) then begin //   //  JUNKTOWN ENTRANCE, BROHERHOOD ENTRANCE, NECROPOLIS HOTEL, L.A. BONEYARD ENTRANCE (ADYTUM)
		float_msg(self_obj, message_str(SCRIPT_CRVNTEAM, random(100, 102)), 0);
	end
	else begin
		float_msg(self_obj, message_str(SCRIPT_CRVNTEAM, random(103, 115)), 0);
	end
end

procedure critter_p_proc begin
	if (cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_BROHDENT) or (cur_map_index == MAP_HOTEL) or (cur_map_index == MAP_LAADYTUM) then begin //   //  JUNKTOWN ENTRANCE, BROHERHOOD ENTRANCE, NECROPOLIS HOTEL, L.A. BONEYARD ENTRANCE (ADYTUM)
		if ((((game_time / (10 * 60 * 60)) - local_var(0)) >= 2) and (local_var(0) != 0)) then begin
			destroy_object(self_obj);
		end
	end
end

procedure destroy_p_proc begin
	if (cur_map_index != MAP_JUNKENT) and (cur_map_index != MAP_BROHDENT) and (cur_map_index != MAP_HOTEL) and (cur_map_index != MAP_LAADYTUM) then begin
		if (obj_pid(self_obj) == PID_MERCHENARY) then begin
			//set_global_var(CVAN_GUARD, (global_var(CVAN_GUARD) - 1));
			rm_caravan_guards(1);
		end
		else begin
			//set_global_var(CVAN_DRIVER, 0);
			rm_caravan_masters(1);
		end
	end
   inc_good_critter
end

procedure initialize_p_proc begin
	variable Item := 0;
	Only_Once := 1;
	if (local_var(0) == 0) then begin
		set_local_var(0, game_time / (10 * 60 * 60));
	end
	if (((game_time / (10 * 60 * 60)) - local_var(0)) >= 2) then begin
		destroy_object(self_obj);
	end

	set_self_team(TEAM_PLAYER);
	set_self_ai(AI_BERZERKER);

	if (obj_pid(self_obj) == PID_MERCHENARY) then begin
		Item := create_object( PID_44_FMJ_MAGNUM, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_44_MAGNUM_JHP, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_44_FMJ_MAGNUM, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_44_MAGNUM_JHP, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_STIMPAK, 0, 0 );
		add_obj_to_inven(self_obj, Item);
	end
	else begin
		Item := create_object( PID_SHOTGUN_SHELLS, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_SHOTGUN_SHELLS, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_SHOTGUN_SHELLS, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_SHOTGUN_SHELLS, 0, 0 );
		add_obj_to_inven(self_obj, Item);
		Item := create_object( PID_STIMPAK, 0, 0 );
		add_obj_to_inven(self_obj, Item);
	end
end


