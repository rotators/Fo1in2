/*

	Caravan cow

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\mapenc.h"

#define NAME                    SCRIPT_CARCOW
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\caravan.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure combat_p_proc;
procedure talk_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
variable initial :=  0;

procedure start begin
end

procedure map_enter_p_proc begin
	if not(initial) then begin
		initial :=  1;
		set_self_ai( AI_COW );

	   if map_is_caravan_escort then
	       set_self_team(TEAM_PLAYER);
	   //else
	   //    set_self_team(TEAM_BRAHMIN);

		if (local_var(0) == 0) then begin
			set_local_var(0, game_time / (10 * 60 * 60));
		end
		if (((game_time / (10 * 60 * 60)) - local_var(0)) >= 2) then begin
			debug("remove cow!");
			destroy_object(self_obj);
		end
	end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
       script_overrides;
       debug_msg("I'm strapped in and won't move.");
   end
end

procedure critter_p_proc begin
	if (cur_map_index == MAP_JUNKENT) or (cur_map_index == MAP_BROHDENT) or (cur_map_index == MAP_HOTEL) or (cur_map_index == MAP_LAADYTUM) then begin //   //  JUNKTOWN ENTRANCE, BROHERHOOD ENTRANCE, NECROPOLIS HOTEL, L.A. BONEYARD ENTRANCE (ADYTUM)
		if ((((game_time / (10 * 60 * 60)) - local_var(0)) >= 2) and (local_var(0) != 0)) then begin
			destroy_object(self_obj);
		end
	end
end

procedure description_p_proc begin
   display_msg(message_str(SCRIPT_BRAHMIN, 100));
end

procedure talk_p_proc begin
	dude_look_at_critter;
	display_msg(message_str(SCRIPT_BRAHMIN, random(102, 105)));
end

procedure use_obj_on_p_proc begin
	variable LVar0 := 0;
	if ((obj_pid(obj_being_used_with) == PID_BEER) or (obj_pid(obj_being_used_with) == PID_BOOZE)) then begin
		script_overrides;
		LVar0 := obj_being_used_with;
		rm_obj_from_inven(source_obj, obj_being_used_with);
		destroy_object(LVar0);
		play_sfx("MABROMAQ");

		reg_anim_func(2, self_obj);
   		reg_anim_func(1, 1);
   		reg_anim_animate(self_obj, 14, -1);
   		reg_anim_animate(self_obj, 20, 5);
   		reg_anim_animate(self_obj, 48, -1);
		reg_anim_func(3, 0);

		critter_injure(self_obj, 2);//    DAM_KNOCKED_DOWN       2		<-- DOES NOT WORK!!!
		critter_injure(dude_obj, 4);//    DAM_CRIP_LEG_LEFT      4
		critter_injure(dude_obj, 8);//    DAM_CRIP_LEG_RIGHT     8
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_SCIENCE) then begin//-- SCIENCE
		script_overrides;
		display_msg(message_str(SCRIPT_BRAHMIN, 101));
	end
	else begin
		if (action_being_used == SKILL_STEAL) then begin//-- STEAL
			script_overrides;
			display_msg(message_str(SCRIPT_GENCHAT, 211));
			script_overrides;
		end
	end
end

procedure destroy_p_proc begin
   inc_good_critter

   if map_is_caravan_escort then begin
   	rm_caravan_brahmin(1);
   	debug("caravan brahmin died! " + caravan_brahmin_left + " brahmin left!");
   end
end

