/*

	Encounter - Generic BoS Paladin

*/

#include "..\headers\define.h"

#define NAME                    SCRIPT_GENPALA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

variable tmp_hostile;
variable initial :=  0;
variable round_counter;
variable scared;

import variable random_seed_1;

procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_BROTHERHOOD );
		set_self_ai( AI_BROTHERHOOD_PALADIN );
		tmp_hostile := global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure );
		initial :=  1;
	end
end

procedure combat_p_proc
begin
	if (fixed_param == 4) then begin
		round_counter := round_counter + 1;
		if (get_critter_stat(self_obj, STAT_current_hp) < 10) then begin
			scared := 1;
		end
	end
end

procedure critter_p_proc begin
	if (scared) then begin
		if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
			call flee_dude;
		end
	end
	else begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
	end
end

procedure destroy_p_proc begin
	inc_good_critter
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
		random_seed_1 := 1;
	end
end

procedure pickup_p_proc begin
	if (not(scared)) then begin
		tmp_hostile := 1;
		set_global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure, 1 );
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (has_rep_berserker) then begin
		set_global_var( GVAR_BERSERKER_REPUTATION, 1 );
		set_global_var( GVAR_CHAMPION_REPUTATION, 0 );
	end
	if (scared) then begin
		float_msg(self_obj, message_str(SCRIPT_GENPALA, 101), 2);
	end
	else begin
		if ((global_var( GVAR_ENEMY_BROTHERHOOD ) == 1) or (random_seed_1 == 1)) then begin
			float_msg(self_obj, message_str(SCRIPT_GENPALA, 100), 2);
			set_global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure, 1 );
			tmp_hostile := 1;
		end
		else begin
			if (global_var( GVAR_BROTHERHOOD_SEED ) == 2) then begin
				float_msg(self_obj, message_str(SCRIPT_GENPALA, 102), 4);
			end
			else begin
				if ((check_general_rep < -20) or (has_rep_berserker) or has_rep_childkiller) then begin
					float_msg(self_obj, message_str(SCRIPT_GENPALA, 104), 2);
				end
				else begin
					float_msg(self_obj, message_str(SCRIPT_GENPALA, 103), 3);
				end
			end
		end
	end
end


