/*

	Encounter, Patrick the singing bard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPV13CAVE.h"

#define NAME                    SCRIPT_DUC
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Patrick01;
procedure Patrick02;
procedure Patrick03;
procedure Patrick04;
procedure Patrick05;
procedure Patrick06;
procedure Patrick06a;
procedure Patrick07;
procedure Patrick08;
procedure Patrick09;
procedure Patrick10;
procedure Patrick11;
procedure Patrick12;
procedure Patrick13;
procedure Patrick14;
procedure Patrick15;
procedure Patrick16;
procedure Patrick17;
procedure PatrickCombat;
procedure Node999;

variable tmp_hostile;
variable initial :=  0;
variable known;
variable scared;

procedure start begin


	if not(initial) then begin
		initial :=  1;
		set_self_team(87);
		set_self_ai( AI_CAREFUL );
	end
end

procedure critter_p_proc begin
	if (scared) then begin
		if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
			flee_dude
		end
	end
	else begin
		if (tmp_hostile) then begin
			tmp_hostile := 0;
			scared := 1;
			attack(dude_obj);
		end
	end
end

procedure destroy_p_proc begin
	set_global_var(IS_PATRICK_DEAD, 1);
	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;
	known := global_var(IS_PATRICK_KNOWN);
	if (known) then begin
		display_msg(message_str(SCRIPT_PATRICK, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_PATRICK, 100));
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	dude_look_at_critter;
	known := global_var(IS_PATRICK_KNOWN);
	if (scared) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else begin
		start_gdialog(667, self_obj, 4, -1, -1);
		gsay_start;
		if (known) then begin
			call Patrick17;
		end
		else begin
			call Patrick01;
		end
		gsay_end;
		end_dialogue;
	end
end

procedure Patrick01 begin
	known := 1;
	set_global_var(IS_PATRICK_KNOWN, known);
	gsay_reply(667, 102);
	NOption( 103, Patrick02, 4 );
	NOption( 104, Patrick03, 4 );
	if ((global_var(QUEST_VAULT13_4_WATERCHIP) == 0) and (obj_is_carrying_obj_pid(dude_obj, PID_WATER_CHIP) == 0)) then begin
		NOption( 105, Patrick04, 4 );
	end
	if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
		BOption( 106, PatrickCombat, 4 );
	end
	NOption( 107, Node999, -3 );
end

procedure Patrick02 begin
	gsay_reply(667, 108);
	NOption( 109, Patrick05, 4 );
	NOption( 110, Patrick06, 4 );
	NOption( 111, Patrick05, 4 );
	NOption( 112, Node999, 4 );
end

procedure Patrick03
begin
	gsay_reply(667, 113);
	NOption( 114, Node999, 4 );
	NOption( 115, Patrick05, 4 );
end

procedure Patrick04
begin
	gsay_reply(667, 116);
	if (dude_iq > 6) then begin
		NOption( 117, Patrick07, 7 );
	end
	else begin
		NOption( 118, Patrick07, 4 );
	end
end

procedure Patrick05
begin
	gsay_reply(667, 119);
	NOption( 120, Patrick08, 4 );
	NOption( 121, Patrick09, 4 );
	NOption( 110, Patrick06, 4 );
	NOption( 122, Node999, 4 );
end

procedure Patrick06 begin
	gsay_reply(667, 123);
	if (global_var(WORLDMAPLIST_SHADY_SANDS) < 1) then
		set_global_var(WORLDMAPLIST_SHADY_SANDS, 1);
	mark_on_map(AREA_SHADY_SANDS)

	if (global_var(WORLDMAPLIST_JUNKTOWN) < 1) then
		set_global_var(WORLDMAPLIST_JUNKTOWN, 1);
	mark_on_map(AREA_JUNKTOWN)

	if (global_var(WORLDMAPLIST_THE_HUB) < 1) then
		set_global_var(WORLDMAPLIST_THE_HUB, 1);
	mark_on_map(AREA_HUB)
	if (global_var(WORLDMAPLIST_NECROPOLIS) < 1) then
		set_global_var(WORLDMAPLIST_NECROPOLIS, 1);
	mark_on_map(AREA_NECROPOLIS)

	call Patrick06a;
end

procedure Patrick06a
begin
	NOption( 124, Patrick10, 4 );
	NOption( 125, Patrick11, 4 );
	NOption( 126, Patrick12, 4 );
	NOption( 127, Patrick13, 4 );
	NOption( 100, Node999, 4 );
end

procedure Patrick07
begin
	gsay_reply(667, 128);
	NOption( 100, Node999, 4 );
	NOption( 115, Patrick05, 4 );
end

procedure Patrick08
begin
	gsay_reply(667, 129);
	if ((global_var(QUEST_VAULT13_4_WATERCHIP) == 0) and (obj_is_carrying_obj_pid(dude_obj, PID_WATER_CHIP) == 0)) then begin
		NOption( 130, Patrick07, 4 );
	end
	NOption( 110, Patrick05, 4 );
	NOption( 112, Node999, 4 );
end

procedure Patrick09
begin
	gsay_reply(667, 131);
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		GOption( 132, Patrick15, 7 );
	end
	NOption( 133, Patrick05, 4 );
	NOption( 122, Node999, 4 );
end

procedure Patrick10
begin
	if global_var(GVAR_SHADY_SANDS_WAS_INVADED) then begin//GLOBAL_VAR_12 = Shady Sands invaded
		gsay_reply(667, 141);
	end
	else begin
		if (global_var(TANDI_HIRELING_STATUS) == 27) then begin
			gsay_reply(667, 135);
		end
		else begin
			gsay_reply(667, 134);
		end
	end
	call Patrick06a;
end

procedure Patrick11
begin
	if (global_var(GVAR_JUNKTOWN_WAS_INVADED) == 1) then begin
		gsay_reply(667, 141);
	end
	else begin
		if (global_var(KILLIAN_DEAD)) then begin
			if (global_var(GIZMO_DEAD)) then begin
				gsay_reply(667, 138);
			end
			else begin
				gsay_reply(667, 137);
			end
		end
		else begin
			gsay_reply(667, 136);
		end
	end
	call Patrick06a;
end

procedure Patrick12
begin
	if (global_var(GVAR_THE_HUB_WAS_INVADED) == 1) then begin
		gsay_reply(667, 141);
	end
	else begin
		gsay_reply(667, 139);
	end
	call Patrick06a;
end

procedure Patrick13
begin
	if (global_var(GVAR_NECROPOLIS_WAS_INVADED) == 1) then begin
		gsay_reply(667, 141);
	end
	else begin
		gsay_reply(667, 140);
	end
	if (dude_iq > 5) then begin
		NOption( 142, Patrick14, 6 );
	end
	else begin
		call Patrick06a;
	end
end

procedure Patrick14
begin
	gsay_reply(667, 143);
	call Patrick06a;
end

procedure Patrick15
begin
	gsay_reply(667, 144);
	GOption( 145, Patrick16, 4 );
	NOption( 146, Node999, 4 );
end

procedure Patrick16
begin
	gfade_out(1);
	set_critter_stat( dude_obj, STAT_ch, 1 );
	display_msg(message_str(SCRIPT_GENCHAT, 164));
	gfade_in(1);
	gsay_message(667, 147, 49);
end

procedure Patrick17
begin
	gsay_reply(667, 148);
	NOption( 149, Patrick06, 4 );
	NOption( 150, Node999, 4 );
	NOption( 107, Node999, -3 );
end

procedure PatrickCombat
begin
	tmp_hostile := 1;
end

procedure Node999 begin
end




