#include "..\headers\define.h"

procedure start;
procedure critter_p_proc;//    script_action == 12
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
import variable removal_ptr;
procedure Hunter01;
procedure Hunter02;
procedure Hunter03;
procedure Hunter04;
procedure Hunter05;
procedure Hunter06;
variable hostile;
variable initial :=  0;
variable my_script_mode;
variable pre_fight;
//local_var(4) == madDumb
//local_var(5) == mad

procedure start
begin
	
	
	if not(initial) then begin
		initial :=  1;
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, 29);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET, 1);
		if (cur_map_index != MAP_LAFOLLWR) then begin
			call Hunter05;
			hostile := 1;
			attack(dude_obj);
			my_script_mode := 1;
		end
		else begin
			if (global_var(HUNTER_STATUS) == 3) then begin
				move_to(self_obj, 7000, 0);
				removal_ptr := self_obj;
			end
		end
	end
	if (not(my_script_mode)) then begin
		if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
			call critter_p_proc;
		end
		else begin
			if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
				call destroy_p_proc;
			end
			else begin
				if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
					call look_at_p_proc;
				end
				else begin
					if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
						call pickup_p_proc;
					end
					else begin
						if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
							call talk_p_proc;
						end
					end
				end
			end
		end
	end
	else begin
		if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
			call destroy_p_proc;
		end
	end
end

procedure critter_p_proc
begin
	if (hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (obj_can_see_obj(self_obj, dude_obj) and not(my_script_mode) and not(pre_fight) and (global_var(CHILDKILLER_REPUTATION) >= global_var(CHILDKILLER_NUM_KILLS))) then begin
			call Hunter05;
			pre_fight := 1;
		end
	end
end

procedure destroy_p_proc
begin
	variable removal_ptr := 0;
	if (global_var(HUNTER_RESPAWNS) == 0) or (global_var(FIXT_BUGFIXES_ONLY) == 1) then begin
		set_global_var(HUNTER_STATUS, 3);
	end
	if obj_carrying_pid_obj(self_obj, PID_COMBAT_ARMOR) then begin// combat armor
		if random(0, 10) then begin
			removal_ptr := obj_carrying_pid_obj(self_obj, PID_COMBAT_ARMOR);
			rm_obj_from_inven(self_obj, removal_ptr);
			destroy_object(removal_ptr);
		end
	end
	if obj_carrying_pid_obj(self_obj, PID_ASSAULT_RIFLE) then begin// assault rifle
		if random(0, 10) then begin
			removal_ptr := obj_carrying_pid_obj(self_obj, PID_ASSAULT_RIFLE);
			rm_obj_from_inven(self_obj, removal_ptr);
			destroy_object(removal_ptr);
		end
	end

	inc_good_critter		
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_HUNTER, 100));
end

procedure pickup_p_proc
begin
	hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (local_var(4)) then begin
		script_overrides;
		float_msg(self_obj, message_str(SCRIPT_HUNTER, 126), 0);
		hostile := 1;
	end
	else begin
		start_gdialog(241, self_obj, 4, -1, -1);
		gsay_start;
		call Hunter01;
		gsay_end;
		end_dialogue;
	end
end

procedure Hunter01
begin
	gsay_reply(241, 101);
	giq_option(4, 241, 102, Hunter02, 50);
	giq_option(4, 241, 103, Hunter03, 50);
	giq_option(4, 241, 104, Hunter04, 50);
	giq_option(4, 241, 105, Hunter04, 50);
	giq_option(-3, 241, 124, Hunter06, 50);
end

procedure Hunter02
begin
	if (not(local_var(5))) then begin
		gsay_message(241, 106, 50);
		set_local_var(5, 1);
	end
	else begin
		gsay_message(241, 107, 50);
		hostile := 1;
	end
end

procedure Hunter03
begin
	gsay_message(241, 108, 50);
end

procedure Hunter04
begin
	gsay_message(241, 109, 50);
end

procedure Hunter05
begin
	float_msg(self_obj, message_str(SCRIPT_HUNTER, 110) + (global_var(CHILDKILLER_REPUTATION) * 500) + message_str(SCRIPT_HUNTER, 200), random(7, 9));
	hostile := 1;
end

procedure Hunter06
begin
	gsay_message(241, 125, 50);
	set_local_var(4, 1);
end
