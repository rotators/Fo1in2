/*

	Boneyard, Adytum - Jon Zimmermann

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\MAPLAADYTUM.h"

#define NAME                    SCRIPT_JON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure JonZimmerman00;
procedure JonZimmerman01;
procedure JonZimmerman02;
procedure JonZimmerman03;
procedure JonZimmerman03a;
procedure JonZimmerman04;
procedure JonZimmerman05;
procedure JonZimmerman06;
procedure JonZimmerman07;
procedure JonZimmerman08;
procedure JonZimmerman08a;
procedure JonZimmerman09;
procedure JonZimmerman10;
procedure JonZimmerman11;
procedure JonZimmerman12;
procedure JonZimmerman13;
procedure JonZimmerman14;
procedure JonZimmerman15;
procedure JonZimmerman16;
procedure JonZimmerman17;
procedure JonZimmerman18;
procedure JonZimmerman19;
procedure JonZimmerman20;
procedure JonZimmerman21;
procedure JonZimmerman22;
procedure JonZimmerman23;
procedure JonZimmerman24;
procedure JonZimmerman25;
procedure JonZimmerman26;
procedure JonZimmerman27;
procedure JonZimmermanEnd;

procedure Cheat001;

import variable JonPtr;
variable Only_Once := 1;
variable DisplayMessage;

procedure Start begin
	if Only_Once then begin
		Only_Once := 0;
		if (obj_is_carrying_obj_pid(self_obj, PID_BOTTLE_CAPS) == 0) then begin
			item_caps_adjust(self_obj, random(50, 150));
		end

		if (global_var(BLADES_STATUS) == 9103) then begin
			if (global_var(ZIMMERMAN_STATUS) != 9003) then begin
				set_self_team(TEAM_LA_REGULATORS);
			end
			else begin
				set_self_team(TEAM_PLAYER);
			end
		end
		else begin
			set_self_team(TEAM_LA_ADYTOWNER);
		end

		set_self_ai(AI_PEASANT);
		JonPtr := self_obj;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(mstr(176));
	end
	else begin
		display_msg(mstr(175));
	end
	script_overrides;
end

procedure description_p_proc begin
end

procedure talk_p_proc begin
	dude_look_at_critter;

	if (global_var(ENEMY_ADYTUM) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else begin
		if ((local_var(4) == 1) and (get_critter_stat(dude_obj, STAT_iq) < 4)) then begin
			float_msg(self_obj, message_str(SCRIPT_JON, 139), 0);
		end
		else begin
			if (global_var(ZIMMERMAN_STATUS) == 2) then begin
				float_msg(self_obj, message_str(SCRIPT_JON, random(183, 187)), 0);
			end
			else begin
				get_reaction
				start_gdialog(288, self_obj, -1, -1, -1);
				gsay_start;
				DisplayMessage := 100;
				if (local_var(4) == 0) then begin
					call JonZimmerman00;
				end
				else begin
					if (global_var(ZIMMERMAN_STATUS) == 0) then begin
						call JonZimmerman01;
					end
					else begin
						if global_var(RECEIVED_HOLODISK_RAZOR) then begin
							if (global_var(ZIMMERMAN_STATUS) == 9001) then begin
								call JonZimmerman24;
							end
							else begin
								call JonZimmerman27;
							end
						end
						else begin
							if (global_var(ZIMMERMAN_STATUS) == 1) then begin
								call JonZimmerman17;
							end
							else if (global_var(ZIMMERMAN_STATUS) == 9001) then begin
								call JonZimmerman19;
							end
							else if (global_var(ZIMMERMAN_STATUS) == 9002) then begin
								call JonZimmerman21;
							end
						end
					end
				end
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure damage_p_proc begin
	if source_obj == dude_obj then begin
		if (global_var(DUDE_ENEMY_REGULATORS) != 1) then begin
			set_global_var(ENEMY_ADYTUM, 1);
			set_self_team(TEAM_LA_ADYTOWNER);
		end
	end
end

procedure destroy_p_proc begin
	if source_obj == dude_obj then begin
		if (global_var(DUDE_ENEMY_REGULATORS) != 1) then begin
			set_global_var(ENEMY_ADYTUM, 1);
			set_self_team(TEAM_LA_ADYTOWNER);
		end
		inc_good_critter
	end
end

procedure critter_p_proc begin
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if ((global_var(BLADES_STATUS) == 9103) and (global_var(ZIMMERMAN_STATUS) != 9003)) then begin
			set_self_team(TEAM_LA_REGULATORS);
			float_msg(self_obj, message_str(SCRIPT_JON, 174), 2);
			attack(dude_obj);
		end
		else begin
			if (global_var(ENEMY_ADYTUM) == 1) then begin
				attack(dude_obj);
			end
		end
	end
	if (global_var(DUDE_ENEMY_REGULATORS) == 1) then begin
		set_self_team(TEAM_LA_REGULATORS);
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	set_global_var(ENEMY_ADYTUM, 1);
end

procedure JonZimmerman00 begin
	DisplayMessage := 182;
	gsay_reply(288, 100);
	giq_option(4, 288, 181, JonZimmerman01, 50);
	giq_option(-3, 288, 104, JonZimmerman13, 50);
	set_local_var(4, 1);
end

procedure JonZimmerman01 begin
	gsay_reply(288, DisplayMessage);
	giq_option(4, 288, 101, JonZimmerman02, 50);
	giq_option(4, 288, 102, JonZimmerman14, 50);
	if global_var(RECEIVED_HOLODISK_RAZOR) then begin
		giq_option(4, 288, 178, JonZimmerman26, 50);
	end
	giq_option(4, 288, 103, JonZimmermanEnd, 50);
	giq_option(-3, 288, 104, JonZimmerman13, 50);

	if debug_mode then
		NOption("***CHEAT *** [Speech: 100%] Open your eyes! The Regulators are evil!",Cheat001,001);
end

// Attack Adytum with the Blades:
procedure Cheat001 begin
	set_global_var(BLADES_STATUS,9103);
	call JonZimmerman26;

	debug("mvar: " + map_var(2));
	debug("invasion: " + global_var(GVAR_BLADES_INVASION_READY));
end

procedure JonZimmerman02 begin
	gsay_reply(288, 105);
	gsay_option(288, 106, JonZimmerman03, 50);
	gsay_option(288, 107, JonZimmerman03, 50);
	gsay_option(288, 108, JonZimmerman04, 50);
end

procedure JonZimmerman03 begin
	gsay_reply(288, 109);
	gsay_option(288, 110, JonZimmerman04, 50);
	gsay_option(288, 111, JonZimmerman13, 50);
	gsay_option(288, 112, JonZimmerman03a, 50);
end

procedure JonZimmerman03a begin
	set_global_var(ZIMMERMAN_STATUS, 1);
end

procedure JonZimmerman04 begin
	gsay_reply(288, 113);
	gsay_option(288, 114, JonZimmerman07, 50);
	gsay_option(288, 115, JonZimmerman05, 50);
	gsay_option(288, 116, JonZimmerman05, 50);
end

procedure JonZimmerman05
begin
	gsay_reply(288, 117);
	gsay_option(288, 118, JonZimmerman06, 50);
	gsay_option(288, 119, JonZimmerman07, 50);
end

procedure JonZimmerman06
begin
	gsay_reply(288, 120);
	gsay_option(288, 121, JonZimmerman07, 50);
	gsay_option(288, 122, JonZimmerman11, 50);
end

procedure JonZimmerman07
begin
	gsay_reply(288, 123);
	gsay_option(288, 124, JonZimmerman09, 50);
	gsay_option(288, 125, JonZimmerman08, 50);
	gsay_option(288, 126, JonZimmerman10, 50);
end

procedure JonZimmerman08
begin
	gsay_reply(288, 127);
	gsay_option(288, 128, JonZimmerman09, 50);
	gsay_option(288, 129, JonZimmerman10, 50);
	gsay_option(288, 130, JonZimmerman08a, 50);
end

procedure JonZimmerman08a
begin
	BottomReact
	set_global_var(ZIMMERMAN_STATUS, 1);
end

procedure JonZimmerman09
begin
	gsay_reply(288, 131);
	gsay_option(288, 132, JonZimmerman10, 50);
	gsay_option(288, 133, JonZimmerman08a, 50);
	set_global_var(ZIMMERMAN_STATUS, 1);
end

procedure JonZimmerman10
begin
	gsay_message(288, 134, 50);
	set_global_var(ZIMMERMAN_STATUS, 9001);
end

procedure JonZimmerman11
begin
	gsay_reply(288, 135);
	gsay_option(288, 136, JonZimmerman16, 50);
	gsay_option(288, 137, JonZimmerman08a, 50);
end

procedure JonZimmerman12 begin
	gsay_message(288, 138, 50);
	BottomReact

	set_global_var(DUDE_ENEMY_REGULATORS, 1);//  ENEMY_REGULATOR == 1
	gfade_out(1);
	move_to(dude_obj, 10500, 0);
	gfade_in(1);
end

procedure JonZimmerman13 begin
	gsay_message(288, 139, 50);
end

procedure JonZimmerman14 begin
	gsay_reply(288, 140);
	gsay_option(288, 141, JonZimmerman15, 50);
	gsay_option(288, 142, JonZimmerman12, 50);
end

procedure JonZimmerman15 begin
	gsay_reply(288, 143);
	gsay_option(288, 144, JonZimmerman03, 50);
	gsay_option(288, 145, JonZimmerman03, 50);
	gsay_option(288, 146, JonZimmerman04, 50);
	gsay_option(288, 147, JonZimmerman12, 50);
end

procedure JonZimmerman16 begin
	gsay_reply(288, 148);
	gsay_option(288, 149, JonZimmerman07, 50);
	gsay_option(288, 150, JonZimmerman07, 50);
	gsay_option(288, 151, JonZimmerman08a, 50);
end

procedure JonZimmerman17 begin
	gsay_reply(288, 152);
	gsay_option(288, 153, JonZimmerman18, 50);
	gsay_option(288, 154, JonZimmerman14, 50);
	gsay_option(288, 155, JonZimmermanEnd, 50);
end

procedure JonZimmerman18 begin
	gsay_message(288, 156, 50);
	set_global_var(ZIMMERMAN_STATUS, 9001);
end

procedure JonZimmerman19 begin
	gsay_reply(288, 157);
	gsay_option(288, 158, JonZimmerman12, 50);
	gsay_option(288, 159, JonZimmerman20, 50);
	gsay_option(288, 160, JonZimmerman13, 50);
end

procedure JonZimmerman20 begin
	gsay_message(288, 161, 50);
end

procedure JonZimmerman21 begin
	gsay_reply(288, 162);
	gsay_option(288, 163, JonZimmerman22, 50);
	gsay_option(288, 164, JonZimmerman23, 50);
	gsay_option(288, 165, JonZimmerman22, 50);
	set_global_var(ZIMMERMAN_STATUS, 2);
end

procedure JonZimmerman22 begin
	gsay_message(288, 166, 50);
	TopReact
	item_caps_adjust(dude_obj, 2000);
end

procedure JonZimmerman23 begin
	gsay_message(288, 167, 50);
	TopReact
	item_caps_adjust(dude_obj, 2500);
end

procedure JonZimmerman24 begin
	gsay_reply(288, 168);
	gsay_option(288, 169, JonZimmerman25, 50);
	gsay_option(288, 170, JonZimmerman26, 50);
end

procedure JonZimmerman25 begin
	gsay_reply(288, 171);
	gsay_option(288, 172, JonZimmerman26, 50);
end

procedure JonZimmerman26 begin
	set_global_var(REMOVE_JON_DISK_SON, 1);

	gsay_message(288, 173, 50);
	set_global_var(ZIMMERMAN_STATUS, 9003);

	// Will the blades help in the attack on adytown?
	if (global_var(BLADES_STATUS) == 9102) then begin
		set_self_team(TEAM_PLAYER);
		set_global_var(BLADES_STATUS, 9103);
	end
	else begin
		set_self_team(TEAM_PLAYER);
	end
end

procedure JonZimmerman27 begin
	gsay_reply(288, 177);
	gsay_option(288, 178, JonZimmerman26, 50);
	gsay_option(288, 179, JonZimmermanEnd, 50);
end

procedure JonZimmermanEnd begin
end


