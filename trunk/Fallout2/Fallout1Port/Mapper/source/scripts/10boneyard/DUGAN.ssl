#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure look_at_p_proc;//    script_action == 21
procedure description_p_proc;//    script_action == 3
procedure talk_p_proc;//    script_action == 11
procedure critter_p_proc;//    script_action == 12
procedure damage_p_proc;//    script_action == 14
procedure destroy_p_proc;//    script_action == 18
procedure pickup_p_proc;//    script_action == 4
procedure Dugan01;
procedure Dugan02;
procedure Dugan03;
procedure Dugan04;
procedure Dugan05;
procedure Dugan06;
procedure Dugan07;
procedure Dugan08;
procedure Dugan09;
procedure DuganEnd;

variable Only_Once := 1;
variable PsstTime;

procedure Start
begin
	

	if Only_Once then begin
		set_self_team(TEAM_LA_BLADES );
		set_self_ai( AI_BLADES );
		Only_Once := 0;
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(SCRIPT_DUGAN, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_DUGAN, 100));
	end
end

procedure description_p_proc
begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(SCRIPT_DUGAN, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_DUGAN, 100));
	end
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (global_var(ENEMY_BLADES) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else begin
		if ((local_var(4) == 1) and (dude_iq < 4)) then begin
			float_msg(self_obj, message_str(SCRIPT_DUGAN, 120), 0);
		end
		else begin
			get_reaction
			start_gdialog(917, self_obj, -1, -1, -1);
			gsay_start;
			call Dugan01;
			gsay_end;
			end_dialogue;
		end
	end
end

procedure critter_p_proc
begin
	if ((((game_time / 10) - PsstTime) >= 30) and (tile_distance_objs(self_obj, dude_obj) <= 10)) then begin
		float_msg(self_obj, message_str(SCRIPT_DUGAN, random(121, 125)), 0);
		PsstTime := game_time / 10;
	end
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if (global_var(ENEMY_BLADES) == 1) then begin
			attack(dude_obj);
		end
	end
end

procedure damage_p_proc
begin
	if (source_obj == dude_obj) then begin
		set_global_var(ENEMY_BLADES, 1);
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		if (has_rep_berserker) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var( GVAR_CHAMPION_REPUTATION, 0 );
		end
		if (has_rep_champion) then begin
			set_global_var( GVAR_CHAMPION_REPUTATION, 1 );
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		set_global_var( GVAR_GOOD_MONSTER, global_var( GVAR_GOOD_MONSTER ) + 1 );// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var( GVAR_GOOD_MONSTER ) % 2) == 0) then begin
			set_global_var( GVAR_PLAYER_REPUTATION, (global_var( GVAR_PLAYER_REPUTATION ) - 1) );
		end
		set_global_var(ENEMY_BLADES, 1);
	end
end

procedure pickup_p_proc
begin
	set_global_var(ENEMY_BLADES, 1);
end

procedure Dugan01
begin
	gsay_reply(917, 102);
	giq_option( 4, SCRIPT_DUGAN, 103, Dugan02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_DUGAN, 104, Dugan03, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_DUGAN, 105, Dugan05, NEUTRAL_REACTION );
	if (global_var( GVAR_BLADES_STATUS ) < 1) then begin
		giq_option( 4, SCRIPT_DUGAN, 106, Dugan08, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_DUGAN, 107, DuganEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_DUGAN, 108, Dugan09, NEUTRAL_REACTION );
end

procedure Dugan02
begin
	gsay_message(917, 109, 50);
end

procedure Dugan03
begin
	gsay_reply(917, 110);
	call Dugan04;
end

procedure Dugan04
begin
	gsay_option(917, 111, Dugan05, 50);
	gsay_option(917, 112, Dugan06, 50);
	gsay_option(917, 113, Dugan07, 50);
	gsay_option(917, 114, Dugan01, 50);
	gsay_option(917, 115, DuganEnd, 50);
end

procedure Dugan05
begin
	gsay_reply(917, 116);
	call Dugan04;
end

procedure Dugan06
begin
	gsay_reply(917, 117);
	call Dugan04;
end

procedure Dugan07
begin
	gsay_reply(917, 118);
	call Dugan04;
end

procedure Dugan08
begin
	gsay_message(917, 119, 50);
	call Dugan01;
end

procedure Dugan09
begin
	gsay_message(917, 120, 50);
end

procedure DuganEnd
begin
end







