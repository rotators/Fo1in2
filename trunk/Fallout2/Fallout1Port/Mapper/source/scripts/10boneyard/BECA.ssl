/*

	Boneyard - Beca

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_BECA
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure show_true_name;
procedure show_false_name;
procedure Beca00;
procedure Beca01;
procedure Beca02;
procedure BecaCombat;
procedure BecaEnd;

variable tmp_hostile;
variable initial :=  0;

procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_LA_BLADES );
		set_self_ai( AI_BLADES );
		initial :=  1;
	end
end

procedure critter_p_proc
begin
	if (self_can_see_dude and (global_var( GVAR_BLADES_LEFT ) < 41)) then begin
		tmp_hostile := 1;
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure destroy_p_proc begin
	set_global_var( GVAR_BLADES_LEFT, global_var( GVAR_BLADES_LEFT ) - 1 );
	inc_good_critter
end

procedure look_at_p_proc
begin
	if ((global_var( GVAR_QUEST_BONEYARD_1_ME_BLADE ) == 2) or (dude_luck > 6)) then begin
		call show_true_name;
	end
	else begin
		if (dude_luck < 4) then begin
			call show_false_name;
		end
		else begin
			if (dude_iq < 5) then begin
				call show_false_name;
			end
			else begin
				if (random(0, 1)) then begin
					call show_false_name;
				end
				else begin
					call show_true_name;
				end
			end
		end
	end
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (dude_iq < 4) then begin
		display_msg(message_str(SCRIPT_GENCHAT, 175));
	end
	else begin
		set_local_var(0, 1);
		if (global_var( GVAR_QUEST_BONEYARD_1_ME_BLADE ) == 2) then begin
			call Beca02;
		end
		else begin
			start_gdialog(281, self_obj, 4, -1, -1);
			gsay_start;
			call Beca00;
			gsay_end;
			end_dialogue;
		end
	end
end

procedure show_true_name
begin
	script_overrides;
	if (local_var(0)) then begin
		if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
			display_msg(message_str(SCRIPT_BECA, 100));
		end
		else begin
			display_msg(message_str(SCRIPT_BECA, 101));
		end
	end
	else begin
		display_msg(message_str(SCRIPT_BECA, 104));
	end
end

procedure show_false_name
begin
	script_overrides;
	display_msg(message_str(SCRIPT_BECA, random(105, 111)));
end

procedure Beca00
begin
	gsay_reply(281, 112);
	giq_option( 4, SCRIPT_BECA, 113, BecaCombat, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_BECA, 114, BecaEnd, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_BECA, 115, Beca01, NEUTRAL_REACTION );
end

procedure Beca01
begin
	gsay_message(281, 116, 50);
end

procedure Beca02
begin
	float_msg(self_obj, message_str(SCRIPT_BECA, 117), 2);
end

procedure BecaCombat
begin
	tmp_hostile := 1;
end

procedure BecaEnd
begin
end
