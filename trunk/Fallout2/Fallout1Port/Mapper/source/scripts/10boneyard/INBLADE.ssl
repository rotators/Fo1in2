#include "..\headers\define.h"

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

variable Only_Once := 1;
variable PsstTime;
variable exit_line

variable whichblade;

import variable InBladePtr1;
import variable InBladePtr2;
import variable InBladePtr3;
import variable InBladePtr4;
import variable InBladePtr5;
import variable InBladePtr6;
import variable InBladePtr7;
import variable InBladePtr8;
import variable RegGuard1;
import variable RegGuard2;


//Sduibek NOTES/FYI or future use:
//
//  Dialog pointer used to be 926
//


procedure start begin
// debug:
//	set_global_var(BLADES_STATUS,9102);
end

procedure map_enter_p_proc begin
	if Only_Once then begin
		variable whichblade := 0;
		if (obj_is_carrying_obj_pid(self_obj, PID_BOTTLE_CAPS) == 0) then begin
			item_caps_adjust(self_obj, random(10, 100));
		end

		if ((global_var(BLADES_STATUS) == 9103) or (global_var(BLADES_STATUS) == 9102)) and (whichblade < 8) then begin
			critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_PLAYER);
			whichblade := global_var(WATER_CHIP_10);
			if (whichblade == 0) then begin
				InBladePtr1 := self_obj;
			end
			else if (whichblade == 1) then begin
				InBladePtr2 := self_obj;
			end
			else if (whichblade == 2) then begin
				InBladePtr3 := self_obj;
			end
			else if (whichblade == 3) then begin
				InBladePtr4 := self_obj;
			end
			else if (whichblade == 4) then begin
				InBladePtr5 := self_obj;
			end
			else if (whichblade == 5) then begin
				InBladePtr6 := self_obj;
			end
			else if (whichblade == 6) then begin
				InBladePtr7 := self_obj;
			end
			else if (whichblade == 7) then begin
				InBladePtr8 := self_obj;
				set_global_var(GVAR_BLADES_INVASION_READY, 1);
			end
			whichblade := whichblade + 1;
			set_global_var(WATER_CHIP_10, whichblade);
		end
		else begin
			critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_BLADES);
		end
		Only_Once := 0;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_BLADE, 100));
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_BLADE, 100));
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var(ENEMY_BLADES) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else if (global_var(BLADES_STATUS) == 2) then begin
		float_msg(self_obj, message_str(SCRIPT_BLADE, random(101, 115)), 0);
	end
end

procedure critter_p_proc begin
	if ((((game_time / 10) - PsstTime) >= 30) and (tile_distance_objs(self_obj, dude_obj) <= 4) and (global_var(ENEMY_BLADES) == 0)) then begin
		if (global_var(BLADES_STATUS) == 2) then begin
			float_msg(self_obj, message_str(SCRIPT_BLADE, random(101, 115)), 0);
		end
		PsstTime := game_time / 10;
	end
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if (global_var(ENEMY_BLADES) == 1) then begin
			attack(dude_obj);
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var(ENEMY_BLADES, 1);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_BLADES);
	end
end

procedure destroy_p_proc begin
	if source_obj == dude_obj then begin
		inc_good_critter
		set_global_var(ENEMY_BLADES, 1);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_BLADES);
	end
end

procedure pickup_p_proc begin
	set_global_var(ENEMY_BLADES, 1);
end


