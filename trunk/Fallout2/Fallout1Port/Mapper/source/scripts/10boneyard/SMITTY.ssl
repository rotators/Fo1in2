/*

	Boneyard - Smitty the blacksmith

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_SMITTY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure Smitty01;
procedure Smitty02;
procedure Smitty03;
procedure Smitty04;
procedure Smitty05;
procedure Smitty06;
procedure Smitty07;
procedure Smitty08;
procedure Smitty09;
procedure Smitty10;
procedure Smitty11;
procedure Smitty12;
procedure Smitty14;
procedure Smitty14a;
procedure Smitty15;
procedure SmittyEnd;

variable Only_Once := 1;
variable DisplayMessage := 100;

#define LVAR_UpgradePlasmaRifle 		(5)

procedure Start begin
	if Only_Once then begin
		if (not(metarule(22, 0)) and (obj_is_carrying_obj_pid(self_obj, PID_BOTTLE_CAPS) == 0)) then begin
			item_caps_adjust(self_obj, random(500, 750));
		end
		if ((global_var(BLADES_STATUS) == 9103) or (global_var(BLADES_STATUS) == 9102)) then begin
			critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_PLAYER);
		end
		else begin
			critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_ADYTOWNER);
		end
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET, AI_PEASANT);
		Only_Once := 0;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(101));
	end
end

procedure description_p_proc begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(101));
	end
end

procedure talk_p_proc begin
	dude_look_at_critter;

	if (global_var(ENEMY_ADYTUM) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else begin
		if ((local_var(4) == 1) and (get_critter_stat(dude_obj, STAT_iq) < 4)) then begin
			float_msg(self_obj, message_str(SCRIPT_SMITTY, 117), 0);
		end
		else begin
			if (local_var(LVAR_UpgradePlasmaRifle) == 2) then begin
				float_msg(self_obj, message_str(SCRIPT_SMITTY, 131), 0);
			end
			else begin
				get_reaction
				start_gdialog(250, self_obj, -1, -1, -1);
				gsay_start;
				if (global_var(QUEST_BONEYARD_5_FIX_FARM) == 9303) then begin
					call Smitty11;
				end
				else begin
					if (global_var(QUEST_BONEYARD_5_FIX_FARM) == 9304) then begin
						if (local_var(LVAR_UpgradePlasmaRifle) == 0) then begin
							call Smitty12;
						end
						else begin
							if (local_var(LVAR_UpgradePlasmaRifle) == 9307) then begin
								if (obj_is_carrying_obj_pid(dude_obj, PID_PLASMA_RIFLE)) then begin
									call Smitty14;
								end
								else begin
									call Smitty15;
								end
							end
						end
					end
					else begin
						if (local_var(1) < 2) then begin
							DisplayMessage := 103;
							call Smitty01;
						end
						else begin
							DisplayMessage := 102;
							call Smitty01;
						end
					end
				end
				gsay_end;
				end_dialogue;
			end
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var(ENEMY_ADYTUM, 1);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_ADYTOWNER);
	end
end

procedure destroy_p_proc begin
	if source_obj == dude_obj then begin
		inc_good_critter
		set_global_var(ENEMY_ADYTUM, 1);
		critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, TEAM_LA_ADYTOWNER);
	end
end

procedure pickup_p_proc begin
	set_global_var(ENEMY_ADYTUM, 1);
end

procedure Smitty01 begin
	gsay_reply(250, DisplayMessage);
	DisplayMessage := 112;

	if (local_var(4) == 0) then begin
		giq_option(4, 250, 104, Smitty03, 50);
	end

	if ((global_var(QUEST_BONEYARD_5_FIX_FARM) < 9303) and (global_var(QUEST_BONEYARD_5_FIX_FARM) > 2)) then begin
		giq_option(4, 250, 105, Smitty10, 50);
	end

	giq_option(4, 250, 106, Smitty02, 50);
	giq_option(4, 250, 107, Smitty04, 50);
	giq_option(4, 250, 108, Smitty06, 50);
	giq_option(-3, 250, 109, Smitty05, 50);
end

procedure Smitty02 begin
	DisplayMessage := 110;
	call Smitty01;
end

procedure Smitty03 begin
	set_local_var(4, 1);
	DisplayMessage := 111;
	call Smitty01;
end

procedure Smitty04 begin
	gsay_reply(250, DisplayMessage);
	gsay_option(250, 113, Smitty07, 50);
	gsay_option(250, 114, Smitty08, 50);
	gsay_option(250, 115, Smitty09, 50);
	gsay_option(250, 116, Smitty06, 50);
end

procedure Smitty05 begin
	set_local_var(4, 1);
	gsay_message(250, 117, 50);
end

procedure Smitty06 begin
	gsay_message(250, 118, 50);
end

procedure Smitty07 begin
	DisplayMessage := 119;
	call Smitty04;
end

procedure Smitty08 begin
	DisplayMessage := 120;
	call Smitty04;
end

procedure Smitty09
begin
	DisplayMessage := 121;
	call Smitty04;
end

procedure Smitty10 begin
	gsay_message(250, 122, 50);
	gfade_out(1);
	game_time_advance(game_ticks(10 * 60 * 60));
	gfade_in(1);
	set_global_var(QUEST_BONEYARD_5_FIX_FARM, 9303);
	gsay_message(250, 123, 50);
end

procedure Smitty11 begin
	gsay_message(250, 124, 50);
end

procedure Smitty12 begin
	gsay_message(250, 125, 50);
	set_local_var(LVAR_UpgradePlasmaRifle, 9307);
	if (obj_is_carrying_obj_pid(dude_obj, PID_PLASMA_RIFLE)) then begin
		call Smitty14;
	end
end

procedure Smitty14 begin
	gsay_reply(250, 127);
	gsay_option(250, 126, Smitty14a, 50);
end

procedure Smitty14a begin
variable Item;

	gsay_message(250, 128, 50);

	remove_pid_qty(dude_obj, PID_PLASMA_RIFLE, 1)

	gfade_out(1);
	game_time_advance(game_ticks(10 * 60 * 60 * 2));
	gfade_in(1);
	gsay_message(250, 129, 50);

   Item:=create_object(PID_TURBO_PLASMA_RIFLE,0,0);
   add_mult_objs_to_inven(dude_obj,Item,1);

	set_local_var(LVAR_UpgradePlasmaRifle, 2);
end

procedure Smitty15 begin
	gsay_message(250, 130, 50);
end

procedure SmittyEnd begin
end


