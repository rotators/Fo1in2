/*

	Boneyard - Mac Rae, Blades member, at the door outside of the house

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_MACRAE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;

procedure MacRae01;
procedure MacRae02;
procedure MacRae03;
procedure MacRae04;
procedure MacRae05;
procedure MacRae06;
procedure MacRae07;
procedure MacRae08;
procedure MacRae09;
procedure MacRae10;
procedure MacRae11;
procedure MacRae12;
procedure MacRae13;
procedure MacRae14;
procedure MacRae15;
procedure MacRae16;
procedure MacRaeEnd;

variable Only_Once := 1;
variable DisplayMessage := 100;

procedure Start begin
	if Only_Once then begin
		set_self_team(TEAM_LA_BLADES );
		set_self_ai( AI_BLADES );
		Only_Once := 0;
	end
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(SCRIPT_MACRAE, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_MACRAE, 100));
	end
end

procedure description_p_proc begin
	script_overrides;
	if (local_var(4) == 1) then begin
		display_msg(message_str(SCRIPT_MACRAE, 101));
	end
	else begin
		display_msg(message_str(SCRIPT_MACRAE, 100));
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (global_var( GVAR_ENEMY_BLADES ) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 2);
	end
	else begin
		if (local_var(5) == 2) then begin
			float_msg(self_obj, message_str(SCRIPT_MACRAE, 141), 0);
		end
		else begin
			get_reaction
			start_gdialog(918, self_obj, -1, -1, -1);
			gsay_start;
			if (global_var( GVAR_BLADES_STATUS ) == 2) then begin
				call MacRae12;
			end
			else begin
				if (local_var(4) == 0) then begin
					if (dude_is_male) then begin
						DisplayMessage := 102;
					end
					else begin
						DisplayMessage := 103;
					end
					call MacRae02;
				end
				else begin
					DisplayMessage := 104;
					call MacRae02;
				end
			end
			gsay_end;
			end_dialogue;
		end
	end
end

procedure damage_p_proc
begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BLADES, 1 );
	end
end

procedure destroy_p_proc
begin

	if (source_obj == dude_obj) then begin
		if (has_rep_berserker) then begin
			set_global_var( GVAR_BERSERKER_REPUTATION, 1 );
			set_global_var( GVAR_CHAMPION_REPUTATION, 0 );
		end
		if (has_rep_champion) then begin
			set_global_var( GVAR_CHAMPION_REPUTATION, 1 );
			set_global_var( GVAR_BERSERKER_REPUTATION, 0 );
		end
		set_global_var( GVAR_GOOD_MONSTER, global_var( GVAR_GOOD_MONSTER ) + 1 );// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		if ((global_var( GVAR_GOOD_MONSTER ) % 2) == 0) then begin
			set_global_var( GVAR_PLAYER_REPUTATION, (check_general_rep - 1) );
		end
		set_global_var( GVAR_ENEMY_BLADES, 1 );
	end
end

procedure critter_p_proc
begin
	if (self_can_see_dude) then begin
		if (global_var( GVAR_ENEMY_BLADES ) == 1) then begin
			attack(dude_obj);
		end
	end
end

procedure pickup_p_proc
begin
	set_global_var( GVAR_ENEMY_BLADES, 1 );
end

procedure MacRae01
begin
	DisplayMessage := 104;
	call MacRae02;
end

procedure MacRae02
begin
	gsay_reply( SCRIPT_MACRAE, DisplayMessage );
	if (local_var(4) == 0) then begin
		giq_option( 4, SCRIPT_MACRAE, 105, MacRae03, NEUTRAL_REACTION );
	end
	giq_option( 4, SCRIPT_MACRAE, 106, MacRae11, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_MACRAE, 107, MacRaeEnd, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_MACRAE, 141, MacRaeEnd, NEUTRAL_REACTION );
end

procedure MacRae03
begin
	gsay_reply( SCRIPT_MACRAE, 108 );
	gsay_option( SCRIPT_MACRAE, 109, MacRae04, 50 );
	gsay_option( SCRIPT_MACRAE, 110, MacRae08, 50 );
	gsay_option( SCRIPT_MACRAE, 111, MacRae09, 50 );
	gsay_option( SCRIPT_MACRAE, 112, MacRae01, 50 );
end

procedure MacRae04
begin
	gsay_reply( SCRIPT_MACRAE, 113 );
	gsay_option( SCRIPT_MACRAE, 114, MacRae05, 50 );
	gsay_option( SCRIPT_MACRAE, 115, MacRae06, 50 );
	gsay_option( SCRIPT_MACRAE, 116, MacRae01, 50 );
end

procedure MacRae05
begin
	gsay_message( SCRIPT_MACRAE, 117, 50 );
	DownReactLevel
end

procedure MacRae06 begin
   if dude_is_male then
	   DisplayMessage := 118;
	else
	   DisplayMessage := 1180;
	set_local_var(5, 1);
	call MacRae07;
end

procedure MacRae07 begin
	gsay_reply( SCRIPT_MACRAE, DisplayMessage );
	gsay_option( SCRIPT_MACRAE, 119, MacRae01, 50 );
	gsay_option( SCRIPT_MACRAE, 120, MacRaeEnd, 50 );
end

procedure MacRae08
begin
	DisplayMessage := 121;
	call MacRae07;
end

procedure MacRae09
begin
	gsay_reply( SCRIPT_MACRAE, 122 );
	gsay_option( SCRIPT_MACRAE, 123, MacRae10, 50 );
	gsay_option( SCRIPT_MACRAE, 124, MacRae01, 50 );
	gsay_option( SCRIPT_MACRAE, 125, MacRaeEnd, 50 );
end

procedure MacRae10
begin
	DisplayMessage := 126;
	call MacRae07;
end

procedure MacRae11
begin
	DisplayMessage := 127;
	call MacRae07;
end

procedure MacRae12
begin
	gsay_reply( SCRIPT_MACRAE, 128 );
	if (local_var(5 == 1)) then begin
		gsay_option( SCRIPT_MACRAE, 129, MacRae15, 50 );
	end
	if (local_var(4) == 1) then begin
		gsay_option( SCRIPT_MACRAE, 130, MacRae13, 50 );
	end
	else begin
		gsay_option( SCRIPT_MACRAE, 131, MacRae16, 50 );
	end
	gsay_option( SCRIPT_MACRAE, 132, MacRaeEnd, 50 );
end

procedure MacRae13
begin
	gsay_reply( SCRIPT_MACRAE, 133 );
	gsay_option( SCRIPT_MACRAE, 134, MacRae14, 50 );
	gsay_option( SCRIPT_MACRAE, 135, MacRaeEnd, 50 );
end

procedure MacRae14
begin
	gsay_reply( SCRIPT_MACRAE, 113 );
	gsay_option( SCRIPT_MACRAE, 136, MacRae15, 50 );
	gsay_option( SCRIPT_MACRAE, 137, MacRaeEnd, 50 );
end

procedure MacRae15
begin
//
// Adds five points into Unarmed and Melee.
// originally added to Melee Damage but the game engine cannot support that.
// Five points should give double if one of those skills is tagged, but need to test this.
//	--Sduibek
//
	variable LVar0 := 5;// Skill points to add
	variable LVar1 := 5;// Skill points to add
	variable LVar2 := 24;// Hours it takes to learn
	gsay_message( SCRIPT_MACRAE, 138, 50 );
	gfade_out(1);
//	if (get_critter_stat(dude_obj, 4) >= 8) or has_trait(dude_obj, 0, #) or has_trait(dude_obj, 0, #) then begin
	game_time_advance(game_ticks(60 * 60 * LVar2));
//	if get_critter_stat(dude_obj, 4) <=
	gfade_in(1);
	critter_mod_skill(dude_obj, SKILL_UNARMED_COMBAT, LVar0);
	critter_mod_skill(dude_obj, SKILL_MELEE, LVar1);
	display_msg(message_str(SCRIPT_GENCHAT, 116) + LVar0 + message_str(SCRIPT_GENCHAT, 120));
	display_msg(message_str(SCRIPT_GENCHAT, 116) + LVar1 + message_str(SCRIPT_GENCHAT, 121));
	give_exp_points(250);
	display_msg(message_str(SCRIPT_MACRAE, 200));
	set_local_var(5, 2);
	gsay_message( SCRIPT_MACRAE, 139, 50 );
end

procedure MacRae16
begin
	gsay_reply( SCRIPT_MACRAE, 140 );
	gsay_option( SCRIPT_MACRAE, 109, MacRae15, 50 );
	gsay_option( SCRIPT_MACRAE, 137, MacRaeEnd, 50 );
end

procedure MacRaeEnd
begin
end
