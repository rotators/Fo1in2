/*

	V13 - Water thief

*/

#include "..\headers\define.h"

#define NAME                    SCRIPT_LYLE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_V13)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Lyle01;
procedure Lyle02;
procedure Lyle03;
procedure Lyle04;
procedure LyleEnd;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

variable tmp_hostile;
variable initial :=  0;
variable DeathType := 57;

#define LVAR_Sleeping 							(4)

procedure start
begin
	if (v13_invaded) then begin//		<-- Vault13 invaded
		if (CUR_MAP_VAULT13) or (CUR_MAP_V13ENT) then begin //  VAULT 13 - ANY MAPS
			if (local_var(9) != 1) then begin
				set_local_var(9, 1);//		<-- only once
				if not(is_loading_game) then begin//		<-- if not during a Game_Load,
					set_self_visible;//		     make sure critter is visibile
				end

				//pick_dead_body_type

				debug_msg("Vault 13 invasion, killing LYLE.SSL with DeathType " + DeathType + ".  ");
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin


		if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
			call critter_p_proc;
		end
		else begin
			if (script_action == 14) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
				call damage_p_proc;
			end
			else begin
				if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
					call destroy_p_proc;
				end
				else begin
					if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
						call look_at_p_proc;
					end
					else begin
						if (script_action == 15) then begin//map_enter_p_proc (or "map_init") called when entering from World Map, on green "exit" grids, SOME ladders, doesn't appear to call on elevators or manholes
							call map_enter_p_proc;
						end
						else begin
							if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
								call pickup_p_proc;
							end
							else begin
								if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
									call talk_p_proc;
								end
							end
						end
					end
				end
			end
		end
	end
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (local_var(5)) then begin
			if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
				call flee_dude;
			end
			else begin
				if (tile_distance(tile_num(dude_obj), home_tile) > 4) then begin
					if (local_var(4) == 0) then begin
						animate_move_obj_to_tile(self_obj, home_tile, 0);
					end
				end
			end
		end
	end
	sleeping
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_local_var(5, 1);
	end
end

procedure destroy_p_proc begin
	if (source_obj == dude_obj) then
		set_global_var( GVAR_VAULT_13_HOSTILE, 1 );

	inc_good_critter
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_LYLE, 100));
end

procedure map_enter_p_proc begin
	set_self_team(TEAM_VAULT13 );
	sleep_time := 1920;
	wake_time := 710;
	home_tile := 24911;
	sleep_tile := home_tile;
end

procedure pickup_p_proc
begin
	if ((GAME_TIME_IN_DAYS) >= 30) then begin
		float_msg(self_obj, message_str(SCRIPT_LYLE, 101), 0);
	end
	set_global_var( GVAR_VAULT_13_HOSTILE, 1 );
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (local_var(4) == 1) then begin
		float_msg(self_obj, message_str(SCRIPT_GENVAULT, 166), 0);
	end
	else begin
		if (global_var( GVAR_VAULT_13_HOSTILE ) or local_var(5)) then begin
			float_msg(self_obj, message_str(SCRIPT_LYLE, 114), 0);
		end
		else begin
			if (global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) == 2) then begin
				float_msg(self_obj, message_str(SCRIPT_LYLE, 102), 0);
			end
			else begin
				if (global_var( GVAR_QUEST_VAULT13_5_WTR_THIEF ) == 2) then begin
					float_msg(self_obj, message_str(SCRIPT_LYLE, 113), 0);
				end
				else begin
					start_gdialog(507, self_obj, 4, -1, -1);
					gsay_start;
					call Lyle01;
					gsay_end;
					end_dialogue;
				end
			end
		end
	end
end

procedure Lyle01
begin
	gsay_reply(507, message_str(SCRIPT_LYLE, 103) + dude_name + message_str(SCRIPT_LYLE, 104));
	NOption( 105, Lyle02, 4 );
	NOption( 107, LyleEnd, -3 );
end

procedure Lyle02
begin
	if ((GAME_TIME_IN_DAYS) < 30) then begin
		gsay_message(507, 106, 50);
	end
	else begin
		gsay_reply(507, 108);
		NOption( 109, Lyle03, 4 );
		GOption( 110, Lyle04, 4 );
	end
end

procedure Lyle03
begin
	gsay_message(507, 111, 50);
end

procedure Lyle04
begin
	set_global_var( GVAR_QUEST_VAULT13_5_WTR_THIEF, 1 );
	gsay_message(507, 112, 49);
end

procedure LyleEnd
begin
end
