#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;    variable SrcObj := 0;    variable SrcIsParty := 0;
procedure critter_p_proc;//    script_action == 12
procedure damage_p_proc;//    script_action == 14
procedure destroy_p_proc;//    script_action == 18
procedure look_at_p_proc;//    script_action == 21
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11

procedure Medic00;
procedure Medic01;
procedure Medic02;
procedure Medic03;
procedure Medic04;
procedure Medic04a;
procedure Medic05;
procedure Medic06;
procedure Medic07;
procedure Medic08;
procedure Medic08a;
procedure Medic09;
procedure Medic10;
procedure Medic11;
procedure Medic12;
procedure Medic13;
procedure Medic14;
procedure Medic15;
procedure Medic16;
procedure Medic17;
procedure Medic18;
procedure Medic19;
procedure Medic20;
procedure Medic21;
procedure Medic22;
procedure Medic23;
procedure Medic24;
procedure Medic25;
procedure Medic26;
procedure Medic27;
procedure Medic28;
procedure Medic29;

procedure flee_dude;
procedure pushstims;
procedure pushdrugs;
procedure medicend;

variable damage := 0;
variable intensity := 0;
variable removal := 0;
variable Minutes := 0;
variable Hours := 0;
variable TempMinutes := 0;
variable rads := 0;
variable player_hits := 0;
variable player_max_hits := 0;
variable PoisAmt := 0;
variable rounded := 0;
variable IfRounded := 0;
variable home_tile := 13084;// was 13881
variable initial :=  0;

procedure PickDeadBodyType;
variable DeathType := 57;


procedure start
begin
	if (global_var(GVAR_VAULT_13_WAS_INVADED) == 1) then begin//		<-- Vault13 invaded
		if (cur_map_index == MAP_VAULT13) or (cur_map_index == MAP_V13ENT) then begin //  VAULT 13 - ANY MAPS
			if (local_var(9) != 1) then begin
				set_local_var(9, 1);//		<-- only once
				if not(is_loading_game) then begin//		<-- if not during a Game_Load,
					set_obj_visibility(self_obj, 0);//		     make sure critter is visibile
				end
				call PickDeadBodyType;
				debug_msg("Vault 13 invasion, killing MEDIC.SSL with DeathType " + DeathType + ".  ");
				kill_critter(self_obj, DeathType);
			end
		end
	end
	else begin
		

		if not(initial) then begin
			variable Locker;
			variable item;
			initial :=  1;
			Locker := create_object( PID_WALL_LOCKER_CLEAN_LEFT, 0, 0 );
			move_to(Locker, 14474, elevation(self_obj));
			item := create_object( PID_FIRST_AID_KIT, 0, 0 );//First Aid Kit
			add_mult_objs_to_inven(Locker, item, 1);
			item := create_object( PID_STIMPAK, 0, 0 );//Stimpak
			add_mult_objs_to_inven(Locker, item, 4 - difficulty_level);
			item := create_object( PID_FLARE, 0, 0 );//Flare
			add_mult_objs_to_inven(Locker, item, 4 - difficulty_level);
			critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM, 1);
			initial :=  1;
		end
		if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
			call critter_p_proc;
		end
		else begin
			if (script_action == 14) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
				call damage_p_proc;
			end
			else begin
				if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
					call destroy_p_proc;
				end
				else begin
					if (script_action == 21) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
						call look_at_p_proc;
					end
					else begin
						if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
							call pickup_p_proc;
						end
						else begin
							if (script_action == 11) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
								call talk_p_proc;
							end
						end
					end
				end
			end
		end
	end
end

procedure critter_p_proc
begin
	if tile_num(self_obj) != home_tile then begin
		animate_move_obj_to_tile(self_obj, home_tile, 0);
	end
	else begin
		if local_var(10) != 2 then begin
			if tile_num(self_obj) == home_tile then begin
				anim(self_obj, 1000, 2);
				set_local_var(10, 2);
			end
		end
	end
end

procedure damage_p_proc
begin
	if source_obj > 0 then begin  SrcObj := 0;  SrcIsParty := 0;  SrcObj := obj_pid(source_obj);  if party_member_obj(SrcObj) then begin  SrcIsParty := 1;  end  end    if (source_obj == dude_obj) or (SrcIsParty == 1) then begin
		set_local_var(4, 1);
		set_local_var(5, 1);
	end
end

procedure destroy_p_proc
begin
	if (source_obj == dude_obj) or party_member_obj(obj_pid(source_obj)) then begin
		set_global_var(VAULT_13_HOSTILE, 1);
	end

	if source_obj == dude_obj then begin
		set_global_var(NUM_GOOD_MONSTERS_KILLED, global_var(NUM_GOOD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A GOOD GUY. INCREASE GoodGuysKilled COUNTER
		set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) - 3);
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_MEDIC, 136));
end

procedure pickup_p_proc
begin
	float_msg(self_obj, message_str(SCRIPT_MEDIC, 160), 2);
	set_local_var(5, 1);
end

procedure talk_p_proc
begin
	if global_var(DEBUG_MODE_MESSAGES_ON) then begin
		display_msg("Current poison: " + get_critter_stat(dude_obj, STAT_current_poison) + ". " + "Current rads: " + get_critter_stat(dude_obj, STAT_current_rad) + ". " + "Current time: " + game_time_hour + ". ");
	end
	dude_look_at_critter;
	if not(local_var(4)) then begin
		get_reaction
		start_gdialog(184, self_obj, 4, -1, -1);
		gsay_start;
		if (local_var(5) == 1) or (global_var(VAULT_13_HOSTILE) == 1) then begin
			call Medic00;// bad player
		end
		else begin
			player_hits := get_critter_stat(dude_obj, STAT_current_hp);
			player_max_hits := get_critter_stat(dude_obj, STAT_max_hp);
			damage := player_max_hits - player_hits;
			PoisAmt := get_critter_stat(dude_obj, STAT_current_poison);
			rads := get_critter_stat(dude_obj, STAT_current_rad);
			if (damage > 7) then begin
				call Medic03;// go right to healing
			end
			else begin
				call Medic04;// MAIN MENU
			end
		end
		gsay_end;
		end_dialogue;
	end
	else begin
		display_msg(message_str(SCRIPT_MEDIC, 159));
	end
	if not(local_var(6)) then begin
		set_local_var(6, 1);
	end
	if global_var(DEBUG_MODE_MESSAGES_ON) then begin
		display_msg("Current poison: " + get_critter_stat(dude_obj, STAT_current_poison) + ". " + "Current rads: " + get_critter_stat(dude_obj, STAT_current_rad) + ". " + "Current time: " + game_time_hour + ". ");
	end
end

procedure Medic00// bad player
begin
	gsay_reply(184, 100);
	giq_option(4, 184, 101, Medic01, 51);
	giq_option(-3, 184, 102, Medic02, 51);
end

procedure Medic01// bad player
begin
	set_local_var(4, 1);
	BigDownReact
	gsay_message(184, 103, 51);
end

procedure Medic02// bad player
begin
	set_local_var(4, 1);
	BigDownReact
	gsay_message(184, 104, 51);
end

procedure Medic03// hurt lots or poisoned
begin
	gsay_reply(184, 105);
	giq_option(0, 184, 164, Medic16, 50);
end

procedure Medic04// MAIN MENU
begin
	if global_var(QUEST_VAULT13_4_WATERCHIP) == 2 then begin
		gsay_reply(184, 157);// thanks for saving us
	end
	else begin
		if not(local_var(6)) then begin
			gsay_reply(184, 113);// what can i do for you?
			giq_option(4, 184, 114, Medic12, 50);
		end
		else begin// hello again!
			gsay_reply(184, message_str(SCRIPT_MEDIC, 106) + proto_data(obj_pid(dude_obj), 1) + message_str(SCRIPT_MEDIC, 107));
			giq_option(4, 184, 108, Medic06, 50);
			giq_option(4, 184, 109, Medic12, 50);
		end
	end
	if PoisAmt > 0 then begin// i've been poisoned
		giq_option(5, 184, 161, Medic07, 50);
	end
	if rads > 0 then begin// i'm irradiated
		giq_option(5, 184, 162, Medic26, 50);
	end
	if global_var(QUEST_VAULT13_4_WATERCHIP) == 2 then begin// you're welcome. bye.
		giq_option(4, 184, 163, medicend, 50);
	end
	if get_critter_stat(dude_obj, STAT_iq) <= 3 then begin// STUPID
		giq_option(-3, 184, 117, Medic12, 50);// need healing
		if not(local_var(6)) then begin
			giq_option(-3, 184, 118, Medic15, 51);// info on healing STUPID
		end
		else begin
			giq_option(-3, 184, 171, Medic15, 51);// info on healing
		end
	end
	if (local_var(7) == 0) then begin
		set_local_var(8, game_time / (10 * 60 * 60 * 24));
		giq_option(-3, 184, 110, Medic05, 50);// i want drugs (STUPID)
		giq_option(5, 184, 115, Medic08, 50);// i want drugs
	end
	else begin
		if (local_var(7) == 1) then begin
			set_local_var(8, game_time / (10 * 60 * 60 * 24));
			giq_option(-3, 184, 110, Medic28, 50);// i want drugs (STUPID)
			giq_option(5, 184, 116, Medic11, 50);// i want more stimpaks
		end
		else begin
			if (local_var(7) == 2) then begin
				if ((game_time / (10 * 60 * 60 * 24)) - local_var(8)) >= 30 then begin
					set_local_var(8, game_time / (10 * 60 * 60 * 24));
					giq_option(-3, 184, 110, Medic29, 50);// i want drugs (STUPID)
					giq_option(5, 184, 116, Medic29, 50);// i want more stimpaks
				end
				else begin
					giq_option(-3, 184, 110, Medic27, 51);// i want drugs (STUPID)
					giq_option(5, 184, 116, Medic27, 50);// i want more stimpaks
				end
			end
		end
	end
end

procedure Medic04a// don't need healing if small dmg
begin
	if (damage > 3) or (PoisAmt > 0) then begin
		call Medic14;
	end
	else begin
		call Medic13;
	end
end

procedure Medic05// STUPID CHARACTER, GIVE SIMPAKS
begin
	if local_var(7) == 0 then begin
		set_local_var(7, 1);
	end
	gsay_message(184, 111, 50);
	call pushstims;
end

procedure Medic06//{112}{}{Good, keep up the good work. And let me know if you need anything.}
begin
	gsay_message(184, 112, 50);
end

procedure Medic07// poisoned heal request
begin
	poison(dude_obj, -PoisAmt);
	gfade_out(1);
	game_time_advance(10 * 60 * PoisAmt * 3);
	gfade_in(1);
	gsay_message(184, 156, 50);
	if (player_max_hits - player_hits) <= 3 then begin
		critter_heal(dude_obj, player_max_hits);
	end
end

procedure Medic08//{119}{}{I'm not authorized to give you any. Sorry.}
begin
	gsay_reply(184, 119);
	giq_option(4, 184, 120, medicend, 51);
	giq_option(6, 184, 121, Medic08a, 50);
end

procedure Medic08a// skillcheck for drug demand
begin
	if local_var(7) == 0 then begin
		set_local_var(7, 1);
	end
	if is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -10)) then begin
		call Medic10;
	end
	else begin
		call Medic09;
	end
end

procedure Medic09//{122}{}{Sorry, I can't give them to you.}
begin
	set_local_var(7, 3);
	gsay_message(184, 122, 51);
end

procedure Medic10//{123}{}{Well, since you put it that way. But don't tell anyone that I let you have these. They are extremely addictive and dangerous to use.
begin
	gsay_reply(184, 123);
	giq_option(4, 184, 124, pushdrugs, 50);
end

procedure pushstims
begin
	variable Stimpak := 0;
	Stimpak := create_object( PID_STIMPAK, 0, 0 );
	add_mult_objs_to_inven(dude_obj, Stimpak, 4 - difficulty_level);
end

procedure Medic11
begin
	if local_var(7) == 1 then begin
		set_local_var(7, 2);
	end
	if is_success(do_check(dude_obj, STAT_ch, 0)) or is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, -20)) then begin
		call Medic29;
	end
	else begin
		gsay_message(184, 125, 50);//{125}{}{Stimpaks are being rationed. You are welcome to what we have in the medical container on the wall. Take as many as you need, but use them sparingly. It will be a while before we'll be able to get you some more.}
	end
end

procedure Medic12// "hmm let's see..."
begin
	gsay_reply(184, 126);
	giq_option(0, 184, 164, Medic04a, 50);
end

procedure Medic13// don't need healing if small dmg
begin
	gsay_message(184, 127, 50);
end

procedure Medic14//"{128}{}{You look hurt. Here, let me help you.}"
begin
	gsay_reply(184, 128);
	giq_option(0, 184, 164, Medic16, 50);
end

procedure Medic15//{129}{}{If you need healing, come back here to me or use some of the stimpaks we have. You can get more stimpaks from the wall.}
begin
	gsay_message(184, 129, 50);
end

procedure Medic16// "lie down and we'll start"
begin
	gsay_reply(184, 130);
	giq_option(0, 184, 164, Medic17, 50);
end

procedure Medic17// healing
begin
	if PoisAmt > 0 then begin
		poison(dude_obj, -PoisAmt);
	end
	gfade_out(1);
	if PoisAmt > 0 then begin
		game_time_advance(10 * 60 * PoisAmt * 3);
	end
	critter_heal(dude_obj, damage);
	game_time_advance(10 * 60 * damage * 5);
	gfade_in(1);
	gsay_message(184, 131, 50);
	if (player_max_hits - player_hits) <= 3 then begin
		critter_heal(dude_obj, player_max_hits);
	end
	if rads then begin
		call Medic18;
	end
end

procedure Medic18// checking for rads amounts
begin
	game_time_advance(10 * 60);
	if (rads > 400) then begin
		intensity := message_str(SCRIPT_MEDIC, 132);
	end
	else begin
		if (rads > 200) then begin
			intensity := message_str(SCRIPT_MEDIC, 133);
		end
		else begin
			if (rads > 100) then begin
				intensity := message_str(SCRIPT_MEDIC, 134);
			end
			else begin
				intensity := message_str(SCRIPT_MEDIC, 135);
			end
		end
	end
	gsay_reply(184, intensity);
	giq_option(4, 184, 138, Medic19, 50);
	giq_option(4, 184, 139, Medic22, 50);
	giq_option(4, 184, 140, Medic20, 50);
	giq_option(6, 184, 141, Medic21, 50);
	giq_option(-3, 184, 142, Medic19, 50);
	giq_option(-3, 184, 143, Medic22, 50);
end

procedure Medic19// "okay fine, bye"
begin
	gsay_message(184, 144, 50);
end

procedure Medic20// how long
begin
	call Medic24;
	if not(Hours) then begin
		gsay_reply(184, message_str(SCRIPT_MEDIC, 165) + message_str(SCRIPT_MEDIC, 166) + message_str(SCRIPT_MEDIC, 170));
	end
	else begin
		if Hours == 1 then begin
			if TempMinutes >= 30 then begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 145) + Hours + message_str(SCRIPT_MEDIC, 169) + message_str(SCRIPT_MEDIC, 146));
			end
			else begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 145) + message_str(SCRIPT_MEDIC, 166) + message_str(SCRIPT_MEDIC, 170));
			end
		end
		else begin
			if TempMinutes >= 30 then begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 145) + Hours + message_str(SCRIPT_MEDIC, 169) + message_str(SCRIPT_MEDIC, 146));
			end
			else begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 145) + Hours + message_str(SCRIPT_MEDIC, 146));
			end
		end
	end
	giq_option(4, 184, 147, Medic19, 50);
	giq_option(4, 184, 148, Medic22, 50);
end

procedure Medic21// how long, how many
begin
	call Medic24;
	if Hours > 1 then begin
		if (removal > rads) or (removal == rads) then begin
			IfRounded := message_str(SCRIPT_MEDIC, 168);
		end
		else begin
			IfRounded := message_str(SCRIPT_MEDIC, 150);
		end
	end
	else begin
		if (removal > rads) or (removal == rads) then begin
			IfRounded := message_str(SCRIPT_MEDIC, 167);
		end
		else begin
			IfRounded := message_str(SCRIPT_MEDIC, 150);
		end
	end
	if not(Hours) then begin
		gsay_reply(184, message_str(SCRIPT_MEDIC, 165) + message_str(SCRIPT_MEDIC, 166) + IfRounded + rounded + message_str(SCRIPT_MEDIC, 151));
	end
	else begin
		if Hours == 1 then begin
			if TempMinutes >= 30 then begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 149) + Hours + message_str(SCRIPT_MEDIC, 169) + IfRounded + rounded + message_str(SCRIPT_MEDIC, 151));
			end
			else begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 149) + message_str(SCRIPT_MEDIC, 166) + IfRounded + rounded + message_str(SCRIPT_MEDIC, 151));
			end
		end
		else begin
			if TempMinutes >= 30 then begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 149) + Hours + message_str(SCRIPT_MEDIC, 169) + IfRounded + rounded + message_str(SCRIPT_MEDIC, 151));
			end
			else begin
				gsay_reply(184, message_str(SCRIPT_MEDIC, 149) + Hours + IfRounded + rounded + message_str(SCRIPT_MEDIC, 151));
			end
		end
	end
	giq_option(4, 184, 152, Medic19, 50);
	giq_option(4, 184, 153, Medic22, 50);
end

procedure Medic22// yes, remove them
begin
	gsay_reply(184, random(154, 155));
	giq_option(0, 184, 164, Medic23, 50);
end

procedure Medic23// time fade & increase for removal
begin
	gfade_out(1);
	radiation_inc(dude_obj, -removal);
	game_time_advance(10 * 60 * Minutes);
	gfade_in(1);
	gsay_message(184, 156, 50);
end

procedure Medic24// COMPUTE RADS TO HEAL, MINUTES, AND HOURS
begin
	removal := random(140, 160) + (get_critter_stat(dude_obj, STAT_lu) * 2);//	140 + random(0, 6) + random(0, 6) + random(0, 6) + ;// base was 70
	if (removal > rads) or (removal == rads) then begin
		Minutes := (rads * 2) + 30;// time per rad to heal. was 20.(!)
		rounded := rads;
	end
	else begin
		Minutes := (removal * 2) + 30;// time per rad to heal. was 20.(!)
		rounded := removal;
		while(rounded % 5) do begin// round the number.
			rounded := rounded - 1;
		end
	end
	TempMinutes := Minutes;
	Hours := 0;
	while(TempMinutes >= 60) do begin
		TempMinutes := (TempMinutes - 60);
		Hours := (Hours + 1);
	end
end

procedure Medic25
begin
	gfade_out(1);
	gfade_in(1);
	call Medic18;
end

procedure Medic26// irradiated heal request
begin
	gsay_reply(184, 137);
	giq_option(0, 184, 164, Medic25, 50);
end

procedure Medic27// Lol dumb if already got drugs twice
begin
	gsay_message(184, 125, 51);
end

procedure Medic28//stupid character, give drugs
begin
	if local_var(7) == 1 then begin
		set_local_var(7, 2);
	end
	gsay_message(184, 172, 50);
	call pushdrugs;
end

procedure Medic29
begin
	variable Stimpak := 0;
	Stimpak := create_object( PID_STIMPAK, 0, 0 );
	add_mult_objs_to_inven(dude_obj, Stimpak, 4 - difficulty_level);
	gsay_message(184, 111, 50);// ah yes, here you go
end

procedure pushdrugs
begin
	variable drugs := 0;
	variable LVar0 := 0;
	LVar0 := 2 - difficulty_level;
	if LVar0 then begin
		drugs := create_object( PID_STIMPAK, 0, 0 );// STIMPAK
		add_mult_objs_to_inven(dude_obj, drugs, LVar0);
		drugs := create_object( PID_BUFFOUT, 0, 0 );// BUFFOUT
		add_mult_objs_to_inven(dude_obj, drugs, LVar0);
	end
	drugs := create_object( PID_SUPER_STIMPAK, 0, 0 );// SUPER STIM
	add_mult_objs_to_inven(dude_obj, drugs, 1);
	drugs := create_object( PID_PSYCHO, 0, 0 );// PSYCHO
	add_mult_objs_to_inven(dude_obj, drugs, 1);
end

procedure medicend
begin
end

procedure flee_dude
begin
	variable LVar0 := 0;
	variable LVar1 := 0;
	variable LVar2 := 0;
	while(LVar1 < 5) do begin
		if (tile_distance(tile_num(dude_obj), tile_num_in_direction(tile_num(self_obj), LVar1, 3)) > LVar2) then begin
			LVar0 := tile_num_in_direction(tile_num(self_obj), LVar1, 3);
			LVar2 := tile_distance(tile_num(dude_obj), LVar0);
		end
		LVar1 := LVar1 + 1;
	end
	animate_move_obj_to_tile(self_obj, LVar0, 1);
end



procedure PickDeadBodyType// FINISHED, for Vault 13
begin
	variable id := 11;
	id := random(0, 6) + random(0, 6) + random(0, 6);
	if id <= 2 then begin
		DeathType := 59;// exploded  [ROCKET LAUNCHER]
	end
	else begin
		if id <= 10 then begin
			DeathType := 57;// burnt, face down  [FLAMER]
		end
		else begin
			if id <= 15 then begin
				DeathType := 60;// melted pile  [PLASMA RIFLE]
			end
			else begin
				DeathType := 63;// face down, blood pool  (generic death, no weapon associated)
			end
		end
	end
end

