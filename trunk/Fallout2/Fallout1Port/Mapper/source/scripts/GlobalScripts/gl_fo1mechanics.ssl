/*

   Adjust Fo2 core mechanics to get them closer to Fo1.

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\updatmap.h"
#include "..\headers\voodoo.h"
#include "..\headers\sfall\main.h"

#include "..\headers\boneyard.h"

#define set_lootable(PID)     set_proto_data(PID, PROTO_CR_FLAGS, get_proto_data(PID, PROTO_CR_FLAGS) bwand bwnot(CFLG_NOSTEAL))
#define set_unlootable(PID)   set_proto_data(PID, PROTO_CR_FLAGS, get_proto_data(PID, PROTO_CR_FLAGS) bwor CFLG_NOSTEAL)

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;

procedure ondeath_handler;
procedure general;
procedure mode_change;
procedure scene_master_destroyed;
procedure scene_vats_destroyed;
procedure night_person;
procedure item_description;

procedure start begin
   if (game_loaded) then begin
   	set_global_script_type(0);
   	register_hook_proc(HOOK_GAMEMODECHANGE, mode_change);
   	register_hook_proc(HOOK_DESCRIPTIONOBJ, item_description);
   	register_hook_proc(HOOK_ONDEATH, ondeath_handler);
   	call map_enter_p_proc;
   end

	call general;
end

procedure map_enter_p_proc begin
   if has_trait(TRAIT_PERK, dude_obj, PERK_gecko_skinning_perk) then begin
      set_lootable(PID_GECKO);
      set_lootable(PID_GOLDEN_GECKO);
      set_lootable(PID_FIRE_GECKO);
   end else begin
      set_unlootable(PID_GECKO);
      set_unlootable(PID_GOLDEN_GECKO);
      set_unlootable(PID_FIRE_GECKO);
   end
end

procedure ondeath_handler begin
   call map_enter_p_proc;
end

procedure general begin
	// Max skill is 200%
   set_skill_max(200);

   // Max rad resistance is 100%
	set_pc_stat_max(STAT_rad_resist,100);

   // Changes the PipBoy rest timer from 08:00 to 06:00
	VOODOO_rest_till0600;
end

procedure mode_change begin
   call scene_master_destroyed;
   call scene_vats_destroyed;
   call night_person;

   // -1 AP for opening the inventory. Max: 3 ranks Quick Pocket
   // in here because we have to refresh it (don't know when player picks it)
   set_inven_ap_cost(4 - dude_quick_pockets);
end

procedure scene_master_destroyed begin
   if (get_game_mode == WORLDMAP) then begin
   	if (global_var( GVAR_MASTER_BLOWN ) == 1) and not(cathedral_destroyed) then begin
   		set_global_var( GVAR_MASTER_BLOWN_CUTSCENE, 1 );
   		set_end_Master;

   		inc_cathedral_rep(REP_BONUS_BLOWN_UP_MASTER);

   		give_exp_points(EXP_MASTER_DESTROYED);
   		display_msg(message_str(SCRIPT_GENCHAT, 702));

   		mark_map_entrance_state(MAP_CHILDRN1,0);
   		mark_map_entrance_state(MAP_CHILDRN2,0);
   		mark_map_entrance_state(MAP_MSTRLR12,0);
   		mark_map_entrance_state(MAP_MSTRLR34,0);
         mark_map_entrance_elev_state(MAP_CHILDRN1,1,0); // We need this additionally, otherwise the stupid entry point won't disable itself.
   		mark_map_entrance_state(MAP_CHILDEAD,1);

   		play_gmovie(CATHEXPLODE_MOVIE);

   		if (global_var( GVAR_VATS_BLOWN )) and (waterchip_returned /*or dude_item_count(PID_WATER_CHIP)*/) then begin
   			force_encounter(MAP_V13ENT);
   		end
   	end
	end
end

procedure scene_vats_destroyed begin
   if (get_game_mode == WORLDMAP) then begin
      if (global_var( GVAR_VATS_BLOWN ) == 1) and not(military_base_destoryed) then begin
   		set_global_var( GVAR_VATS_BLOWN_CUTSCENE_DONE, 1 );
   		set_end_Vats;

   		inc_mbase_rep(REP_BONUS_BLOWN_UP_VATS);

   		give_exp_points(EXP_VATS_DESTROYED);
   		display_msg(message_str(SCRIPT_GENCHAT, 701));

   		mark_map_entrance_state(MAP_MBENT,0);
   		mark_map_entrance_state(MAP_MBSTRG12,0);
   		mark_map_entrance_state(MAP_MBVATS12,0);
   		mark_map_entrance_state(MAP_MBDEAD,1);

   		play_gmovie(VATSEXPLODE_MOVIE);

   		if (global_var( GVAR_MASTER_BLOWN )) and waterchip_returned then begin
   			force_encounter(MAP_V13ENT);
   		end
   	end
   end
end

procedure night_person begin
	/* Night Person Trait
	As a night-time person, you are more awake when the sun goes down.
	Your Intelligence and Perception are improved at night, but dulled during the day. */
	if (dude_trait( TRAIT_night_person )) then begin
	   if (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 0) then
	      set_global_var(GVAR_NIGHT_PERSON_ACTIVE,1);
	      mod_skill_points_per_level(2);

		if night and (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 1) then begin
		   set_global_var(GVAR_NIGHT_PERSON_ACTIVE,2);
			set_critter_extra_stat(dude_obj, 1, get_critter_extra_stat(dude_obj,1) + 2);
			set_critter_extra_stat(dude_obj, 4, get_critter_extra_stat(dude_obj,4) + 2);
			debug("Night person trait is active!");
		end
		else if not(night) and (global_var(GVAR_NIGHT_PERSON_ACTIVE) == 2) then begin
		   set_global_var(GVAR_NIGHT_PERSON_ACTIVE,1);
			set_critter_extra_stat(dude_obj, 1, get_critter_extra_stat(dude_obj,1) - 2);
			set_critter_extra_stat(dude_obj, 4, get_critter_extra_stat(dude_obj,4) - 2);
			debug("Night person trait is not active!");
		end
	end
	// If used Mutate!-perk to remove Night Person, reset everything to default:
	else if (not(dude_trait(TRAIT_night_person)) and (global_var(GVAR_NIGHT_PERSON_ACTIVE) > 0)) then begin
	   set_global_var(GVAR_NIGHT_PERSON_ACTIVE,0);
	   mod_skill_points_per_level(0);
		if night then begin
			set_critter_extra_stat(dude_obj, 1, get_critter_extra_stat(dude_obj,1) - 2);
			set_critter_extra_stat(dude_obj, 4, get_critter_extra_stat(dude_obj,4) - 2);
		end
		debug("Night person trait is disabled!");
	end
end

procedure item_description begin
   variable item := obj_pid(get_sfall_arg);
   variable description;

   if (item == PID_FARM_PARTS) and get_quest_farm_active then begin
		if (is_success(roll_vs_skill(dude_obj, SKILL_SCIENCE, 0))) then begin
         description := get_string_pointer(message_str(SCRIPT_FARMPART,101));
         set_sfall_return(description);
		end
   end
end

