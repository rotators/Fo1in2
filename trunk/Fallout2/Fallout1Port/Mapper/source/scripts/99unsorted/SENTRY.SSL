/*

	Brotherhood - Paladin, lone sentry

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_SENTRY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;//    script_action == 21
procedure damage_p_proc;//    script_action == 14
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure critter_p_proc;//    script_action == 12
procedure destroy_p_proc;//    script_action == 18

procedure node01;
procedure node02;
procedure node03;
procedure node04;
procedure node05;
procedure node06;
procedure node07;
procedure node08;
procedure node09;
procedure node11;
procedure node12;
procedure node_exit;

variable initial :=  0;
variable tmp_hostile;
variable weaponPtr;


procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_BROTHERHOOD );
		set_self_ai( AI_BROTHERHOOD_PALADIN );
		self_rotate( 2 );
		if (local_var(0) == 0) then begin
			set_local_var(0, 1);
			weaponPtr := create_object( PID_GATLING_LASER, 0, 0 );
			add_obj_to_inven(self_obj, weaponPtr);
			wield_obj_critter(self_obj, weaponPtr);
			add_obj_to_inven(self_obj, create_object( PID_MICRO_FUSION_CELL, 0, 0 ));
		end
		initial :=  1;
	end
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_SENTRY, 100));
end

procedure damage_p_proc
begin
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (local_var(1) == 0) then begin
		set_local_var(1, 1);
		start_gdialog(324, self_obj, 4, -1, -1);
		gsay_start;
		if (global_var( GVAR_QUEST_BROHOOD_1_JOIN_THEM ) != 2) then begin
			call node01;
		end
		else begin
			call node09;
		end
		gsay_end;
		end_dialogue;
	end
	else begin
		display_msg(message_str(SCRIPT_SENTRY, 150));
	end
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure node01
begin
	gsay_reply(324, 101);
	giq_option( -3, SCRIPT_SENTRY, message_str(SCRIPT_SENTRY, 102) + dude_name + message_str(SCRIPT_SENTRY, 103), node02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SENTRY, message_str(SCRIPT_SENTRY, 104) + dude_name + message_str(SCRIPT_SENTRY, 105), node04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SENTRY, 106, node12, NEUTRAL_REACTION );
end

procedure node02
begin
	gsay_reply(324, message_str(SCRIPT_SENTRY, 107) + dude_name + message_str(SCRIPT_SENTRY, 108));
	giq_option( -3, SCRIPT_SENTRY, 109, node03, NEUTRAL_REACTION );
end

procedure node03
begin
	gsay_reply(324, 110);
	giq_option( -3, SCRIPT_SENTRY, 111, node_exit, NEUTRAL_REACTION );
end

procedure node04
begin
	gsay_reply(324, 112);
	giq_option( 4, SCRIPT_SENTRY, 113, node05, NEUTRAL_REACTION );
end

procedure node05
begin
	gsay_reply(324, 114);
	giq_option( 4, SCRIPT_SENTRY, 115, node06, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SENTRY, 116, node07, NEUTRAL_REACTION );
end

procedure node06
begin
	gsay_reply(324, 117);
	giq_option( 4, SCRIPT_SENTRY, 118, node_exit, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SENTRY, 119, node07, NEUTRAL_REACTION );
end

procedure node07
begin
	gsay_reply(324, 120);
	if (global_var( GVAR_QUEST_BROHOOD_1_JOIN_THEM ) != 2) then begin
		giq_option( 4, SCRIPT_SENTRY, 121, node08, NEUTRAL_REACTION );
	end
	else begin
		giq_option( 4, SCRIPT_SENTRY, 121, node11, NEUTRAL_REACTION );
	end
end

procedure node08
begin
	gsay_message(324, message_str(SCRIPT_SENTRY, 122) + dude_name + message_str(SCRIPT_SENTRY, 123), 50);
end

procedure node09
begin
	gsay_reply(324, message_str(SCRIPT_SENTRY, 124) + dude_name + message_str(SCRIPT_SENTRY, 125));
	giq_option( -3, SCRIPT_SENTRY, 126, node02, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_SENTRY, 127, node04, NEUTRAL_REACTION );
end

procedure node11
begin
	gsay_reply(324, message_str(SCRIPT_SENTRY, 130) + dude_name + message_str(SCRIPT_SENTRY, 131));
	giq_option( 4, SCRIPT_SENTRY, 132, node_exit, NEUTRAL_REACTION );
end

procedure node12
begin
	gsay_message(324, 133, 50);
end

procedure node_exit
begin
end
