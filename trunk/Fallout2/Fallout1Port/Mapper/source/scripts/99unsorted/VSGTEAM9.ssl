/*

	Supermutant Sgt.

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_VSGTEAM9
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;//    script_action == 12
procedure destroy_p_proc;//    script_action == 18
procedure pickup_p_proc;//    script_action == 4

import variable Team9_Count;

variable tmp_hostile;
variable initial :=  0;

procedure start
begin


	if not(initial) then begin
		set_self_team(TEAM_NECROPOLIS_MUTANT );
		set_self_ai( AI_SUPER_MUTANT_AGGRESSIVE );
		initial :=  1;
	end
	if (script_action == 12) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
		call critter_p_proc;
	end
	else begin
		if (script_action == 18) then begin//destroy_p_proc - Object or Critter has been killed or otherwise eradicated. Fall down go boom.
			call destroy_p_proc;
		end
		else begin
			if (script_action == 4) then begin//<---caught stealing! (pickup_p_proc)
				tmp_hostile := 1;
			end
		end
	end
end

procedure critter_p_proc
begin
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		else begin
			if (global_var(VATS_ALERT) == 1) then begin
				attack(dude_obj);
			end
			else begin
				if (not(obj_pid(critter_inven_obj(dude_obj, 0)) == PID_PURPLE_ROBE)) then begin
					tmp_hostile := 1;
				end
			end
		end
	end
end

procedure destroy_p_proc begin
	Team9_Count := Team9_Count - 1;
	inc_evil_critter
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

