/*

	Poorly repaired slot machine

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_CHEPSLOT

#include "..\headers\command.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure use_p_proc;

variable rndx;
variable bonus;
variable test;
variable money;

procedure start begin
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_CHEPSLOT, 100));
end

procedure map_enter_p_proc
begin
	if (global_var( GVAR_GIZMO_DEAD ) == 1) then begin
		destroy_object(self_obj);
	end
end

procedure use_p_proc
begin
	script_overrides;
	if (dude_caps < 10) then begin
		display_msg(message_str(SCRIPT_CHEPSLOT, 108));
	end
	else begin
		display_msg(message_str(SCRIPT_CHEPSLOT, 101));
		dude_caps_adjust( -10 );
		bonus := random(1, 50) - random(1, 50);
		test := roll_vs_skill(dude_obj, SKILL_GAMBLING, bonus);
		if (is_success(test)) then begin
			if (is_critical(test)) then begin
				rndx := random(15, 30);
				float_msg(self_obj, message_str(SCRIPT_CHEPSLOT, 102), 0);
				display_msg(message_str(SCRIPT_CHEPSLOT, 103) + rndx + message_str(SCRIPT_CHEPSLOT, 104));
			end
			else begin
				float_msg(self_obj, message_str(SCRIPT_CHEPSLOT, 105), 0);
				display_msg(message_str(SCRIPT_CHEPSLOT, 106));
				rndx := 15;
			end
			dude_caps_adjust( rndx );
		end
		else begin
			display_msg(message_str(SCRIPT_CHEPSLOT, 107));
		end
	end
end
