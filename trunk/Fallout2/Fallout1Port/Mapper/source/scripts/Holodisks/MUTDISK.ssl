//
// KEEP ORIGINAL ITEM	DCMUTANT.SSL	ProtoID	196	Name:	Mutant Transmissions
// Cost:	125	script:	641	MutDisk.int    ; disk w/ transmissions from mutants in Deathclaw cave
// 	GVAR	209	This holodisk looks like it was set to record audio data from a radio.	<-- could turn into dialog

#include "..\headers\define.h"

procedure start;

procedure newPtr;

#define LVAR_DiskUsed 		(0)
#define LVAR_DiskExamined 	(1)

procedure start begin
	call newPtr;
end

procedure use_p_proc begin
	script_overrides;
	set_local_var(LVAR_DiskExamined, 1);
	if local_var(LVAR_DiskUsed) == 1 then begin
		display_msg(message_str(SCRIPT_MUTDISK, 101));
	end
	else begin
		set_local_var(LVAR_DiskUsed, 1);
		set_global_var( GVAR_HOLODISK_MUTANT, 1 );
		display_msg(message_str(SCRIPT_MUTDISK, 102));
		display_msg(message_str(SCRIPT_GENCHAT, 103) + "185" + message_str(SCRIPT_GENCHAT, 104));
		give_exp_points(185);
	end
	call newPtr;
end

procedure look_at_p_proc begin
	script_overrides;
	if local_var(LVAR_DiskExamined) == 1 then begin
		display_msg(message_str(SCRIPT_MUTDISK, 104));
	end
	else begin
		display_msg(message_str(SCRIPT_MUTDISK, 100));
	end
	call newPtr;
end

procedure description_p_proc begin
	set_local_var(LVAR_DiskExamined, 1);
	display_msg(message_str(SCRIPT_MUTDISK, 103));
	call newPtr;
end

procedure pickup_p_proc begin
	call newPtr;
end

procedure drop_p_proc begin
	call newPtr;
end

procedure newPtr begin
	if cur_map_index != MAP_DETHCLAW then begin
		set_global_var( GVAR_DCMUTANT_DISK_POINTER, self_obj );
	end
end


