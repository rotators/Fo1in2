//
// ---HOLODISK SCRIPT---  Sduibek
//
// KEEP ORIGINAL ITEM	rebuild via critter_p_proc - DONE.
// ProtoID	190	Name:	FEV Disk	Cost:	200
// script:	347	FEVDisk.int    ; Holodisk w/ FEV Experiment in the Glow
// GVAR	162	"A holodisk containing medical information."

#include "..\headers\define.h"

#define NAME                    SCRIPT_FEVDISK

#include "..\headers\command.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_p_proc;

procedure NewPtr;

#define LVAR_Used          (0)
#define LVAR_Examined      (1)

procedure start begin
	//call NewPtr;
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(LVAR_Examined) < 1) and (global_var( GVAR_FEVDISK_HOLODISK_USED ) != 1) then begin
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(104));
	end
end

procedure description_p_proc begin
	set_local_var(LVAR_Examined, 1);
	display_msg(mstr(103));
end

procedure use_p_proc begin
	script_overrides;
	set_local_var(LVAR_Examined, 1);
	if local_var(LVAR_Used) == 1 then begin
		display_msg(mstr(101));
	end
	else begin
		set_local_var(LVAR_Used, 1);
		set_global_var( GVAR_FEVDISK_HOLODISK_USED, 1 );
		set_global_var( GVAR_FEVDISK_HOLODISK_FLAG, 1 );// Flag for destroying disk later.
		display_msg(mstr(102));//Downloaded OK
		display_msg(message_str(SCRIPT_GENCHAT, 103) + "200" + message_str(SCRIPT_GENCHAT, 104));// more exp, for Fallout Fixt
		give_exp_points(200);
	end
end

procedure NewPtr begin
	if self_obj != 0 then begin
		if (cur_map_index != MAP_GLOW1) and (cur_map_index != MAP_GLOW2) then begin
			set_global_var( GVAR_FEVDISK_HOLODISK_POINTER, self_obj );
		end
	end
end


