#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure combat_p_proc;//    script_action == 13
procedure critter_p_proc;//    script_action == 12
procedure destroy_p_proc;//    script_action == 18
procedure pickup_p_proc;//    script_action == 4
procedure talk_p_proc;//    script_action == 11
procedure timed_event_p_proc;//    script_action == 22
procedure VFenceMt00;
procedure VFenceMt03;
procedure VFenceMt03a;
procedure VFenceMt03b;
procedure VFenceMt04;
procedure VFenceMt05;
procedure VFenceMt06;
procedure VFenceMt07;
procedure VFenceMt08;
procedure VFenceMtAlert;
procedure VFenceMtxx;

variable initial :=  0;
variable tmp_hostile;
variable round_count;
variable home_tile := 7000;
variable fence_tile_1 := 16706;
variable fence_tile_2 := 25705;
variable waypoint;

import variable removal_ptr;

procedure start
begin
	

	if not(initial) then begin
		initial :=  1;
		set_self_team(TEAM_NECROPOLIS_MUTANT );
		add_timer_event(self_obj, game_ticks(30), 1);
	end
end

procedure combat_p_proc
begin
	if (fixed_param == 4) then begin
		round_count := round_count + 1;
	end
	if (round_count > 2) then begin
		call VFenceMtAlert;
	end
end

procedure critter_p_proc
begin
	if (obj_can_see_obj(self_obj, dude_obj)) then begin
		if (tmp_hostile) then begin// This must come FIRST as an if/then/else before "attack dude" type code, otherwise it runs too soon and can override other attack calls
			tmp_hostile := 0;
			attack(dude_obj);
		end
		else begin
			if (global_var(VATS_ALERT)) then begin
				tmp_hostile := 1;
			end
			else begin
				if (not(map_var(4))) then begin
					if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
						dialogue_system_enter;
					end
				end
			end
		end
	end
	if (map_var(0)) then begin
		if (not(waypoint)) then begin
			if (tile_num(self_obj) != 22312) then begin
				animate_move_obj_to_tile(self_obj, 22312, 0);
			end
			else begin
				waypoint := 1;
			end
		end
		else begin
			if (waypoint == 1) then begin
				if (tile_num(self_obj) != 26317) then begin
					animate_move_obj_to_tile(self_obj, 26317, 0);
				end
				else begin
					waypoint := 2;
				end
			end
			else begin
				if (waypoint == 2) then begin
					if (tile_num(self_obj) != 31517) then begin
						animate_move_obj_to_tile(self_obj, 32319, 0);
					end
					else begin
						waypoint := 3;
					end
				end
				else begin
					if (waypoint == 3) then begin
						removal_ptr := self_obj;
					end
				end
			end
		end
	end
end

procedure destroy_p_proc
begin
	rm_timer_event(self_obj);

	if source_obj == dude_obj then begin
		set_global_var(NUM_BAD_MONSTERS_KILLED, global_var(NUM_BAD_MONSTERS_KILLED) + 1);// THIS MONSTER WAS A BAD GUY. INCREASE BadGuysKilled COUNTER
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_GOOD_MONSTERS_KILLED) > (2 * global_var(NUM_BAD_MONSTERS_KILLED))) or (global_var(BERSERKER_REPUTATION) == 1))) then begin
			set_global_var(BERSERKER_REPUTATION, 1);
			set_global_var(CHAMPION_REPUTATION, 0);
		end
		if (((global_var(NUM_BAD_MONSTERS_KILLED) + global_var(NUM_GOOD_MONSTERS_KILLED)) >= 25) and ((global_var(NUM_BAD_MONSTERS_KILLED) > (3 * global_var(NUM_GOOD_MONSTERS_KILLED))) or (global_var(CHAMPION_REPUTATION) == 1))) then begin
			set_global_var(CHAMPION_REPUTATION, 1);
			set_global_var(BERSERKER_REPUTATION, 0);
		end
		if ((global_var(NUM_BAD_MONSTERS_KILLED) % 6) == 0) then begin
			set_global_var(PLAYER_REPUTATION_GENERAL, global_var(PLAYER_REPUTATION_GENERAL) + 1);
		end
	end
	rm_timer_event(self_obj);
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_critter;
	if (global_var(LIEUTENANTS_DEAD)) then begin
		call VFenceMt08;
	end
	else begin
		if (dude_is_armed) and not(tmp_hostile) then begin
			if (random(0, 5) == 5) then begin
				call VFenceMt00;
			end
			else begin
				tmp_hostile := 1;
			end
		end
		else begin
			start_gdialog(433, self_obj, 4, -1, -1);
			gsay_start;
			call VFenceMt03;
			gsay_end;
			end_dialogue;
		end
	end
end

procedure timed_event_p_proc
begin
	if (not(map_var(0))) then begin
		if (tile_num(self_obj) == fence_tile_1) then begin
			animate_move_obj_to_tile(self_obj, fence_tile_2, 0);
		end
		else begin
			animate_move_obj_to_tile(self_obj, fence_tile_1, 0);
		end
		add_timer_event(self_obj, game_ticks(60), 1);
	end
end

procedure VFenceMt00
begin
	float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(101, 103)), 2);
	tmp_hostile := 1;
end

procedure VFenceMt03
begin
	gsay_reply(433, random(104, 106));
	giq_option(-3, 433, 107, VFenceMt04, 51);
	giq_option(4, 433, 108, VFenceMt04, 51);
	giq_option(5, 433, 109, VFenceMt05, 50);
	giq_option(6, 433, 110, VFenceMt03a, 50);
	if dude_wearing_coc_robe then begin
		giq_option(6, 433, 111, VFenceMt03b, 50);
	end
end

procedure VFenceMt03a
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call VFenceMt07;
	end
	else begin
		call VFenceMt06;
	end
end

procedure VFenceMt03b
begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 25))) then begin
		call VFenceMt07;
	end
	else begin
		call VFenceMt06;
	end
end

procedure VFenceMt04
begin
	tmp_hostile := 1;
	gsay_message(433, random(112, 114), 51);
end

procedure VFenceMt05
begin
	gsay_reply(433, 115);
	giq_option(5, 433, 116, VFenceMtxx, 50);
	giq_option(5, 433, 117, VFenceMtAlert, 51);
end

procedure VFenceMt06
begin
	tmp_hostile := 1;
	gsay_message(433, random(118, 120), 51);
end

procedure VFenceMt07
begin
	set_map_var(4, 1);
	gsay_message(433, random(121, 123), 50);
end

procedure VFenceMt08
begin
	float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(124, 127)), 2);
	tmp_hostile := 1;
end

procedure VFenceMtAlert
begin
	set_global_var(VATS_ALERT, 1);
	tmp_hostile := 1;
end

procedure VFenceMtxx
begin
	set_global_var(ENTERING_VATS_HOW, 2);
	load_map(MAP_MBVATS12, 5);
end


