/*

	Brotherhood - Invasion force for Military Base / Cathedral

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_BROINVAD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure push_p_proc;
procedure talk_p_proc;
procedure initialize_p_proc;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure destroy_p_proc;

variable Only_Once := 1;
variable Hex_Number;

procedure start begin
	/*if global_var( GVAR_DEBUG_MODE_MESSAGES_ON ) then begin
		debug("attempting to move BroInvad...");
		move_to(self_obj, 26893, elevation(dude_obj));
		debug("dude at " + tile_num(dude_obj));
		debug("self at " + tile_num(self_obj));
	end*/

	if Only_Once then begin
		call initialize_p_proc;
	end
end

procedure push_p_proc begin
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var( GVAR_BROTHERHOOD_SEND_HELP ) != 10) then begin
		if (dude_is_male) then begin
			float_msg(self_obj, message_str(SCRIPT_BROINVAD, random(100, 105)), 9);
		end
		else begin
			float_msg(self_obj, message_str(SCRIPT_BROINVAD, random(106, 111)), 9);
		end
	end
end

procedure initialize_p_proc begin
	set_self_team(TEAM_PLAYER );
	set_self_ai( AI_BROTHERHOOD_PALADIN );
	Only_Once := 0;
end

procedure critter_p_proc begin
	// Remove and destroy if on an invalid map.
	if (not((cur_map_index == MAP_CHILDRN1) or (cur_map_index == MAP_CHILDRN2) or (cur_map_index == MAP_MSTRLR12) or (cur_map_index == MAP_MSTRLR34) or (cur_map_index == MAP_MBENT) or (cur_map_index == MAP_MBSTRG12) or (cur_map_index == MAP_MBVATS12))) then begin //  CHILDREN - ALL LEVELS, MILITARY BASE ENTRANCE (now all levels), MASTER'S LAIR ALL LEVELS
		party_remove(self_obj);
		destroy_object(self_obj);
	end

	// Follow player if on valid map, and, display one-time float message on ground-floor maps.
	if ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 1) and ((cur_map_index == MAP_CHILDRN1) or (cur_map_index == MAP_CHILDRN2) or (cur_map_index == MAP_MSTRLR12) or (cur_map_index == MAP_MSTRLR34))) or ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 2) and ((cur_map_index == MAP_MBENT) or (cur_map_index == MAP_MBSTRG12) or (cur_map_index == MAP_MBVATS12))) then begin //  CHILDREN - ALL LEVELS, MILITARY BASE ENTRANCE, MASTER'S LAIR ALL LEVELS
		if (tile_distance_objs(dude_obj, self_obj) > 6) then begin
			animate_move_obj_to_tile(self_obj, tile_num_in_direction(tile_num(dude_obj), random(0, 5), random(3, 6)), 1);
		end
		if (local_var(0) == 0) then begin
			if ((tile_distance_objs(dude_obj, self_obj) < 8) and ((cur_map_index == MAP_CHILDRN1) or (cur_map_index == MAP_MBENT))) then begin //  CHILDREN - ENTRANCE AND GROUND FLOOR, MILITARY BASE ENTRANCE
				set_local_var(0, 1);
				float_msg(self_obj, message_str(SCRIPT_BROINVAD, 112), 9);
			end
		end
	end

	// Removal processes after coming back to ground floor.
	if ((local_var(4) == 1) and (cur_map_index == MAP_CHILDRN1) and (elevation(self_obj) == local_var(3)) and not(combat_is_initialized)) then begin //  CHILDREN - ENTRANCE AND GROUND FLOOR
		set_local_var(4, 0);
		set_local_var(1, 2);
		party_remove(self_obj);
		set_global_var( GVAR_BROTHERHOOD_SEND_HELP, 10 );
		reg_anim_func(2, self_obj);
		float_msg(self_obj, message_str(SCRIPT_BROINVAD, 115), 9);
		add_timer_event(self_obj, 10, 1);
	end
	if ((global_var(GVAR_MBASE_DOOR_HOLODISK) == 2) and (local_var(1) == 1) and (cur_map_index == MAP_MBENT) and (local_var(5) == 1) and not(combat_is_initialized)) then begin //  MILITARY BASE - ENTRANCE
		set_local_var(1, 2);
		party_remove(self_obj);
		set_global_var( GVAR_BROTHERHOOD_SEND_HELP, 10 );
		reg_anim_func(2, self_obj);
		float_msg(self_obj, message_str(SCRIPT_BROINVAD, 115), 9);
		add_timer_event(self_obj, 10, 1);
	end
end

procedure map_enter_p_proc begin
// Not IS_LOADING_GAME
	if not(is_loading_game) then begin
		// Add to party if first time on valid entrance map.
		if ((local_var(1) == 0) and ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 1) and (cur_map_index == MAP_CHILDRN1) or ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 2) and (cur_map_index == MAP_MBENT)))) then begin //  CHILDREN - ENTRANCE AND GROUND FLOOR, MILITARY BASE ENTRANCE
			set_local_var(1, 1);
			if not(is_loading_game) then begin    set_obj_visibility(self_obj, 0);    end//  MAKE VISIBLE
			party_add(self_obj);
		end
		else begin
			// Remove and destroy if on valid map but player not supposed to have them.
			if (((global_var( GVAR_BROTHERHOOD_SEND_HELP ) != 1) and ((cur_map_index == MAP_CHILDRN1) or (cur_map_index == MAP_CHILDRN2) or (cur_map_index == MAP_MSTRLR12) or (cur_map_index == MAP_MSTRLR34))) or ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) != 2) and ((cur_map_index == MAP_MBENT) or (cur_map_index == MAP_MBSTRG12) or (cur_map_index == MAP_MBVATS12)))) then begin //  CHILDREN - ALL LEVELS, MILITARY BASE ENTRANCE, MASTER'S LAIR ALL LEVELS
				if not(is_loading_game) then begin    set_obj_visibility(self_obj, 1);    end//  MAKE INVISIBLE
				if (local_var(1) != 0) then begin
					party_remove(self_obj);
					destroy_object(self_obj);
				end
			end
			else begin
				// Remove and destroy when triggered to leave.
				if ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) >= 10) and (local_var(1) != 0)) then begin
					destroy_object(self_obj);
				end
			end
		end
		// Boring float messages inside Master's Vault.
		if ((global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 1) or (global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 2)) then begin
			if (cur_map_index == MAP_MSTRLR12) then begin //  MASTER'S VAULT -  ENTRANCE AND FIRST LEVEL   (LEVELS 1 AND 2)
				float_msg(self_obj, message_str(SCRIPT_BROINVAD, 113), 9);
			end
			if (cur_map_index == MAP_MSTRLR34) then begin //  MASTER'S VAULT -  LEVELS 3 AND 4
				float_msg(self_obj, message_str(SCRIPT_BROINVAD, 114), 9);
			end
		end
	end
	// Flags to initiate process of them leaving the map.
	if (cur_map_index == MAP_MSTRLR12) then begin //  MASTER'S VAULT -  ENTRANCE AND FIRST LEVEL   (LEVELS 1 AND 2)
		set_local_var(4, 1);
	end
	// Flag that they've reached Vats, so it'll trigger leaving map when get back to surface.
	if local_var(5) == 0 then begin
		if cur_map_index == MAP_MBVATS12 then begin
			set_local_var(5, 1);
		end
	end
end

procedure timed_event_p_proc begin
	if not(is_loading_game) then begin
		set_obj_visibility(self_obj, 1);
	end//  MAKE INVISIBLE

	if (global_var( GVAR_BROTHERHOOD_SEND_HELP ) == 10) then begin
		set_global_var( GVAR_BROTHERHOOD_SEND_HELP, 11 );

		//game_time_advance(600);//Hopefully help make the floating msgs not linger around after...
		gfade_out(1);
			float_msg(self_obj, " ", 9);
		gfade_in(1);
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	if (combat_is_initialized == 1) then begin

	end
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
	end
	inc_good_critter
	rm_timer_event(self_obj);
end



