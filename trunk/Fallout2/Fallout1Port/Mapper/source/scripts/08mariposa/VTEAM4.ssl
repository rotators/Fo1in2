/*

	Mariposa - Guard Team 4

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\MAPNECRO.h"

#define NAME                    SCRIPT_VTEAM4
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure map_enter_p_proc;

procedure GenSupr00;
procedure GenSupr03;
procedure GenSupr03a;
procedure GenSupr03b;
procedure GenSupr04;
procedure GenSupr05;
procedure GenSupr06;
procedure GenSupr07;
procedure GenSupr08;
procedure GenSuprAlert;
procedure GenSuprxx;

procedure set_alert_tile;

import variable radio_room_attacked;

variable tmp_hostile;
variable round_counter;
variable home_tile;
variable alert_tile;
variable night_tile;

procedure start begin
end

procedure combat_p_proc begin
	if (fixed_param == 4) then begin
		round_counter := round_counter + 1;
	end
	if (round_counter > 2) then begin
		call GenSuprAlert;
	end
	if (fixed_param == 2) then begin
		if (global_var( GVAR_DESTROY_VATS_4 )) then begin
			if (random(0, 3) == 3) then begin
				critter_injure(dude_obj, 1);//    DAM_KNOCKED_OUT      1    <-- DOES NOT WORK!!!
			end
		end
	end
end

procedure critter_p_proc begin
	if (tmp_hostile and obj_can_see_obj(self_obj, dude_obj)) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (obj_can_see_obj(self_obj, dude_obj)) then begin
			if (global_var( GVAR_VATS_ALERT )) then begin
				tmp_hostile := 1;
			end
			else begin
				if (not(map_var(2))) then begin
					if (tile_distance_objs(self_obj, dude_obj) < 12) then begin
						dialogue_system_enter;
					end
				end
			end
		end
		if ((global_var( GVAR_DESTROY_VATS_1 ) >= 1) and (global_var( GVAR_DESTROY_VATS_1 ) <= 3) and (elevation(self_obj) == 0)) then begin
			kill_critter(self_obj, ANIM_fall_front_blood);
		end
		else begin
			if (global_var( GVAR_VATS_ALERT )) then begin
				if (elevation(self_obj) == 0) then begin
					if (tile_num(self_obj) != 14520) then begin
						animate_move_obj_to_tile(self_obj, 14520, 0);
					end
					else begin
						move_to(self_obj, 14520, 1);
					end
				end
				else begin
					if (tile_num(self_obj) != alert_tile) then begin
						animate_move_obj_to_tile(self_obj, alert_tile, 0);
					end
				end
			end
			else begin
				if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
					if (elevation(self_obj) == 0) then begin
						if (tile_num(self_obj) != 14520) then begin
							animate_move_obj_to_tile(self_obj, 14520, 0);
						end
						else begin
							move_to(self_obj, 14520, 1);
						end
					end
					else begin
						if (tile_num(self_obj) != night_tile) then begin
							animate_move_obj_to_tile(self_obj, night_tile, 0);
						end
					end
				end
				else begin
					if (elevation(self_obj) == 1) then begin
						if (tile_num(self_obj) != 14520) then begin
							animate_move_obj_to_tile(self_obj, 14520, 0);
						end
						else begin
							move_to(self_obj, 14520, 0);
						end
					end
					else begin
						if (tile_num(self_obj) != local_var(4)) then begin
							animate_move_obj_to_tile(self_obj, local_var(4), 0);
						end
					end
				end
			end
		end
	end
end

procedure damage_p_proc begin
	if (elevation(self_obj) == 0) then begin
		radio_room_attacked := 1;
	end
end

procedure destroy_p_proc begin
	inc_inv_evil_crit
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	dude_look_at_critter;
	if (global_var( GVAR_LIEUTENANTS_DEAD )) then begin
		call GenSupr08;
	end
	else begin
		if (dude_is_armed) and not(tmp_hostile) then begin
			if (random(0, 5) == 5) then begin
				call GenSupr00;
			end
			else begin
				tmp_hostile := 1;
			end
		end
		else begin
			start_gdialog(433, self_obj, 4, -1, -1);
			gsay_start;
			call GenSupr03;
			gsay_end;
			end_dialogue;
		end
	end
end

procedure map_enter_p_proc begin
	set_self_team(TEAM_NECROPOLIS_MUTANT );
	set_self_ai( AI_SUPER_MUTANT_AGGRESSIVE );
	call set_alert_tile;
	if not(is_loading_game) then begin
		if (not((global_var( GVAR_DESTROY_VATS_1 ) >= 1) and (global_var( GVAR_DESTROY_VATS_1 ) <= 3))) then begin
			if (global_var( GVAR_VATS_ALERT )) then begin
				move_to(self_obj, alert_tile, 1);
			end
			else begin
				if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
					move_to(self_obj, night_tile, 1);
				end
				else begin
					move_to(self_obj, local_var(4), 0);
				end
			end
		end
	end
end

procedure GenSupr00 begin
	float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(101, 103)), 0);
	tmp_hostile := 1;
end

procedure GenSupr03 begin
	gsay_reply(433, random(104, 106));
	NOption( 107, GenSupr04, -3 );
	NOption( 108, GenSupr04, 4 );
	NOption( 109, GenSupr05, 5 );
	NOption( 110, GenSupr03a, 6 );
	if dude_wearing_coc_robe then begin
		NOption( 111, GenSupr03b, 6 );
	end
end

procedure GenSupr03a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call GenSupr07;
	end
	else begin
		call GenSupr06;
	end
end

procedure GenSupr03b begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 25))) then begin
		call GenSupr07;
	end
	else begin
		call GenSupr06;
	end
end

procedure GenSupr04 begin
	tmp_hostile := 1;
	gsay_message(433, random(112, 114), 50);
end

procedure GenSupr05 begin
	gsay_reply(433, 115);
	NOption( 116, GenSuprxx, 5 );
	NOption( 117, GenSuprAlert, 5 );
end

procedure GenSupr06 begin
	tmp_hostile := 1;
	gsay_message(433, random(118, 120), 50);
end

procedure GenSupr07 begin
	set_map_var(2, 1);
	gsay_message(433, random(121, 123), 50);
end

procedure GenSupr08 begin
	float_msg(self_obj, message_str(SCRIPT_GENSUPR, random(124, 127)), 0);
	tmp_hostile := 1;
end

procedure GenSuprAlert begin
	set_global_var( GVAR_VATS_ALERT, 1 );
	tmp_hostile := 1;
end

procedure GenSuprxx begin
	set_global_var( GVAR_ENTERING_VATS_HOW, 2 );
	load_map(MAP_MBVATS12, 5);
end

procedure set_alert_tile begin
	if (not(local_var(4))) then begin
		set_local_var(4, tile_num(self_obj));
	end
	if (local_var(4) == 23065) then begin
		alert_tile := 21917;
		night_tile := 26274;
	end
	else if (local_var(4) == 22466) then begin
		alert_tile := 21922;
		night_tile := 27476;
	end
	else if (local_var(4) == 22470) then begin
		alert_tile := 22318;
		night_tile := 27072;
	end
end





