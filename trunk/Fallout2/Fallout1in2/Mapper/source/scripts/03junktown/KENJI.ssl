/*

	Kenji, the assassin send after Killian

*/

#include "..\headers\define.h"
#include "..\headers\maps\junkkill.h"

#define NAME                    SCRIPT_KENJI

#include "..\headers\command.h"
#include "..\headers\modreact.h"


procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;

variable tmp_hostile;
variable SrcObj := 0;
variable SrcIsParty := 0;

import variable Killian_ptr;

procedure start begin

end

procedure critter_p_proc begin
	if (tile_distance(self_tile, 26281) > 3) then begin
		self_walk_to_tile( 26281 );
	end
	else begin
		if (obj_on_screen(self_obj)) then begin
			float_msg(self_obj, message_str(SCRIPT_KENJI, 103), 2);
			attack(Killian_ptr);
		end
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) or (party_member_obj(source_obj)) then begin
		set_map_var(MVAR_helped_Killian, 1);
	end
end

procedure destroy_p_proc begin
	inc_evil_critter
	if (map_var(MVAR_helped_Killian) == 1) then begin
		set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep + 2 );
		display_msg(message_str(SCRIPT_KENJI, 104));
		give_exp_points(EXP_KILLED_KENJI);
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_KENJI, 102));
end

procedure talk_p_proc begin
	dude_look_at_self;
	display_msg(message_str(SCRIPT_KENJI, random(100, 101)));
end
