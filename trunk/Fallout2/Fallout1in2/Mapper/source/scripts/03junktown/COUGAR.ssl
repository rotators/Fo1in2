/*

	Junktown - Cougar, Doc Morbid guard

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_COUGAR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure Cougar00;
procedure Cougar01;
procedure Cougar02;
procedure Cougar03;
procedure Cougar04;

variable tmp_hostile;

import variable fetch_dude;

procedure damage_p_proc;

procedure start begin
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_COUGAR, 100));
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_COUGAR, 100));
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (fetch_dude) then begin
			tmp_hostile := 1;
			fetch_dude := 0;
		end
	end
	if (global_var( GVAR_IS_MORBID_TEAM_HOSTILE ) == 1) then begin
		if (self_can_see_dude) then begin
			attack(dude_obj);
		end
	end
end

procedure map_enter_p_proc
begin
	if junktown_invaded then begin
		kill_critter(self_obj, ANIM_fall_back_sf);
	end
	set_self_team(TEAM_JUNKTOWN_MORBID );
end

procedure pickup_p_proc
begin
	tmp_hostile := 1;
end

procedure talk_p_proc
begin
	dude_look_at_self;
	if (local_var(0)) then begin
		call Cougar04;
	end
	else begin
		start_gdialog(35, self_obj, 4, -1, -1);
		gsay_start;
		call Cougar00;
		gsay_end;
		end_dialogue;
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure Cougar00
begin
	set_local_var(0, 1);
	gsay_reply( SCRIPT_COUGAR, 101 );
	giq_option( 4, SCRIPT_COUGAR, 102, Cougar01, NEUTRAL_REACTION );
	giq_option( 5, SCRIPT_COUGAR, 103, Cougar02, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_COUGAR, 104, Cougar03, NEUTRAL_REACTION );
end

procedure Cougar01
begin
	gsay_reply( SCRIPT_COUGAR, 105 );
	giq_option( 4, SCRIPT_COUGAR, 106, Cougar02, NEUTRAL_REACTION );
end

procedure Cougar02
begin
	gsay_message( SCRIPT_COUGAR, 107, 50 );
end

procedure Cougar03
begin
	gsay_message( SCRIPT_COUGAR, 108, 50 );
end

procedure Cougar04
begin
	float_msg(self_obj, message_str(SCRIPT_COUGAR, 109), 50);
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_IS_MORBID_TEAM_HOSTILE, 1 );
	end
end
