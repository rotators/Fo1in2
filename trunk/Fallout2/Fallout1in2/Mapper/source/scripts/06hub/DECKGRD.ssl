/*

	Hub - Decker's Guards

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_DECKGRD
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure combat_p_proc;

variable tmp_hostile;
variable Only_Once := 1;
variable surrendered;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		if (global_var( GVAR_UNDERGROUND_STATUS ) == 1) then begin
			if not(is_loading_game) then begin    set_self_invisible;    end
		end
		set_self_team(TEAM_HUB_UNDERGROUND );
		set_self_ai( AI_GUARD_SADISTIC );
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	get_reaction
	if (global_var( GVAR_UNDERGROUND_STATUS ) == 0) then begin
		float_msg( self_obj, message_str(SCRIPT_DECKGRD, random(101, 106)), FLOAT_MSG_RED );
	end
	else begin
		float_msg( self_obj, message_str(SCRIPT_DECKGRD, random(108, 110)), FLOAT_MSG_RED );
	end
end

procedure destroy_p_proc
begin

	inc_evil_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_DECKGRD, 100));
end

procedure combat_p_proc
begin
	if ((global_var( GVAR_UNDERGROUND_STATUS ) == 1) and (surrendered == 0)) then begin
		script_overrides;
		set_map_var(54, 1);
		surrendered := 1;
		inven_unwield;
		float_msg( self_obj, message_str(SCRIPT_DECKGRD, 107), FLOAT_MSG_RED );
	end
	else begin
		if ((global_var( GVAR_UNDERGROUND_STATUS ) == 1) and (surrendered == 1)) then begin
			script_overrides;
		end
	end
end
