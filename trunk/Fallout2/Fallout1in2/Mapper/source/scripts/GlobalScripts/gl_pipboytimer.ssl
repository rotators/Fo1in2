/*

   Adds Fallout 1 water chip note to the pipboy screen.
   Also other shit related to the counter

*/

/* Include Files */
#include "..\headers\command.h"
#include "..\headers\define.h"
#include "..\headers\sfall\sfall.h"
#include "..\headers\sfall\dik.h"
#include "..\headers\sfall\main.h"

/* Standard Script Procedures */
procedure start;

procedure pipboy_note;
procedure pipboy_note_refresh;

procedure check_watertimer;
procedure check_invasions;

procedure KeyPressHandler;
procedure show_note;
procedure show_days;
procedure delete_all;

variable lastDay;
variable validate;
variable OffsetX;
variable OffsetY;

#define Scr_Width          get_screen_width
#define Scr_Heigh          get_screen_height

#define NoteOffsetX        get_ini_setting("config\\fo1_settings.ini|Fo1in2|PipBoyNoteOffsetX")
#define NoteOffsetY        get_ini_setting("config\\fo1_settings.ini|Fo1in2|PipBoyNoteOffsetY")
#define validate_number(x,y,z)   validate := x;                      \
                                 if (validate != "") then begin      \
                                    if (validate != "0") then begin  \
                                       validate := atoi(x);          \
                                       if (validate == z) then begin \
                                          y := z;                    \
                                       end                           \
                                    end                              \
                                    else                             \
                                       validate := atoi(validate);   \
                                 end                                 \
                                 else                                \
                                    y := z
#define get_offsets        OffsetX := NoteOffsetX; \
                           OffsetY := NoteOffsetY; \
                           validate_number(get_ini_string("config\\fo1_settings.ini|Fo1in2|PipBoyNoteOffsetX"),OffsetX,0); \
                           validate_number(get_ini_string("config\\fo1_settings.ini|Fo1in2|PipBoyNoteOffsetY"),OffsetY,0)

#define timer_active       ((global_var( GVAR_QUEST_VAULT13_4_WATERCHIP ) < 2) and (global_var( GVAR_VAULT13_WATER_DAYS_LEFT ) > 0))

procedure start begin
	if (game_loaded) then begin
		set_global_script_repeat(100);
		set_global_script_type(1);

      register_hook_proc(HOOK_GAMEMODECHANGE, pipboy_note);
      register_hook_proc(HOOK_RESTTIMER, pipboy_note_refresh);
   end

	if not(combat_is_initialized) then begin
   	// Find the Water Chip for V13:
      if (timer_active) then
   		call check_watertimer;

      // Invasion timers
      // TODO: Toggle like in Fixt? Master or Vats or both required?
   	if not(cathedral_destroyed) then
   		call check_invasions;
   end
end

procedure pipboy_note begin
   // Show the pipboy note if the remaining water timer is > 0 days:
   if (timer_active) then begin

   	if ((get_game_mode == PIPBOY) and (get_water_days_left > 0)) then begin
         if (get_sfall_global_int("opennote") != 1) then begin
   	      set_sfall_global("opennote", 1);
            call show_note;
         end
      end
      else if (get_sfall_global_int("opennote") != 0) then begin
         set_sfall_global("opennote", 0);
         call delete_all;
      end

   end
end

procedure pipboy_note_refresh begin
   if (get_game_mode == PIPBOY) then begin
   	if (game_time > get_sfall_global_int("lastchek")) then begin
         set_sfall_global("lastchek", game_time + ONE_GAME_MINUTE * ((ticks_til_hour(0) / 60) / 10));
         DeleteWin("win_days");
         call show_days;
      end
   end
end

procedure check_watertimer begin
	//debug("water days left: " + get_water_days_left);
	if (get_water_days_left <= 100) and (global_var(GVAR_PLAY_BOIL_MOVIE) == 0) then begin
		set_global_var(GVAR_PLAY_BOIL_MOVIE,1);
		play_gmovie(BOIL1_MOVIE);
	end
	else if (get_water_days_left <= 50) and (global_var(GVAR_PLAY_BOIL_MOVIE) == 1) then begin
		set_global_var(GVAR_PLAY_BOIL_MOVIE,2);
		play_gmovie(BOIL2_MOVIE);
	end
	else if (get_water_days_left <= 0) and (global_var(GVAR_PLAY_BOIL_MOVIE) == 2) then begin
		set_global_var(GVAR_PLAY_BOIL_MOVIE,3);
		play_gmovie(BOIL3_MOVIE);
		signal_end_game;
	end
end

procedure check_invasions begin
	if get_v13_days_left <= 0 and not(v13_invaded) then begin
		set_global_var(GVAR_VAULT_13_WAS_INVADED, 1);

		play_gmovie(OVERRUN_MOVIE);
		signal_end_game;
		debug("v13 just got invaded!");
	end
	if get_hub_days_left <= 0 and not(hub_invaded) then begin
		set_global_var(GVAR_THE_HUB_WAS_INVADED, 1);
		debug("the hub just got invaded!");
	end
	if get_bos_days_left <= 0 and not(bos_invaded) then begin
		set_global_var(GVAR_BROTHERHOOD_WAS_INVADED, 1);
		debug("the bos just got invaded!");
	end
	if get_boneyard_days_left <= 0 and not(boneyard_invaded) then begin
		set_global_var(GVAR_FOLLOWERS_INVADED, 1);
		debug("the bos just got invaded!");
	end
	if get_necropolis_days_left <= 0 and not(necropolis_invaded) then begin
		set_global_var(GVAR_NECROPOLIS_WAS_INVADED, 1);
		debug("necropolis just got invaded!");
	end
	if get_shady_days_left <= 0 and not(shady_invaded) then begin
		set_global_var(GVAR_SHADY_SANDS_WAS_INVADED, 1);
		debug("shady sands just got invaded!");
	end
	if get_junktown_days_left <= 0 and not(junktown_invaded) then begin
		set_global_var(GVAR_JUNKTOWN_WAS_INVADED, 1);
		debug("junktown just got invaded!");
	end
end

procedure show_note begin
   get_offsets;

   create_win("win_note_top", ((Scr_Width/2)-288 + OffsetX), ((Scr_Heigh/2)-207 + OffsetY), 148, 97);
   SelectWin("win_note_top");
   Display("PCX/pip2_top.pcx");
   ShowWin;

   create_win("win_note_bottom", ((Scr_Width/2)-288 + OffsetX), ((Scr_Heigh/2)-91 + OffsetY), 148, 111);
   SelectWin("win_note_bottom");
   Display("PCX/pip2_bottom.pcx");
   ShowWin;

   create_win("win_note_daysbg", ((Scr_Width/2)-253 + OffsetX), ((Scr_Heigh/2)-110 + OffsetY), 40, 19);
   SelectWin("win_note_daysbg");
   Display("PCX/pip2_daysbg.pcx");
   ShowWin;

   create_win("win_note_left", ((Scr_Width/2)-288 + OffsetX), ((Scr_Heigh/2)-110 + OffsetY), 39, 19);
   SelectWin("win_note_left");
   Display("PCX/pip2_left.pcx");
   ShowWin;

   create_win("win_note_right", ((Scr_Width/2)-213 + OffsetX), ((Scr_Heigh/2)-110 + OffsetY), 73, 19);
   SelectWin("win_note_right");
   Display("PCX/pip2_right.pcx");
   ShowWin;

   call show_days;
end

// Initial "days left" when opening the PipBoy:
procedure show_days begin
   get_offsets;

   // We only have 250 images, so bigger than that can't be shown:
   // YES I KNOW THESE ARE 250 IMAGES, BUT IF YOU WANT IT TO LOOK EXACTLY
   // THE SAME AS IN FALLOUT 1, IT IS A NECESSARY THING TO DO. Sue me.
   // We will rework this shit some day in the future. Promise.
   if (get_water_days_left <= 250) then begin
      create_win("win_days", ((Scr_Width/2)-253 + OffsetX), ((Scr_Heigh/2)-110 + OffsetY), 40, 19);
      SelectWin("win_days");
      Display("PCX/days/"+ get_water_days_left +".pcx");
      ShowWin;
   end
end

procedure delete_all begin
   DeleteWin("win_note_top");
   DeleteWin("win_note_bottom");
   DeleteWin("win_note_daysbg");
   DeleteWin("win_note_left");
   DeleteWin("win_note_right");
   DeleteWin("win_days");
end
