/*

   Imitates Fo1's barter price formula.

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\sfall\sfall.h"

procedure start;
procedure barterprice_handler;

procedure start begin
   if game_loaded then begin
      register_hook_proc(HOOK_BARTERPRICE, barterprice_handler);
   end
end

procedure barterprice_handler begin
   variable
      dude := get_sfall_arg,
      trader := get_sfall_arg,
      defPrice := get_sfall_arg,  // unused
      npcTable := get_sfall_arg,  // unused
      itemCaps := get_sfall_arg,
      basePrice := get_sfall_arg,
      dudeTable := get_sfall_arg, // unused
      soldPrice := get_sfall_arg, // unused
      offerBtn := get_sfall_arg,  // unused
      partyMem := get_sfall_arg,
      dude_barter,
      master_trader := 100,
      trader_barter,
      barter_diff,
      base_price_goods,
      final_price;

   if not(partyMem) then begin
      dude_barter := has_skill(dude_obj, SKILL_BARTER);
      trader_barter := has_skill(trader, SKILL_BARTER);
      if (has_trait(TRAIT_PERK, dude_obj, PERK_master_trader)) then master_trader := 125;
      barter_diff := gdialog_get_barter_mod + dude_barter - trader_barter + master_trader;

      if (barter_diff < 10) then barter_diff := 10;
      else if (barter_diff > 300) then barter_diff := 300;

      base_price_goods := basePrice - itemCaps;
      final_price := itemCaps + (base_price_goods * 100) / barter_diff;

      set_sfall_return(final_price);
      set_sfall_arg(0, final_price);
   end
end

