/*

	Encounter - Generic merchant

*/

#include "..\headers\define.h"

#define NAME                    SCRIPT_GENMERCB
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

variable dude_attacked_guards;
variable dude_attacked_raiders;
variable initial :=  0;
variable raiders_left;
variable rewarded;
variable scared;

import variable random_seed_1;
import variable random_seed_2;
import variable random_seed_3;

procedure start begin
	if not(initial) then begin
		set_self_team(TEAM_HUB_WATER_MERCHANTS );
		set_self_ai( AI_MERCHANT );
		scared := global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure );
		initial :=  1;
	end
end

procedure critter_p_proc begin
	if (scared and (tile_distance_objs(dude_obj, self_obj) < 8)) then begin
		call flee_dude;
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		scared := 1;
		set_global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure, 1 );
	end
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure pickup_p_proc begin
	scared := 1;
	set_global_var( GVAR_AGGRO_FLAG_RND_ENC_unsure, 1 );
end

procedure talk_p_proc
begin
	dude_look_at_self;
	raiders_left := random_seed_1;
	dude_attacked_raiders := random_seed_2;
	dude_attacked_guards := random_seed_3;
	if (scared) then begin
		float_msg( self_obj, message_str(SCRIPT_GENMERCB, 104), FLOAT_MSG_RED );
	end
	else begin
		if ((raiders_left == 0) and dude_attacked_raiders) then begin
			if (rewarded) then begin
				float_msg( self_obj, message_str(SCRIPT_GENMERCB, 101), FLOAT_MSG_BLUE );
			end
			else begin
				if (dude_is_male) then begin
					float_msg( self_obj, message_str(SCRIPT_GENMERCB, 106), FLOAT_MSG_BLUE );
				end
				else begin
					float_msg( self_obj, message_str(SCRIPT_GENMERCB, 107), FLOAT_MSG_BLUE );
				end
				rewarded := 1;
				move_obj_inven_to_obj(self_obj, dude_obj);
				set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep + 3 );
				give_exp_points(50);
				display_msg(message_str(SCRIPT_GENMERCB, 106));
				display_msg(message_str(SCRIPT_GENCHAT, 103) + "50" + message_str(SCRIPT_GENCHAT, 104));
			end
		end
		else begin
			if (dude_attacked_guards) then begin
				float_msg( self_obj, message_str(SCRIPT_GENMERCB, 103), FLOAT_MSG_RED );
			end
			else begin
				if (raiders_left != 0) then begin
					if (random(0, 1)) then begin
						float_msg( self_obj, message_str(SCRIPT_GENMERCB, 105), FLOAT_MSG_GREEN );
					end
					else begin
						float_msg( self_obj, message_str(SCRIPT_GENMERCB, 102), FLOAT_MSG_GREEN );
					end
				end
			end
		end
	end
end
