/*

	Assistant of Lorry, the Doctor in BoS

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\v13ent.h"

#define NAME                    SCRIPT_BOSASIST
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

/*******************************************************************
   Local variables which do not need to be saved between map changes.
*******************************************************************/

variable tmp_hostile;
variable Only_Once := 1;
variable SrcObj := 0;
variable SrcIsParty := 0;

/*******************************************************************
*******                         PROCEDURES                   *******
*******************************************************************/


/*******************************************************************
   The start procedure is the first procedure called when the map is
   first entered. Any initial information that needs to be set up
   should be placed in here.
*******************************************************************/

procedure start begin

end

procedure map_enter_p_proc begin
	Only_Once := 0;
   if (map_first_run) then begin
		set_self_team(TEAM_BROTHERHOOD);
		set_self_ai(AI_BROTHERHOOD_SCRIBE);
   end
end

procedure critter_p_proc begin
	if (global_var( GVAR_ENEMY_BROTHERHOOD )) then begin
		tmp_hostile := 1;
	end

	if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
		tmp_hostile := 0;
	end

	if (tmp_hostile) then begin
		set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	float_msg( self_obj, message_str(SCRIPT_BOSLORRI, random(279, 286)), FLOAT_MSG_RED );
end

procedure destroy_p_proc begin
	inc_good_critter
	set_global_var( GVAR_ENEMY_BROTHERHOOD, 1 );
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_BOSLORRI, 200));
end
