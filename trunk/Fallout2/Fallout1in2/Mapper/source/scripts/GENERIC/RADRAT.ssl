#include "..\headers\define.h"
#include "..\headers\command.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure use_skill_on_p_proc;
procedure combat_p_proc;

variable rndx;
variable init_teams := 0;
variable tmp_hostile := 0;

procedure start begin
	if (not(init_teams)) then begin
		init_teams := 1;
		set_self_team(TEAM_RATS );
		if debug_mode then begin
			display_msg("INIT_RADRAT.SSL - please tell Sduibek when and where you saw this message. Thank you!");
			debug_msg("INIT_RADRAT.SSL - please tell Sduibek when and where you saw this message. Thank you!");
		end
	end
end

procedure combat_p_proc begin
   if (fixed_param == COMBAT_SUBTYPE_HIT_SUCCEEDED) then begin
      rndx := random(1, 6) - 5;
      if (rndx < 0) then begin
         rndx := 0;
      end
      if (rndx > 0) then begin
         radiation_inc(target_obj, rndx);
      end
   end
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if not(dude_animal_friend) then begin
			if (self_can_see_dude or self_can_hear_dude) then begin// or (tile_distance_objs(self_obj, dude_obj) <= (self_perception * 2)) then begin
				tmp_hostile := 1;
				attack(dude_obj);
			end
		end
	end
end

procedure use_skill_on_p_proc begin
	if (action_being_used == SKILL_STEAL) then begin
		script_overrides;
		display_msg(message_str(SCRIPT_GENCHAT, 211));
		script_overrides;
	end
end

procedure damage_p_proc begin
	if obj_in_party(source_obj) then begin
		party_remove(self_obj);
		set_self_team(TEAM_RATS );
		tmp_hostile := 1;
	end
end
