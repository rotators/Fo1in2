/*

	Necropolis - Garret

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_GUARD2
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure damage_p_proc;

variable Only_Once := 1;
variable tmp_hostile;
variable oktoyell := 1;
variable active;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_NECROPOLIS_GANGER );
		set_self_ai( AI_GHOUL_NASTY );
		if (global_var( GVAR_HUB_FILLER_30 ) == 1) then begin
			active := 1;
			if not(is_loading_game) then begin    set_self_visible;    end//  MAKE VISIBLE
		end
		else begin
			if not(is_loading_game) then begin    set_self_invisible;    end//  MAKE INVISIBLE
		end
	end
	if (active) then begin
		if (script_action == talk_proc) then begin//<--- talk_p_proc (Face icon), can also call "do_dialogue" or "do_dialog"
			if (necropolis_wpump_fixed) then begin
				float_msg( self_obj, message_str(SCRIPT_GUARD2, 102), FLOAT_MSG_YELLOW );
			end
			else begin
				float_msg( self_obj, message_str(SCRIPT_GUARD2, 101), FLOAT_MSG_YELLOW );
			end
		end
		else begin
			if (script_action == damage_proc) then begin//<-- damage_p_proc - has this Critter or Object been shot, hit with TNT, punched, etc.
				call damage_p_proc;
			end
			else begin
				if (script_action == critter_proc) then begin//<-- critter_p_proc - (can also be "Critter_Action") - do they see you, should they wander, should they attack you, etc..
					if (global_var( GVAR_ENEMY_NECROPOLIS ) and self_can_see_dude) then begin
						tmp_hostile := 1;
					end
					if (tmp_hostile) then begin
						tmp_hostile := 0;
						set_global_var( GVAR_HUB_FILLER_30, 0 );
						set_global_var( GVAR_ENEMY_NECROPOLIS, 1 );
						attack(dude_obj);
					end
					else begin
						if not(necropolis_wpump_fixed) then begin
							if (tile_distance_objs(self_obj, dude_obj) > 7) then begin
								tmp_hostile := 1;
							end
							else begin
								if ((tile_distance_objs(self_obj, dude_obj) > 4) and oktoyell) then begin
									oktoyell := 0;
									float_msg( self_obj, message_str(SCRIPT_GUARD2, 103), FLOAT_MSG_YELLOW );
								end
								else begin
									if (tile_distance_objs(self_obj, dude_obj) < 5) then begin
										oktoyell := 1;
									end
								end
							end
						end
					end
				end
				else begin
					if (script_action == pickup_proc) then begin//<---caught stealing! (pickup_p_proc)
						tmp_hostile := 1;
					end
					else begin
						if (script_action == look_at_proc) then begin//MOUSE-OVER DESCRIPTION -- look_at_p_proc - (usually brief length. hovered mouse over object, haven't clicked on it.)
							script_overrides;
							display_msg(message_str(SCRIPT_GUARD2, 100));
						end
					end
				end
			end
		end
	end
end

procedure damage_p_proc
begin
tmp_hostile := 1;
end

procedure destroy_p_proc begin
	set_global_var( GVAR_GARRET_STATUS, 3 );
	set_global_var( GVAR_HUB_FILLER_30, 0 );
	set_global_var( GVAR_ENEMY_NECROPOLIS, 1 );
	inc_inv_evil_crit
end
