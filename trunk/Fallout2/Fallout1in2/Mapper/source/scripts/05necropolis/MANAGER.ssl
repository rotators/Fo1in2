/*

	Necropolis - Hotel Manager

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_MANAGER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure combat_p_proc;

procedure ghoul00;
procedure ghoul01;
procedure ghoul02;
procedure ghoul03;
procedure ghoul04;
procedure ghoul05;
procedure ghoul05a;
procedure ghoul06;
procedure ghoul07;
procedure ghoul08;
procedure ghoul09;
procedure ghoul10;
procedure ghoul11;
procedure ghoulend;
procedure ghoulcbt;

variable tmp_hostile;
variable initial :=  0;
variable noevent;
variable loopcount;
variable rndRads;

variable rndx;

// local_var(0) HAS_TALKED_TO_PLAYER
// local_var(1) HAS_BEEN_EXAMINED

procedure start begin
	if (not(initial)) then begin
		initial :=  1;
		set_self_team( TEAM_32 );//  TEAM -- was 32 in Manager.ssl, 30 in Ghoul.ssl
		set_self_ai( AI_GHOUL_NASTY );
	end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure description_p_proc
begin
	script_overrides;
	display_msg(message_str(SCRIPT_GHOUL, 200));
	set_local_var(1, 1);
end

procedure look_at_p_proc begin
	script_overrides;
	if (not(local_var(1)) and not(local_var(0))) then begin
		display_msg(message_str(SCRIPT_GHOUL, 100));
	end
	else begin
		display_msg(message_str(SCRIPT_GHOUL, 201));
	end
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin

		if ((game_time_hour >= 1900) or (game_time_hour < 600) and (self_tile != 22531)) then begin
			self_walk_to_tile( 22531 );
		end
		if (day and (self_tile != 24730)) then begin
			self_walk_to_tile( 24730 );
		end

		loopcount := loopcount + 1;
		if (loopcount > 40) then begin
			loopcount := 0;
			if (tile_distance_objs(self_obj, dude_obj) < 7) then begin
				if (self_can_see_dude) then begin
					if (noevent == 0) then begin
						noevent := 1;
						add_timer_event(self_obj, game_ticks(5), 0);
					end
				end
			end
		end
	end
end

procedure destroy_p_proc begin
	rm_timer_event(self_obj);
	inc_neutral_critter
end

procedure timed_event_p_proc begin
	if (fixed_param == 1) then begin
		tmp_hostile := 1;
	end
	else begin
		if (local_var(0) == 0) then begin
			dialogue_system_enter;
		end
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	if (local_var(0)) then begin
		call ghoul08;
	end
	else begin
   	start_gdialog(30, self_obj, 4, -1, -1);
   	gsay_start;
   	set_local_var(0, 1);
   	call ghoul00;
   	gsay_end;
   	end_dialogue;
	end
end

procedure ghoulend begin
end

procedure ghoulcbt begin
	add_timer_event(self_obj, game_ticks(2), 1);
end

procedure ghoul00 begin
	gsay_reply( SCRIPT_GHOUL, 101 );
	giq_option( 6, SCRIPT_GHOUL, 102, ghoul01, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GHOUL, 103, ghoul04, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GHOUL, 118, ghoul09, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GHOUL, 104, ghoul05, NEUTRAL_REACTION );
	giq_option( -3, SCRIPT_GHOUL, 105, ghoulend, NEUTRAL_REACTION );
end

procedure ghoul01 begin
	gsay_reply( SCRIPT_GHOUL, 106 );
	giq_option( 6, SCRIPT_GHOUL, 107, ghoul02, NEUTRAL_REACTION );
	giq_option( 6, SCRIPT_GHOUL, 108, ghoul03, NEUTRAL_REACTION );
end

procedure ghoul02 begin
	gsay_message( SCRIPT_GHOUL, 109, 50 );
end

procedure ghoul03 begin
	gsay_message( SCRIPT_GHOUL, 110, 50 );
end

procedure ghoul04 begin
	gsay_message( SCRIPT_GHOUL, 111, 50 );
end

procedure ghoul05 begin
	gsay_reply( SCRIPT_GHOUL, 112 );
	giq_option( 4, SCRIPT_GHOUL, 113, ghoul07, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GHOUL, 114, ghoul05a, NEUTRAL_REACTION );
end

procedure ghoul05a begin
	if (is_success(do_check( dude_obj, STAT_heal_rate, 0 ))) then begin
		call ghoul06;
	end
	else begin
		call ghoul07;
	end
end

procedure ghoul06 begin
	gsay_message( SCRIPT_GHOUL, 115, 50 );
end

procedure ghoul07 begin
	gsay_message( SCRIPT_GHOUL, 116, 50 );
	call ghoulcbt;
end

procedure ghoul08 begin
	float_msg(self_obj, message_str(SCRIPT_GHOUL, 117), 7);
	call ghoulend;
end

procedure ghoul09 begin
	gsay_reply( SCRIPT_GHOUL, 119 );
	giq_option( 4, SCRIPT_GHOUL, 120, ghoul10, NEUTRAL_REACTION );
	giq_option( 4, SCRIPT_GHOUL, 121, ghoul11, NEUTRAL_REACTION );
end

procedure ghoul10 begin
	gsay_message( SCRIPT_GHOUL, 122, 50 );
end

procedure ghoul11 begin
	gsay_message( SCRIPT_GHOUL, 123, 50 );
end

procedure combat_p_proc begin
end
