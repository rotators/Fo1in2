//
// ---HOLODISK SCRIPT---  Sduibek
//
// KEEP ORIGINAL ITEM	rebuild via critter_p_proc - DONE.
// ProtoID	164	Name:	Brotherhood Disk	Cost:	70
// script:	333	Artifact.int   ; the artifact the Brotherhood sends you to find.
// GVAR	161	"Ancient Brotherhood Disk" - A holodisk with information about the Brotherhood of Steel.

#include "..\headers\define.h"

#define NAME                    SCRIPT_ARTIFACT

#include "..\headers\command.h"

procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_p_proc;

procedure NewPtr;

#define LVAR_Used          (0)
#define LVAR_Examined      (1)

procedure start begin
	//call NewPtr;
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(LVAR_Examined) < 1) and (global_var( GVAR_ARTIFACT_HOLODISK_USED ) != 1) then begin// not used or Examined yet
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(104));
	end
end

procedure description_p_proc begin
	set_local_var(LVAR_Examined, 1);
	display_msg(mstr(103));
end

procedure use_p_proc begin
	script_overrides;
	set_local_var(LVAR_Examined, 1);
	if local_var(LVAR_Used) == 1 then begin
		display_msg(mstr(101));
	end
	else begin
		set_local_var(LVAR_Used, 1);
		set_global_var( GVAR_ARTIFACT_HOLODISK_USED, 1 );
		display_msg(mstr(102));//Downloaded OK
		display_msg(message_str(SCRIPT_GENCHAT, 103) + "250" + message_str(SCRIPT_GENCHAT, 104));// more exp, for Fallout Fixt
		give_exp_points(250);
	end
end

procedure NewPtr begin
	if self_obj != 0 then begin
		if (cur_map_index != MAP_GLOW1) and (cur_map_index != MAP_GLOW2) then begin
			set_global_var( GVAR_ARTIFACT_HOLODISK_POINTER, self_obj );
		end
	end
end
