/*

	Shady Sands - Cook

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\shadysands.h"

#define NAME                    SCRIPT_COOK
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SHADYSANDS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure description_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;

procedure Cook01;
procedure PickDeadBodyType;
variable tmp_hostile;
variable initial :=  0;
variable round_counter;
variable DeathType := 56;

procedure start begin
	if shady_invaded then begin
		if CUR_AREA_SHADY_SANDS then begin //  SHADY SANDS - EAST OR WEST
			if (local_var(5) != 1) then begin
				set_local_var(5, 1);
				call PickDeadBodyType;
				kill_critter(self_obj, DeathType);
			end
		end
	end

	if not(initial) then begin
		initial :=  1;
		set_self_team( TEAM_SHADY_SANDS );
		set_self_ai( AI_PEASANT );
	end
end

procedure combat_p_proc begin
	if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
		round_counter := round_counter + 1;
	end
	if (round_counter > 3) then begin
		if not(global_var( GVAR_ENEMY_SHADY_SANDS )) then begin//Shady Sands is NOT tmp_hostile to player;  i.e. globalvar ENEMY_SHADY_SANDS is not enabled
			set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
			set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep - 5 );
		end
	end
end

procedure critter_p_proc begin
	if (self_can_see_dude) then begin
		if global_var( GVAR_ENEMY_SHADY_SANDS ) then begin//   Is Shady Sands tmp_hostile to player?
			tmp_hostile := 1;
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_local_var(1, 1);
		if CUR_AREA_SHADY_SANDS then begin //  SHADY SANDS - EAST OR WEST
			set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
		end
	end
end

procedure description_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_COOK, 100));
end

procedure destroy_p_proc begin
	if (source_obj == dude_obj) then begin
		if CUR_AREA_SHADY_SANDS then begin //  SHADY SANDS - EAST OR WEST
			set_global_var( GVAR_ENEMY_SHADY_SANDS, 1 );
		end
		inc_good_critter
	end
end

procedure look_at_p_proc begin
	script_overrides;
	display_msg(message_str(SCRIPT_COOK, 100));
end

procedure talk_p_proc begin
	dude_look_at_self;
	if (global_var( GVAR_ENEMY_SHADY_SANDS ) or (local_var(1) == 1)) then begin
		float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), 0);
	end
	else begin
		if (is_success(do_check(dude_obj, STAT_lu, 0)) and (local_var(0) == 0) and (dude_iq >= 4)) then begin
			start_gdialog(114, self_obj, 4, -1, -1);
			gsay_start;
			gsay_reply( SCRIPT_COOK, 101 );
			gsay_option( SCRIPT_COOK, 102, Cook01, 50 );
			gsay_end;
			end_dialogue;
		end
		else begin
			float_msg(self_obj, message_str(SCRIPT_COOK, 103), 8);
		end
		set_local_var(0, 1);
	end
end

procedure pickup_p_proc begin
	set_local_var(1, 1);
end

procedure Cook01 begin
	set_global_var( GVAR_PLAYER_REPUTATION, check_general_rep + 1 );
	gsay_message( SCRIPT_COOK, 104, 50 );
end

procedure PickDeadBodyType begin
	pick_dead_body_type
end
